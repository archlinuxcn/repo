# Maintainer: bitwave <aur [at] oomlu [d0T] de>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=libappindicator
pkgver=12.10.0
pkgrel=2
pkgdesc="Library to allow applications to export a menu into the Unity Menu bar (GTK+ 2 library)"
arch=('i686' 'x86_64')
url="https://launchpad.net/libappindicator"
license=('LGPL')
depends=('libindicator-gtk2' 'libdbusmenu-gtk2')
makedepends=('gobject-introspection' 'vala' 'gtk-sharp-2' 'pygtk' 'perl-xml-libxml' 'dbus-glib')
options=('!libtool')
source=(http://launchpad.net/libappindicator/${pkgver%.*}/$pkgver/+download/$pkgname-$pkgver.tar.gz)
md5sums=('41910f2005edee9240da1e53fffcdc12')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Fix mono bindings location
  sed -i 's|/cli/|/mono/|' bindings/mono/{appindicator-sharp-0.1.pc.in,Makefile.in}

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static --with-gtk=2
  make -j1
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}
