# Maintainer: WorMzy Tykashi <wormzy.tykashi@gmail.com>
# Contributor: Nerdy Espeon <nerdyespeon@gmail.com>
# Contributor: Kirill A. Shutemov <kirill+arch@shutemov.name>

pkgname=dwarftherapist-git
epoch=1
pkgver=37.0.0_r48_g0f00a17
pkgrel=1
pkgdesc="Heavily modified version of the original Dwarf Therapist."
url="https://github.com/splintermind/Dwarf-Therapist"
arch=('x86_64' 'i686')
license=('MIT')
depends=('qt5-declarative' 'hicolor-icon-theme' 'libcap')
makedepends=('git' 'texlive-latexextra')
install="dwarftherapist.install"
source=($pkgname::git+"https://github.com/splintermind/Dwarf-Therapist.git#branch=DF2016")
md5sums=('SKIP')

pkgver() {
  cd $pkgname
  _curtag="$(git rev-list --tags --max-count=1)"
  _tagver="$(git describe --tags $_curtag | sed 's:^v::')"
  _commits="$(git log v${_tagver}..HEAD --oneline | wc -l)"
  _sha="$(git rev-parse --short HEAD)"
  printf "%s_r%s_g%s" $_tagver $_commits $_sha | sed 's:-:_:g'
}

build() {
  cd $pkgname
  qmake -qt=5 PREFIX=/usr
  make
}

package() {
  cd $pkgname
  make INSTALL_ROOT="$pkgdir" install
  rm "$pkgdir/usr/bin/dwarftherapist"
  mv "$pkgdir/usr/bin/DwarfTherapist" "$pkgdir/usr/bin/dwarftherapist"
  install -Dm644 "dist/dwarftherapist.desktop" \
    "$pkgdir"/usr/share/applications/dwarftherapist.desktop
  install -Dm644 resources/img/hammer.png \
    "$pkgdir"/usr/share/icons/hicolor/128x128/apps/dwarftherapist.png
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 "doc/Dwarf Therapist.pdf" \
    "$pkgdir/usr/share/doc/dwarftherapist/Dwarf Therapist.pdf"
}

# vim:set ts=2 sw=2 et:
