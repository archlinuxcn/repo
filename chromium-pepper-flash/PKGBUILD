# Maintainer: ava1ar <mail(at)ava1ar(dot)me>

pkgname=chromium-pepper-flash
pkgdesc="Google Chrome's Pepper Flash plugin for Chromium (stable version)"
_verbld=42.0.2311.90-1
pkgver=17.0.0.169
pkgrel=2
epoch=1
arch=('i686' 'x86_64')
url="http://www.google.com/chrome"
license=('custom:chrome')
conflicts=('chromium-pepper-flash-dev')
provides=('pepper-flash')
install=chromium-pepper-flash.install
source=(${pkgname}-license.html::https://www.google.com/chrome/intl/en/eula_text.html)
source_i686+=("https://dl.google.com/linux/chrome/rpm/stable/i386/google-chrome-stable-${_verbld}.i386.rpm")
source_x86_64+=(https://dl.google.com/linux/chrome/rpm/stable/x86_64/google-chrome-stable-${_verbld}.x86_64.rpm)
sha256sums=('c714679be9b1de35475bfcd58b4d2fa5baf87c00eb049185ec062de276203d13')
sha256sums_i686=('de7f8dfa03b0dbf5acdf52dae64ab6ced3fb54b5887f95a129e1185861626ed4')
sha256sums_x86_64=('dc1e6cc3d1b38c24df7717ca682c83abae8d27321ba1b1637434a20ae3e5c2c8')

package() {
	# create required directories
	install -d "${pkgdir}"/usr/lib/PepperFlash
	# copy required files
	install -m644 "${srcdir}"/opt/google/chrome/PepperFlash/* "${pkgdir}"/usr/lib/PepperFlash
	# copy license
	install -Dm644 "${srcdir}"/${pkgname}-license.html "${pkgdir}"/usr/share/licenses/${pkgname}/license.html
}
