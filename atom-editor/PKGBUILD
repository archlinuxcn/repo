# Maintainer: John Reese <john@noswap.com>
# Upstream URL: https://github.com/atom/atom
#
# For improvements/fixes to this package, please send a pull request:
# https://github.com/jreese/arch

pkgname=atom-editor
pkgver=0.125.0
pkgrel=1
pkgdesc="Chrome-based text editor from Github"
arch=('x86_64' 'i686')
url="https://github.com/atom/atom"
license=('MIT')
conflicts=('atom-editor-git')
depends=('alsa-lib' 'gconf' 'gtk2' 'libatomic_ops' 'libgcrypt' 'libgnome-keyring' 'libnotify' 'libxtst' 'nodejs' 'nss' 'python2')
source=("https://github.com/atom/atom/archive/v${pkgver}.tar.gz" 'atom-python.patch')
sha256sums=('1a2c5abf6c9740568c1468dcff678c30f72194faa9aa0c53afef3e55aaf04dc6' '18a19b895afbb5c72fe73b1efb37a662e8e8fa0f4731c9fa634f58c341c5ec3b')
makedepends=('git')

_gitref='f6bb7fc08969544c206d2cd0ea7a2864ed7b6967'
_gitbranch='master'

package() {
  cd "$srcdir/atom-$pkgver"

  export PYTHON=python2
  export INSTALL_PREFIX="$pkgdir/usr"
  export JANKY_SHA1=$_gitref
  export JANKY_BRANCH=$_gitbranch

  script/build --install-dir=$pkgdir/usr
  script/grunt install

  patch "$pkgdir/usr/bin/atom" < "$srcdir/atom-python.patch"

  sed -e "s/<%= description %>/$pkgdesc/" \
    -e "s|<%= installDir %>/share/atom/atom|/usr/bin/atom|"\
    -e "s|<%= iconName %>|atom|"\
    resources/linux/Atom.desktop.in > resources/linux/Atom.desktop
  install -Dm644 resources/linux/Atom.desktop "$pkgdir/usr/share/applications/Atom.desktop"

  install -Dm644 resources/atom.png "$pkgdir/usr/share/pixmaps/atom.png"
  install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"
}

# vim:set ts=2 sw=2 et:
