# Maintainer: Yichao Yu <yyc1992@gmail.com>
# Contributor: SÃ©bastien Luttringer
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Fabio Volpe <volpefabio@gmail.com>

_pypyname=pypy-hg
_pypyabi=26
_pypy3name=pypy3-hg
_pypy3abi=25

pkgbase=${_pypyname}-sphinx
pkgname=(${_pypy3name}-sphinx ${_pypyname}-sphinx)
pkgver=1.2.3
pkgrel=4
arch=('any')
url='http://sphinx.pocoo.org/'
license=('GPL')
makedepends=(pypy3-${_pypy3abi}-setuptools
             pypy3-${_pypy3abi}-docutils
             pypy3-${_pypy3abi}-jinja
             pypy3-${_pypy3abi}-pygments
             pypy-setuptools
             pypy-docutils
             pypy-jinja
             pypy-pygments
             pypy3-${_pypy3abi})
checkdepends=(pypy3-${_pypy3abi}-nose
              pypy-nose
              texlive-latexextra)
source=("http://pypi.python.org/packages/source/S/Sphinx/Sphinx-$pkgver.tar.gz")
md5sums=('a98c93124035b4cd7183604aec656cb3')

prepare() {
  # souce duplication is required because makefile modify source code
  # setyp.py --build tricks don't works well
  cp -a Sphinx-$pkgver Sphinx-${pkgver}2

  # change pypy interpreter
  find Sphinx-${pkgver}2 -type f -exec \
       sed -i '1s,^#! \?/usr/bin/\(env \|\)python$,#!/usr/bin/pypy,' {} \;

  # change sphinx-binaries name in source code
  find Sphinx-${pkgver}2 -type f -name '*.py' -exec \
       sed -ri 's,(sphinx-(:?build|apidoc|autogen|quickstart)),\1-pypy,' {} \;

  # change pypy interpreter
  find Sphinx-${pkgver} -type f -exec \
       sed -i '1s,^#! \?/usr/bin/\(env \|\)python$,#!/usr/bin/pypy3,' {} \;

  # change sphinx-binaries name in source code
  find Sphinx-${pkgver} -type f -name '*.py' -exec \
       sed -ri 's,(sphinx-(:?build|apidoc|autogen|quickstart)),\1-pypy3,' {} \;
}

build() {
  msg2 'PyPy 3 version'
  (cd Sphinx-$pkgver
   make PYTHON=pypy3 build)

  msg2 'PyPy 2 version'
  (cd Sphinx-${pkgver}2
   make PYTHON=pypy build)
}

check() {
  msg2 'PyPy 3 version'
  (cd Sphinx-$pkgver
   make PYTHON=pypy3 test
   rm -r build/lib/tests)

  msg2 'PyPy 2 version'
  (cd Sphinx-${pkgver}2
   make PYTHON=pypy test
   rm -r build/lib/tests)
}

package_pypy3-hg-sphinx() {
  pkgdesc='Pypy3 documentation generator'
  depends=(pypy3-${_pypy3abi}-jinja pypy3-${_pypy3abi}-pygments
           pypy3-${_pypy3abi}-docutils pypy3-${_pypy3abi})
  provides=(pypy3-sphinx=${pkgver}
            pypy3-${_pypy3abi}-sphinx=${pkgver})
  conflicts=(pypy3-sphinx)
  optdepends=('texlive-latexextra: for generation of PDF documentation')

  cd Sphinx-$pkgver
  pypy3 setup.py install --root="$pkgdir" --optimize=1

  install -dm755 "${pkgdir}/usr"
  mv "${pkgdir}/opt/pypy3/bin" "${pkgdir}/usr"
}

package_pypy-hg-sphinx() {
  pkgdesc='Pypy documentation generator'
  depends=(pypy-jinja pypy-pygments pypy-docutils)
  provides=(pypy-sphinx)
  conflicts=(pypy-sphinx)
  optdepends=('texlive-latexextra: for generation of PDF documentation')

  cd Sphinx-${pkgver}2
  pypy setup.py install --root="$pkgdir" --optimize=1

  install -dm755 "${pkgdir}/usr"
  mv "${pkgdir}/opt/pypy/bin" "${pkgdir}/usr"
}
