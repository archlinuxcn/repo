# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: mickele
# Contributor: marcus fritzsch <fritschy@googlemail.com>

pkgname=coin
pkgver=3.1.3
pkgrel=12
pkgdesc='A high-level 3D graphics toolkit on top of OpenGL'
url='http://www.coin3d.org/'
license=('GPL')
arch=('i686' 'x86_64')
depends=('libgl' 'expat')
makedepends=('doxygen')
optdepends=('openal: sound/dynamic linking support'
  'fontconfig: dynamic linking support'
  'zlib: dynamic linking support'
  'freetype2: dynamic linking support'
  'js: dynamic linking support'
  'simage: image format support')
provides=("coin-doc=${pkgver}")
conflicts=(coin-doc)
options=('!libtool' '!staticlibs' 'debug' 'strip')
source=("https://bitbucket.org/Coin3D/coin/downloads/Coin-${pkgver}.tar.gz")
sha256sums=('583478c581317862aa03a19f14c527c3888478a06284b9a46a0155fa5886d417')

build() {
  cd Coin-${pkgver}

  # fix prefix in coin-config
  sed -i '/^prefix/c prefix="/usr/"' bin/coin-config

  # fix compilation
  sed -i '/^#include "fonts\/freetype.h"$/i #include <cstdlib>\n#include <cmath>' src/fonts/freetype.cpp

  # fix http://bugs.debian.org/cgi-bin/bugreport.cgi?bug=667139
  sed -i '/^#include <Inventor\/C\/basic.h>$/i #include <Inventor/C/errors/debugerror.h>' include/Inventor/SbBasic.h

  export CFLAGS="-g -O2 ${CFLAGS} -g -O2"
  export CXXFLAGS="-g -O2 ${CXXFLAGS} -g -O2"

  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man \
    --enable-optimization \
    --enable-3ds-import \
    --enable-javascript-api \
    --enable-threadsafe \
    --enable-exceptions \
    --enable-man \
    --enable-html \
    --with-mesa \
    --disable-debug \
    --enable-shared \
    --disable-maintainer-mode \
    --disable-dependency-tracking \
    --enable-system-expat

  make
}

package() {
  cd Coin-${pkgver}

  make DESTDIR=${pkgdir} install

  # final adjustments
  for _FILE in threads deprecated errors events ; do
    mv "${pkgdir}/usr/share/man/man3/${_FILE}.3" \
      "${pkgdir}/usr/share/man/man3/coin-${_FILE}.3"
  done
}
