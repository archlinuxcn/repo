# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Pierre Schmitz <pierre@archlinux.de> ([core] package)
# Contributor: Fran√ßois Charette <firmicus@gmx.net>

pkgname=xz-git
pkgver=5.1.3alpha.r21.gd716acd
pkgrel=2
pkgdesc='Library and command line tools for XZ and LZMA compressed files - git checkout'
arch=('i686' 'x86_64')
url='http://tukaani.org/xz/'
license=('GPL' 'LGPL' 'custom')
depends=('sh')
makedepends=('git')
provides=('lzma' 'lzma-utils' 'xz-utils' 'xz=5.1.2')
replaces=('lzma' 'lzma-utils' 'xz-utils')
conflicts=('lzma' 'lzma-utils' 'xz-utils' 'xz')
options=('!libtool')
source=('git+http://git.tukaani.org/xz.git'
	'xz-32bit.patch')

# keep an upgrade path for older installations
PKGEXT='.pkg.tar.gz'

pkgver() {
	cd xz/

	if GITTAG="$(git describe --abbrev=0 --tags 2>/dev/null)"; then
		echo "$(sed -e "s/^${pkgname%%-git}//" -e 's/^[-_/a-zA-Z]\+//' -e 's/[-_+]/./g' <<< ${GITTAG}).r$(git rev-list --count ${GITTAG}..).g$(git log -1 --format="%h")"
	else
		echo "0.r$(git rev-list --count master).g$(git log -1 --format="%h")"
	fi
}

build() {
	cd xz/

	patch -Np1 < "${srcdir}/xz-32bit.patch"

	./autogen.sh
	./configure --prefix=/usr \
		--disable-rpath \
		--enable-werror
	make
}

check() {
	cd xz/

	make check
}

package() {
	cd xz/

	make DESTDIR=${pkgdir} install
	install -d -m0755 ${pkgdir}/usr/share/licenses/xz/
	ln -sf /usr/share/doc/xz/COPYING ${pkgdir}/usr/share/licenses/xz/
	ln -sf /usr/share/licenses/common/GPL2/license.txt ${pkgdir}/usr/share/doc/xz/COPYING.GPLv2
}

sha256sums=('SKIP'
            'dee9744f90f51fd136f490f9463f01a628080eff58fb21133e4ba24646f2bf41')
