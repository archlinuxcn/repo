# Maintainer: Kevin Slagle <kjslag at gmail dot com>

pkgname='loadcaffe-git'
pkgver=r106.e691592
pkgrel=1
pkgdesc='Load Caffe networks in Torch7'
arch=('x86_64' 'i686')
url='https://github.com/szagoruyko/loadcaffe'
license=('BSD')
depends=('protobuf' 'torch7-nn-git')
makedepends=('git' 'cmake')
conflicts=('loadcaffe')
provides=('loadcaffe')
source=("${pkgname}::git+${url}")
md5sums=('SKIP')

# https://wiki.archlinux.org/index.php/VCS_package_guidelines#Git
pkgver() {
  cd "$pkgname"
  ( set -o pipefail
    git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

build() {
  cd "${pkgname}"
  
  # loadcaffe.cpp assumes a build directory due to the following line: #include "build/caffe.pb.h"
  mkdir build
  cd build
  
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd "${pkgname}/build"
  make DESTDIR="${pkgdir}" install
  
  # Install license
  mkdir -p "${pkgdir}/usr/share/licenses/loadcaffe"
  install -Dm644 "${srcdir}/${pkgname}/LICENSE" "${pkgdir}/usr/share/licenses/loadcaffe/"
  
  # Move Lua C module
  mkdir -p "${pkgdir}/usr/lib/lua/5.1"
  mv "${pkgdir}/usr/lib/libloadcaffe.so" "${pkgdir}/usr/lib/lua/5.1/"
  
  # Move pure Lua modules
  mkdir -p "${pkgdir}/usr/share/lua/5.1"
  mv "${pkgdir}/usr/lua/loadcaffe" "${pkgdir}/usr/share/lua/5.1/"
  rm -rf "${pkgdir}/usr/lua"
}
