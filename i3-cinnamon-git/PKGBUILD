# Maintainer: Janek Thomaschewski <janek@jbbr.net>
# Contributors: Jes√∫s Castro <x51v4n@gmail.com>, @killajoe, @Snowmobil, @ThomasWeiser (GitHub)
pkgname=i3-cinnamon-git
_pkgname=i3-cinnamon
pkgver=3.0.r7.g7d432f2
pkgrel=1
epoch=1
pkgdesc="Starts i3 inside a cinnamon session."
arch=('any')
url="https://github.com/jbbr/i3-cinnamon/"
license=('MIT')
depends=('i3-wm' 'cinnamon-session' 'cinnamon-settings-daemon')
makedepends=('git')
provides=('i3-cinnamon')
conflicts=('i3-cinnamon')
optdepends=('gdm: Recommended display manager')
source=('git+https://github.com/jbbr/i3-cinnamon')
sha256sums=('SKIP')

pkgver() {
  cd "$_pkgname"
  git describe --tags --long|sed -r "s,^[^0-9],,;s,([0-9]*-g),r\1,;s,[-_],.,g"
}

build() {
  cd "$_pkgname"

  make
}

package() {
  cd "$_pkgname"

  make DESTDIR="$pkgdir/" install
}
