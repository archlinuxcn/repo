# Maintainer: Yichao Yu <yyc1992@gmail.com>

_pypyname=pypy-hg
_pypyabi=26
_pypy3name=pypy3-hg
_pypy3abi=25

pkgbase=${_pypyname}-pytest
pkgname=(${_pypy3name}-pytest ${_pypyname}-pytest)
pkgver=2.6.4
pkgrel=3
pkgdesc="Simple powerful testing with Python"
arch=('any')
license=('MIT')
url="http://pytest.org/"
makedepends=(pypy3-${_pypy3abi}-setuptools pypy-setuptools
             pypy3-${_pypy3abi}-py pypy-py pypy3-${_pypy3abi})
source=("http://pypi.python.org/packages/source/p/pytest/pytest-$pkgver.tar.gz")
sha512sums=('b17f79b0f3c0f6051bedb0d7acadc5d569b3b90b2fce2eddda06a21b654f6077f934aaa9617152d6b2f8babb95c37494f18e050841af601f7f37de25c15b32fd')

prepare() {
  cp -a pytest-${pkgver}{,-py2}
}

build() {
  (cd pytest-${pkgver}
   pypy3 setup.py build)

  (cd pytest-${pkgver}-py2
   pypy setup.py build)
}

package_pypy3-hg-pytest() {
  depends=(pypy3-${_pypy3abi} pypy3-${_pypy3abi}-py)
  provides=(pypy3-pytest=${pkgver}
            pypy3-${_pypy3abi}-pytest=${pkgver})
  conflicts=(pypy3-pytest)
  cd pytest-${pkgver}

  pypy3 setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  install -dm755 "${pkgdir}/usr/bin"
  ln -s "/opt/pypy3/bin/py.test" "${pkgdir}/usr/bin/pypy.test3"
}

package_pypy-hg-pytest() {
  depends=(pypy pypy-py)
  provides=(pypy-pytest=${pkgver})
  conflicts=(pypy-pytest)
  cd pytest-${pkgver}-py2

  pypy setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  install -dm755 "${pkgdir}/usr/bin"
  ln -s "/opt/pypy/bin/py.test" "${pkgdir}/usr/bin/pypy.test"
}
