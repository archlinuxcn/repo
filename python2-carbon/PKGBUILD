# Maintainer: lilydjwg <lilydjwg@gmail.com>
# Maintainer='Gilles Hamel <hamelg@laposte.net>'
# Initial Maintainer: Augusto F. Hack <hack.augusto@gmail.com>
pkgname=python2-carbon
pkgver=1.1.5
pkgrel=1
pkgdesc='Backend data caching and persistence daemon for Graphite'
arch=('any')
url='https://github.com/graphite-project/carbon'
license=('Apache')
depends=('python2' 'python2-whisper' 'python2-twisted>=13.2.0')
optdepends=('python2-txamqp: AMQP support')
makedepends=('python2-setuptools')
options=(!emptydirs)
source=("$pkgname-$pkgver.tar.gz::https://github.com/graphite-project/carbon/archive/$pkgver.tar.gz"
        carbon.service
        carbon.tmpfiles
        carbon.sysusers
        carbon.conf)
sha1sums=('05f11865190734e93eae37b963f0d4bb766c78a4'
          'a2d216abc6f5a27a0725eb4f87cc533074ed63b2'
          '1cbaf55a0d5c532901b6c6fe039bc15c8a54c7dd'
          'c2bdf80507458a2a9fb999f4c99db45eb5ac6f64'
          '0051b8752666cfc869f7fe6894743cc1984fc9fe')
backup=(etc/carbon/aggregation-rules.conf
        etc/carbon/blacklist.conf
        etc/carbon/carbon.amqp.conf
        etc/carbon/carbon.conf
        etc/carbon/relay-rules.conf
        etc/carbon/rewrite-rules.conf
        etc/carbon/storage-aggregation.conf
        etc/carbon/storage-schemas.conf
        etc/carbon/whitelist.conf)

package() {
  cd "$srcdir/carbon-$pkgver"
  python2 setup.py install --root="$pkgdir/" --install-lib=/usr/lib/python2.7/site-packages --install-data=/var/lib/graphite --install-scripts=/usr/bin --optimize=1
  install -Dm644 "$srcdir"/carbon.service "$pkgdir"/usr/lib/systemd/system/carbon.service
  install -Dm644 "$srcdir"/carbon.sysusers "$pkgdir"/usr/lib/sysusers.d/carbon.conf
  install -Dm644 "$srcdir"/carbon.tmpfiles "$pkgdir"/usr/lib/tmpfiles.d/carbon.conf

  # change the directory of the config files
  ls conf | while read conf; do
    install -D -m644 conf/$conf $pkgdir/etc/carbon/${conf/.example}
  done;
  rm -r $pkgdir/var

  # use our config with FHS
  install -D -m644 $srcdir/carbon.conf $pkgdir/etc/carbon/carbon.conf
}

# vim:set ts=2 sw=2 et:
