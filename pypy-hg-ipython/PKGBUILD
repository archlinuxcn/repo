# Maintainer: Yichao Yu <yyc1992@gmail.com>
# Contributor: Kyle Keen <keenerd@gmail.com>
# Contributor: PepeSmith
# Contributor: Aron Asor <aronasorman at gmail.com>
# Contributor: Chris Brannon <chris@the-brannons.com>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>

_pypyname=pypy-hg
_pypyabi=26
_pypy3name=pypy3-hg
_pypy3abi=25

# IPython 2.1 requires python 3.3 but pypy3 provides 3.2.5
_BUILD_PYPY3=0
_PATCH_REVERT_XDG=1

pkgbase=${_pypyname}-ipython
pkgname=(${_pypyname}-ipython)
pkgver=2.3.0
pkgrel=3
pkgdesc="An enhanced Interactive Python shell. pypy version"
arch=('any')
url="http://ipython.org"
license=('BSD')
makedepends=(sqlite pypy)
if ((_BUILD_PYPY3)); then
    pkgname+=(${_pypy3name}-ipython)
    makedepends+=(pypy-${_pypy3abi})
fi
source=("https://pypi.python.org/packages/source/i/ipython/ipython-$pkgver.tar.gz"
        revert-xdg.patch
        pypy3-version.patch)
md5sums=('222eecd3a8299c0119c56954c79e4d0f'
         'f252c7449083c5b4a59dab260f5466c6'
         '0b72aadfce0dc7d01da6d3262b28612e')

prepare() {
  cd ipython-$pkgver
  if ((_PATCH_REVERT_XDG)); then
      patch -Np1 < ../revert-xdg.patch
  fi
  patch -Rp0 < ../pypy3-version.patch
  if ((_BUILD_PYPY3)); then
      rm -rf pypy3-ipython-$pkgver
      cp -r ipython-$pkgver pypy3-ipython-$pkgver
  fi
}

package_pypy3-hg-ipython() {
  pkgdesc="An enhanced Interactive Python shell. pypy3 version."
  depends=(pypy3-${_pypy3abi} sqlite)
  provides=(pypy3-${_pypy3abi}-ipython=${pkgver}
            pypy3-ipython=${pkgver})
  conflicts=(pypy3-ipython)
  optdepends=("pypy3-nose: if you want to run IPython's test suite"
              # "pypy3-pyqt4: for ipython qtconsole"
              # "pypy3-sip: for ipython qtconsole"
              # "wxpython: needed for ipythonx and ipython-wx"
              "pypy3-pygments: for ipython qtconsole"
              "pypy3-pyzmq: for ipython notebook and ipython qtconcole"
              "pypy3-tornado: for ipython notebook"
              "pypy3-jinja: for ipython notebook"
              "haskell-pandoc: ipython notebook conversion"
              "pypy3-readline: for better command line interaction support")
  cd pypy3-ipython-$pkgver

  # see https://github.com/ipython/ipython/issues/2057
  export LC_ALL=en_US.UTF-8
  pypy3 setup.py install --root="$pkgdir" --optimize=1
  find "$pkgdir" -name '*.py' -print0 | \
      xargs -0 sed -i -e 's,^#!/usr/bin/env python$,#!/usr/bin/env pypy3,' \
            -e 's,^#!/usr/bin/python$,#!/usr/bin/pypy3,'

  install -m755 -d "${pkgdir}/usr"
  mv "$pkgdir/opt/pypy/"{share,bin} "$pkgdir/usr/"

  install -Dm644 docs/source/about/license_and_copyright.rst \
          "$pkgdir/usr/share/licenses/pypy3-ipython/LICENSE"

  # hack to get around ipython collision
  cd "$pkgdir/usr/share/man/man1/"
  for i in *; do
    mv $i ${i/%.1/-pypy3.1}
  done
  ln -s ipython-pypy3.1 ipypy3.1

  find "$pkgdir/usr/bin/" -type f -regex '.*[^3]$' -delete
  cd "$pkgdir/usr/bin/"
  for f in *; do
    mv $f $f-pypy
  done
  ln -s ipython3-pypy ipypy3

  # cd "$srcdir/ipython-$pkgver/examples/IPython Kernel/"
  # install -Dm644 ipython.desktop \
  #   "$pkgdir/usr/share/applications/ipython.desktop"
  # install -Dm644 ipython-qtconsole.desktop \
  #   "$pkgdir/usr/share/applications/ipython-qtconsole.desktop"
}

package_pypy-hg-ipython() {
  depends=(pypy sqlite)
  provides=(pypy-ipython=${pkgver})
  conflicts=(pypy-ipython)
  optdepends=("pypy-nose: if you want to run IPython's test suite"
              # "pypy-pyqt4: for ipython qtconsole"
              # "pypy-sip: for ipython qtconsole"
              # "wxpython: needed for ipythonx and ipython-wx"
              "pypy-pygments: for ipython qtconsole"
              "pypy-pyzmq: for ipython notebook and ipython qtconcole"
              "pypy-tornado: for ipython notebook"
              "pypy-jinja: for ipython notebook"
              "haskell-pandoc: ipython notebook conversion"
              "pypy-readline: for better command line interaction support")
  cd ipython-$pkgver

  pypy setup.py install --root="$pkgdir" --optimize=1
  rm -rf "$pkgdir/usr/share/doc"
  find "$pkgdir" -name '*.py' -print0 | \
      xargs -0 sed -i -e 's,^#!/usr/bin/env python$,#!/usr/bin/env pypy,' \
            -e 's,^#!/usr/bin/python$,#!/usr/bin/pypy,'

  install -m755 -d "${pkgdir}/usr"
  mv "$pkgdir/opt/pypy/"{share,bin} "$pkgdir/usr/"

  install -Dm644 docs/source/about/license_and_copyright.rst \
          "$pkgdir/usr/share/licenses/pypy-ipython/LICENSE"

  # hack to get around ipython collision
  cd "$pkgdir/usr/share/man/man1/"
  for i in *; do
    mv $i ${i/%.1/-pypy.1}
  done
  ln -s ipython-pypy.1 ipypy2.1
  ln -s ipython-pypy.1 ipypy.1

  cd "$pkgdir/usr/bin/"
  for f in *; do
    mv $f $f-pypy
  done
  ln -s ipython-pypy ipypy
  ln -s ipython2-pypy ipypy2

  # cd "$srcdir/ipython-$pkgver/examples/IPython Kernel/"
  # sed -i 's/ython/ython2/g' *.desktop
  # install -Dm644 ipython.desktop \
  #   "$pkgdir/usr/share/applications/ipython2.desktop"
  # install -Dm644 ipython-qtconsole.desktop \
  #   "$pkgdir/usr/share/applications/ipython2-qtconsole.desktop"
}
