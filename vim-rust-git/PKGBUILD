# Maintainer: lilydjwg <lilydjwg@gmail.com>

pkgname=vim-rust-git
pkgver=20181026
pkgrel=1
pkgdesc="Rust language support for Vim"
arch=('any')
depends=('vim')
makedepends=('git')
groups=('vim-plugins')
url="https://github.com/rust-lang/rust.vim"
license=('MIT' 'Apache')
source=(${pkgname%-git}::git+https://github.com/rust-lang/rust.vim)
sha256sums=('SKIP')
provides=('vim-rust')
conflicts=('vim-rust')
optdepends=('rust: for RustRun and similar commands'
            'vim-syntastic: syntax checking')

pkgver() {
  cd ${pkgname%-git}
  git log -1 --format="%cd" --date=short | sed "s|-||g"
}

package() {
  cd ${pkgname%-git}

  msg 'Installing dirs...'
  install -dm 755 "$pkgdir/usr/share/vim/vimfiles"
  for _dir in after autoload compiler doc ftdetect ftplugin indent plugin syntax syntax_checkers; do
    cp -dpr --no-preserve=ownership $_dir "$pkgdir/usr/share/vim/vimfiles/$_dir"
  done
}
