# Maintainer: Jingbei Li <i@jingbei.li>
pkgdesc='An Image toolbox for Torch.'
pkgname='torch7-image-git'
pkgver=1.0.1.0.r287.g705393f
pkgrel=1
makedepends=('cmake' 'git')
depends=('torch7-git>=r819' 'libjpeg-turbo' 'libpng' 'torch7-sys-git')
conflicts=('torch7-image')
provides=('torch7-image')
arch=('x86_64' 'i686')
url='https://github.com/torch/image'
license=('BSD')
source=("${pkgname}::git+${url}")
sha512sums=('SKIP')

pkgver () {
	cd "${pkgname}"
	(
		set -o pipefail
		git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
		printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
	)
}

build () {
	cd "${pkgname}"
	cmake . -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release
	make
}

package () {
	cd "${pkgname}"
	make DESTDIR="${pkgdir}" install

	# Move Lua C module
	mkdir -p "${pkgdir}/usr/lib/lua/5.1"
	find "${pkgdir}/usr/lib" -name "*.so" -exec mv {} "${pkgdir}/usr/lib/lua/5.1/" \;

	# Move pure Lua modules
	mkdir -p "${pkgdir}/usr/share/lua/5.1"
	mv "${pkgdir}/usr/lua/image" "${pkgdir}/usr/share/lua/5.1/"
	rm -rf "${pkgdir}/usr/lua"
}
