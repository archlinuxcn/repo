# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=evince-nodbus
pkgver=3.16.0
pkgrel=1
pkgdesc="Document viewer (PDF, Postscript, djvu, tiff, dvi, XPS, SyncTex support with gedit, comics books (cbr,cbz,cb7 and cbt))"
url="https://wiki.gnome.org/Apps/Evince"
arch=(i686 x86_64)
license=(GPL)
depends=(gtk3 libgxps libspectre gsfonts poppler-glib djvulibre t1lib libsecret desktop-file-utils dconf gsettings-desktop-schemas gnome-desktop)
makedepends=(itstool libnautilus-extension texlive-bin intltool gobject-introspection docbook-xsl python)
optdepends=('texlive-bin: DVI support'
	    'gvfs: bookmark support and session saving'
            'p7zip: cbz and cb7 compressed comic books'
            'tar: cbt compressed comic books'
            'unrar: cbr compressed comic books')
groups=(gnome)
conflicts=('evince')
provides=("evince=$pkgver")

install=evince.install
options=('!emptydirs')
source=(http://ftp.gnome.org/pub/gnome/sources/evince/${pkgver:0:4}/evince-$pkgver.tar.xz)
sha256sums=('5138e2d05ccfe5e40df0c840b34f334c1f2bc03e5456aa954e64673d504ccd04')

build() {
  cd evince-$pkgver
  ./configure --prefix=/usr   --sysconfdir=/etc \
      --localstatedir=/var    --libexecdir=/usr/lib/evince \
      --disable-static        --enable-nautilus \
      --enable-pdf            --enable-tiff \
      --enable-djvu           --enable-dvi \
      --enable-t1lib          --enable-comics \
      --disable-scrollkeeper  --disable-schemas-compile \
      --enable-introspection --disable-browser-plugin  --disable-dbus
  sed -i '/"win.reload",/a          "win.show-properties",	"<Alt>Return", NULL,' shell/ev-application.c
  make
}

package() {
  cd evince-$pkgver
  make DESTDIR="$pkgdir" install
}
