# $Id: PKGBUILD 210428 2014-04-14 20:06:04Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: lilydjwg <lilydjwg@gmail.com>

pkgname=evince-nodbus
pkgver=3.12.1
pkgrel=1
pkgdesc="Document viewer (PDF, Postscript, djvu, tiff, dvi, XPS, SyncTex support with gedit, comics books (cbr,cbz,cb7 and cbt))"
url="https://wiki.gnome.org/Apps/Evince"
arch=(i686 x86_64)
license=(GPL)
depends=(gtk3 libgxps libspectre gsfonts poppler-glib djvulibre gnome-icon-theme t1lib libsecret desktop-file-utils dconf gsettings-desktop-schemas)
makedepends=(itstool libnautilus-extension texlive-bin intltool gobject-introspection docbook-xsl python)
optdepends=('texlive-bin: DVI support'
	    'gvfs: bookmark support and session saving'
            'p7zip: cbz and cb7 compressed comic books'
            'tar: cbt compressed comic books'
            'unrar: cbr compressed comic books')
groups=(gnome)
conflicts=('evince')
provides=("evince=$pkgver")
install=evince.install
options=('!emptydirs')
source=(http://ftp.gnome.org/pub/gnome/sources/evince/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('ef22cc29a7cbe70d2e7ce8c0b5b7ee774187ea69f3ae49a64c6d4a91559ef137')

build() {
  cd "evince-$pkgver"
  ./configure --prefix=/usr   --sysconfdir=/etc \
      --localstatedir=/var    --libexecdir=/usr/lib/evince \
      --disable-static        --enable-nautilus \
      --enable-pdf            --enable-tiff \
      --enable-djvu           --enable-dvi \
      --enable-t1lib          --enable-comics \
      --disable-scrollkeeper  --disable-schemas-compile \
      --enable-introspection  --disable-dbus
  make
}

package() {
  cd "evince-$pkgver"
  make DESTDIR="$pkgdir" install
}
