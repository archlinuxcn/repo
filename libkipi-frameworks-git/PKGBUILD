# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=libkipi-frameworks-git
pkgver=5.0.0.r1103.1a65688
pkgrel=1
pkgdesc="A interface to use kipi-plugins from a KDE image management program like digiKam. KF5 Frameworks branch (GIT version)"
url='http://www.digikam.org/sharedlibs'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kxmlgui' 'hicolor-icon-theme' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'kdoctools' 'python' 'git')
conflicts=('libkipi')
source=("git://anongit.kde.org/libkipi#branch=frameworks")
sha1sums=('SKIP')
install="libkipi-frameworks-git.install"

pkgver() {
  cd libkipi
  _ver="$(cat CMakeLists.txt | grep -e KIPI_LIB_MAJOR_VERSION -e KIPI_LIB_MINOR_VERSION -e KIPI_LIB_PATCH_VERSION | head -n3 | cut -d '"' -f2)"
  echo "$(echo ${_ver} | tr ' ' .).r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../libkipi \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  make -C build DESTDIR="${pkgdir}" install
}

