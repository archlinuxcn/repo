#Maintainer: Xyne <ac xunilhcra enyx, backwards>
pkgname=python3-aur
pkgver=2014.12
pkgrel=1
pkgdesc='AUR-related modules and helper utilities (aurploader, aurquery, aurtomatic).'
arch=(any)
license=(GPL)
url="http://xyne.archlinux.ca/projects/python3-aur"
depends=(python3 python-xdg)
optdepends=('curl: Aurploader support')
replaces=(aurploader)
conflicts=(aurploader)
provides=(aurploader)
source=(
  http://xyne.archlinux.ca/projects/python3-aur/src/python3-aur-2014.12.tar.xz
  http://xyne.archlinux.ca/projects/python3-aur/src/python3-aur-2014.12.tar.xz.sig
)
sha512sums=(
  e345cde10d0951b12ba41acbb80259716495156adef8d1ad1649741563ce427b41452ea4fb8dc40426da58e4ad6d1e90d1690259610ade2d6ee3258a3ea25a6a
  21dc1b0b1e68082fc53504a07bd4b3336e1f0741c0b737f7e89b1cc00420859bf61618dcbdb06f92df6ddea9caf5dbb90957bfbf55d36b29e28ac1bc0a96282b
)
md5sums=(
  72452b0fadcce493f55be5a0614f95bd
  cc10e00a306321911feb03eb74d81bf3
)

package ()
{
  cd "$srcdir/$pkgname-$pkgver"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  for aurfoo in aur*
  do
    if [[ -x "$aurfoo" ]]
    then
      install -Dm755 "$aurfoo" "$pkgdir/usr/bin/$aurfoo"
    fi
  done
}


# vim: set ts=2 sw=2 et:
