# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: frownlee <florez.brownlee@gmail.com>

pkgname=android-ndk
pkgver=r10e
pkgrel=1
pkgdesc='Android C/C++ developer kit'
arch=('x86_64' 'i686')
makedepends=('p7zip')
url='https://developer.android.com/ndk/'
license=('GPL' 'LGPL' 'custom')
options=('!strip' 'staticlibs')
backup=('etc/profile.d/android-ndk.sh')
install="$pkgname.install"
provides=('android-ndk')
replaces=('android-ndk32' 'android-ndk64')
source=('android-ndk.sh')
source_x86_64=("${pkgname}_x86_64.bin::http://dl.google.com/android/ndk/${pkgname}-$pkgver-linux-x86_64.bin")
source_i686=("${pkgname}_i686.bin::http://dl.google.com/android/ndk/${pkgname}-$pkgver-linux-x86.bin")
md5sums=('372f74400bfa061642c4b4175f2ddb48')
md5sums_x86_64=('19af543b068bdb7f27787c2bc69aba7f')
md5sums_i686=('c3edd3273029da1cbd2f62c48249e978')

prepare() {
7z x -y ${pkgname}_$CARCH.bin
}

package() {
  install -d "$pkgdir/opt"
  mv "$pkgname-$pkgver" "$pkgdir/opt/$pkgname"

  install -Dm755 android-ndk.sh "$pkgdir/etc/profile.d/android-ndk.sh"

  # Fix broken permissions
  chmod -R o=g "$pkgdir/opt/$pkgname"
  find "$pkgdir/opt/$pkgname" -perm 744 -exec chmod 755 {} +
}

# vim:set ts=2 sw=2 et:
