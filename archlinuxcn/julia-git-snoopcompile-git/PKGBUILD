pkgbase=julia-git-snoopcompile-git
_subpkgs=(SnoopCompileBot
          SnoopCompileCore
          SnoopPrecompile)
pkgname=(julia-git-snoopcompile-git)
pkgver=2.9.5.0.2.gd11c578
pkgrel=1
pkgdesc="SnoopCompile.jl"
url="https://github.com/timholy/SnoopCompile.jl"
arch=('any')
license=('MIT')
makedepends=(git julia-pkg-scripts)
depends=(julia-git)
source=(git+https://github.com/timholy/SnoopCompile.jl)
md5sums=('SKIP')

pkgver() {
  cd SnoopCompile.jl

  git describe --tags | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

package_julia-git-snoopcompile-git() {
  provides=(julia-git-snoopcompile)
  cd SnoopCompile.jl

  . /usr/lib/julia/julia-install-pkg.sh SnoopCompile "${pkgdir}" "${pkgname}" julia-git
}

_subpackage() {
  local _name=$1
  provides=(julia-git-${_name,,})
  cd SnoopCompile.jl/${_name}

  . /usr/lib/julia/julia-install-pkg.sh ${_name} "${pkgdir}" "${pkgname}" julia-git
}
for _name in "${_subpkgs[@]}"; do
  pkgname=("${pkgname[@]}" julia-git-${_name,,}-git)
  eval "package_julia-git-${_name,,}-git() { _subpackage ${_name}; }"
done
