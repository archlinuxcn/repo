# Maintainer: Hu Butui <hot123tea123@gmail.com>

_pkgname=IRkernel
_pkgver=1.2
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//[:-]/.}
pkgrel=1
pkgdesc="Native R Kernel for the 'Jupyter Notebook'"
arch=('any')
url="https://cran.r-project.org/package=${_pkgname}"
license=('MIT')
depends=(
  jupyter-notebook
  r
  r-crayon
  r-digest
  r-evaluate
  r-irdisplay
  r-jsonlite
  r-pbdzmq
  r-repr
  r-uuid
)
optdepends=(
  r-roxygen2
  r-testthat
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
sha256sums=('5fb4dbdb741d05043120a8be0eb73f054b607d9854f314bd79cfec08d219ff91')

build() {
  R CMD INSTALL ${_pkgname}_${_pkgver}.tar.gz -l "${srcdir}"
}

package() {
  install -dm0755 "${pkgdir}/usr/lib/R/library"
  cp -a --no-preserve=ownership "${_pkgname}" "${pkgdir}/usr/lib/R/library"
  install -Dm644 "${_pkgname}/LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
