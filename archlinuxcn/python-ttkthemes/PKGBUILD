# Maintainer: Juliette Monsel <j_4321 at protonmail dot com>

_name=ttkthemes
pkgname="python-${_name}"
pkgver=3.2.2
pkgrel=2
pkgdesc="A group of themes for the ttk extenstions for Tkinter"
arch=('any')
url="https://github.com/TkinterEP/${_name}"
license=('GPL-3.0-only')
depends=(
    'python'
    'python-pillow'
    'tk'
)
makedepends=(
    'python-build'
    'python-installer'
    'python-setuptools'
    'python-wheel'
)
source=("${pkgname}-${pkgver}.tar.gz::https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('620baa326d1e37913c0f052c3e748eaf4d08f3ca99d861730a69258789f9a76707a20c5229d9c13f6fe6f7208035ed54f6dc14c6eb5f548a38a171229c24be56')

build() {
    cd "${srcdir}/${_name}-${pkgver}"
    python -m build --wheel --no-isolation
}

package() {
    cd "${srcdir}/${_name}-${pkgver}"
    python -m installer --destdir="${pkgdir}" dist/*.whl
}
