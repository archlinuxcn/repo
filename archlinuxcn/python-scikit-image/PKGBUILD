# Maintainer: Astro Benzene <universebenzene at sina dot com>
# Maintainer: Jaroslav Lichtblau <dragonlord at aur dot archlinux dot org>
# Contributor: Andrzej Giniewicz <gginiu at gmail dot com>

_name=scikit-image
pkgname=python-scikit-image
pkgver=0.17.2
pkgrel=6
pkgdesc="Image processing routines for SciPy"
arch=('x86_64')
url="http://scikit-image.org/"
license=('BSD')
makedepends=(
  cython
  python-setuptools
)
depends=(
  python-imageio
  python-matplotlib
  python-networkx
  python-numpy
  python-pillow
  python-pywavelets
  python-scipy
  python-tifffile
)
checkdepends=(
  python-pytest
  python-pytest-cov
  python-pytest-flake8
  python-pytest-localserver
)
optdepends=('python-pyqt5: for imshow(x, fancy=True) and skivi'
            'python-imagecodecs: for reading image formats with various compression algorithm'
            'python-qtpy'
            'freeimage: for reading various types of image file formats'
            'python-pyamg: fast cg_mg mode of random walker segmentation'
            'python-astropy: Provides FITS I/O capability'
            'python-imread: Optional I/O plugin providing most standard formats'
            'python-simpleitk: Optional I/O plugin providing a wide variety of formats. including specialized formats using in medical imaging')
options=('!emptydirs')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha256sums=('bd954c0588f0f7e81d9763dc95e06950e68247d540476e06cb77bcbcd8c2d8b3')

build() {
  cd "${srcdir}/${_name}-${pkgver}"
  python setup.py build_ext --inplace
  python setup.py build
}

check() {
  cd "${_name}-${pkgver}"
  pytest -v
}

package() {
  cd ${srcdir}/${_name}-${pkgver}
  install -D -m644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  python setup.py install --root=${pkgdir} --prefix=/usr --optimize=1 --skip-build
}
# vim:set ts=2 sw=2 et:

