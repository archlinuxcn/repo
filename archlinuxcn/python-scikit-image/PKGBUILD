# Maintainer: Astro Benzene <universebenzene at sina dot com>
# Maintainer: Jaroslav Lichtblau <dragonlord at aur dot archlinux dot org>
# Contributor: Andrzej Giniewicz <gginiu at gmail dot com>

_name=scikit-image
pkgname=python-scikit-image
pkgver=0.25.2
pkgrel=1
pkgdesc="Image processing routines for SciPy"
arch=('x86_64')
url="http://scikit-image.org/"
license=('BSD-3-Clause')
makedepends=(
  cython
  meson-python
  python-build
  python-installer
  python-pythran
  python-setuptools
  python-wheel
)
depends=(
  gcc-libs
  glibc
  python-imageio
  python-lazy-loader
  python-matplotlib
  python-networkx
  python-numpy
  python-pillow
  python-pooch
  python-pywavelets
  python-scipy
  python-tifffile
)
optdepends=('python-pyqt5: for imshow(x, fancy=True) and skivi'
            'python-imagecodecs: for reading image formats with various compression algorithm'
            'python-qtpy'
            'freeimage: for reading various types of image file formats'
            'python-pyamg: fast cg_mg mode of random walker segmentation'
            'python-astropy: Provides FITS I/O capability'
            'python-imread: Optional I/O plugin providing most standard formats'
            'python-simpleitk: Optional I/O plugin providing a wide variety of formats. including specialized formats using in medical imaging')
options=('!emptydirs')
source=("${_name}-${pkgver}.tar.gz::https://github.com/scikit-image/scikit-image/archive/v${pkgver}.tar.gz")
sha256sums=('ca7cf6861179dc60f56fdd4e7f1df88ce44984472529f66d3ff472b7e75e67a1')

build() {
  cd "${srcdir}/${_name}-${pkgver}"
  python -m build --wheel --no-isolation --skip-dependency-check
}

package() {
  cd ${srcdir}/${_name}-${pkgver}
  install -D -m644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  python -m installer --destdir="$pkgdir" dist/*.whl
}
# vim:set ts=2 sw=2 et:

