# Maintainer: Butui Hu <hot123tea123@gmail.com>

pkgname=vxl
pkgver=3.3.0
pkgrel=1
pkgdesc='A multi-platform collection of C++ software libraries for Computer Vision and Image Understanding'
arch=('x86_64')
url='https://github.com/vxl/vxl'
license=('BSD')
depends=(
  ffmpeg
  freeglut
  glu
  libdc1394
  libgeotiff
  libpng
  libtiff
  libxft
  libxinerama
  libxkbfile
  libxmu
  libxpm
  libxres
  libxss
  libxt
  openjpeg2
)
makedepends=('cmake')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/vxl/vxl/archive/v${pkgver}.tar.gz")
sha512sums=('b68c36f870890f2a61d786d4728677fbe7e2956b2b882a6cde7f8139477b917f22aa089a6cdec2c4ed8237e209ea1b75e72de2c1e17067008688545599cde723')

build() {
 cmake \
   -B "build" \
   -S "${pkgname}-${pkgver}" \
   -DVXL_BUILD_EXAMPLES:BOOL=OFF \
   -DBUILD_SHARED_LIBS=BOOL:ON \
   -DBUILD_TESTING:BOOL=OFF \
   -DCMAKE_INSTALL_PREFIX=/usr \
   -DCMAKE_SKIP_INSTALL_RPATH:BOOL=ON \
   -DVXL_BUILD_CORE_VIDEO:BOOL=ON \
   -DVXL_BUILD_DCMTK:BOOL=ON \
   -DVXL_BUILD_VGUI:BOOL=ON \
   -DVXL_USE_LFS:BOOL=ON \
   -Wno-dev
 make -C "build"
}

package() {
  make -C "build" DESTDIR="${pkgdir}" install
  install -Dm644 "${srcdir}/${pkgname}-${pkgver}/core/vxl_copyright.h" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  rm -rfv "${pkgdir}/usr/v3p"
}
# vim:set ts=2 sw=2 et:

