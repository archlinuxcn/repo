# Maintainer: Butui Hu <hot123tea123@gmail.com>

pkgname=vxl
pkgver=3.5.0
pkgrel=1
pkgdesc='A multi-platform collection of C++ software libraries for Computer Vision and Image Understanding'
arch=('x86_64')
url='https://github.com/vxl/vxl'
license=('BSD')
depends=(
#  ffmpeg
  freeglut
  glu
  libdc1394
  libgeotiff
  libpng
  libtiff.so
  libxft
  libxinerama
  libxkbfile
  libxmu
  libxpm
  libxres
  libxss
  libxt
)
makedepends=('cmake')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/vxl/vxl/archive/v${pkgver}.tar.gz")
sha512sums=('0b33e12557315058e7786c2049af3b01f1208e50660ccbc45f4d9a4dba4eeadfa5e3125380d8781eed2a9abf1d153ffb71c416ed2d196ab4194f5b3722fe6f2b')

build() {
  cmake \
    -B "build" \
    -S "${pkgname}-${pkgver}" \
    -DVXL_BUILD_CONTRIB:BOOL=ON \
    -DVXL_BUILD_EXAMPLES:BOOL=OFF \
    -DBUILD_SHARED_LIBS=BOOL:ON \
    -DBUILD_TESTING:BOOL=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_INSTALL_RPATH:BOOL=ON
 make -C "build"
}

package() {
  make -C "build" DESTDIR="${pkgdir}" install
  install -Dm644 "${srcdir}/${pkgname}-${pkgver}/core/vxl_copyright.h" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  rm -rfv "${pkgdir}/usr/v3p"
}
# vim:set ts=2 sw=2 et:

