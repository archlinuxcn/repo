# Maintainer: Bert Peters <bert@bertptrs.nl>

_gemname=kramdown-parser-gfm
pkgname=ruby-$_gemname
pkgver=1.1.0
pkgrel=2
pkgdesc='A parser for kramdown that converts Markdown documents in the GFM dialect to HTML.'
arch=('any')
url='https://github.com/kramdown/parser-gfm'
license=(MIT)
depends=(ruby-kramdown)
makedepends=(ruby-rdoc)
source=(https://rubygems.org/downloads/$_gemname-$pkgver.gem)
noextract=($_gemname-$pkgver.gem)
sha256sums=('fb39745516427d2988543bf01fc4cf0ab1149476382393e0e9c48592f6581729')

package() {
  local _gemdir="$(ruby -e'puts Gem.default_dir')"
  gem install --ignore-dependencies --no-user-install -i "$pkgdir/$_gemdir" -n "$pkgdir/usr/bin" $_gemname-$pkgver.gem
  rm "$pkgdir/$_gemdir/cache/$_gemname-$pkgver.gem"
  install -D -m644 "$pkgdir/$_gemdir/gems/$_gemname-$pkgver/COPYING" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
