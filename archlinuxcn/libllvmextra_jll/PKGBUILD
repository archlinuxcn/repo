pkgname=libllvmextra_jll
pkgver=6.5.0
_commit=90b07c7e08fbdeb79d9d0f2010682b3e309fddfb
pkgrel=1
pkgdesc="LLVM.jl"
url="https://github.com/maleadt/LLVM.jl.git"
arch=(aarch64 armv7h x86_64)
license=('MIT')
makedepends=(git cmake llvm-julia-git)
source=("git+https://github.com/maleadt/LLVM.jl.git#commit=$_commit")
sha256sums=('SKIP')
options=('debug')

_versioned_llvm=

build() {
  cd LLVM.jl/deps/LLVMExtra

  mkdir -p build
  cd build
  if [[ -n $_versioned_llvm ]]; then
    export CMAKE_PREFIX_PATH=/usr/lib/llvm$_versioned_llvm/
  fi
  cmake .. -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  if [[ -n $_versioned_llvm ]]; then
    _llvm_ver=$(llvm-config-${_versioned_llvm} --version)
  else
    _llvm_ver=$(llvm-config --version)
  fi
  [[ $_llvm_ver =~ ^([0-9]*).* ]]
  llvm_maj_ver=${BASH_REMATCH[1]}
  ((next_llvm_maj=llvm_maj_ver + 1))

  _deps=("llvm-libs-julia-git>=${llvm_maj_ver}.0"
         "llvm-libs-julia-git<${next_llvm_maj}.0")

  depends+=("${_deps[@]}")

  cd LLVM.jl/deps/LLVMExtra/build

  make install DESTDIR="${pkgdir}"

  mkdir "${pkgdir}/usr/lib/julia"
  mv "${pkgdir}/usr/lib/"libLLVMExtra-* "${pkgdir}/usr/lib/julia"
}
