# Maintainer: Maxim Kurnosenko <asusx2@mail.ru>
# Contibutor: Salvador Pardi√±as <darkfm@vera.com.uy>

pkgname=woeusb-git
pkgver=3.3.1.r1.g7171bff
pkgrel=1
pkgdesc="A Linux program to create Windows USB stick installer from a real Windows DVD or an image"
arch=('x86_64')
url="https://github.com/slacka/WoeUSB"
license=('GPL3')
depends=('wxgtk3' 'grub' 'dosfstools' 'parted' 'wget' 'ntfs-3g')
optdepends=('gksu'
            'p7zip: For extracting EFI bootloader from Windows 7 installation media for EFI booting support')
makedepends=('git')
provides=("woeusb")
conflicts=("woeusb")
source=('git+https://github.com/slacka/WoeUSB.git')
md5sums=('SKIP')

pkgver() {
    cd "$srcdir/WoeUSB"
    git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
	cd "$srcdir/WoeUSB"
    ./setup-development-environment.bash
	autoreconf --force --install
}

build() {
	cd "$srcdir/WoeUSB"
	autoconf
	./configure --with-wx-config=wx-config-gtk3
	make
}

package() {
	cd "$srcdir/WoeUSB"
	make DESTDIR="$pkgdir/" prefix="/usr" install
}
