# Maintainer: Maxim Kurnosenko <asusx2@mail.ru>
# Contibutor: Salvador Pardi√±as <darkfm@vera.com.uy>

pkgname=woeusb-git
pkgver=3.3.0.r0.g5e9a3a7
pkgrel=2
pkgdesc="A Linux program to create Windows USB stick installer from a real Windows DVD or an image"
arch=('x86_64')
url="https://github.com/WoeUSB/WoeUSB-frontend-wxgtk"
license=('GPL3')
depends=('wxgtk3' 'grub' 'dosfstools' 'parted' 'wget' 'ntfs-3g')
optdepends=('gksu'
            'p7zip: For extracting EFI bootloader from Windows 7 installation media for EFI booting support')
makedepends=('git')
provides=("woeusb")
conflicts=("woeusb")
source=('git+https://github.com/WoeUSB/WoeUSB-frontend-wxgtk.git')
md5sums=('SKIP')

pkgver() {
    cd "$srcdir/WoeUSB-frontend-wxgtk"
    git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
	cd "$srcdir/WoeUSB-frontend-wxgtk"
    ./setup-development-environment.bash
	autoreconf --force --install
}

build() {
	cd "$srcdir/WoeUSB-frontend-wxgtk"
	autoconf
	./configure --with-wx-config=wx-config-gtk3
	make
}

package() {
	cd "$srcdir/WoeUSB-frontend-wxgtk"
	make DESTDIR="$pkgdir/" prefix="/usr" install
}
