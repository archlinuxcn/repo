# Maintainer: Roald Clark <roaldclark at gmail dot com>

_pkgname=clash-meta
pkgname="${_pkgname}-alpha-git"
pkgver=20250710.045625
pkgrel=1
pkgdesc="Another Clash Kernel by MetaCubeX (git version)"
arch=('x86_64')
url="https://github.com/MetaCubeX/mihomo"
license=('GPL-3.0-or-later')
depends=(
    'clash-geoip'
    'glibc'
)
makedepends=(
    'git'
    'go'
)
provides=(
    "${_pkgname}=${pkgver}"
    "${_pkgname}-alpha=${pkgver}"
)
conflicts=(
    "${_pkgname}"
    "${_pkgname}-alpha"
)
backup=('etc/clash-meta/config.yaml')
install="${_pkgname}.install"
source=("${_pkgname}::git+${url}.git#branch=Alpha"
        "${_pkgname}.service"
        "${_pkgname}.sysusers"
        "${_pkgname}.tmpfiles")
sha256sums=('SKIP'
            '7fcbbcf5c00cfadcebcff74cce954ec5d764c83ae123b913aab537f4073627ba'
            'f9708634e34dd6ee878c85a5a93c5107befce354112253bab87d520b6be7b5f6'
            '50737592c7bd743fe8f543924034718337477a203fa11ef4272cae496df3769c')

pkgver() {
    cd "${srcdir}/${_pkgname}"
    printf "%s" "$(TZ=UTC git log -1 --pretty='%cd' --date=format-local:%Y%m%d.%H%M%S)"
}

build() {
    cd "${srcdir}/${_pkgname}"

    export CGO_CPPFLAGS="${CPPFLAGS}"
    export CGO_CFLAGS="${CFLAGS}"
    export CGO_CXXFLAGS="${CXXFLAGS}"
    export CGO_LDFLAGS="${LDFLAGS}"
    export CGO_ENABLED=1

    NAME="${_pkgname}"
    BUILDTIME="$(date -u --rfc-3339=seconds)"
    LDFLAGS="-linkmode external -extldflags \"${LDFLAGS}\""
    OTHER_FLAGS="-X \"github.com/metacubex/mihomo/constant.Version=${pkgver}\" \
                 -X \"github.com/metacubex/mihomo/constant.BuildTime=${BUILDTIME}\""
    GOOS=linux go build \
        -trimpath \
        -buildmode=pie \
        -mod=readonly \
        -modcacherw \
        -ldflags "${LDFLAGS} ${OTHER_FLAGS}" \
        -tags with_gvisor \
        -o "${_pkgname}-${pkgver}"
}

check() {
    cd "${srcdir}/${_pkgname}"
    go test ./...
}

package() {
    cd "${srcdir}/${_pkgname}"
    install -Dm0755 "${_pkgname}-${pkgver}" "${pkgdir}/usr/bin/clash-meta"
    install -Dm0644 "../${_pkgname}.sysusers" "${pkgdir}/usr/lib/sysusers.d/${_pkgname}.conf"
    install -Dm0644 "../${_pkgname}.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/${_pkgname}.conf"
    install -Dm0644 "../${_pkgname}.service" -t "${pkgdir}/usr/lib/systemd/system/"
    install -Dm0644 docs/config.yaml -t "${pkgdir}/etc/clash-meta/"
    ln -s /etc/clash/Country.mmdb "${pkgdir}/etc/${_pkgname}/Country.mmdb"
}
