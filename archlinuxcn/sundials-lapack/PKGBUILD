# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Elmar Klausmeier <Elmar.Klausmeier@gmail.com>

pkgname=sundials-lapack
pkgver=6.6.1
pkgrel=2
pkgdesc='Suite of nonlinear differential/algebraic equation solvers'
arch=(armv7h aarch64 x86_64)
url='https://computing.llnl.gov/projects/sundials'
license=(BSD)
depends=(openmpi suitesparse lapack)
makedepends=(cmake gcc-fortran python)
provides=("sundials=$pkgver")
source=(https://github.com/LLNL/sundials/archive/v$pkgver/sundials-$pkgver.tar.gz)
sha256sums=('4830d6f6943e95a6edf33a90a7181ce5bece28efe6d21e1e39ea1135708a948f')

build() {
  cmake -B build -S sundials-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_STATIC_LIBS=OFF \
    -DENABLE_MPI=ON \
    -DENABLE_PTHREAD=ON	\
    -DENABLE_OPENMP=ON \
    -DENABLE_KLU=ON \
    -DKLU_LIBRARY_DIR=/usr/lib \
    -DENABLE_LAPACK=ON \
    -DEXAMPLES_INSTALL_PATH=/usr/share/sundials/examples
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -Dm644 sundials-$pkgver/LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
