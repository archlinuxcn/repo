# Maintainer: Butui Hu <hot123tea123@gmail.com>

_pkgname=pypdf2
pkgname=python-pypdf2
pkgver=2.12.1
pkgrel=1
pkgdesc='A utility to read and write PDFs with Python'
arch=('any')
url='https://mstamy2.github.io/PyPDF2'
license=('BSD')
depends=(
  python
)
optdepends=(
  python-pillow
  python-pycryptodome
)
makedepends=(
  python-build
  python-flit-core
  python-installer
  python-wheel
)
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/mstamy2/PyPDF2/archive/${pkgver}.tar.gz")
sha512sums=('2b832d10cb9217de503a6d4ee7b137aa68f98d6c21f0a8db09d722e02c853cc1a5b6d4df78a99e7a0b9681b572b04a133458b05026978b4979cde796433437be')

build() {
  cd "${_pkgname}-${pkgver}"
  python -m build --wheel --no-isolation
}

package() {
  cd "${_pkgname}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
