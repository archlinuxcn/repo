# Maintainer: Hu Butui <hot123tea123@gmail.com>
# Contributor: Marius Lindvall <(firstname) {cat} varden {dog} info>

_pkgname=pywebview
pkgname=python-pywebview
pkgver=4.0.1
pkgrel=1
pkgdesc='Build GUI for your Python program with JavaScript, HTML, and CSS'
arch=('any')
url='https://pywebview.flowrl.com/'
license=('BSD')
depends=(
  python-proxy_tools
)
makedepends=(
  python-setuptools
)
optdepends=(
  'python-gobject: use with GTK'
  'python-cairo: use with GTK'
  'webkit2gtk>=2.22: use with GTK'
  'python-pyqtwebengine: use with Qt'
  'qt5-webkit: use with Qt')
install="${pkgname}.install"
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/r0x0r/pywebview/archive/refs/tags/${pkgver}.tar.gz")
sha256sums=('3d617d521404dc6a57351ef274bec5288638a7020d403ace3db476b8e606f8db')

build() {
  cd "${_pkgname}-${pkgver}"
  python setup.py build
}

package() {
  cd "${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}/" --optimize=1 --skip-build
  install -Dm 644 "LICENSE.md" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
# vim:set ts=2 sw=2 et:
