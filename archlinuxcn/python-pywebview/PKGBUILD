# Maintainer: Hu Butui <hot123tea123@gmail.com>
# Contributor: Marius Lindvall <(firstname) {cat} varden {dog} info>

_pkgname=pywebview
pkgname=python-pywebview
pkgver=4.3.1
pkgrel=1
pkgdesc='Build GUI for your Python program with JavaScript, HTML, and CSS'
arch=('any')
url='https://pywebview.flowrl.com/'
license=('BSD')
depends=(
  python-proxy_tools
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-setuptools-scm
  python-wheel
)
optdepends=(
  'python-gobject: use with GTK'
  'python-cairo: use with GTK'
  'webkit2gtk>=2.22: use with GTK'
  'python-pyqtwebengine: use with Qt'
  'qt5-webkit: use with Qt')
install="${pkgname}.install"
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/r0x0r/pywebview/archive/refs/tags/${pkgver}.tar.gz")
sha256sums=('6ef7afc1e12ddc4299feabd57e0c4ecdb364a79ca8dc57a271424b706a73d387')

build() {
  cd "${_pkgname}-${pkgver}"
  SETUPTOOLS_SCM_PRETEND_VERSION=${pkgver} \
  python -m build --wheel --no-isolation
}

package() {
  cd "${_pkgname}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm 644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
# vim:set ts=2 sw=2 et:
