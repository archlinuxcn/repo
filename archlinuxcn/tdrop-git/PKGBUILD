# Maintainer: Fox Kiester <noct[at]openmailbox[dot]org>

_pkgname=tdrop
pkgname=tdrop-git
pkgver=0.4.0.r28.g1e2f395
pkgrel=1
pkgdesc="A WM-Independent dropdown window and terminal creator"
arch=('any')
url="https://github.com/noctuid/tdrop"
license=('Simplified BSD')
depends=('bash' 'xdotool' 'xorg-xwininfo' 'xorg-xprop')
optdepends=('tmux: session starting support'
            'tmuxinator: session starting support'
            'sxhkd: for a tdrop keybinding'
            'xorg-xrandr: multiple monitor resizing support')
makedepends=('git')
provides=("${_pkgname}")
md5sums=('SKIP')
source=("git+https://github.com/noctuid/${_pkgname}.git")

pkgver() {
  cd "$srcdir/$_pkgname"
  git describe --long | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

package() {
  cd "$srcdir/$_pkgname"
  make PREFIX=/usr DESTDIR="$pkgdir" install
}
