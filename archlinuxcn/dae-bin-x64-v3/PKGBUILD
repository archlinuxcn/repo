# Maintainer: cubercsl <2014cais01 at gmail dot com>
# Maintainer: Integral <integral at member dot fsf dot org>

pkgname=dae-bin-x64-v3
_pkgname=dae
pkgver=0.6.0rc2
pkgrel=3
pkgdesc="A Linux lightweight and high-performance transparent proxy solution based on eBPF."
arch=('x86_64')
url="https://github.com/daeuniverse/${_pkgname}"
license=('AGPL-3.0-or-later')
depends=(
	'glibc'
	'v2ray-geoip'
	'v2ray-domain-list-community'
)
backup=("etc/${_pkgname}/config.${_pkgname}")
source=("${pkgname}-${pkgver}.zip::${url}/releases/download/v${pkgver}/${_pkgname}-linux-x86_64_v3_avx2.zip")
install="${pkgname}.install"
sha256sums=('3735f7459d4bac32ee91cae7983458ed6a19228d039cb0d62b750fe2ce2653e3')

package() {
	# Install binary
	install -vDm755 "${_pkgname}-linux-x86_64_v3_avx2" "${pkgdir}/usr/bin/${_pkgname}"

	# Install systemd service
	install -vDm644 "${_pkgname}.service" -t "${pkgdir}/usr/lib/systemd/system/"

	# Install geoip.dat & geosite.dat
	install -vDm644 {geoip,geosite}.dat -t "${pkgdir}/usr/share/${_pkgname}/"

	# Install exmaple config
	install -vDm644 "example.${_pkgname}" -t "${pkgdir}/etc/${_pkgname}/"
}
