# Maintainer: cubercsl <2014cais01 at gmail dot com>
# Maintainer: Integral <integral at member dot fsf dot org>

pkgname=dae-bin-x64-v3
_pkgname=dae
pkgver=1.0.0rc2
pkgrel=1
pkgdesc="A Linux lightweight and high-performance transparent proxy solution based on eBPF."
arch=('x86_64')
url="https://github.com/daeuniverse/${_pkgname}"
license=('AGPL-3.0-or-later')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
backup=("etc/${_pkgname}/config.${_pkgname}")
source=(
	"${pkgname}-${pkgver}.zip::${url}/releases/download/v${pkgver}/${_pkgname}-linux-x86_64_v3_avx2.zip"
	"https://raw.githubusercontent.com/daeuniverse/${_pkgname}/refs/tags/v${pkgver}/install/empty.${_pkgname}"
)
install="${pkgname}.install"
sha256sums=('d1598d69bde4d0364a19f9c1c220804a2773aeefff550c4df37ba3696a2a7a71'
            '8c3b3e962bc1288394e54122e5da8d4d54994d17af2fd290331c464dc6b75c86')

package() {
	# Install binary
	install -vDm755 "${_pkgname}-linux-x86_64_v3_avx2" "${pkgdir}/usr/bin/${_pkgname}"

	# Install systemd service
	install -vDm644 "${_pkgname}.service" -t "${pkgdir}/usr/lib/systemd/system/"

	# Install geoip.dat & geosite.dat
	install -vDm644 geo{ip,site}.dat -t "${pkgdir}/usr/share/${_pkgname}/"

	# Install exmaple config
	install -vDm644 "example.${_pkgname}" -t "${pkgdir}/etc/${_pkgname}/"

	# Install empty config
	install -vDm640 "empty.${_pkgname}" "${pkgdir}/etc/${_pkgname}/config.${_pkgname}"
}
