# Maintainer: xiliuya <xiliuya@aliyun.com>
# Maintainer: Integral <integral@member.fsf.org>

pkgname=tdlib-td-git
_pkgname=${pkgname%-git}
pkgver=1.8.0.r8281.g5c77c46
pkgrel=1
pkgdesc="Cross-platform library for building Telegram clients"
arch=('x86_64' 'aarch64' 'riscv64')
url="https://core.telegram.org/tdlib"
license=('BSL-1.0')
conflicts=("${_pkgname}")
provides=("${_pkgname}")
depends=('gcc-libs' 'zlib' 'openssl')
makedepends=('git' 'gperf' 'gcc14' 'cmake')
source=("${_pkgname}::git+https://github.com/tdlib/td.git")
sha256sums=('SKIP')

pkgver() {
	cd "${_pkgname}/"
	git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/v//'
}

build() {
	cmake -S "${_pkgname}" \
		-B build \
		-DCMAKE_BUILD_TYPE=None \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_C_COMPILER=gcc-14 \
		-DCMAKE_CXX_COMPILER=g++-14

	cmake --build build
}

package() {
	DESTDIR="${pkgdir}" cmake --install build
	install -Dm644 "${_pkgname}/LICENSE_1_0.txt" -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
