# Maintainer: librewish <librewish at gmail dot com>
# Co-Maintainer: Mark Wagie <mark dot wagie at tutanota dot com>
# Contributor: Chris Severance aur.severach aATt spamgourmet dott com
# Contributor: Jay Garcia <morbidj at gmail dot com>
# Contributor: Doug Newgard <scimmia22 at outlook dot com>
# Contributor: Robert Orzanna <orschiro at gmail dot com>
pkgname=timeshift
pkgver=20.11.1+4+gd437358
pkgrel=2
pkgdesc="A system restore utility for Linux"
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
url="https://github.com/teejee2008/timeshift"
license=('GPL3')
depends=('gtk3' 'libsoup' 'cronie' 'rsync' 'libgee' 'vte3' 'xapp' 'xorg-xhost')
makedepends=('git' 'vala')
checkdepends=('appstream-glib' 'desktop-file-utils')
optdepends=('btrfs-progs: BTRFS support')
options=('!emptydirs')
install="$pkgname.install"
_commit=d437358ac3debf7625aefda4d0bd387a91b69df5
source=("git+https://github.com/teejee2008/timeshift.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/$pkgname"
	git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
	cd "$srcdir/$pkgname"
	sed -i -e 's/--Xcc="-O3" //g' src/makefile
}

build() {
	export CFLAGS="${CFLAGS} --std=c99"

	cd "$srcdir/$pkgname/src"
	make app-gtk
	make app-console
	make pot
}

check() {
	cd "$srcdir/$pkgname"
	appstream-util validate-relax --nonet debian/*.appdata.xml
	desktop-file-validate "src/$pkgname-gtk.desktop"
}

package() {
	cd "$srcdir/$pkgname/src"
	make DESTDIR="$pkgdir" install
}
