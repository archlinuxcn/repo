# Maintainer:  Giovanni 'ItachiSan' Santini <giovannisantini93@yahoo.it>
# Contributor: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Contributor: Pieter Goetschalckx <3.14.e.ter <at> gmail <dot> com>

pkgname=franz
_pkgver=5.6.1 # -beta.0
pkgver=${_pkgver//-/} # Leaving it here for possible dev/beta package :)
pkgrel=2
# Due to the previous "_beta" naming
epoch=1
pkgdesc='Free messaging app for services like WhatsApp, Slack, Messenger and many more.'
arch=(x86_64 i686 armv7h aarch64)
url='https://meetfranz.com'
license=(Apache)
# Allow to easily switch between Electron versions.
# Expected one is 'electron'. May change soon.
_electron='electron'
depends=($_electron bash)
makedepends=(expac git npm python2 $_electron)
source=("git+https://github.com/meetfranz/$pkgname#tag=v$_pkgver"
        'franz.desktop'
        'franz.sh.in'
        0001-Add-cmdline-option-to-open-with-window-closed.patch)
sha512sums=('SKIP'
            '049c4bf2e0f362f892e8eef28dd18a6c321251c686a9c9e49e4abfb778057de2fc68b95b4ff7bb8030a828a48b58554a56b810aba078c220cb01d5837083992e'
            '0e2a871139453b505d7fc0d652911225c0e721fe32b98dceba86ec4b5969f28405bf88ed3769d09b66ad6eab8e188fd6a840c346477391ef4b1eddd1def7783a'
            '8f59b911eccd4f8c965f60ae4ba111195f48a76326e1c63391b1ab61b74d74dcb021fdb835da07037b28823cac1c3fd3b01f5ace501b6b6b7fe6db5e2aef8068')

prepare() {
  # Small patching
  cd $pkgname

  patch -Np1 --no-backup-if-mismatch < ../0001-Add-cmdline-option-to-open-with-window-closed.patch

  # Prevent franz from being launched in dev mode
  sed -i \
    "s|export const isDevMode = .*|export const isDevMode = false;|g" \
    src/environment.js
  sed -i \
    "s|import isDevMode from 'electron-is-dev'|export const isDevMode = false|g" \
    src/index.js

  # Adjust the electron version to use when building
  electron_version=$(expac %v $_electron | cut -d'-' -f1)
  sed -i "s|\(\s\+\"electron\":\).*,|\1 \"$electron_version\",|" package.json

  # Better configuration for npm cache and calling installed binaries
  export npm_config_cache="$srcdir"/npm_cache

  # Install tricky dependencies before-hand
  node_sass_version="4.14.1"
  sed -i "s|\(\s\+\"node-sass\":\).*,|\1 \"$node_sass_version\",|" package.json

  # Prepare the packages for building
  npx lerna bootstrap
}

build() {
  cd $pkgname

  # Better configuration for npm cache and calling installed binaries
  export npm_config_cache="$srcdir"/npm_cache

  # Actually build the package
  npx gulp build
  npx electron-builder --linux dir
}

package() {
  cd $pkgname
  electron_version=$(expac %v $_electron | cut -d'-' -f1)
  depends+=("$_electron=${electron_version}")

  # Install the .asar files
  install -dm 755 "$pkgdir"/usr/lib/$pkgname
  cp -r --no-preserve=ownership --preserve=mode out/linux-unpacked/resources "$pkgdir"/usr/lib/$pkgname/

  # Install icon
  install -Dm 644 "$srcdir"/franz.desktop "$pkgdir"/usr/share/applications/franz.desktop
  install -Dm 644 build-helpers/images/icon.png "$pkgdir"/usr/share/icons/franz.png

  # Install run script
  sed -e "s/@electron@/$_electron/g" "$srcdir"/franz.sh.in > "$srcdir"/franz.sh
  install -Dm 755 "$srcdir"/franz.sh "$pkgdir"/usr/bin/franz
}
