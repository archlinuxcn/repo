# Maintainer: Roald Clark <roaldclark@gmail.com>
# Contributor: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Peter Jung <ptr1337@archlinux.org>

_kernel=lts
_lkname="linux-${_kernel}"
pkgname="nvidia-open-${_kernel}"
pkgver=570.86.16
pkgrel=2
pkgdesc="NVIDIA open kernel modules for ${_lkname}"
arch=('x86_64')
url="https://github.com/NVIDIA/open-gpu-kernel-modules"
license=('MIT AND GPL-2.0-only')
depends=(
    "libglvnd"
    "nvidia-utils=${pkgver}"
)
makedepends=(
    "${_lkname}-headers"
    "nvidia-open-dkms=${pkgver}"
)
provides=("nvidia-open=${pkgver}")
conflicts=('nvidia-open-dkms')
options=('!strip')

build() {
    _kernver=$(</usr/src/${_lkname}/version)

    fakeroot dkms build --dkmstree "${srcdir}" -m "nvidia/${pkgver}" -k "${_kernver}"
}

package() {
    depends+=("${_lkname}=6.12.13-1")

    _kernver=$(</usr/src/${_lkname}/version)

    install -Dm0644 /usr/share/licenses/nvidia-open-dkms/LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
    install -Dm0644 "nvidia/${pkgver}/${_kernver}/${CARCH}/module/"*.ko -t "${pkgdir}/usr/lib/modules/${_kernver}/extramodules/"

    # compress each module individually
    find "${pkgdir}" -name '*.ko' -exec zstd --rm -19 {} +
}
