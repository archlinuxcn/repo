pkgname=julia-git-pardiso-src
pkgver=1.1.1
_commit=01de2fd9d6076ce4e298498f61f5d81f4f2b65b3
pkgrel=1
pkgdesc="Pardiso.jl"
url="https://github.com/JuliaSparse/Pardiso.jl.git"
arch=(aarch64 armv7h i686 x86_64)
license=('MIT')
makedepends=(git julia-pkg-scripts)
depends=(julia-git)
depends_x86_64=(intel-mkl)
source=("git+https://github.com/JuliaSparse/Pardiso.jl.git#commit=$_commit"
        f2ae3b4f639e981c6bcdaa6187051958eab4c356.patch)
sha256sums=('a09b9f121236e0e74b3f721511a6f22e9146fa9c5a84a8d636b0eaaca8c7fecc'
            'ec8698197cc1e0f300dc6890965168bde8a1f54e508c95782699b8c302eb3d62')

prepare() {
  cd Pardiso.jl

  patch -Np1 --no-backup-if-mismatch < "../f2ae3b4f639e981c6bcdaa6187051958eab4c356.patch"

  if [[ $CARCH != x86_64 ]]; then
    sed -i -e '/function  *__init__()/a\ \ \ \ return' src/Pardiso.jl
  fi
}

build() {
  cd Pardiso.jl/deps

  JULIA_DEPOT_PATH="${srcdir}/.julia" julia --startup-file=no build.jl
}

package() {
  cd Pardiso.jl

  JULIA_INSTALL_SRCPKG=1 . /usr/lib/julia/julia-install-pkg.sh Pardiso "${pkgdir}" "${pkgname}" julia-git
}
