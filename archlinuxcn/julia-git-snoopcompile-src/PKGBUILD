pkgbase=julia-git-snoopcompile-src
_subpkgs=(SnoopCompileCore
          SnoopPrecompile)
pkgname=(julia-git-snoopcompile-src)
pkgver=2.10.8
_commit=3abf32c5784e9df0a19187f771c59112618dc46d
pkgrel=4
pkgdesc="SnoopCompile.jl"
url="https://github.com/timholy/SnoopCompile.jl.git"
arch=('any')
license=('MIT')
makedepends=(git julia-pkg-scripts)
depends=(julia-git)
source=("git+https://github.com/timholy/SnoopCompile.jl.git#commit=$_commit")
sha256sums=('SKIP')

package_julia-git-snoopcompile-src() {
  cd SnoopCompile.jl

  JULIA_INSTALL_SRCPKG=1 . /usr/lib/julia/julia-install-pkg.sh SnoopCompile "${pkgdir}" "${pkgname}" julia-git

  rm -rf "${dest_dir}/"{SnoopCompileBot,SnoopCompileCore,SnoopPrecompile}
}

_subpackage() {
  local _subpkg=$1
  cd SnoopCompile.jl/${_subpkg}

  JULIA_INSTALL_SRCPKG=1 . /usr/lib/julia/julia-install-pkg.sh ${_subpkg} "${pkgdir}" "${pkgname}" julia-git
}

for _subpkg in "${_subpkgs[@]}"; do
  pkgname=("${pkgname[@]}" julia-git-${_subpkg,,}-src)
  eval "package_julia-git-${_subpkg,,}-src() { _subpackage ${_subpkg}; }"
done
