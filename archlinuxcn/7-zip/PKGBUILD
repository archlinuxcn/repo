# Maintainer: Oleksandr Natalenko <oleksandr@natalenko.name>

pkgname=7-zip
pkgver=24.07
pkgrel=2
pkgdesc="File archiver with a high compression ratio"
url=https://7-zip.org
license=('LGPL-2.1-or-later AND BSD-3-Clause AND LicenseRef-unRAR')
arch=(x86_64)
depends=(gcc-libs glibc)
makedepends=(meson uasm)
# Alternative sources:
# * https://7-zip.org/a/7z${pkgver//\./}-src.tar.xz
# * https://downloads.sourceforge.net/project/sevenzip/7-Zip/${pkgver}/7z${pkgver//\./}-src.tar.xz
source=("${url}/a/7z${pkgver/./}-src.tar.xz"
		meson.build.template
		BSD-2-Clause.txt
		BSD-3-Clause.txt)
sha256sums=('d1b0874a3f1c26df21c761a4a30691dc1213e8577f18ee78326c14ca4d683e2b'
            '73c785bcdb161296048ec56e0ba2f28b6052890eea01405b6a1e837782d28d65'
            '017b5ee48f680e82cba79141c7895a1f02b856df2512225f5c427fe36765ef0b'
            '3cf06aba3588c41c514f6946bb2d757b413ff6491647d474800f55edca75dcb4')

prepare() {
	cp meson.build.template meson.build

	meson rewrite kwargs set project / version ${pkgver}
}

build() {
	arch-meson . build

	meson compile -C build
}

package() {
	meson install -C build --destdir "${pkgdir}"
}
