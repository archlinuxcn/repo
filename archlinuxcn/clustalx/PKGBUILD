# Maintainer: Hu Butui <hot123tea123@gmail.com>
# Contributor: Matt Neilson <ichthyoboy at gmail dot com>
# Contributor: Mick Elliot <micke at sfu dot ca>

pkgname=clustalx
pkgver=2.1
pkgrel=2
pkgdesc='Multiple alignment of nucleic acid and protein sequences (command line tool'
arch=('x86_64')
url='http://www.clustal.org/clustal2'
license=('LGPL3')
depends=(
  qt5-base
)
makedepends=(
  gendesk
)
source=("${pkgname}-${pkgver}.tar.gz::http://www.clustal.org/download/current/clustalx-${pkgver}.tar.gz"
  "0001-fix-qt.patch"
  "0002-fix-qt.patch"
  "${pkgname}.png::http://www.clustal.org/images/clustalw_title.png"
)
sha256sums=('e10adb728c320598a165ca529f1aa3d2560061de0236e0a0926eaca9554afa05'
            '5c070a5cd2e7ba5eddf984b6d5ad2bdbb1dc5fd1c50edaefd109e094ed33431c'
            '8a5bd0329015d70e2e649c33c13aaabbb88999305f83f4c0fa0c01fe1af3db72'
            '6444ab0c22ecbefba05b968558eb543dda4c7e0ad140b485832d763e9f8c2dbd')

prepare() {
  cd "${pkgname}-${pkgver}"
  patch -p1 -i "${srcdir}/0001-fix-qt.patch"
  patch -p1 -i "${srcdir}/0002-fix-qt.patch"
  sed -i 's/static//g' clustalx.pro

  echo "Creating desktop file"
  gendesk -f -n --pkgname ${pkgname} \
    --pkgdesc "${pkgdesc}" \
    --categories "Graphics;MedicalSoftware;Science;" \
    --icon "${pkgname}" \
    --exec "${pkgname}"
}

build() {
  cd "${pkgname}-${pkgver}"
  qmake
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 clustalx "${pkgdir}/usr/bin/clustalx"
  install -Dm644 colprot.xml "${pkgdir}/usr/share/${pkgname}/colprot.xml"
  install -Dm644 coldna.xml "${pkgdir}/usr/share/${pkgname}/coldna.xml"
  install -Dm644 coldna.xml "${pkgdir}/usr/share/${pkgname}/colprint.xml"
  install -Dm644 clustalx.hlp "${pkgdir}/usr/share/${pkgname}/clustalx.hlp"
  install -Dm644 "${srcdir}/${pkgname}.png" "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
}
# vim:set ts=2 sw=2 et:
