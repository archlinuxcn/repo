# Maintainer: Y7n05h <Y7n05h(aT)protonmail--d0t--com>
# Contributor: Stefan Tatschner <stefan@rumpelsepp.org>
# Contributor: Stephen Gregoratto <dev@sgregoratto.me>
pkgname=wdisplays-git
_pkgname=wdisplays
pkgver=1.1.1.r0.g1a32d8b
pkgrel=1
pkgdesc='GUI display configurator for wlroots compositors'
url='https://github.com/artizirk/wdisplays'
license=('GPL3')
provides=("$_pkgname")
conflicts=("$_pkgname")
arch=('x86_64')
depends=('gtk3' 'wayland-protocols')
makedepends=('meson' 'git')

source=("$pkgname::git+$url")
sha256sums=('SKIP')

pkgver() {
  cd "$pkgname"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "$pkgname"
  arch-meson "build"
  ninja -C "build"
}

package() {
  cd "$pkgname"
  DESTDIR="$pkgdir" ninja -C "build" install
  install -D -m 644 "resources/wdisplays.svg" "$pkgdir"/usr/share/pixmaps/wdisplays.svg
  install -d -m 755 "$pkgdir"/usr/share/licenses/"$pkgname"
  install -D -m 644 LICENSES/* "$pkgdir"/usr/share/licenses/"$pkgname"/
}
