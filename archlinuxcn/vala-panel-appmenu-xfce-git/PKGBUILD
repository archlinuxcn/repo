# Maintainer: rilian-la-te <ria.freelander@gmail.com>

_disable_mate=0
_disable_xfce=0
_disable_vala=0
_disable_budgie=0

_opts=(
	-DCMAKE_INSTALL_PREFIX=/usr
	-DCMAKE_INSTALL_LIBDIR=lib
	-DCMAKE_INSTALL_LIBEXECDIR=lib
    -DENABLE_APPMENU_GTK_MODULE=OFF
    -DENABLE_REGISTRAR=OFF
    -DENABLE_JAYATANA=OFF
)

pkgname=(
'vala-panel-appmenu-common-git'
)

makedepends=('cmake' 'vala' 'gtk3' 'libwnck3' 'bamf>=0.5.0' 'git')

if (("${_disable_mate}" == 0));then
	_opts+=(-DENABLE_MATE=ON)
	pkgname+=('vala-panel-appmenu-mate-git')
	makedepends+=('mate-panel')
	msg "Mate applet enabled"
else
	_opts+=(-DENABLE_MATE=OFF)
fi

if (("${_disable_xfce}" == 0));then
	_opts+=(-DENABLE_XFCE=ON)
	pkgname+=('vala-panel-appmenu-xfce-git')
	makedepends+=('xfce4-panel>=4.11.2' 'xfconf')
	msg "Xfce applet enabled"
else
	_opts+=(-DENABLE_XFCE=OFF)
fi

if (("${_disable_vala}" == 0));then
	_opts+=(-DENABLE_VALAPANEL=ON)
	pkgname+=('vala-panel-appmenu-valapanel-git')
	makedepends+=('vala-panel>=0.3.75')
	msg "Vala Panel applet enabled"
else
	_opts+=(-DENABLE_VALAPANEL=OFF)
fi

if (("${_disable_budgie}" == 0));then
	_opts+=(-DENABLE_BUDGIE=ON)
	pkgname+=('vala-panel-appmenu-budgie-git')
	makedepends+=('budgie-desktop')
	msg "Budgie applet enabled"
else
	_opts+=(-DENABLE_BUDGIE=OFF)
fi


msg "If you want to disable an applet, edit pkgbuild variables _disable_[applet]"

_pkgbase=vala-panel-appmenu
pkgbase=${_pkgbase}-xfce-git
_cmakename=cmake-vala
pkgver=0.7.3.r1.g8e8b2d0
pkgrel=1
pkgdesc="AppMenu (Global Menu) plugin"
url="https://gitlab.com/vala-panel-project/vala-panel-appmenu"
arch=('i686' 'x86_64')
license=('LGPL3')

source=("git+https://gitlab.com/vala-panel-project/${_pkgbase}.git"
        "git+https://gitlab.com/vala-panel-project/${_cmakename}.git")
sha256sums=('SKIP'
            'SKIP')

pkgver() {
  cd "${srcdir}/${_pkgbase}"
  ( set -o pipefail
    git describe --long --tags 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

prepare() {
  cd "${srcdir}/${_cmakename}"
  cp -r . "${srcdir}/${_pkgbase}/cmake"
}

build() {
  cd "${srcdir}/${_pkgbase}"
  cmake ./ "${_opts[@]}"
  make
}

package_vala-panel-appmenu-xfce-git() {
  pkgdesc="AppMenu (Global Menu) plugin for xfce4-panel"
  depends=('gtk3' 'bamf>=0.5.0' 'xfce4-panel>=4.11.2' 'xfconf' 'libwnck3' 'vala-panel-appmenu-common-git')
  optdepends=('gtk2-ubuntu: for hiding gtk2 menus'
            'unity-gtk-module: for gtk2/gtk3 menus'
            'vala-panel-appmenu-registrar: for DBusMenu registrar' 
			'jayatana: for Java applications support'
            'appmenu-qt: for qt4 menus'
            'appmenu-qt5: for qt5 menus')
  cd "${srcdir}/${_pkgbase}"
  make -C "applets" DESTDIR="${pkgdir}" install
  make -C "lib" DESTDIR="${pkgdir}" install
  make -C "data" DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}/usr/lib/vala-panel"
  rm -rf "${pkgdir}/usr/lib/mate-panel"
  rm -rf "${pkgdir}/usr/share/vala-panel"
  rm -rf "${pkgdir}/usr/share/mate-panel"
  rm -rf "${pkgdir}/usr/share/dbus-1"
  rm -rf "${pkgdir}/usr/lib/budgie-desktop"
  rm -rf "${pkgdir}/usr/share/glib-2.0"
}

package_vala-panel-appmenu-valapanel-git() {
  pkgdesc="AppMenu (Global Menu) plugin for vala-panel"
  depends=('gtk3' 'bamf>=0.5.0' 'vala-panel' 'libwnck3' 'vala-panel-appmenu-common-git')
  optdepends=('gtk2-ubuntu: for hiding gtk2 menus'
            'unity-gtk-module: for gtk2/gtk3 menus'
            'vala-panel-appmenu-registrar: for DBusMenu registrar' 
			'jayatana: for Java applications support'
            'appmenu-qt: for qt4 menus'
            'appmenu-qt5: for qt5 menus')
  cd "${srcdir}/${_pkgbase}"
  make -C "applets" DESTDIR="${pkgdir}" install
  make -C "lib" DESTDIR="${pkgdir}" install
  make -C "data" DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}/usr/lib/xfce4"
  rm -rf "${pkgdir}/usr/share/mate-panel"
  rm -rf "${pkgdir}/usr/share/xfce4"
  rm -rf "${pkgdir}/usr/lib/mate-panel"
  rm -rf "${pkgdir}/usr/lib/budgie-desktop"
  rm -rf "${pkgdir}/usr/share/glib-2.0"
}

package_vala-panel-appmenu-mate-git() {
  pkgdesc="AppMenu (Global Menu) plugin for mate-panel"
  depends=('gtk3' 'bamf>=0.5.0' 'mate-panel' 'libwnck3' 'vala-panel-appmenu-common-git')
  optdepends=('gtk2-ubuntu: for hiding gtk2 menus'
            'unity-gtk-module: for gtk2/gtk3 menus'
            'vala-panel-appmenu-registrar: for DBusMenu registrar' 
			'jayatana: for Java applications support'
            'appmenu-qt: for qt4 menus'
            'appmenu-qt5: for qt5 menus')
  cd "${srcdir}/${_pkgbase}"
  make -C "applets" DESTDIR="${pkgdir}" install
  make -C "lib" DESTDIR="${pkgdir}" install
  make -C "data" DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}/usr/lib/vala-panel"
  rm -rf "${pkgdir}/usr/lib/xfce4"
  rm -rf "${pkgdir}/usr/share/xfce4"
  rm -rf "${pkgdir}/usr/share/vala-panel"
  rm -rf "${pkgdir}/usr/lib/budgie-desktop"
  rm -rf "${pkgdir}/usr/share/glib-2.0"
}

package_vala-panel-appmenu-budgie-git() {
  pkgdesc="AppMenu (Global Menu) plugin for budgie-panel"
  depends=('gtk3' 'bamf>=0.5.0' 'budgie-desktop' 'libwnck3' 'vala-panel-appmenu-common-git')
  optdepends=('gtk2-ubuntu: for hiding gtk2 menus'
            'unity-gtk-module: for gtk2/gtk3 menus'
            'vala-panel-appmenu-registrar: for DBusMenu registrar' 
			'jayatana: for Java applications support'
            'appmenu-qt: for qt4 menus'
            'appmenu-qt5: for qt5 menus')
  cd "${srcdir}/${_pkgbase}"
  make -C "applets" DESTDIR="${pkgdir}" install
  make -C "lib" DESTDIR="${pkgdir}" install
  make -C "data" DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}/usr/lib/xfce4"
  rm -rf "${pkgdir}/usr/share"
  rm -rf "${pkgdir}/usr/lib/vala-panel"
  rm -rf "${pkgdir}/usr/lib/mate-panel"
}

package_vala-panel-appmenu-common-git() {
  pkgdesc="Translations and common files for Global Menu"
  replaces=('vala-panel-appmenu-translations-git')
  optdepends=('vala-panel-appmenu-xfce-git'
              'vala-panel-appmenu-valapanel-git'
              'vala-panel-appmenu-mate-git'
              'vala-panel-appmenu-budgie-git')
  arch=('any')
  cd "${srcdir}/${_pkgbase}"
  make -C "po" DESTDIR="${pkgdir}" install
  make -C "data" DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}/usr/lib"
  rm -rf "${pkgdir}/usr/share/dbus-1"
  rm -rf "${pkgdir}/usr/share/mate-panel"
  rm -rf "${pkgdir}/usr/share/vala-panel"
  rm -rf "${pkgdir}/usr/share/xfce4"
}
