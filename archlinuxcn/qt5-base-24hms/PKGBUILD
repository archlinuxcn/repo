# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgbase=qt5-base-24hms
_origpkgname=qt5-base
pkgname=(qt5-base-24hms)
pkgver=5.15.2+kde+r195
pkgrel=1.6
_commit=4102271bbd16d93984b54a3435995259e5641e1c
arch=('x86_64')
url='https://www.qt.io'
license=('GPL3' 'LGPL3' 'FDL' 'custom')
pkgdesc='A cross-platform application and UI framework. This package uses 24-hour notation HH:mm:ss in all locales.'
depends=('libjpeg-turbo' 'xcb-util-keysyms' 'xcb-util-renderutil' 'libgl' 'fontconfig' 'xdg-utils'
         'shared-mime-info' 'xcb-util-wm' 'libxrender' 'libxi' 'sqlite' 'xcb-util-image' 'mesa'
         'tslib' 'libinput' 'libxkbcommon-x11' 'libproxy' 'libcups' 'double-conversion' 'md4c')
makedepends=('python2' 'libfbclient' 'mariadb-libs' 'unixodbc' 'postgresql-libs' 'alsa-lib' 'gst-plugins-base-libs'
             'gtk3' 'libpulse' 'cups' 'freetds' 'vulkan-headers' 'git')
optdepends=('qt5-svg: to use SVG icon themes'
            'qt5-wayland: to run Qt applications in a Wayland session'
            'qt5-translations: for some native UI translations'
            'postgresql-libs: PostgreSQL driver'
            'mariadb-libs: MariaDB driver'
            'unixodbc: ODBC driver'
            'libfbclient: Firebird/iBase driver'
            'freetds: MS SQL driver'
            'gtk3: GTK platform plugin'
            'perl: for fixqt4headers and syncqt')
conflicts=("qt5-base" "qt5-3d>=5.16" "qt5-charts>=5.16" "qt5-connectivity>=5.16" "qt5-datavis3d>=5.16" "qt5-declarative>=5.16" "qt5-doc>=5.16" "qt5-examples>=5.16" "qt5-gamepad>=5.16" "qt5-graphicaleffects>=5.16" "qt5-imageformats>=5.16" "qt5-location>=5.16" "qt5-lottie>=5.16" "qt5-multimedia>=5.16" "qt5-networkauth>=5.16" "qt5-purchasing>=5.16" "qt5-quick3d>=5.16" "qt5-quickcontrols>=5.16" "qt5-quickcontrols2>=5.16" "qt5-remoteobjects>=5.16" "qt5-script>=5.16" "qt5-scxml>=5.16" "qt5-sensors>=5.16" "qt5-serialbus>=5.16" "qt5-serialport>=5.16" "qt5-speech>=5.16" "qt5-svg>=5.16" "qt5-tools>=5.16" "qt5-translations>=5.16" "qt5-virtualkeyboard>=5.16" "qt5-wayland>=5.16" "qt5-webchannel>=5.16" "qt5-webglplugin>=5.16" "qt5-websockets>=5.16" "qt5-webview>=5.16" "qt5-x11extras>=5.16" "qt5-xmlpatterns>=5.16" 'qtchooser')

provides=("qt5-base=$pkgver")

_pkgfqn=qtbase
source=(
      oldherl-24hms.patch
      'ftp://unicode.org/Public/cldr/36/core.zip'
      git+https://invent.kde.org/qt/qt/$_pkgfqn#commit=$_commit
        qt5-base-cflags.patch
        qt5-base-nostrip.patch)
sha256sums=(
      '9c62800980e97b1614e4fc85ecad3b032606d0e620295e791fc3f34629bb0a44'
      '07279e56c1f4266d140b907ef3ec379dce0a99542303a9628562ac5fe460ba43'
      'SKIP'
            'cf707cd970650f8b60f8897692b36708ded9ba116723ec8fcd885576783fe85c'
            '4b93f6a79039e676a56f9d6990a324a64a36f143916065973ded89adc621e094')

pkgver() {
  cd $_pkgfqn
  echo "5.15.2+kde+r"`git rev-list --count origin/5.15.2..$_commit`
}

prepare() {
  cd ${_pkgfqn}

  git revert -n 6344955d17e17e2398720fe60c34cfc2a4a95208 # Revert version bump
  patch -p1 < ../qt5-base-cflags.patch # Use system CFLAGS in qmake
  patch -p1 < ../qt5-base-nostrip.patch # Don't strip binaries with qmake

  patch -p1 -i ../oldherl-24hms.patch
  cd util/locale_database
  echo "This is slow. It takes about 4 minutes on my desktop."
  ./cldr2qlocalexml.py ../../../ > ./24h.xml
  ./qlocalexml2cpp.py ./24h.xml ../..
}

build() {
  cd ${_pkgfqn}

  ./configure -confirm-license -opensource -v \
    -prefix /usr \
    -docdir /usr/share/doc/qt \
    -headerdir /usr/include/qt \
    -archdatadir /usr/lib/qt \
    -datadir /usr/share/qt \
    -sysconfdir /etc/xdg \
    -examplesdir /usr/share/doc/qt/examples \
    -plugin-sql-{psql,mysql,sqlite,odbc,ibase} \
    -system-sqlite \
    -openssl-linked \
    -nomake examples \
    -no-rpath \
    -dbus-linked \
    -system-harfbuzz \
    -journald \
    -no-mimetype-database \
    -no-use-gold-linker \
    -reduce-relocations \
    -no-strip
  make
}

package() {
install -dm755 "$pkgdir"/usr/share/licenses/
ln -s /usr/share/licenses/${pkgname} "$pkgdir"/usr/share/licenses/qt5-base

  pkgdesc='A cross-platform application and UI framework'

  cd ${_pkgfqn}
  make INSTALL_ROOT="${pkgdir}" install

  install -Dm644 LICENSE* -t "$pkgdir"/usr/share/licenses/$pkgbase

  # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
  find "${pkgdir}/usr/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;

  # Fix wrong qmake path in pri file
  sed -i "s|${srcdir}/${_pkgfqn}|/usr|" \
    "${pkgdir}"/usr/lib/qt/mkspecs/modules/qt_lib_bootstrap_private.pri

  # Symlinks for backwards compatibility
  for b in "${pkgdir}"/usr/bin/*; do
    ln -s $(basename $b) "${pkgdir}"/usr/bin/$(basename $b)-qt5
  done
}

nopackage_qt5-xcb-private-headers() {
  pkgdesc='Private headers for Qt5 Xcb'

  depends=("qt5-base=$pkgver")
  optdepends=()
  groups=()
  conflicts=()

  cd ${_pkgfqn}
  install -d -m755 "$pkgdir"/usr/include/qtxcb-private
  cp -r src/plugins/platforms/xcb/*.h "$pkgdir"/usr/include/qtxcb-private/
}
