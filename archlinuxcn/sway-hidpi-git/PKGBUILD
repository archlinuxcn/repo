# Maintainer: Antonin DÃ©cimo <antonin dot decimo at gmail dot com>
# Contributor: Drew DeVault <sir@cmpwn.com>
pkgname=sway-hidpi-git
_pkgname=sway
pkgver=r7328.dcb142bf5
pkgrel=1
license=("MIT")
pkgdesc="Tiling Wayland compositor and replacement for the i3 window manager, with XWayland HiDPI (git version)"
makedepends=(
  "git"
  "meson"
  "scdoc"
  "wayland-protocols"
)
depends=(
  "cairo"
  "gdk-pixbuf2"
  "json-c"
  "pango"
  "polkit"
  "pcre2"
  "swaybg-git"
  "ttf-font"
  "wlroots-hidpi-git"
  "xorg-xwayland-hidpi-git"
  "xsettingsd"
  "xcb-util-wm"
)
optdepends=(
  "alacritty: Terminal emulator used by the default config"
  "dmenu: Application launcher"
  "grim: Screenshot utility"
  "i3status: Status line"
  "mako: Lightweight notification daemon"
  "slurp: Select a region"
  "swayidle: Idle management daemon"
  "swaylock: Screen locker"
  "wallutils: Timed wallpapers"
  "waybar: Highly customizable bar"
)
backup=(etc/sway/config)
arch=("i686" "x86_64")
url="https://swaywm.org"
source=("git+https://github.com/swaywm/sway"
        "50-systemd-user.conf"
        "xwayland_hidpi.diff")
sha512sums=('SKIP'
            '57590bc0d14c87289a4a9cd67991c6a841e54244d2a6186b5da5a08e633de2e8631959fa8c77ede211b0a5f315d920f2c1350951a53d6f2e9e81859056cb3c9e'
            '79d007a283796389c35e70363f0e32830ee00d3a3d58bbfbf3cdc818d4ad85e0979538851410f11c679df11d6afc70eb1884cde35e5672dce30b8fbe399a2a34')
provides=("sway" "sway-git")
conflicts=("sway" "sway-git")
install="sway.install"
options=(debug)

prepare() {
  cd "$_pkgname"
  patch --forward --strip=1 --input="${srcdir}/xwayland_hidpi.diff"
}

pkgver() {
  cd "$_pkgname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  arch-meson \
    -Dsd-bus-provider=libsystemd \
    -Dwerror=false \
    "$_pkgname" build
  meson compile -C build
}

package() {
  install -Dm644 50-systemd-user.conf -t "$pkgdir/etc/sway/config.d/"

  DESTDIR="$pkgdir" meson install -C build

  cd "$_pkgname"
  install -Dm644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

post_upgrade() {
  echo "Make sure to upgrade wlroots-hidpi-git and sway-hidpi-git together."
  echo "Upgrading one but not the other is unsupported."
}
