# Maintainer: envolution
# Contributor: FuzzyCheese
# shellcheck shell=bash disable=SC2034,SC2154
pkgname=wfview
pkgver=2.11
pkgrel=4
pkgdesc="Interface for Icom transceivers"
arch=('i686' 'x86_64')
url="https://wfview.org/"
license=('GPL-3.0-only')
depends=(
  qt5-serialport 
  qt5-multimedia 
  qcustomplot 
  eigen 
  qt5-gamepad 
  rtaudio 
  qt5-websockets 
  opus 
  hidapi 
  portaudio
  glibc
  qt5-base
  systemd-libs
  gcc-libs
)

source=("https://gitlab.com/eliggett/wfview/-/archive/v$pkgver/wfview-v$pkgver.tar.gz")
md5sums=('cbd4c4fec0a6bf922ccdc71e4a2ebe88')

prepare() {
  sed -i '/^linux:QMAKE_POST_LINK += echo; echo; echo "Run install.sh as root from the build directory to install."; echo; echo;$/s/^/#/' ${srcdir}/wfview-v$pkgver/wfview.pro
  echo "linux:LIBS += -lqcustomplot" >>${srcdir}/wfview-v$pkgver/wfview.pro
}

build() {
  cd "${pkgname}-v${pkgver}"
  mkdir -p build
  cd build
  qmake ../wfview.pro PREFIX=/usr
  make
}

package() {
  cd "${pkgname}-v${pkgver}/build"
  make INSTALL_ROOT=${pkgdir} install
}
# vim:set ts=2 sw=2 et:
