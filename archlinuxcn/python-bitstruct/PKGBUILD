# Maintainer: Xeonacid <h.dwwwwww@gmail.com>

_name=bitstruct
pkgname=python-${_name}
pkgver=8.21.0
pkgrel=2
pkgdesc="Python bit pack/unpack package."
arch=(x86_64)
url="https://github.com/eerimoq/${_name}"
license=(MIT)
depends=(glibc python)
makedepends=(python-build python-installer python-setuptools python-wheel)
checkdepends=(python-pytest)
source=(${_name}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz)
sha512sums=('d9dc6fda11722a125699964b84bf4bc64c70c649f74c6ea18138987c9ae53d4f34212b7f330e512efac176a2b1cd982a59d6f3567d25f4b20a736b097ab43f98')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check(){
  cd $_name-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest -vv tests/
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm644 README.rst -t "$pkgdir/usr/share/doc/$pkgname"
}
