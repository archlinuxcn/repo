# Maintainer: Xeonacid <h.dwwwwww@gmail.com>

_name=bitstruct
pkgname=python-${_name}
pkgver=8.20.0
pkgrel=1
pkgdesc="Python bit pack/unpack package."
arch=(x86_64)
url="https://github.com/eerimoq/${_name}"
license=(MIT)
depends=(glibc python)
makedepends=(python-build python-installer python-setuptools python-wheel)
checkdepends=(python-pytest)
source=(${_name}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz)
sha512sums=('6540b417213ade354e139318042c8357ed0cbc4ed1f68ae4b493dd1b716b4c8efcc232901bb731b63ef9eaedc4e7315ecb9915efcbfc466a04e559486815e5a6')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check(){
  cd $_name-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest -vv tests/
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm644 README.rst -t "$pkgdir/usr/share/doc/$pkgname"
}
