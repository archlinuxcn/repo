# Maintainer: Xeonacid <h.dwwwwww@gmail.com>

_name=bitstruct
pkgname=python-${_name}
pkgver=8.22.0
pkgrel=1
pkgdesc="Python bit pack/unpack package."
arch=(x86_64)
url="https://github.com/eerimoq/${_name}"
license=(MIT)
depends=(glibc python)
makedepends=(python-build python-installer python-setuptools python-wheel)
checkdepends=(python-pytest)
source=(${_name}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz)
sha512sums=('64f11b84af9302c24e2cb086f0c0e718eef2b81eaec003fb84c672455ec3c2c87f5730a4b675eb1efe95294ab819eccbc57e8a5da53d6bcc4daa340bbe607b2b')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check(){
  cd $_name-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest -vv tests/
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm644 README.rst -t "$pkgdir/usr/share/doc/$pkgname"
}
