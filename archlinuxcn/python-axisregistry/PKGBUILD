# Maintainer: Caleb Maclennan <caleb@alerque.com>

_pyname=axisregistry
pkgname=python-$_pyname
pkgver=0.4.12
pkgrel=1
pkgdesc='easy access to the GF Axis Registry'
arch=(any)
url="https://github.com/googlefonts/$_pyname"
license=('Apache-2.0')
depends=(
    'python'
    'python-protobuf'
)
makedepends=(
    'python-build'
    'python-installer'
    'python-setuptools-scm'
    'python-wheel'
)
checkdepends=(
    'python-fonttools'
    'python-pytest'
)
_archive="$_pyname-$pkgver"
source=("https://files.pythonhosted.org/packages/source/${_pyname::1}/$_pyname/$_archive.tar.gz")
sha256sums=('77ad156f39438802fe27cb02134b29d91801d36586ae282a71dcea5b9e5ec75b')

prepare() {
    cd "$_archive"
    sed -i -e '/setuptools_scm/s/>=4,<6.1//' setup.py
}

build() {
    cd "$_archive"
    python -m build --wheel --no-isolation
}

check() {
    cd "$_archive"
    #PYTHONPATH=Lib pytest
    PROTOCOL_BUFFERS_PYTHON_IMPLEMENTATION=python PYTHONPATH=Lib pytest
}

package() {
    cd "$_archive"
    python -m installer --destdir="$pkgdir" dist/*.whl
}
