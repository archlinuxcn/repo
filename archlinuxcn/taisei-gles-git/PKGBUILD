# Maintainer: Lukas Weber <laochailan web de>
# Contributor: Andrei Alexeyev <akari taisei-project org>
# Contributor: cyberpunkrocker <l33tmmx gmail com>
# Contributor: carstene1ns <arch carsten-teibes de> - http://git.io/ctPKG

pkgname=taisei-gles-git
_pkgname=${pkgname%-gles-git}
pkgver=1.4.r62.g5a8c7a1
pkgrel=1
pkgdesc="Open source Touhou clone (using OpenGL ES backends)"
arch=('i686' 'x86_64')
url="https://taisei-project.org/"
license=('MIT')
depends=('opengl-driver' 'cglm' 'sdl2_mixer' 'freetype2' 'libpng' 'libwebp' 'libzip' 'hicolor-icon-theme' 'openssl' 'spirv-cross' 'shaderc')
makedepends=('git' 'meson' 'python-docutils' 'python-zstandard' 'gamemode')
optdepends=('gamemode: GameMode integration')
conflicts=("${_pkgname}")
provides=("${_pkgname}")
source=("git+https://github.com/taisei-project/${_pkgname}.git")
sha256sums=('SKIP')

pkgver() {
	cd "${_pkgname}/"
	git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/v//'
}

prepare() {
	cd "${_pkgname}/"
	git submodule update --init --recursive
}

build() {
	cd "${_pkgname}/"
	arch-meson --auto-features auto build
	ninja -C build
}

package() {
	cd "${_pkgname}/"
	DESTDIR="${pkgdir}" ninja install -C build
	install -Dm644 COPYING -t "${pkgdir}/usr/share/licenses/${_pkgname}/"
}
