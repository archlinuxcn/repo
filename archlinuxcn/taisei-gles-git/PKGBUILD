# Maintainer: Lukas Weber <laochailan web de>
# Contributor: Andrei Alexeyev <akari taisei-project org>
# Contributor: cyberpunkrocker <l33tmmx gmail com>
# Contributor: carstene1ns <arch carsten-teibes de> - http://git.io/ctPKG

pkgname=taisei-gles-git
_pkgname=${pkgname%-gles-git}
pkgver=1.4.r413.g768cfd8
pkgrel=1
pkgdesc="Open source Touhou clone (using OpenGL ES backends)"
arch=('i686' 'x86_64')
url="https://taisei-project.org/"
license=('MIT')
depends=('opengl-driver' 'cglm' 'sdl2_mixer' 'freetype2' 'libpng' 'libwebp' 'libzip' 'hicolor-icon-theme' 'openssl' 'spirv-cross' 'shaderc')
makedepends=('git' 'meson' 'python-docutils' 'python-zstandard' 'gamemode')
optdepends=('gamemode: GameMode integration')
conflicts=("${_pkgname}")
provides=("${_pkgname}")
source=(
	"git+https://github.com/taisei-project/${_pkgname}.git"
	"git+https://github.com/taisei-project/SDL_GameControllerDB.git"
	"git+https://github.com/taisei-project/koishi.git"
	"git+https://github.com/taisei-project/basis_universal.git"
	"git+https://github.com/taisei-project/python-zipfile-zstd.git"
)
sha256sums=('SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP')

pkgver() {
	cd "${_pkgname}/"
	git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/v//'
}

prepare() {
	cd "${_pkgname}/"
	git submodule init
	git config submodule.external/gamecontrollerdb.url "${srcdir}/SDL_GameControllerDB"
	git config submodule.external/koishi.url "${srcdir}/koishi"
	git config submodule.external/basis_universal.url "${srcdir}/basis_universal"
	git config submodule.external/python-zipfile-zstd.url "${srcdir}/python-zipfile-zstd"
	git -c protocol.file.allow=always submodule update
}

build() {
	cd "${_pkgname}/"
	arch-meson --auto-features auto build
	ninja -C build
}

package() {
	cd "${_pkgname}/"
	DESTDIR="${pkgdir}" ninja install -C build
	install -Dm644 COPYING -t "${pkgdir}/usr/share/licenses/${_pkgname}/"
}
