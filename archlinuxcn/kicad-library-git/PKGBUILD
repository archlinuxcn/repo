# Mainainer: edward-p <edward at edward-p dot xyz>
# Mainainer: taotieren <admin@taotieren.com>

_kicad_ver=9.0
pkgname=kicad-library-git
pkgver=9.0.20250828.041446.gc6ef1993b1
pkgrel=5
pkgdesc="KiCad symbol, footprint and template libraries"
arch=('any')
url="https://klc.kicad.org/"
license=('CC-BY-SA-4.0')
makedepends=('cmake' 'git')
options=('!strip')
source=("git+https://gitlab.com/kicad/libraries/kicad-footprints.git"
        "git+https://gitlab.com/kicad/libraries/kicad-symbols.git"
        "git+https://gitlab.com/kicad/libraries/kicad-templates.git")
provides=('kicad-library')
conflicts=('kicad-library')
sha256sums=('SKIP'
            'SKIP'
            'SKIP')

pkgver() {
  cd kicad-footprints
  printf "${_kicad_ver}.%s.g%s" "$(TZ=UTC git log -1 --pretty='%cd' --date=format-local:%Y%m%d.%H%M%S)" "$(git log -1 --format='%h')"
}
            
build() {
  cd "$srcdir/kicad-symbols/"
  cmake ./ -DCMAKE_INSTALL_PREFIX=/usr
  make

  cd "$srcdir/kicad-footprints/"
  cmake ./ -DCMAKE_INSTALL_PREFIX=/usr
  make

  cd "$srcdir/kicad-templates/"
  cmake ./ -DCMAKE_INSTALL_PREFIX=/usr
  make

}

package() {
  cd "$srcdir/kicad-symbols/"
  make DESTDIR="$pkgdir" install

  cd "$srcdir/kicad-footprints/"
  make DESTDIR="$pkgdir" install

  cd "$srcdir/kicad-templates/"
  make DESTDIR="$pkgdir" install
}

