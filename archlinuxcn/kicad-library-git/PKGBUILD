# Mainainer: edward-p <edward at edward-p dot xyz>
# Mainainer: taotieren <admin@taotieren.com>

pkgname=kicad-library-git
pkgver=8.0.0rc1.r71.g30b9f75589
pkgrel=1
pkgdesc="KiCad symbol, footprint and template libraries"
arch=('any')
url="https://klc.kicad.org/"
license=('CC-BY-SA 4.0')
makedepends=('cmake' 'git')
options=('!strip')
source=("git+https://gitlab.com/kicad/libraries/kicad-footprints.git"
        "git+https://gitlab.com/kicad/libraries/kicad-symbols.git"
        "git+https://gitlab.com/kicad/libraries/kicad-templates.git")
provides=('kicad-library')
conflicts=('kicad-library')
sha256sums=('SKIP'
            'SKIP'
            'SKIP')

pkgver() {
  cd kicad-footprints
  git describe --long | sed 's/-rc/rc/;s/\([^-]*-g\)/r\1/;s/-/./g'
}
            
build() {
  cd "$srcdir/kicad-symbols/"
  cmake ./ -DCMAKE_INSTALL_PREFIX=/usr
  make

  cd "$srcdir/kicad-footprints/"
  cmake ./ -DCMAKE_INSTALL_PREFIX=/usr
  make

  cd "$srcdir/kicad-templates/"
  cmake ./ -DCMAKE_INSTALL_PREFIX=/usr
  make

}

package() {
  cd "$srcdir/kicad-symbols/"
  make DESTDIR="$pkgdir" install

  cd "$srcdir/kicad-footprints/"
  make DESTDIR="$pkgdir" install

  cd "$srcdir/kicad-templates/"
  make DESTDIR="$pkgdir" install
}

