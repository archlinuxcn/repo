# Maintainer: Integral <integral@member.fsf.org>
# Contributor: Julian <juliannfairfax@protonmail.com>

pkgname=flare
pkgver=0.17.5
pkgrel=2
epoch=1
pkgdesc="Chat with your friends on Signal"
url="https://gitlab.com/schmiddi-on-mobile/${pkgname}"
arch=("i686" "x86_64" "armv6h" "armv7h" "aarch64")
license=("AGPL-3.0-or-later")
makedepends=("blueprint-compiler" "cargo" "meson")
depends=("libadwaita" "libsecret" "protobuf" "gtksourceview5" "libspelling")
optdepends=("feedbackd: for vibrating notifications")
source=("${url}/-/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('ebdd2a66dd55fec72dfd24345c4f58aea771986cf0acd80cacbcb03cdd23b767')

build() {
	arch-meson "${pkgname}-${pkgver}" build
	CFLAGS+=" -ffat-lto-objects" meson compile -C build
}

check() {
	meson test -C build --print-errorlogs
}

package() {
	meson install -C build --no-rebuild --destdir "${pkgdir}"
	install -Dm644 ${pkgname}-${pkgver}/doc/* -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
