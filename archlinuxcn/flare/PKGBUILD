# Maintainer: Julian <juliannfairfax@protonmail.com>

pkgname=flare
pkgver=0.15.4
_pkgver=${pkgver/beta/-beta/}
pkgrel=1
epoch=1
pkgdesc="Chat with your friends on Signal"
url="https://gitlab.com/schmiddi-on-mobile/${pkgname}"
arch=("i686" "x86_64" "armv6h" "armv7h" "aarch64")
license=("AGPL-3.0-or-later")
makedepends=("blueprint-compiler" "cargo" "meson")
depends=("libadwaita" "libsecret" "protobuf" "gtksourceview5" "libspelling")
optdepends=("feedbackd: for vibrating notifications")
source=("https://gitlab.com/schmiddi-on-mobile/${pkgname}/-/archive/${_pkgver}/${pkgname}-${_pkgver}.tar.gz")
sha256sums=('cdde38252759715f6ce3a45906c608a4232f215ff1c510f08d0d7afc169f30c4')
options=('!lto')

prepare() {
	cd "${pkgname}-${_pkgver}/"
	arch-meson . build/
}

package() {
	cd "${pkgname}-${_pkgver}/"
	DESTDIR="${pkgdir}" ninja -C build/ install
	install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
	install -Dm644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
