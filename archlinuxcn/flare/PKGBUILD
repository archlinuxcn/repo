# Maintainer: Integral <integral@member.fsf.org>
# Contributor: Julian <juliannfairfax@protonmail.com>

pkgname=flare
pkgver=0.18.3
pkgrel=1
epoch=1
pkgdesc="Chat with your friends on Signal"
url="https://gitlab.com/schmiddi-on-mobile/${pkgname}"
arch=("i686" "x86_64" "armv6h" "armv7h" "aarch64")
license=("AGPL-3.0-or-later")
makedepends=("blueprint-compiler" "cargo" "meson")
depends=("libadwaita" "libsecret" "protobuf" "gtksourceview5" "libspelling")
optdepends=("feedbackd: for vibrating notifications")
source=("${url}/-/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('b4929f1b61b58fa8977c5943cd865f56c99451ed7cb7567af51a1f92cf15714e')

build() {
	arch-meson "${pkgname}-${pkgver}" build
	CFLAGS+=" -ffat-lto-objects" meson compile -C build
}

check() {
	meson test -C build --print-errorlogs
}

package() {
	meson install -C build --no-rebuild --destdir "${pkgdir}"
	install -Dm644 ${pkgname}-${pkgver}/doc/* -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
