# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Alexey Yakovenko <waker@users.sourceforge.net>

pkgname=deadbeef
pkgver=1.9.6
pkgrel=9
pkgdesc='A GTK+ audio player for GNU/Linux.'
arch=('x86_64')
url='http://deadbeef.sourceforge.net'
license=('GPL2')
depends=('alsa-lib' 'hicolor-icon-theme' 'desktop-file-utils' 'jansson' 'libdispatch')
makedepends=('libvorbis' 'libmad' 'flac' 'curl' 'imlib2' 'wavpack' 'libsndfile' 'libcdio' 'libcddb'
             'libx11' 'faad2' 'zlib' 'intltool' 'pkgconfig' 'libpulse' 'libzip' 'libsamplerate'
             'yasm' 'ffmpeg' 'gtk2' 'gtk3' 'clang' 'opusfile' 'mpg123' 'git' 'libpipewire')
optdepends=('gtk2: for the GTK2 interface'
            'gtk3: for the GTK3 interface'
            'libsamplerate: for Resampler plugin'
            'libvorbis: for Ogg Vorbis playback'
            'libmad: for MP1/MP2/MP3 playback'
            'mpg123: for MP1/MP2/MP3 playback'
            'flac: for FLAC playback'
            'curl: for Last.fm scrobbler, SHOUTcast, Icecast, Podcast support'
            'imlib2: for artwork plugin'
            'wavpack: for WavPack playback'
            'libsndfile: for Wave playback'
            'libcdio: audio cd plugin'
            'libcddb: audio cd plugin'
            'faad2: for AAC/MP4 support'
            'dbus: for OSD notifications support'
            'pulseaudio: for PulseAudio output plugin'
            'libx11: for global hotkeys plugin'
            'zlib: for Audio Overload plugin'
            'libzip: for vfs_zip plugin'
            'ffmpeg4.4: for ffmpeg plugin'
            'opusfile: for opus plugin'
            'pipewire: for pipewire support')
source=($pkgname::"git+https://github.com/DeaDBeeF-Player/${pkgname}.git#tag=$pkgver")
replaces=('deadbeef-pipewire-plugin-git' 'deadbeef-pipewire-plugin')
provides=('deadbeef-pipewire-plugin')
md5sums=('SKIP')

prepare() {
	cd "${srcdir}/${pkgname}"
    git submodule update --init --recursive
    sed -i 's/adplug_la_CXXFLAGS = $(CXXFLAGS)/adplug_la_CXXFLAGS = $(CXXFLAGS) -std=c++11/g' plugins/adplug/Makefile.am

	./autogen.sh 
	CC=clang CXX=clang++ ./configure --prefix=/usr
}

build () {
	cd "${srcdir}/${pkgname}"
	make
}

package() {
	cd "${srcdir}/${pkgname}"
	make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
