# Maintainer: zjuyk <ownbyzjuyk@gmail.com>

pkgname=git-delta-git
_pkgname=delta
pkgver=0.11.3.20220114
pkgrel=1
pkgdesc="A syntax-highlighting pager for git"
arch=('any')
url="https://github.com/dandavison/delta"
license=('MIT')
makedepends=('git' 'rust')
provides=(git-delta)
source=(git+$url)
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  echo $(grep -oPm1 '(?<=^version = ")[^"]+' Cargo.toml).$(TZ=UTC git log -1 --pretty='%cd' --date=short-local | tr -d '-')
}

build() {
  cd "$srcdir/$_pkgname"
  sed -i '/profile.release/alto = true' Cargo.toml
  cargo build --release
  git checkout Cargo.toml
}

package() {
  cd "$srcdir/$_pkgname"

  install -Dm755 "target/release/$_pkgname" -t "$pkgdir/usr/bin"
  install -Dm644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
