# Maintainer: poly000 <1348292515@qq.com>

pkgname=waylyrics-git
_pkgname=waylyrics
_appname="io.poly000.${_pkgname}"
pkgver=0.1.0_r494.g87367a17
pkgrel=2
pkgdesc="general desktop lyrics with QQMusic/NetEase Cloud Music source"
url="https://github.com/waylyrics/waylyrics"
conflicts=('waylyrics')
provides=('waylyrics')
arch=('x86_64' 'aarch64')
license=('MIT')
depends=('openssl' 'hicolor-icon-theme'
    # base
    'dbus' 'gcc-libs' 'glibc'
    # gtk4
    'glib2' 'cairo' 'dconf' 'gtk4')
makedepends=('rust' 'git' 'jq' 'mimalloc')
optdepends=()

source=("git+${url}.git")

sha256sums=('SKIP')

options=('!lto')

prepare() {
    cd "${_pkgname}"
    export RUSTUP_TOOLCHAIN=stable

    if which rustup
    then rustup update stable
    fi

    cargo fetch --locked --target ${CARCH}-unknown-linux-gnu
}

pkgver() {
    cd "${_pkgname}"
    export RUSTUP_TOOLCHAIN=stable
    semver=$(cargo metadata --no-deps --format-version=1 | jq -r '.packages | .[0] | .version')
    echo "${semver}_r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

build() {
    cd "${_pkgname}"

    export RUSTUP_TOOLCHAIN=stable
    export CARGO_TARGET_DIR=target

    # template files
    export WAYLYRICS_THEME_PRESETS_DIR="/usr/share/${_pkgname}/themes"

    cargo build --frozen --release
}

package() {
    cd "${_pkgname}"

    install -vDm644 "${_appname}.desktop" -t "${pkgdir}/usr/share/applications/"

    install -vDm755 "target/release/${_pkgname}" -t "${pkgdir}/usr/bin/"
    install -vDm644 "${_appname}.gschema.xml" -t "${pkgdir}/usr/share/glib-2.0/schemas/"

    install -vdm755 "${pkgdir}/usr/share/${_pkgname}/themes"
    cp -arv themes/* "${pkgdir}/usr/share/${_pkgname}/themes/"
    cp -arv res/* "${pkgdir}/usr/share/"

    # License
    install -vDm644 "LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
