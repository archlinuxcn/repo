# Maintainer: Butui Hu <hot123tea123@gmail.com>

pkgname=denseflow
pkgver=0.1.0
pkgrel=3
pkgdesc='Extracting optical flow and frames'
arch=('x86_64')
url='https://github.com/open-mmlab/denseflow'
license=('BSD-3-Clause')
depends=(
  boost-libs
  cuda
  gcc-libs
  glibc
  hdf5
  opencv-cuda
)
makedepends=(
  boost
  cmake
  qt5-base
)
source=(
  "${pkgname}-${pkgver}.tar.gz::https://github.com/open-mmlab/denseflow/archive/refs/tags/v${pkgver}.tar.gz"
)
sha512sums=('37262dd107e0b80ed8bf735727d59c73bcc2dc79dfbdffbd9ff63139c590e63a4b3063c0138d0fea8ff7a87e30b3f329c09b1b5c8025912c86f40d113da5e51a')

build() {
  cmake \
    -B build \
    -S "${pkgname}-${pkgver}" \
    -DUSE_HDF5=ON \
    -DUSE_NVFLOW=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_INSTALL_RPATH:BOOL=ON
  make -C "build"
}

package() {
  make -C "${srcdir}/build" DESTDIR="${pkgdir}" install
  install -Dm644 "${pkgname}-${pkgver}/LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:

