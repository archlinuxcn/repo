# Maintainer: Hu Butui <hot123tea123@gmail.com>
# Contributor: Carlos Aznar√°n <caznaranl@uni.pe>
# Contributor: Simon Legner <Simon.Legner@gmail.com>

_pkgname=rasterio
pkgname=python-rasterio
pkgver=1.4.0rc1
pkgrel=1
pkgdesc="Fast and direct raster I/O for use with Numpy and SciPy"
url="https://github.com/rasterio/rasterio"
license=('LicenseRef-rasterio')
arch=('x86_64')
depends=(
  gcc-libs
  gdal
  glibc
  python-affine
  python-attrs
  python-click
  python-click-plugins
  python-cligj
  python-numpy
  python-setuptools
  python-snuggs
)
makedepends=(
  python-build
  python-installer
  cython
  python-wheel
)
optdepends=('ipython: for ipython support'
  'python-boto3: for s3 support'
  'python-matplotlib: for plotting support'
  'python-swiftclient: for OpenStack support')
source=(${_pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz)
sha512sums=('10796a6a0eecf76d6327141525dc72da189d349445778ad648099e89dff3b953e7819a344ee1af4259a9fed918ea3b1f114f409bb93936cb2e2f6dd68a51298b')

build() {
  cd ${_pkgname}-${pkgver}
  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd ${_pkgname}-${pkgver}
  PYTHONPYCACHEPREFIX="${PWD}/.cache/cpython/" python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
