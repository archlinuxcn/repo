# Maintainer: hekel <hekel(at)vivaldi(dot)net>

pkgname=kwin-effects-burn-my-windows-git
_pkgname=Burn-My-Windows-git
pkgver=r1543.4538ff3
pkgrel=1
pkgdesc="Disintegrate your windows with style."
arch=('any')
url="https://github.com/Schneegans/Burn-My-Windows"
license=('GPL-3.0-or-later')
depends=('kwin')
makedepends=('git')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("$_pkgname::git+$url.git")
sha256sums=('SKIP')

pkgver() {
  cd "$_pkgname"
  ( set -o pipefail
    git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

build() {
  cd "$_pkgname/kwin"
  sh build.sh
}

package() {
  cd "$_pkgname"
  install -dm0755 "$pkgdir/usr/share/kwin/effects"
  tar -xf "$srcdir/$_pkgname/kwin/burn_my_windows_kwin6.tar.gz" -C "$pkgdir/usr/share/kwin/effects"

  install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
