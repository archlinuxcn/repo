# Maintainer: Roald Clark <roaldclark@gmail.com>
# Contributor: Antonio Rojas <arojas@archlinux.org>

_name=kseexpr
_pkgname="${_name}-qt6"
pkgname="${_name}-qt6-git"
pkgver=6.0.0.0.20250718.013707
pkgrel=1
pkgdesc="An embeddable expression evaluation engine (Krita fork)"
arch=('x86_64')
url="https://invent.kde.org/graphics/${_name}"
license=('GPL-3.0-or-later')
depends=(
    'gcc-libs'
    'glibc'
    'qt6-base'
)
makedepends=(
    'extra-cmake-modules'
    'git'
    'qt6-tools'
)
provides=(
    "${_name}=${pkgver}"
    "${_pkgname}=${pkgver}"
)
conflicts=(
    "${_name}"
    "${_pkgname}"
)
source=("${_pkgname}::git+${url}.git")
sha256sums=('SKIP')

pkgver() {
    cd "${srcdir}/${_pkgname}"
    version="$(git describe --tags --abbrev=0 | sed 's/^v//')"
    [[ "${version}" =~ ^[0-9]+\.[0-9]+\.[0-9]+\.[0-9]+$ ]] || exit 1
    printf "%s.%s" "${version}" "$(TZ=UTC git log -1 --pretty='%cd' --date=format-local:%Y%m%d.%H%M%S)"
}

build() {
    cd "${srcdir}/${_pkgname}"
    cmake -S . -B build \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DBUILD_DEMOS=OFF \
        -DBUILD_TESTS=OFF
    cmake --build build --clean-first
}

package() {
    cd "${srcdir}/${_pkgname}"
    DESTDIR="${pkgdir}" cmake --install build
}
