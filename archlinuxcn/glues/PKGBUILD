# Maintainer: orhun <orhunparmaksiz@gmail.com>
# https://github.com/orhun/pkgbuilds

pkgname=glues
pkgver=0.5.4
pkgrel=1
pkgdesc="Vim-inspired TUI note-taking app with Git, MongoDB support"
arch=('x86_64')
url="https://github.com/gluesql/glues"
license=('Apache-2.0')
depends=("gcc-libs" "glibc")
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('0950ef6837d887180606df3dd25959c6188b307c9928447bdbdfdf160c7e9720d167a144952d11eabb54d4ed9d708b0bd9bcfba616f8dd3a0efafa44ed2a5353')
options=('!lto')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
}
