# Maintainer: orhun <orhunparmaksiz@gmail.com>
# https://github.com/orhun/pkgbuilds

pkgname=glues
pkgver=0.5.2
pkgrel=1
pkgdesc="Vim-inspired TUI note-taking app with Git, MongoDB support"
arch=('x86_64')
url="https://github.com/gluesql/glues"
license=('Apache-2.0')
depends=("gcc-libs" "glibc")
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('ae7cd3b83488e757015006732f43e92f6368eb67dd0b53087bd986a282b86ad33684a21e416dc58278abd6306f9328c1fcd4ad79bdfada58ff593f542bb7751f')
options=('!lto')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
}
