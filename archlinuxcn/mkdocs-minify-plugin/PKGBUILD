pkgname=mkdocs-minify-plugin
pkgver=0.8.0
pkgrel=1
pkgdesc="A mkdocs plugin to minify the HTML of a page before it is written to disk"
url="https://github.com/byrnereese/mkdocs-minify-plugin"
license=("MIT")
arch=("any")
depends=("mkdocs" "python-htmlmin2>=0.1.13" "python-jsmin" "python-csscompressor")
makedepends=("python-setuptools" "python-build" "python-installer" "python-wheel" git)
source=("source::git+$url#tag=${pkgver}")
sha256sums=('342a666de780235da91c7186781c03ab3a5992fd9d572119b49d4149549a1ed2')

build(){
	cd source
	python -m build --wheel --no-isolation
}

package(){
	cd source
	install -vDm644 -t \
		"${pkgdir}/usr/share/licenses/${pkgname}" LICENSE
	python -m installer --destdir="$pkgdir" dist/*.whl
}