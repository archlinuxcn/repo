# Maintainer: Butui Hu <hot123tea123@gmail.com>

_pkgname=skorch
pkgname=python-skorch
pkgver=1.3.0
pkgrel=1
pkgdesc='A scikit-learn compatible neural network library that wraps pytorch'
arch=(any)
url=https://github.com/skorch-dev/skorch
license=('BSD-3-Clause')
depends=(
  python-matplotlib
  python-numpy
  python-pandas
  python-pytorch
  python-scikit-learn
  python-scipy
  python-tabulate
  python-tqdm)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
source=("${_pkgname}-${pkgver}.tar.gz"::"https://github.com/skorch-dev/skorch/archive/v${pkgver}.tar.gz")
sha512sums=('dbc76aef13df39b8cea8e427c273e1b3c9af399d86246a437c58acda241ebf4d5f6eed29e73b6a927552591a1c3e28905c7f8f3e34f259090fc32578cae17063')

build() {
  cd "${_pkgname}-${pkgver}"
  python -m build --wheel --no-isolation
}

package() {
  cd "${_pkgname}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
