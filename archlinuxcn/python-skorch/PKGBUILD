# Maintainer: Butui Hu <hot123tea123@gmail.com>

_pkgname=skorch
pkgname=python-skorch
pkgver=1.3.1
pkgrel=1
pkgdesc='A scikit-learn compatible neural network library that wraps pytorch'
arch=(any)
url=https://github.com/skorch-dev/skorch
license=('BSD-3-Clause')
depends=(
  python-matplotlib
  python-numpy
  python-pandas
  python-pytorch
  python-scikit-learn
  python-scipy
  python-tabulate
  python-tqdm)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
source=("${_pkgname}-${pkgver}.tar.gz"::"https://github.com/skorch-dev/skorch/archive/v${pkgver}.tar.gz")
sha512sums=('b6ce64e29b3b936b4dd5bc73647ecc00f1d8b8e0139ec1ac394c90bb0700dfe3b3466b1bc29c4d59e895ca1b56ca182030ce56b2b414f9be47f267e5fb9d1fd0')

build() {
  cd "${_pkgname}-${pkgver}"
  python -m build --wheel --no-isolation
}

package() {
  cd "${_pkgname}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
