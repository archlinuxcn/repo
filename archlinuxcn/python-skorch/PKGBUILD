# Maintainer: Butui Hu <hot123tea123@gmail.com>

_pkgname=skorch
pkgname=python-skorch
pkgver=1.1.0
pkgrel=1
pkgdesc='A scikit-learn compatible neural network library that wraps pytorch'
arch=(any)
url=https://github.com/skorch-dev/skorch
license=('BSD-3-Clause')
depends=(
  python-matplotlib
  python-numpy
  python-pandas
  python-pytorch
  python-scikit-learn
  python-scipy
  python-tabulate
  python-tqdm)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
source=("${_pkgname}-${pkgver}.tar.gz"::"https://github.com/skorch-dev/skorch/archive/v${pkgver}.tar.gz")
sha512sums=('efb32a2ca95e077558bb356d077abff8f5a9e8aa48b45e068c8f3ad2e7909ec5008e264f411434cf2eebf45f6ec61bbc98043a1d2810839876c294ca1096651f')

build() {
  cd "${_pkgname}-${pkgver}"
  python -m build --wheel --no-isolation
}

package() {
  cd "${_pkgname}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
