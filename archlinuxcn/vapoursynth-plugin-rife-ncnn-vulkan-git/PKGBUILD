# Maintainer: Coelacanthus <CoelacanthusHex@gmail.com>
# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>

_plug=rife-ncnn-vulkan
pkgname=vapoursynth-plugin-${_plug}-git
pkgver=5.5.gb85b529
pkgrel=1
pkgdesc="Plugin for Vapoursynth: ${_plug} (GIT version)"
arch=('x86_64')
url='https://github.com/HomeOfVapourSynthEvolution/VapourSynth-RIFE-ncnn-Vulkan'
license=('MIT')
depends=('vapoursynth'
         'vulkan-icd-loader' 'libvulkan.so'
         )
makedepends=('git'
             'meson'
             'cmake'
             'vulkan-headers'
             )
provides=("vapoursynth-plugin-${_plug}")
conflicts=("vapoursynth-plugin-${_plug}")
source=("${_plug}::git+https://github.com/HomeOfVapourSynthEvolution/VapourSynth-RIFE-ncnn-Vulkan.git"
        'git+https://github.com/Tencent/ncnn.git'
        'git+https://github.com/KhronosGroup/glslang.git'
        )
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            )
options=('debug' 'strip')

pkgver() {
    cd "${_plug}"
    echo "$(git describe --long --tags | tr - . | tr -d r)"
}

prepare() {
    cd "${_plug}"
    git config submodule.subprojects/ncnn.url "${srcdir}/ncnn"
    git submodule update --init subprojects/ncnn

    sed "s|models|${_plug}-models|g" -i RIFE/plugin.cpp

    cd subprojects/ncnn
    git config submodule.glslang.url "${srcdir}/glslang"
    git submodule update --init glslang
}

build() {
    arch-meson "${_plug}" build \
        -Ddebug=true -Doptimization=3

    meson compile -C build
}

package(){
    meson install -C build --no-rebuild --destdir="${pkgdir}"

    mv "${pkgdir}/usr/lib/vapoursynth/models" "${pkgdir}/usr/lib/vapoursynth/${_plug}-models"

    install -Dm644 "${_plug}/README.md" "${pkgdir}/usr/share/doc/vapoursynth/plugins/${_plug}/README.md"
    install -Dm644 "${_plug}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
