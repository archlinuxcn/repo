# Maintainer: Coelacanthus <CoelacanthusHex@gmail.com>
# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>

_plug=rife-ncnn-vulkan
pkgname=vapoursynth-plugin-${_plug}-git
pkgver=9.r0.g6c6d0fa
pkgrel=4
epoch=1
pkgdesc="Plugin for Vapoursynth: ${_plug} (GIT version)"
arch=('x86_64')
url='https://github.com/HomeOfVapourSynthEvolution/VapourSynth-RIFE-ncnn-Vulkan'
license=('MIT')
depends=('vapoursynth'
         'ncnn'
         'gcc-libs'
         )
makedepends=('git'
             'meson'
             'cmake'
             'vulkan-icd-loader'
             'vulkan-headers'
             )
provides=("vapoursynth-plugin-${_plug}")
conflicts=("vapoursynth-plugin-${_plug}")
source=("${_plug}::git+https://github.com/HomeOfVapourSynthEvolution/VapourSynth-RIFE-ncnn-Vulkan.git"
        )
sha256sums=('SKIP'
            )
options=('debug' 'strip')

pkgver() {
    cd "${_plug}"
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/^r//'
}

prepare() {
    cd "${_plug}"
    sed "s|models|${_plug}-models|g" -i RIFE/plugin.cpp
}

build() {
    arch-meson "${_plug}" build \
        -Ddebug=true -Doptimization=3 \
        -Duse_system_ncnn=true

    meson compile -C build
}

package(){
    meson install -C build --no-rebuild --destdir="${pkgdir}"

    mv "${pkgdir}/usr/lib/vapoursynth/models" "${pkgdir}/usr/lib/vapoursynth/${_plug}-models"

    install -Dm644 "${_plug}/README.md" "${pkgdir}/usr/share/doc/vapoursynth/plugins/${_plug}/README.md"
    install -Dm644 "${_plug}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
