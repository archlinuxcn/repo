# Maintainer: Butui Hu <hot123tea123@gmail.com>

pkgname=python-visions
_pkgname=visions
pkgver=0.7.6
pkgrel=3
pkgdesc='Type System for Data Analysis in Python'
arch=('any')
url='https://github.com/dylan-profiler/visions'
license=('BSD-4-Clause')
depends=(
  python-attrs
  python-matplotlib
  python-multimethod
  python-networkx
  python-numpy
  python-pandas
  python-pillow
  python-tangled-up-in-unicode
)
makedepends=(
  mypy
  python-black
  python-build
  python-installer
  python-isort
  python-recommonmark
  python-setuptools
  python-sphinx-autodoc-typehints
  python-sphinx_rtd_theme
  python-wheel
)
source=("${_pkgname}-${pkgver}.tar.gz::https://files.pythonhosted.org/packages/source/${_pkgname::1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
sha512sums=('0ae1b6dd9778c1fcfb16dd6f88d49b59df05655eea21472026ddb749d4febdadc35990a514d6eaccd3a2f7a118381f66437a0bbb0069cafc1e1251e6169ce224')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python -m build --wheel --no-isolation
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
