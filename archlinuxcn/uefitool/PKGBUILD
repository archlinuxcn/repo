# Maintainer: Yurii Kolesnykov <root@yurikoles.com>
# Based on uefitool-ng-git by Bailey Fox <bfox200012@gmail.com>
# Contributor: Integral <integral@member.fsf.org>

pkgname=uefitool
_pkgname=UEFITool
pkgver=A70
pkgrel=1
epoch=2
pkgdesc="UEFI firmware image viewer and editor and utilities"
arch=('armv7h' 'aarch64' 'i686' 'x86_64')
url="https://github.com/LongSoft/${_pkgname}"
license=('BSD-2-Clause')
depends=('qt6-base')
makedepends=('cmake')
provides=('uefipatch')
conflicts=('uefipatch')
replaces=('uefipatch')
source=("${_pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha256sums=('30da5061314d5c408c93d8f630408bca5ac28833c2ed4a4ef102517286e15625')

build() {
	cmake -B build -S "${_pkgname}-${pkgver}" -DCMAKE_INSTALL_PREFIX=/usr
	cmake --build build
}

package() {
	DESTDIR="${pkgdir}" cmake --install build
	install -Dm644 "${_pkgname}-${pkgver}/LICENSE.md" -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
