# Maintainer: Yurii Kolesnykov <root@yurikoles.com>
# Based on uefitool-ng-git by Bailey Fox <bfox200012@gmail.com>
# Contributor: Integral <integral@member.fsf.org>

pkgname=uefitool
_pkgname=UEFITool
pkgver=A71
pkgrel=1
epoch=2
pkgdesc="UEFI firmware image viewer and editor and utilities"
arch=('armv7h' 'aarch64' 'i686' 'x86_64')
url="https://github.com/LongSoft/${_pkgname}"
license=('BSD-2-Clause')
depends=('qt6-base')
makedepends=('cmake')
provides=('uefipatch')
conflicts=('uefipatch')
replaces=('uefipatch')
source=("${_pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha256sums=('87ad768161a579d08c97c47ad7ab383668487fb65ac29e0079f80876513e3a7e')

build() {
	cmake -B build -S "${_pkgname}-${pkgver}" -DCMAKE_INSTALL_PREFIX=/usr
	cmake --build build
}

package() {
	DESTDIR="${pkgdir}" cmake --install build
	install -Dm644 "${_pkgname}-${pkgver}/LICENSE.md" -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
