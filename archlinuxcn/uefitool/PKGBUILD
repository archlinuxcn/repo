# Maintainer: Integral <integral@member.fsf.org>
# Contributor: Yurii Kolesnykov <root@yurikoles.com>
# Based on uefitool-ng-git by Bailey Fox <bfox200012@gmail.com>
# Contributor: Integral <integral@member.fsf.org>

pkgname=uefitool
_pkgname=UEFITool
pkgver=A73
pkgrel=1
epoch=2
pkgdesc="UEFI firmware image viewer and editor and utilities"
arch=('armv7h' 'aarch64' 'i686' 'x86_64')
url="https://github.com/LongSoft/${_pkgname}"
license=('BSD-2-Clause')
depends=('qt6-base')
makedepends=('cmake')
provides=('uefipatch')
conflicts=('uefipatch')
replaces=('uefipatch')
source=("${_pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha256sums=('81d9944d75e27d4afbc8559d9ba913552006fd21ce9cadf357a4a0f58e59507e')

build() {
	cmake -B build \
		-S "${_pkgname}-${pkgver}" \
		-D CMAKE_BUILD_TYPE=None \
		-D CMAKE_INSTALL_PREFIX=/usr

	cmake --build build
}

package() {
	DESTDIR="${pkgdir}" cmake --install build
	install -Dm644 "${_pkgname}-${pkgver}/LICENSE.md" -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
