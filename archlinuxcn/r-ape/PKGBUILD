# Maintainer: Hu Butui <hot123tea123@gmail.com>

_pkgname=ape
_pkgver=5.5
pkgname=r-${_pkgname,,}
pkgver=5.5
pkgrel=1
pkgdesc='Analyses of Phylogenetics and Evolution'
arch=('x86_64')
url="https://cran.r-project.org/package=${_pkgname}"
license=('GPL')
depends=(
  r
  r-rcpp
)
optdepends=(
  r-expm
  r-gee
  r-igraph
  r-phangorn
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
sha256sums=('a3aa01c74b99eafec7d98284e05957b6487b6971ced93f26881f2479bcd5299a')

build() {
  R CMD INSTALL ${_pkgname}_${_pkgver}.tar.gz -l "${srcdir}"
}

package() {
  install -dm0755 "${pkgdir}/usr/lib/R/library"
  cp -a --no-preserve=ownership "${_pkgname}" "${pkgdir}/usr/lib/R/library"
}
# vim:set ts=2 sw=2 et:
