# Maintainer: Integral <integral@member.fsf.org>

pkgname=payload-dumper-go
pkgver=1.2.2
pkgrel=2
epoch=
pkgdesc="An Android OTA payload dumper written in Go"
arch=('x86_64' 'aarch64' 'riscv64')
url="https://github.com/ssut/${pkgname}"
license=('Apache-2.0')
makedepends=("go>=1.14")
options=(!buildflags)
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/$pkgver.tar.gz")
sha256sums=('7f80f6c29ad8b835d71f361ba073988a27a33043acec37eea9d9430c1fb04b57')

prepare() {
	export GOPATH="${srcdir}/gopath"

	cd "${pkgname}-${pkgver}/"
	go mod tidy
	go mod download
}

build() {
	export GOPATH="${srcdir}/gopath"
	export CGO_CPPFLAGS="${CPPFLAGS}"
	export CGO_CFLAGS="${CFLAGS}"
	export CGO_CXXFLAGS="${CXXFLAGS}"
	export CGO_LDFLAGS="${LDFLAGS}"

	cd "${pkgname}-${pkgver}/"
	CGO_ENABLE=0 go build -trimpath -o "${pkgname}" .
}

package() {
	cd "${pkgname}-${pkgver}/"
	install -Dm755 "${pkgname}" -t "${pkgdir}/usr/bin/"
	install -Dm644 LICENSE -t "${pkgdir}/usr/share/${pkgname}/"
}
