# Maintainer:  Peter Mattern <pmattern@arcor.de>

_pkgname=lxqt-themes
pkgname=$_pkgname-git
pkgver=r135.3b9f1de
pkgrel=1
pkgdesc='Themes, graphics and icons for LXQt'
arch=('any')
url='https://github.com/lxde/lxqt-themes'
license=('LGPL')
depends=('hicolor-icon-theme')
makedepends=('git' 'cmake' 'lxqt-build-tools-git')
provides=("$_pkgname")
conflicts=("$_pkgname" "lxqt-common"{,-git})
source=("git+https://github.com/lxde/$_pkgname.git")
sha256sums=('SKIP')

pkgver() {
      cd $_pkgname
      printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
      mkdir -p build
      cd build
      cmake "$srcdir/$_pkgname" -DCMAKE_INSTALL_PREFIX=/usr
      make
}

package() {
      cd build
      make DESTDIR="$pkgdir" install
}
