# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Christian Heusel <gromit@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

_pkgname=devtools
pkgname=devtools-archlinuxcn
epoch=1
pkgver=1.2.0
pkgrel=1
# curl https://api.github.com/repos/archlinuxcn/devtools/git/ref/tags/$pkgver-archlinuxcn1 | jq -r .object.sha
_tag=74fb845cf28be24f73fbb1b096fad64e4f725d2c
_upstream_pkgrel=1
pkgdesc='Tools for Arch Linux package maintainers, archlinuxcn fork'
arch=('any')
license=('GPL-3.0-or-later')
url='https://gitlab.archlinux.org/archlinux/devtools'
depends=(
  arch-install-scripts
  awk
  bash
  binutils
  coreutils
  curl
  diffutils
  expac
  fakeroot
  findutils
  grep
  jq
  openssh
  parallel
  rsync
  sed
  util-linux

  breezy
  git
  mercurial
  subversion
)
makedepends=(
  asciidoctor
  shellcheck
)
checkdepends=(
  bats
)
optdepends=(
  'btrfs-progs: btrfs support'
  'bat: pretty printing for pkgctl search'
  'nvchecker: pkgctl version subcommand'
)
provides=("devtools=$pkgver-$pkgrel")
conflicts=("devtools")
source=("$pkgname::git+https://github.com/archlinuxcn/devtools.git?signed#tag=$_tag")
b2sums=('SKIP')
validpgpkeys=(
  'E62545315B012B69C8C94A1D56EC201BFC794362'  # https://github.com/yan12125
)

pkgver() {
  cd ${pkgname}
  git describe --tags | sed -E 's/-archlinuxcn[0-9]+//'
}

build() {
  cd ${pkgname}
  make BUILDTOOLVER="${epoch}:${pkgver}-${_upstream_pkgrel}-${arch}" PREFIX=/usr
}

check() {
  cd ${pkgname}
  make PREFIX=/usr test
}

package() {
  cd ${pkgname}
  make PREFIX=/usr DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
