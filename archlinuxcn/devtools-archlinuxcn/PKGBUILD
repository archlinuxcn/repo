# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Christian Heusel <gromit@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

_pkgname=devtools
pkgname=devtools-archlinuxcn
epoch=1
pkgver=1.2.1
pkgrel=2
_tag=$pkgver-archlinuxcn1
_upstream_pkgrel=1
pkgdesc='Tools for Arch Linux package maintainers, archlinuxcn fork'
arch=('any')
license=('GPL-3.0-or-later')
url='https://gitlab.archlinux.org/archlinux/devtools'
depends=(
  arch-install-scripts
  awk
  bash
  binutils
  coreutils
  curl
  diffutils
  expac
  fakeroot
  findutils
  grep
  jq
  openssh
  parallel
  rsync
  sed
  util-linux

  breezy
  git
  mercurial
  subversion
)
makedepends=(
  asciidoctor
  shellcheck
)
checkdepends=(
  bats
)
optdepends=(
  'btrfs-progs: btrfs support'
  'bat: pretty printing for pkgctl search'
  'nvchecker: pkgctl version subcommand'
)
provides=("devtools=$pkgver-$pkgrel")
conflicts=("devtools")
source=("$pkgname::git+https://github.com/archlinuxcn/devtools.git#tag=$_tag"
        "ssh_allowed_signers")
b2sums=('07c725a8b12c1327c088c37d9f5e1a76cab05daac536ca0dada7ca86a23cf41b23c863cfa2f62a395d9a19d73624258a095b97e6fadf25a2c53083884fb75262'
        '6c9fab6619bcc3ab0d1bf0944a6dc53296caa1a2dccb0cf833957c5820f3f47bae44d26ed85edf294b4efffb18b59773cfb7f89ea961c582556b4a28ee1f9d0c')

# XXX: move to verify() when devtools supports it
# https://gitlab.archlinux.org/archlinux/devtools/-/issues/224
prepare() {
  git -C ${pkgname} -c gpg.ssh.allowedSignersFile="$srcdir/ssh_allowed_signers" verify-tag $_tag
}

pkgver() {
  cd ${pkgname}
  git describe --tags | sed -E 's/-archlinuxcn[0-9]+//'
}

build() {
  cd ${pkgname}
  make BUILDTOOLVER="${epoch}:${pkgver}-${_upstream_pkgrel}-${arch}" PREFIX=/usr
}

check() {
  cd ${pkgname}
  make PREFIX=/usr test
}

package() {
  cd ${pkgname}
  make PREFIX=/usr DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
