# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Christian Heusel <gromit@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

_pkgname=devtools
pkgname=devtools-archlinuxcn
epoch=1
pkgver=1.5.0
pkgrel=1
_tag=$pkgver-archlinuxcn1
_upstream_pkgrel=1
pkgdesc='Tools for Arch Linux package maintainers, archlinuxcn fork'
arch=('any')
license=('GPL-3.0-or-later')
url='https://gitlab.archlinux.org/archlinux/devtools'
depends=(
  arch-install-scripts
  awk
  bash
  binutils
  coreutils
  curl
  diffutils
  expac
  fakeroot
  findutils
  glow
  grep
  gum
  jq
  openssh
  parallel
  reuse
  rsync
  sed
  util-linux
  sudo
  breezy
  git
  mercurial
  subversion
)
makedepends=(
  asciidoctor
)
checkdepends=(
  bats
)
optdepends=(
  'btrfs-progs: btrfs support'
  'bat: pretty printing for pkgctl search'
  'nvchecker: pkgctl version subcommand'
)
provides=("devtools=$pkgver-$pkgrel")
conflicts=("devtools")
source=("$pkgname::git+https://github.com/archlinuxcn/devtools.git#tag=$_tag"
        "ssh_allowed_signers")
sha256sums=('ad28c6604539a2f52779a379e234f2aa4e8df109c0e9c99d0806c2c2fdf78872'
            'b83e8c654e3af93d6bb173db51d59f2f422fc5c777efe4253b26cfbaba6f0943')
b2sums=('1372f345809edb9a16ffa8f7e2ec1594fbb86121a506e82908b17a4253787f287c607ef5df4703c94b9296557c0b665cc3bbcd7248cd40709ff9c891440d8a49'
        '6c9fab6619bcc3ab0d1bf0944a6dc53296caa1a2dccb0cf833957c5820f3f47bae44d26ed85edf294b4efffb18b59773cfb7f89ea961c582556b4a28ee1f9d0c')

# XXX: move to verify() when devtools supports it
# https://gitlab.archlinux.org/archlinux/devtools/-/issues/224
prepare() {
  git -C ${pkgname} -c gpg.ssh.allowedSignersFile="$srcdir/ssh_allowed_signers" verify-tag $_tag
}

pkgver() {
  cd ${pkgname}
  git describe --tags | sed -E 's/-archlinuxcn[0-9]+//'
}

build() {
  cd ${pkgname}
  make BUILDTOOLVER="${epoch}:${pkgver}-${_upstream_pkgrel}-${arch}" PREFIX=/usr
}

check() {
  cd ${pkgname}
  make PREFIX=/usr test
}

package() {
  cd ${pkgname}
  make PREFIX=/usr DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
