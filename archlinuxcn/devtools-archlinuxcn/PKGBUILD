# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_pkgname=devtools
pkgname=devtools-archlinuxcn
pkgver=20221012
pkgrel=4
# curl https://api.github.com/repos/archlinuxcn/devtools/git/ref/tags/$pkgver-archlinuxcn2 | jq -r .object.sha
_tag=1249cadb2edb5aa73477eb248dc854fcf365b479
_upstream_pkgrel=2
pkgdesc='Tools for Arch Linux package maintainers, archlinuxcn fork'
arch=('any')
license=('GPL')
url='https://gitlab.archlinux.org/archlinux/devtools'
depends=('bash' 'openssh' 'subversion' 'rsync' 'arch-install-scripts'
         'git' 'bzr' 'mercurial' 'diffutils' 'util-linux' 'awk')
makedepends=('asciidoc')
optdepends=('btrfs-progs: btrfs support')
provides=("devtools=$pkgver-$pkgrel")
conflicts=("devtools")
source=("$pkgname::git+https://github.com/archlinuxcn/devtools.git?signed#tag=$_tag")
b2sums=('SKIP')
validpgpkeys=(
  'E62545315B012B69C8C94A1D56EC201BFC794362'  # https://github.com/yan12125
)

pkgver() {
  cd ${pkgname}
  git describe --tags | sed -E 's/-archlinuxcn[0-9]+//'
}

build() {
  cd ${pkgname}
  make BUILDTOOLVER="${pkgver}-${_upstream_pkgrel}-${arch}" PREFIX=/usr
}

package() {
  cd ${pkgname}
  make PREFIX=/usr DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
