# Maintainer: Hu Butui <hot123tea123@gmail.com>
# Contributor: morguldir <morguldir@protonmail.com>

_pkgname=yappi
pkgname=python-yappi
pkgver=1.7.3
pkgrel=1
pkgdesc='Yet Another Python Profiler, but this time thread & coroutine & greenlet aware'
url='https://github.com/sumerc/yappi'
license=('MIT')
arch=('x86_64')
depends=(
  python
)
makedepends=(
    python-setuptools
)
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/sumerc/yappi/archive/refs/tags/${pkgver}.tar.gz")
sha256sums=('46e2a82bbd0c60c6b4f3fe3a58c9983ec04e7fdb715c0937f7d5a52e6e111f7f')

build() {
    cd "${_pkgname}-${pkgver}"
    python setup.py build
}

package() {
    cd "${_pkgname}-${pkgver}"
    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
# vim:set ts=2 sw=2 et:
