# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>
# Contributor: Thomas Wei√üschuh <thomas t-8ch de>

pkgname=logcat-color3
pkgver=0.9.1
pkgrel=2
pkgdesc='A colorful and highly configurable alternative to the standard "adb logcat" command from the Android SDK'
arch=(any)
url='https://github.com/yan12125/logcat-color3'
license=(Apache)
depends=(python-colorama)
makedepends=(python-setuptools-scm python-wheel)
conflicts=(logcat-color)
source=("https://files.pythonhosted.org/packages/source/l/logcat-color3/logcat-color3-$pkgver.tar.gz"{,.asc})
sha256sums=('800e852869e5c00c07b14cdbae77b8c76091727faeb76c0c03fd0b639e37a497'
            'SKIP')
validpgpkeys=(
  '481C4474AF1572165AE4C6AF3FDDD575826C5C30'  # Chih-Hsuan Yen <yan12125@gmail.com>
)

build() {
  cd logcat-color3-$pkgver
  python setup.py build
}

check() {
  cd logcat-color3-$pkgver
  python -m unittest discover ./test
}

package() {
  cd logcat-color3-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
