# Maintainer: Bumsik Kim <k.bumsik@gmail.com>
# Contributors: Ariel AxionL <i at axionl dot me>
# Scripts inspired by nushell-git and nushell packages

pkgname=nushell-git
pkgver=0.91.1.r8633.g0cf7de598b
pkgrel=1
pkgdesc="A modern shell written in Rust"
arch=('x86_64')
url="https://www.nushell.sh"
depends=('openssl' 'zlib' 'curl' 'libxcb')
makedepends=('cargo' 'git' 'libx11' 'python')
provides=('nu' 'nushell')
conflicts=('nushell')
options+=(!lto)
license=('MIT')
source=("$pkgname::git+https://github.com/nushell/nushell.git#branch=main"
		"LICENSE::https://raw.githubusercontent.com/nushell/nushell/main/LICENSE")
sha256sums=('SKIP'
            '57fdb6634a2372af0978dd178f8e157f227d96f3ca27ae9c73f1320f6c0c41d4')

pkgver() {
	cd "${pkgname}"
	printf "%s.r%s.g%s" $(grep '^version =' Cargo.toml|head -n1|cut -d\" -f2) $(git rev-list --count HEAD) $(git rev-parse --short HEAD)
}

build() {
	cd "${pkgname}"
	cargo build --release --features stable
}

package() {
	cd "${srcdir}"
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

	cd "${pkgname}"
	install -Dm755 "target/release/nu" "${pkgdir}/usr/bin/nu"
	ln -sf "/usr/bin/nu" "${pkgdir}/usr/bin/nushell"
}
