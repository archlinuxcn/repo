# Maintainer: oldherl <oldherl@gmail.com>
# Contributor: Miguel de Val-Borro <miguel dot deval at gmail dot com>
# Contributor: Astro Benzene <universebenzene at sina dot com>

pkgname=astrometry.net
pkgver=0.96
pkgrel=3
pkgdesc="Automatic recognition of astronomical images. Compiled with -march=x86-64 -mtune=core2 for most modern x86-64 CPUs."
arch=('x86_64')
url="https://astrometry.net/"
license=("GPL-3.0-or-later")
depends=('bzip2' 'cairo' 'libpng' 'libjpeg-turbo' 'python-astropy' 'netpbm' 'cfitsio' 'gsl' 'swig')
makedepends=('python-setuptools')
optdepends=(
'astrometry.net-data-4108-wide: index data for wide field'
'astrometry.net-data-5007: index data for features 22 to 30 arcmin'
'astrometry.net-data-5006: index data for features 16 to 22 arcmin'
)
backup=(etc/astrometry.cfg)
install=astrometry.net.install
source=("https://github.com/dstndstn/astrometry.net/releases/download/${pkgver}/astrometry.net-${pkgver}.tar.gz"
)
sha1sums=('d4c975fc45f83649aef7e23003ee7c8b70f8c35b')

prepare() {
    cd ${pkgname}-${pkgver}
    # for fixing numpy 2.0
    #patch -p1 -i ../298.patch
    #patch -p1 -i ../299.patch
    rm util/util_wrap.c
    #grep -RnI elsize . || echo 0
}

build() {
    cd ${pkgname}-${pkgver}
    # Parrallel build is broken and upstream is not going to fix it.
    # https://github.com/dstndstn/astrometry.net/issues/178#issuecomment-650149535
    # If no ARCH_FLAGS, it would compile with -march=native and not usable on older CPUs.
    ARCH_FLAGS='-march=x86-64 -mtune=core2' make -j 1 SYSTEM_GSL=yes all py extra
}

check() {
    cd ${pkgname}-${pkgver}
    make -j 1 SYSTEM_GSL=yes test
}

package() {
    cd ${pkgname}-${pkgver}
    _pyver=$(python -c 'import sys; print("%d.%d" % sys.version_info[:2])')

    make INSTALL_DIR="${pkgdir}/usr" \
         ETC_INSTALL_DIR="${pkgdir}/etc" \
         DOC_INSTALL_DIR="${pkgdir}/usr/share/doc/astrometry" \
         EXAMPLE_INSTALL_DIR="${pkgdir}/usr/share/astrometry/examples" \
         DATA_INSTALL_DIR="${pkgdir}/usr/share/astrometry/data" \
         PY_BASE_INSTALL_DIR="${pkgdir}/usr/lib/python${_pyver}/site-packages/astrometry" \
         PY_BASE_LINK_DIR="../lib/python${_pyver}/site-packages/astrometry" \
         install

    # fix data path and enable inparallel by default
    sed -e "s|${pkgdir}/usr/data|/usr/share/astrometry/data|" -e 's|#inparallel|inparallel|' -i ${pkgdir}/etc/astrometry.cfg
    # remove useless file
    rm ${pkgdir}/usr/share/doc/astrometry/report.txt
    # those executables are included in cfitsio package
    rm ${pkgdir}/usr/bin/fitscopy
    rm ${pkgdir}/usr/bin/imcopy
    rm ${pkgdir}/usr/bin/fitsverify
}
