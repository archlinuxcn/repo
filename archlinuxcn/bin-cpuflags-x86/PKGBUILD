# Maintainer: Integral <integral@archlinuxcn.org>

pkgname=bin-cpuflags-x86
pkgver=1.0.6
pkgrel=1
pkgdesc="A small CLI tool to detect CPU flags (instruction sets) of X86 binaries"
arch=('x86_64')
url="https://github.com/HanabishiRecca/${pkgname}"
license=('MIT')
depends=('gcc-libs')
makedepends=('cargo')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/${pkgver}.tar.gz")
sha256sums=('0f150346d0148b296fb3824bc6d13a6854c1eb9342ada033f6559bd5f0b75dd1')

prepare() {
	cd "${pkgname}-${pkgver}/"
	cargo fetch --locked --target $(rustc --print host-tuple)
}

build() {
	cd "${pkgname}-${pkgver}/"
	cargo build --frozen --release --all-features
}

check() {
	cd "${pkgname}-${pkgver}/"
	cargo test --frozen --all-features
}

package() {
	cd "${pkgname}-${pkgver}/"
	install -Dm755 "target/release/${pkgname}" -t "${pkgdir}/usr/bin/"
	install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
