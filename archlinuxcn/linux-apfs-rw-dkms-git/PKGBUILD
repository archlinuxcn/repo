# Maintainer: Melvyn <melvyn2@brcok.tk>
# Maintainer: HurricanePootis <hurricanepootis@protonmail.com>
# Forked from: aur/linux-apfs-dkms-git
# Contributor: Chih-Hsuan Yen <yan12125@archlinux.org>
# Contributor: ManU
# Forked from aur/linux-can-dkms
# Contributor: Kyle Manna <kyle(at)kylemanna(dot)com>

pkgname=linux-apfs-rw-dkms-git
_pkgname=${pkgname%-dkms-git}
epoch=1
pkgver=r292.41af006
pkgrel=1
pkgdesc="Experimental APFS kernel module with Write support (DKMS)"
arch=('any')
url="https://github.com/linux-apfs/${_pkgname}"
license=('GPL-2.0-only')
depends=('dkms')
makedepends=('git')
source=("git+${url}.git"
        "version.patch")
sha256sums=('SKIP'
            '8a2814889a0a8a016d45cc2d7c032aec4a9f6eaa0a8551166c0124ce35bf766c')

prepare() {
  cd "$srcdir/linux-apfs-rw"
  patch -p1 < "$srcdir/version.patch"
}

pkgver() {
	cd "${_pkgname}/"
	git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/v//'
}

package() {
	cd "${_pkgname}/"
	dkms_version=$(grep PACKAGE_VERSION dkms.conf | sed -r 's#PACKAGE_VERSION="([0-9.]+)"#\1#')
	dkms_dir="${pkgdir}/usr/src/linux-apfs-rw-${dkms_version}/"
	install -Ddm755 "${dkms_dir}"
	cp -dr --no-preserve=ownership * "${dkms_dir}"
}
