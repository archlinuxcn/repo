# Maintainer: Jguer <pkgbuilds at jguer.space>
pkgname=yay
pkgver=12.5.2
pkgrel=2
pkgdesc="Yet another yogurt. Pacman wrapper and AUR helper written in go."
arch=('i686' 'pentium4' 'x86_64' 'arm' 'armv7h' 'armv6h' 'aarch64' 'riscv64')
url="https://github.com/Jguer/yay"
options=(!lto)
license=('GPL-3.0-or-later')
depends=(
  'pacman>6.1'
  'git'
)
optdepends=(
  'sudo: privilege elevation'
  'doas: privilege elevation'
)
makedepends=('go>=1.24')
source=(
  "${pkgname}-${pkgver}.tar.gz::https://github.com/Jguer/yay/archive/v${pkgver}.tar.gz"
  "yay-fix-gibberish-chars-when-listing-aur.patch"
)
sha256sums=(
  'f1a089862b56a0ecd04c28d3e10d9964dc54c8477b38ffcf8a820ece11e80580'
  'b8f1b79a462a8af3429cd42bd218cacf2e22bd7476a27bb853a5d7fcb6f2c8f0'
)

prepare() {
  cd "$pkgname-$pkgver"
  patch -Np1 -i ../yay-fix-gibberish-chars-when-listing-aur.patch
}

build() {
  export GOPATH="$srcdir"/gopath
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_ENABLED=1

  cd "$srcdir/$pkgname-$pkgver"
  make VERSION=$pkgver DESTDIR="$pkgdir" PREFIX="/usr" build
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make VERSION=$pkgver DESTDIR="$pkgdir" PREFIX="/usr" install
}
