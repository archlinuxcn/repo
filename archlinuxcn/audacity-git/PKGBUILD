# Maintainer: Ong Yong Xin <ongyongxin2020+github@gmail.com>
# Contributor: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix
# Contributor: Bernhard Landauer <oberon@manjaro.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=audacity-git
_pkgname=${pkgname%-git}
pkgver=3.6.4.r459.gb1df79366b
pkgrel=1
pkgdesc="A program that lets you manipulate digital audio waveforms"
arch=('i686' 'x86_64')
url="https://www.audacityteam.org"
license=('(GPL-3.0-only OR GPL-2.0-or-later) AND CC-BY-3.0')
groups=('pro-audio-git')
depends=(
	'alsa-lib'
	'expat'
	'flac'
	'gcc-libs'
	'gdk-pixbuf2'
	'glib2'
	'gtk3'
	'hicolor-icon-theme'
	'jack'
	'lame'
	'libid3tag'
	'libmad'
	'libogg'
	'libsbsms'
	'libsndfile'
	'libsoxr'
	'libvorbis'
	'libx11'
	'lilv'
	'mpg123'
	'opusfile'
	'portaudio'
	'portmidi'
	'portsmf'
	'python'
	'rapidjson'
	'soundtouch'
	'sqlite'
	'suil'
	'twolame'
	'util-linux-libs'
	'wxwidgets-gtk3'
	'vamp-plugin-sdk'
	'wavpack'
	'wxwidgets-common'
	'qt6-base'
	'qt6-networkauth'
	'qt6-declarative'
	'qt6-svg'
	'qt6-5compat'
)
makedepends=(
	'cmake'
	'git'
	'nasm'
	'rapidjson'
	'vst3sdk'
	'xcb-util'
	'xcb-util-cursor'
	'xcb-util-keysyms'
	'gtkmm3'
	'libxkbcommon-x11'
	'qt6-tools'
)
optdepends=(
	'audacity-docs: for documentation'
	'ffmpeg: for additional import/export capabilities'
)
provides=(
	"${_pkgname}"
	"ladspa-host"
	"lv2-host"
	"vamp-host"
	"vst-host"
	"vst3-host"
)
conflicts=("${_pkgname}")
source=("git+https://github.com/${_pkgname}/${_pkgname}.git")
sha256sums=('SKIP')

pkgver() {
	cd "${_pkgname}/"
	git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g' | cut -d'.' -f2-
}

build() {
	export VST3SDK='/usr/src/vst3sdk'
	export CFLAGS+=" -DNDEBUG"
	export CXXFLAGS+=" -DNDEBUG"

	cmake -B build \
		-D CMAKE_BUILD_TYPE=None \
		-D CMAKE_INSTALL_PREFIX=/usr \
		-D AUDACITY_BUILD_LEVEL=2 \
		-D audacity_conan_enabled=OFF \
		-D audacity_has_networking=OFF \
		-D audacity_has_crashreports=OFF \
		-D audacity_has_updates_check=OFF \
		-D audacity_has_sentry_reporting=OFF \
		-D audacity_lib_preference=system \
		-D audacity_obey_system_dependencies=ON \
		-D audacity_use_vst3sdk=system \
		-D audacity_has_tests=ON \
		-S "${_pkgname}" \
		-W no-dev

	cmake --build build --verbose
}

check() {
	# disable two tests which do not work without an alsa/jack instance
	ctest --test-dir build --output-on-failure --exclude-regex "lib-stretching-sequence|journal_sanity"
}

package() {
	DESTDIR="${pkgdir}" cmake --install build
	install -Dm644 "${_pkgname}/lib-src/libnyquist/nyquist/license.txt" "${pkgdir}/usr/share/licenses/${_pkgname}/LICENSE.nyquist"
}
