# Maintainer: Ong Yong Xin <ongyongxin2020+github@gmail.com>
# Contributor: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix
# Contributor: Bernhard Landauer <oberon@manjaro.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=audacity-git
_pkgname=${pkgname%-git}
pkgver=3.4.2.r355.ga49f64735d
pkgrel=1
pkgdesc="A program that lets you manipulate digital audio waveforms"
arch=('i686' 'x86_64')
url="https://www.audacityteam.org/"
license=('(GPL-3.0-only OR GPL-2.0-or-later) AND CC-BY-3.0')
groups=('pro-audio-git')
depends=(
	'alsa-lib'
	'expat'
	'flac'
	'glib2'
	'gtk3'
	'hicolor-icon-theme'
	'jack'
	'lame'
	'libid3tag'
	'libmad'
	'libogg'
	'libsbsms'
	'libsndfile'
	'libsoxr'
	'libvorbis'
	'libx11'
	'lilv'
	'mpg123'
	'portaudio'
	'portmidi'
	'portsmf'
	'soundtouch'
	'sqlite'
	'suil'
	'twolame'
	'util-linux-libs'
	'vamp-plugin-sdk'
	'wavpack'
	'wxwidgets-gtk3'
)
makedepends=('cmake' 'git' 'nasm' 'rapidjson' 'vst3sdk' 'xcb-util' 'xcb-util-cursor' 'xcb-util-keysyms' 'gtkmm3' 'libxkbcommon-x11' 'opusfile')
optdepends=('ffmpeg: additional import/export capabilities')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=("git+https://github.com/${_pkgname}/${_pkgname}.git")
sha256sums=('SKIP')

pkgver() {
	cd "${_pkgname}/"
	git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g' | cut -d'.' -f2-
}

build() {
	export CC=gcc
	export VST3_SDK_DIR="/usr/src/vst3sdk"

	cmake_args=(
		-D CMAKE_BUILD_TYPE=Release
		-D CMAKE_INSTALL_PREFIX=/usr
		-D audacity_lib_preference=system
		-D audacity_obey_system_dependencies=On
		-D audacity_conan_enabled=Off
		-D audacity_use_wxwidgets=system
	)

	cmake -S ${_pkgname} -B build "${cmake_args[@]}"
	cmake --build build
}

package() {
	cd build
	make DESTDIR="${pkgdir}" install
}
