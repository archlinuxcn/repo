# Maintainer: erk <v at erk dot io>
pkgname=vnote-git
pkgver=1557.7c768c44
epoch=1
pkgrel=1
pkgdesc="A Vim-inspired note-taking application, especially for Markdown."
arch=('any')
url='https://tamlok.github.io/vnote/'
license=('MIT')
depends=('qt5-base' 'qt5-webengine')
makedepends=('qt5-svg' 'git')

source=("git://github.com/vnotex/vnote.git")
sha256sums=(SKIP)

gitname="vnote"

pkgver() {
    cd "$srcdir/$gitname"
    printf "%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    cd "$srcdir/$gitname"
    git submodule update --init --recursive
}
build () {
    cd "$srcdir/$gitname"
    mkdir build
    cd build
    qmake-qt5 ../vnote.pro
    make
}
package() {
    cd "$srcdir/$gitname/build"
    make INSTALL_ROOT="$pkgdir" install
}
