# Maintainer: erk <v at erk dot io>
pkgname=vnote-git
pkgver=1809.d499b0d7
epoch=1
pkgrel=1
pkgdesc="A Vim-inspired note-taking application, especially for Markdown."
arch=('x86_64')
url='https://tamlok.github.io/vnote/'
license=('MIT')
depends=(qt5-webengine qt5-svg qt5-x11extras)
makedepends=('git')

source=("git+https://github.com/vnotex/vnote.git")
sha256sums=(SKIP)

gitname="vnote"

pkgver() {
    cd "$srcdir/$gitname"
    printf "%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    cd "$srcdir/$gitname"
    git submodule update --init --recursive
}
build () {
    cd "$srcdir/$gitname"
    mkdir build
    cd build
    qmake-qt5 ../vnote.pro
    make
}
package() {
    cd "$srcdir/$gitname/build"
    make INSTALL_ROOT="$pkgdir" install
}
