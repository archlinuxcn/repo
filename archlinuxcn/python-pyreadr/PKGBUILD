# Maintainer: Butui Hu <hot123tea123@gmail.com>

pkgname=python-pyreadr
_pkgname=pyreadr
pkgver=0.5.3
pkgrel=1
epoch=1
pkgdesc='Read and write R RData and Rds files into/from pandas dataframes'
arch=('x86_64')
url='https://github.com/ofajardo/pyreadr'
license=('AGPL-3.0-or-later AND LGPL-3.0-or-later AND MIT')
depends=(
  bzip2
  glibc
  python-numpy
  python-pandas
  xz
  zlib
)
makedepends=(
  cython  
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-sphinx_rtd_theme
  python-xarray
)
optdepends=(
  "python-xarray: for reading 3D arrays"
)
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/ofajardo/pyreadr/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('258c0fb5c64c7ada846c3629a715a4a36d02a6da23af13041942eda3459d51e605ef67bd45e0f823eba38e5f243ef1aa09f99ae4222e572a6fb8a878b6cc4fdc')

build() {
  cd "${_pkgname}-${pkgver}"
  python -m build --wheel --no-isolation
}

check() {
  local python_version=$(python -c 'import sys; print("".join(map(str, sys.version_info[:2])))')
  cd "${_pkgname}-${pkgver}"
  PYTHONPATH="${PWD}/build/lib.linux-${CARCH}-cpython-${python_version}" python tests/test_basic.py
}

package() {
  cd "${_pkgname}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE_librdata -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
