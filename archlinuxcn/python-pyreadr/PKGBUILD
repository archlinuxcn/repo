# Maintainer: Butui Hu <hot123tea123@gmail.com>

pkgname=python-pyreadr
_pkgname=pyreadr
pkgver=0.5.0
pkgrel=3
pkgdesc='Read and write R RData and Rds files into/from pandas dataframes'
arch=('x86_64')
url='https://github.com/ofajardo/pyreadr'
license=('AGPL-3.0-or-later AND LGPL-3.0-or-later AND MIT')
depends=(
  bzip2
  python-numpy
  python-pandas
  xz
  zlib
)
makedepends=(
  cython  
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-sphinx_rtd_theme
  python-xarray
)
optdepends=(
  "python-xarray: for reading 3D arrays"
)
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/ofajardo/pyreadr/archive/refs/tags/v${pkgver}.tar.gz"
        "0001-fix-building-with-zlib-1.3.1.patch"
)
sha512sums=('6914344534e10bace76de6a01f9c170a8b264fcf1c4ab756532cbda49d59437be72c322dcea4b7cda0294a69c8947fc4857425bf41598185025bbe3e2613eb1c'
            '5f39b53d2e414c6998d90feb9b62c639f87d2c8d6e8ce67fb2aa4680d6dd92d9dd6eebfb10a8c0994bab6ab92dccdf088ee892fa475d7b2d2bb73341e1020139')

prepare() {
  cd "${_pkgname}-${pkgver}"
  patch -p1 -i "${srcdir}/0001-fix-building-with-zlib-1.3.1.patch"
  # quick fix for numpy 2.0
  find . -type f -name "*.py" -exec sed -i "s,np.NaN,np.nan,g" {} \;
}

build() {
  cd "${_pkgname}-${pkgver}"
  python -m build --wheel --no-isolation
}

check() {
  local python_version=$(python -c 'import sys; print("".join(map(str, sys.version_info[:2])))')
  cd "${_pkgname}-${pkgver}"
  PYTHONPATH="${PWD}/build/lib.linux-${CARCH}-cpython-${python_version}" python tests/test_basic.py
}

package() {
  cd "${_pkgname}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE_librdata -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
