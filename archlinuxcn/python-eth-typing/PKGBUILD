# Maintainer: Xeonacid <h.dwwwwww@gmail.com>

_name=eth-typing
pkgname=python-${_name}
pkgver=5.2.0
pkgrel=1
pkgdesc="Common type annotations for ethereum python packages"
arch=(any)
url="https://github.com/ethereum/${_name}"
license=(MIT)
depends=(python python-typing_extensions)
makedepends=(python-build python-installer python-setuptools python-wheel python-sphinx python-sphinx_rtd_theme)
checkdepends=(python-pytest)
source=(${_name}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz)
sha512sums=('75f7e09f6f5b24b5ec8299563d1af07b805360cd7b6c16213b35cef4ba6c93658fa33f75da028cca1b8f6f293f0b88dbf2b2b0ebadc441fa6b2f9a881ecabed9')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
  make -C docs man
}

check(){
  cd $_name-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest -vv --showlocals tests/
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 docs/_build/man/eth_typing.1 -t "$pkgdir/usr/share/man/man1"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
