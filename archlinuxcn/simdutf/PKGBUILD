pkgname=simdutf
pkgver=7.7.1
pkgrel=1
pkgdesc="Unicode validation and transcoding at billions of characters per second"
arch=('i686' 'x86_64')
url="https://simdutf.github.io/simdutf/"
license=('Apache-2.0' 'MIT')
depends=('gcc-libs' glibc)
makedepends=('git' 'cmake' 'ninja')
provides=("simdutf=$pkgver")
conflicts=('simdutf')
source=("source::git+https://github.com/simdutf/simdutf.git#tag=v${pkgver}")
sha256sums=('c5de6d3991b58925e9e94353079aa7e448d48b0c123a6a8b5fae30d0cc06f82e')

build() {
  cd source

  CFLAGS="$CFLAGS -ffat-lto-objects" \
  CXXFLAGS="$CXXFLAGS -ffat-lto-objects" \
  cmake -G Ninja\
    -B "_build" \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_SHARED_LIBS=ON \
    -DCMAKE_INSTALL_PREFIX="/usr" \
    -DCMAKE_INSTALL_LIBDIR="lib" \
    -DSIMDUTF_TESTS=OFF \
    ./
  cmake --build "_build"
}

check() {
  cd source

  cmake --build "_build" --target test
}

package() {
  cd source

  DESTDIR="$pkgdir" cmake --install "_build"
  install -Dm644 "LICENSE-MIT" -t "$pkgdir/usr/share/licenses/simdutf"
}
