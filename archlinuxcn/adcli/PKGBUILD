# Maintainer: Mantas MikulÄ—nas <grawity@gmail.com>
pkgname=adcli
pkgver=0.9.0.r6.g08bac09
pkgrel=1
pkgdesc="Active Directory account management tool"
arch=(i686 x86_64)
url="https://freedesktop.org/software/realmd/"
license=(GPL3)
depends=(krb5 openldap)
makedepends=(docbook-xml docbook-xsl git intltool xmlto)
_commit=08bac0946de29f3e5de90743ce6dfc7118d4ad20
source=("git+https://gitlab.freedesktop.org/realmd/adcli.git#commit=$_commit")
sha256sums=('SKIP')
#source=("https://www.freedesktop.org/software/realmd/releases/$pkgname-$pkgver.tar.gz"
#        "https://www.freedesktop.org/software/realmd/releases/$pkgname-$pkgver.tar.gz.sig")
#sha256sums=('72f6db406e35d96de2bdc413a5ed69f28a4a735c08670c6556713c3f83921aa4'
#            'SKIP')
validpgpkeys=('C0F67099B808FB063E2C81117BFB1108D92765AF')

pkgver() {
  cd "$pkgname"
  git describe --tags | sed "s/-/.r/; s/-/./"
}

prepare() {
  cd "$pkgname"
  autoreconf -fi
}

build() {
  cd "$pkgname"
  ./configure \
    --prefix=/usr           \
    --sbindir=/usr/bin      \
    --sysconfdir=/etc       ;
  make
}

package() {
  cd "$pkgname"
  make DESTDIR="$pkgdir" install
}

# vim: ts=2:sw=2:et

