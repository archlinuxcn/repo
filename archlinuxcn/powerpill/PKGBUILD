#Maintainer: Xyne <ac xunilhcra enyx, backwards>
pkgname=powerpill
pkgver=2018.11
pkgrel=1
pkgdesc='Pacman wrapper for faster downloads.'
arch=(any)
license=(GPL)
url="https://xyne.archlinux.ca/projects/powerpill"
depends=(aria2 'pm2ml>2012.12.12' pyalpm python3 python3-xcgf python3-xcpf)
optdepends=('python3-threaded_servers: internal Pacserve support' 'reflector: Reflector and Rsync support' 'rsync: Rsync download support')
backup=(etc/powerpill/powerpill.json)
source=(
  https://xyne.archlinux.ca/projects/powerpill/src/powerpill-2018.11.tar.xz
#  https://xyne.archlinux.ca/projects/powerpill/src/powerpill-2018.11.tar.xz.sig
)
sha512sums=(
  af1df264f92efe3622ad543093e77482f7219d628a12c7902d17319e201837b5c736c09038499fa97432a45e020b3881c4a37eb1a9654c2ffbc5fa85989a7f58
#  2ff3e80fcca07f8bdaf34ff8824d31b31b9144718ccb52421212b1cfef9034f89c775a5e77001a58b4319f832a70c62c77acc5b18d05232be6152a0bd79d7dd0
)
md5sums=(
  0ae8494ebfd21d6144681ef7b4247162
#  c35a6c2ed6bf0599fb88c27d4d1fe1b6
)
#validpgpkeys=('EC3CBE7F607D11E663149E811D1F0DC78F173680')

package ()
{
  cd "$srcdir/$pkgname-$pkgver"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  install -Dm755 'powerpill' "$pkgdir/usr/bin/powerpill"
  install -Dm644 'powerpill.json' "$pkgdir/etc/powerpill/powerpill.json"
  install -Dm644 'man/powerpill.json.1.gz' "$pkgdir/usr/share/man/man1/powerpill.json.1.gz"
  install -Dm644 'powerpill-bash-completion.sh' "$pkgdir/usr/share/bash-completion/completions/powerpill"
  install -Dm644 '_powerpill.zsh' "$pkgdir/usr/share/zsh/site-functions/_powerpill"
}

# vim: set ts=2 sw=2 et:
