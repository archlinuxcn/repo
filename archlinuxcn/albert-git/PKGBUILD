# Maintainer: Evgenii Alekseev
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Manuel Schneider  <manuelschneid3r at googles mail>

pkgname=albert-git
_pkgname=${pkgname%-git}
pkgver=0.22.17.r33.g7402d06
pkgrel=1
pkgdesc="A sophisticated standalone keyboard launcher"
arch=('x86_64')
url="https://github.com/albertlauncher"
license=('LicenseRef-Albert')
depends=('hicolor-icon-theme' 'qt6-5compat' 'qt6-declarative' 'qt6-scxml' 'qt6-shadertools' 'qt6-svg')
makedepends=('cmake' 'git' 'libqalculate' 'pybind11' 'python' 'qt6-tools')
optdepends=('ddgr: duckduckgo search plugin'
	'libarchive: documentation plugin'
	'libqalculate: calculator plugin'
	'python: python extension'
	'python-docker: docker plugin'
	'python-googletrans: google translate plugin'
	'python-pylatexenc: TeX plugin'
	'python-urllib3: python web plugins'
	'python-virtualbox: virtualbox plugin')
conflicts=("${_pkgname}")
provides=("${_pkgname}")
source=("mirrors/${_pkgname}::git+${url}/${_pkgname}.git"
	"mirrors/plugins::git+${url}/plugins.git"
	"mirrors/python::git+${url}/python.git"
	"mirrors/qhotkey::git+https://github.com/Skycoder42/QHotkey.git")
sha512sums=('SKIP'
	'SKIP'
	'SKIP'
	'SKIP')

pkgver() {
	cd "${_pkgname}/"
	git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/v//'
}

prepare() {
	mkdir -p build

	cd "${_pkgname}/"
	git submodule init

	git config submodule.plugins.url "${srcdir}/plugins/"
	git -c protocol.file.allow=always submodule update plugins

	git config submodule.lib/QHotkey.url "${srcdir}/qhotkey/"
	git -c protocol.file.allow=always submodule update lib/QHotkey

	cd plugins/
	git submodule init
	git config submodule.python/plugins.url "${srcdir}/python"
	git -c protocol.file.allow=always submodule update python/plugins
}

build() {
	cd build

	cmake "../${_pkgname}" \
		-DCMAKE_BUILD_TYPE=MinSizeRel \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_INSTALL_LIBDIR=lib \
		-DCMAKE_FIND_PACKAGE_RESOLVE_SYMLINKS=ON \
		-DQHOTKEY_INSTALL=OFF \
		-DBUILD_APPLICATIONS_XDG=ON \
		-DBUILD_CALCULATOR_QALCULATE=ON \
		-DBUILD_CHROMIUM=ON \
		-DBUILD_CLIPBOARD=ON \
		-DBUILD_DATETIME=ON \
		-DBUILD_DEBUG=OFF \
		-DBUILD_FILES=ON \
		-DBUILD_HASH=ON \
		-DBUILD_PYTHON=ON \
		-DBUILD_QMLBOXMODEL=ON \
		-DBUILD_SNIPPETS=ON \
		-DBUILD_SSH=ON \
		-DBUILD_SYSTEM=ON \
		-DBUILD_TEMPLATE=OFF \
		-DBUILD_TERMINAL=ON \
		-DBUILD_URLHANDLER=ON \
		-DBUILD_WEBSEARCH=ON \
		-DBUILD_WIDGETSBOXMODEL=ON

	make
}

package() {
	cd build

	make DESTDIR="${pkgdir}/" install
	install -Dm644 "${srcdir}/${_pkgname}/LICENSE.md" -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
