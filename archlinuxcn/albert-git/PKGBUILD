# Maintainer: Evgenii Alekseev
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Manuel Schneider  <manuelschneid3r at googles mail>

pkgname=albert-git
_pkgname=${pkgname%-git}
pkgver=0.26.13.r4.g3fb82f6
pkgrel=1
pkgdesc="A sophisticated standalone keyboard launcher"
arch=('x86_64')
url="https://github.com/${_pkgname}launcher"
license=('LicenseRef-Albert')
depends=('hicolor-icon-theme' 'qt6-5compat' 'qt6-declarative' 'qt6-scxml' 'qt6-shadertools' 'qt6-svg')
makedepends=('cmake' 'git' 'libqalculate' 'pybind11' 'python' 'qt6-tools')
optdepends=('ddgr: duckduckgo search plugin'
	'libarchive: documentation plugin'
	'libqalculate: calculator plugin')
conflicts=("${_pkgname}")
provides=("${_pkgname}")
source=("git+${url}/${_pkgname}.git"
	"git+${url}/plugins.git"
	"git+${url}/i18n.git"
	"git+${url}/python.git"
	"git+https://github.com/pybind/pybind11.git"
	"git+https://github.com/Skycoder42/QHotkey.git"
	"git+https://github.com/QtCommunity/QNotification.git")
sha512sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP')

pkgver() {
	cd "${_pkgname}/"
	git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/v//'
}

prepare() {
	cd "${_pkgname}/"
	git submodule init
	git config submodule.plugins.url "${srcdir}/plugins/"
	git config submodule.i18n.url "${srcdir}/i18n/"
	git config submodule.lib/QHotkey.url "${srcdir}/QHotkey/"
	git config submodule.lib/QNotification.url "${srcdir}/QNotification/"
	git -c protocol.file.allow=always submodule update

	cd plugins
	git submodule init
	git config submodule.python/plugins.url "${srcdir}/python/"
	git config submodule.python/pybind11.url "${srcdir}/pybind11/"
	git -c protocol.file.allow=always submodule update
}

build() {
	cmake -B build -S "${_pkgname}" \
		-DCMAKE_BUILD_TYPE=None \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_INSTALL_LIBDIR=lib \
		-DCMAKE_FIND_PACKAGE_RESOLVE_SYMLINKS=ON \
		-DQHOTKEY_INSTALL=OFF \
		-DBUILD_APPLICATIONS_XDG=ON \
		-DBUILD_CALCULATOR_QALCULATE=ON \
		-DBUILD_CHROMIUM=ON \
		-DBUILD_CLIPBOARD=ON \
		-DBUILD_DATETIME=ON \
		-DBUILD_DEBUG=OFF \
		-DBUILD_FILES=ON \
		-DBUILD_HASH=ON \
		-DBUILD_PYTHON=ON \
		-DBUILD_QMLBOXMODEL=ON \
		-DBUILD_SNIPPETS=ON \
		-DBUILD_SSH=ON \
		-DBUILD_SYSTEM=ON \
		-DBUILD_TEMPLATE=OFF \
		-DBUILD_TERMINAL=ON \
		-DBUILD_URLHANDLER=ON \
		-DBUILD_WEBSEARCH=ON \
		-DBUILD_WIDGETSBOXMODEL=ON

	cmake --build build
}

package() {
	DESTDIR="${pkgdir}" cmake --install build
	install -Dm644 "${_pkgname}/LICENSE.md" -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
