# Maintainer: Dct Mei <dctxmei@gmail.com>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=xray-domain-list-community
pkgver=20210105135438
pkgrel=1
pkgdesc="A list of domains to be used as geosites for routing purpose in Project V"
arch=('any')
url="https://github.com/v2fly/domain-list-community"
license=('MIT')
makedepends=('git' 'go')
source=("${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('e1cf3def10c718daa67e9b2d783b61a70e54a3af4ae1e2b0f346234da69c9a8c')

prepare() {
    mkdir .gopath
    export GOPATH="${srcdir}"/.gopath

    mkdir -p .gopath/src/github.com/v2fly
    ln -s "${PWD}"/"domain-list-community-$pkgver" .gopath/src/github.com/v2fly/domain-list-community

    go get github.com/golang/protobuf/proto
    go get -insecure v2ray.com/core/app/router
}

build() {
    cd .gopath
    go run ./src/github.com/v2fly/domain-list-community/main.go
}

package() {
    cd .gopath
    install -Dm 644 dlc.dat "${pkgdir}"/usr/share/xray/geosite.dat
    install -Dm 644 "${srcdir}"/"domain-list-community-${pkgver}"/LICENSE -t "${pkgdir}"/usr/share/licenses/"$pkgname"/
}
