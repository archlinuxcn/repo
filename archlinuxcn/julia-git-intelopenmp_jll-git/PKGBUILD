pkgname=julia-git-intelopenmp_jll-git
pkgver=2018.0.3+2
pkgrel=4
pkgdesc="IntelOpenMP_jll.jl"
url="https://github.com/JuliaBinaryWrappers/IntelOpenMP_jll.jl"
arch=(aarch64 armv7h x86_64)
license=('MIT')
makedepends=(git julia-pkg-scripts)
depends=(julia-git)
if [[ $CARCH = x86_64 ]]; then
  depends+=(intel-mkl)
fi
provides=(julia-git-intelopenmp_jll)
source=(git://github.com/JuliaBinaryWrappers/IntelOpenMP_jll.jl
        jll.toml
        jll_x86_64.toml)
md5sums=('SKIP'
         'd41d8cd98f00b204e9800998ecf8427e'
         '8659ddd6d9f3e7c4e24921335cc7efef')

pkgver() {
  cd IntelOpenMP_jll.jl

  git describe --tags | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

build() {
  cd IntelOpenMP_jll.jl

  if [[ $CARCH = x86_64 ]]; then
    julia /usr/lib/julia/julia-gen-jll.jl IntelOpenMP_jll ../jll_x86_64.toml
  else
    julia /usr/lib/julia/julia-gen-jll.jl IntelOpenMP_jll ../jll.toml
  fi
}

package() {
  cd IntelOpenMP_jll.jl

  . /usr/lib/julia/julia-install-pkg.sh IntelOpenMP_jll "${pkgdir}" "${pkgname}" julia-git
}
