# Maintainer: Integral <integral@member.fsf.org>
# Contributor: twa022 <twa022 at gmail dot com>

pkgname=gimp-devel
_pkgname=${pkgname%-devel}
pkgver=3.0.0rc1
_pkgver=${pkgver/rc/-RC}
_majorver=$(cut -d '.' -f 1-2 <<<${pkgver})
pkgrel=3
pkgdesc="GNU Image Manipulation Program (Development version)"
arch=('i686' 'x86_64' 'armv7h' 'aarch64')
url="https://www.${_pkgname}.org"
license=('GPL-3.0-or-later')
depends=("cfitsio" "desktop-file-utils" "enchant" "gtk3" "icu" "lcms2" "libgexiv2" "librsvg" "libwmf"
	'libexif' 'libgudev' 'openjpeg2' 'poppler-glib' 'poppler-data' 'openexr' 'mypaint-brushes1'
	'babl' 'gegl' 'cairo' 'python-gobject' 'appstream-glib' 'libxmu' 'graphviz')
makedepends=("appstream" "cmake" "glib-networking" "intltool" "libxslt"
	'alsa-lib' 'curl' 'ghostscript' 'libxpm'
	'libheif' 'libwebp' 'libmng' 'iso-codes' 'aalib' 'zlib' 'libjxl'
	'gjs' 'luajit' 'meson' 'gobject-introspection'
	'gi-docgen' 'xorg-server-xvfb' 'vala' 'highway' 'meson' 'qoi-headers'
	'cfitsio' 'glib2-devel')
checkdepends=('xorg-server-xvfb')
optdepends=('gutenprint: for sophisticated printing only as gimp has built-in cups print support'
	'alsa-lib: for MIDI event controller module'
	'curl: for URI support'
	'ghostscript: for postscript support'
	'libxpm: XPM support'
	'libheif: HEIF support'
	'libjxl: JPEG XL support'
	'libwebp: WebP support'
	'libmng: MNG support'
	'qoi-headers: QOI image support'
	'iso-codes: Language support'
	'aalib: ASCII art support'
	'zlib: Compression routines'
	'gjs: JavaScript scripting support'
	'luajit: LUA scripting support'
	'lua51-lgi: LUA scripting support'
	'cfitsio: FITS support')
conflicts=("${_pkgname}")
provides=("${_pkgname}")
source=("https://download.gimp.org/gimp/v${_majorver}/${_pkgname}-${_pkgver}.tar.xz")
sha256sums=('b3d0b264c5e38e789faaf3417003397f3240014c59c7f417f9ca3bd39c5ffb66')

build() {
	arch-meson "${_pkgname}-${_pkgver}/" build
	meson compile -C build
}

package() {
	meson install -C build --destdir "${pkgdir}"

	# Fix wayland icon issue
	sed -i "s/${_pkgname}-${_majorver}/${_pkgname}/g" "${pkgdir}/usr/share/applications/${_pkgname}.desktop"
}
