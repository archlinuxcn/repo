# Maintainer: twa022 <twa022 at gmail dot com>

pkgname=gimp-devel
_pkgname=${pkgname%-devel}
pkgver=2.99.18
_pkgver=${pkgver/rc/-RC}
_majorver=$(cut -d '.' -f 1-2 <<<${pkgver})
pkgrel=4
pkgdesc="GNU Image Manipulation Program (Development version)"
arch=('i686' 'x86_64' 'armv7h' 'aarch64')
url="https://www.gimp.org/"
license=('GPL-3.0-or-later')
depends=("cfitsio" "desktop-file-utils" "enchant" "gtk3" "icu" "lcms2" "libgexiv2" "librsvg" "libwmf"
	'libexif' 'libgudev' 'openjpeg2' 'poppler-glib' 'poppler-data' 'openexr' 'mypaint-brushes1'
	'babl>=0.1.98' 'gegl>=0.4.48' 'cairo' 'python-gobject' 'appstream-glib' 'libxmu' 'graphviz')
makedepends=("appstream" "cmake" "glib-networking" "intltool" "libxslt"
	'alsa-lib' 'curl' 'ghostscript' 'libxpm'
	'libheif' 'libwebp' 'libmng' 'iso-codes' 'aalib' 'zlib' 'libjxl'
	'gjs' 'luajit' 'meson' 'gobject-introspection'
	'gi-docgen' 'xorg-server-xvfb' 'vala' 'highway' 'meson' 'qoi-headers'
	'cfitsio' 'glib2-devel')
checkdepends=('xorg-server-xvfb')
optdepends=('gutenprint: for sophisticated printing only as gimp has built-in cups print support'
	'alsa-lib: for MIDI event controller module'
	'curl: for URI support'
	'ghostscript: for postscript support'
	'libxpm: XPM support'
	'libheif: HEIF support'
	'libjxl: JPEG XL support'
	'libwebp: WebP support'
	'libmng: MNG support'
	'qoi-headers: QOI image support'
	'iso-codes: Language support'
	'aalib: ASCII art support'
	'zlib: Compression routines'
	'gjs: JavaScript scripting support'
	'luajit: LUA scripting support'
	'lua51-lgi: LUA scripting support'
	'cfitsio: FITS support')
conflicts=("${_pkgname}")
provides=("${_pkgname}=${pkgver}")
source=("https://download.gimp.org/gimp/v${_majorver}/${_pkgname}-${_pkgver}.tar.xz"
	'linux.gpl')
sha256sums=('8c1bb7a94ac0d4d0cde4d701d8b356387c2ecd87abbd35bbf7d222d40f6ddb6e'
            '1003bbf5fc292d0d63be44562f46506f7b2ca5729770da9d38d3bb2e8a2f36b3')

build() {
	local meson_options=(
		-Dilbm=disabled
	)

	arch-meson "${_pkgname}-${_pkgver}/" build "${meson_options[@]}"
	meson compile -C build
}

package() {
	meson install -C build --destdir "${pkgdir}"
	install -Dm644 linux.gpl -t "${pkgdir}/usr/share/gimp/2.99/palettes/"

	# Fix wayland icon issue
	sed -i "s/${_pkgname}-2.99/${_pkgname}/g" "${pkgdir}/usr/share/applications/${_pkgname}.desktop"
}
