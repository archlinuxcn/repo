# Maintainer: c0gnitivefl0w
# Co-Maintainer: eldios

pkgbase=seadrive-daemon
pkgname=(seadrive-daemon python-seadrive)
pkgver=2.0.28
pkgrel=5
pkgdesc="SeaDrive daemon with FUSE interface"
arch=('aarch64' 'armv7h' 'x86_64')
url="https://github.com/haiwen/seadrive-fuse"
license=('GPL3')
_daemon_depends=(openssl libuuid.so sqlite libevent zlib libcurl.so fuse2 libsearpc
                 libgobject-2.0.so libglib-2.0.so jansson libwebsockets.so)
makedepends=(git intltool python "${_daemon_depends[@]}")
source=("git+https://github.com/haiwen/seadrive-fuse#tag=v${pkgver}")
sha256sums=('6570d214aa0c6e973154723fb9f766801aa599625a832031b8f42b4e1d44deb3')

build() {
  cd seadrive-fuse

  ./autogen.sh
  ./configure --prefix=/usr
  make

  make install DESTDIR="${PWD}/pkg"
}

package_seadrive-daemon() {
  depends=("${_daemon_depends[@]}")
  cd seadrive-fuse

  mkdir -p "${pkgdir}/usr/bin/"
  cp -a pkg/usr/bin/seadrive "${pkgdir}/usr/bin/seadrive"
}

package_python-seadrive() {
  depends=(python libsearpc)
  cd seadrive-fuse

  mkdir -p "${pkgdir}/usr/lib/"
  cp -a pkg/usr/lib/python* "${pkgdir}/usr/lib"
}
