# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>
pkgname=emacs-llvm-mode-git
pkgver=449085.32c26e27b6fc
pkgrel=1
pkgdesc="Emacs major mode for llvm IR"
arch=('any')
url="http://llvm.org/"
license=('GPL')
makedepends=('git' 'emacs')
provides=('emacs-llvm-mode')
conflicts=('emacs-llvm-mode')
source=('git+https://github.com/llvm/llvm-project')
md5sums=('SKIP')

pkgver() {
  cd llvm-project

  echo "$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

build() {
  cd llvm-project/llvm/utils/emacs

  emacs --batch --eval \
        '(progn
          (push "." load-path)
          (batch-byte-compile))' llvm-mode.el tablegen-mode.el
}

package() {
  cd llvm-project/llvm/utils/emacs

  mkdir -p "$pkgdir/usr/share/emacs/site-lisp"
  install -m644 llvm-mode.el "$pkgdir/usr/share/emacs/site-lisp"
  install -m644 llvm-mode.elc "$pkgdir/usr/share/emacs/site-lisp"
  install -m644 tablegen-mode.el "$pkgdir/usr/share/emacs/site-lisp"
  install -m644 tablegen-mode.elc "$pkgdir/usr/share/emacs/site-lisp"
}
