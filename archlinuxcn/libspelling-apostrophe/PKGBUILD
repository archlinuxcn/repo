# Maintainer: Heddxh <g311571057 at gmail dot com>
# Maintainer: Fabian Bornschein <fabiscafe-at-mailbox-dot-org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=libspelling-apostrophe
pkgver=0.2.1
pkgrel=3
pkgdesc="Spellcheck library for GTK 4, patched for Apostrophe"
url="https://gitlab.gnome.org/chergert/libspelling"
arch=(x86_64)
license=(LGPL-2.1-or-later)
provides=(libspelling=$pkgver libspelling-1.so)
conflicts=(libspelling)
depends=(
  enchant
  gcc-libs
  glib2
  glibc
  gtk4
  gtksourceview5-apostrophe
  icu
  pango
)
makedepends=(
  gi-docgen
  git
  gobject-introspection
  meson
  vala
)
source=(
  "git+https://gitlab.gnome.org/chergert/libspelling.git#tag=$pkgver"
  "https://gitlab.gnome.org/World/apostrophe/-/raw/main/build-aux/flatpak/libspelling_text_commits.patch"
)
b2sums=('dc5652e327307165231869321010c9d98e0b4521c4ee090aa326ed2c25d17070a8217cc4bd049a4d89c76de73ed0457b8fc8b8533bfb209b7b2f6d2a0eb2895c'
        '32d9ddcaf56c5f660da0d518ade97446f8bfa0a87309bd6729fe3b6d596712ae5390f2fbb5197fb0b3714ab9b57c47c2e38bf4e3d12cba605a55e5f4dfa940a9')

prepare() {
  patch -d libspelling -p1 <libspelling_text_commits.patch
}

build() {
  arch-meson libspelling build -D docs=false -D vapi=false
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
