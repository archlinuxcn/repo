# Merged with official ABS kconfig PKGBUILD by João, 2021/02/02 (all respective contributors apply herein)
# Maintainer: João Figueiredo & chaotic-aur <islandc0der@chaotic.cx>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kconfig-git
_pkgname=${pkgname%-git}
pkgver=6.18.0.r5.gb4099c8
pkgrel=1
epoch=3
pkgdesc="Configuration system"
arch=('x86_64' 'aarch64' 'riscv64')
url="https://community.kde.org/Frameworks"
license=('LGPL-2.0-only AND LGPL-3.0-only')
depends=('glibc' 'gcc-libs' 'qt6-base' 'qt6-declarative')
makedepends=('git' 'extra-cmake-modules-git' 'qt6-tools' 'doxygen' 'qt6-doc')
conflicts=("${_pkgname}")
provides=("${_pkgname}")
groups=('kf6-git')
source=("git+https://invent.kde.org/frameworks/${_pkgname}.git")
sha256sums=('SKIP')

pkgver() {
	cd "${_pkgname}/"
	git describe --long --tags --match="v[0-9]*" --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/v//;s/.rc/rc/'
}

build() {
	cmake -B build -S "${_pkgname}/" \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_INSTALL_LIBEXECDIR=lib \
		-DBUILD_TESTING=OFF \
		-DBUILD_QCH=ON

	cmake --build build
}

package() {
	DESTDIR="${pkgdir}" cmake --install build
}
