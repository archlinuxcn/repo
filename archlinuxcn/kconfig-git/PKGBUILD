# Merged with official ABS kconfig PKGBUILD by João, 2021/02/02 (all respective contributors apply herein)
# Maintainer: João Figueiredo & chaotic-aur <islandc0der@chaotic.cx>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kconfig-git
_pkgname=${pkgname%-git}
pkgver=6.0.0_r1287.g760942b6
pkgrel=1
pkgdesc="Configuration system"
arch=($CARCH)
url="https://community.kde.org/Frameworks"
license=('LGPL-2.0-or-later')
depends=(qt6-base)
makedepends=(git extra-cmake-modules-git qt6-tools clang python-pyqt6 doxygen sip qt6-declarative)
conflicts=("${_pkgname}")
provides=("${_pkgname}")
optdepends=('python-pyqt6: for the Python bindings')
groups=(kf6-git)
source=("git+https://github.com/KDE/${_pkgname}.git")
sha256sums=('SKIP')

pkgver() {
	cd "${_pkgname}/"
	_ver="$(grep -m1 'set(KF_VERSION' CMakeLists.txt | cut -d '"' -f2 | tr - .)"
	echo "${_ver}_r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

build() {
	cmake -B build -S "${_pkgname}/" \
		-DQT_MAJOR_VERSION=6 \
		-DCMAKE_INSTALL_LIBEXECDIR=lib \
		-DBUILD_TESTING=OFF \
		-DBUILD_QCH=ON
	cmake --build build
}

package() {
	DESTDIR="${pkgdir}" cmake --install build
}
