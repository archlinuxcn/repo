#Maintainer: Xyne <ac xunilhcra enyx, backwards>
pkgname=pm2ml
pkgver=2017.12
pkgrel=6
pkgdesc='Generate metalinks for downloading Pacman packages and databases.'
arch=(any)
license=(GPL)
url="https://xyne.archlinux.ca/projects/pm2ml"
depends=(pyalpm python3 python3-xcgf python3-xcpf)
optdepends=('aria2: ppl script support.' 'python3-aur: AUR support' 'reflector: Reflector support')
backup=(etc/ppl.conf)
source=(
  https://xyne.archlinux.ca/projects/pm2ml/src/pm2ml-2017.12.tar.xz
  https://xyne.archlinux.ca/projects/pm2ml/src/pm2ml-2017.12.tar.xz.sig
)
sha512sums=(
  cc54a48882c35362b675d1deffc1a05631b35c8fe40ce5a192d3bed69daab01b26c76a71b29d8606ed36d6eb592eb474411a67510cb1dab351fa837c7dde57e9
  958421ca7c1f7654f81734a4fda60863dd5154ad141e1492146e77351a62f62e18bd213ec475b0e3e40d17512424ae1948995b758ff718a13c22ecdc21197ff4
)
md5sums=(
  cb7cc817be7e464d81e19e9103e477c2
  d4b5b2d6a317e9fcb31457643a176892
)
validpgpkeys=('EC3CBE7F607D11E663149E811D1F0DC78F173680')

package ()
{
  cd "$srcdir/$pkgname-$pkgver"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  install -Dm755 "$pkgname" "$pkgdir/usr/bin/$pkgname"
  for foo_ in ppl pplsyu ppls; do
    install -Dm755 "$foo_" "$pkgdir/usr/bin/$foo_"
  done
  install -Dm644 "ppl.conf" "$pkgdir/etc/ppl.conf"
}

# vim: set ts=2 sw=2 et:
