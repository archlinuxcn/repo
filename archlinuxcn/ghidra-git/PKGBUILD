# Maintainer: Luke Street <luke@street.dev>
# Contributor: Arnaud Dovi <mr.dovi@gmail.com>
# Contributor: Jean Lucas <jean@4ray.co>
# Contributor: Danny Bautista <pyrolagus@gmail.com>
# Contributor: nullableVoidPtr <nullableVoidPtr _ gmail _ com>

pkgname=ghidra-git
_pkgname=${pkgname%-git}
pkgver=11.2.1.r420.6a818106fd
pkgrel=1
pkgdesc="Software reverse engineering framework"
arch=('x86_64' 'aarch64')
url="https://www.nsa.gov/${_pkgname}"
license=('Apache-2.0')
provides=("${_pkgname}")
conflicts=(${_pkgname}{,-{dev,desktop}})
depends=(
	'java-environment=21'
	'polkit'
)
makedepends=(
	'git'
	'python-pip'
	'fop'
	'gradle'
	'unzip'
)
source=(
	"git+https://github.com/NationalSecurityAgency/${_pkgname}"
	${_pkgname}{,-root}.desktop
	"${_pkgname}.policy"
)
sha512sums=('SKIP'
            'a85b8b3276e2ff4ed8bda6470c15d02711ebaa48463c775cd2a36549fad738e9fe073dab80f8c57646490ffc959cdc27e9d25b1dc2a5810b0ddb249b5dc99a9b'
            'c717029cf31860e27b5563c3ff4b2740d4b1997bc50481214e24c38f12d9acbfa9ca2cbfe594d43071fbf8420ac8f022119c2c23ddef0c717d96860e22eb35c3'
            '0a35f58b1820ac65ce37d09b0a6904ab7018c773c73ecd29bcfda37cbd27f34af868585084b5cd408b1066b7956df043cb1573a1e3d890e173be737d2de51401')
options=('!strip')

pkgver() {
	cd "${_pkgname}/"
	printf "%s" "$(git describe --tags | sed 's/Ghidra_\([^_]*\)_build/\1/;s/^v//;s/\([^-]*-\)g/r\1/;s/-/./g')"
}

prepare() {
	cd "${_pkgname}/"
	echo "Setting up the build dependencies..."
	gradle --parallel --init-script gradle/support/fetchDependencies.gradle init --overwrite
}

build() {
	cd "${_pkgname}/"
	echo "Building Ghidra..."
	gradle --parallel buildGhidra -x ip
}

package() {
	cd "${_pkgname}/"

	echo "Creating the package base..."
	install -d ${pkgdir}/{opt,usr/bin}

	echo "Extracting the zip archive..."
	local _appver=$(grep -oP '(?<=^application.version=).*$' Ghidra/application.properties)
	local _relname=$(grep -oP '(?<=^application.release.name=).*$' Ghidra/application.properties)
	unzip -u build/dist/${_pkgname}_${_appver}_${_relname}_$(date +%Y%m%d)_linux_*.zip -d "${pkgdir}/opt/"

	echo "Setting up a versionless directory name..."
	mv ${pkgdir}/opt/${_pkgname}{_${_appver}_${_relname},}

	echo "Setting up /usr/bin launchers..."
	ln -s "/opt/${_pkgname}/${_pkgname}Run" "${pkgdir}/usr/bin/${_pkgname}"
	ln -s "/opt/${_pkgname}/support/analyzeHeadless" "${pkgdir}/usr/bin/${_pkgname}-headless"

	echo "Setting up desktop shortcuts..."
	install -Dm644 ../${_pkgname}{,-root}.desktop -t "${pkgdir}/usr/share/applications/"

	echo "Setting up desktop icon..."
	install -Dm644 Ghidra/Framework/Gui/src/main/resources/images/GhidraIcon64.png "${pkgdir}/usr/share/pixmaps/${_pkgname}.png"

	echo "Setting up policy file for the \"run as root\" desktop shortcut..."
	install -Dm644 "../${_pkgname}.policy" -t "${pkgdir}/usr/share/polkit-1/actions/"
}
