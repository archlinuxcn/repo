# Maintainer: Javier Ti√° <javier dot tia at gmail dot com>
# Contributor: Daan De Meyer <daan.j.demeyer@gmail.com>

pkgname=reproc
pkgver=14.2.4
pkgrel=2
pkgdesc='Cross-platform library that simplifies working with external CLI applications from C and C++'
arch=('aarch64' 'x86_64')
url='https://github.com/DaanDeMeyer/reproc'
license=('MIT')
provides=('libreproc.so' 'libreproc++.so')
makedepends=('cmake')
source=("${url}/archive/v${pkgver}.tar.gz"
        "fix-gcc13-build.patch")
sha256sums=('55c780f7faa5c8cabd83ebbb84b68e5e0e09732de70a129f6b3c801e905415dd'
            'b6b06e3726de562cd108ee0db665b09befe55de719dea84fc8606be895c30ba0')

prepare() {
  cd "${pkgname}-${pkgver}"
  patch -p1 -i "${srcdir}"/fix-gcc13-build.patch
}

build() {
  cmake \
    -S "${pkgname}-${pkgver}" \
    -B build \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_SHARED_LIBS=ON \
    -DREPROC++=ON \
    -DREPROC_TEST=ON
  cmake --build build
}

check() {
  cmake --build build --target test
}

package() {
  DESTDIR="${pkgdir}" cmake --install build
  install -D --mode=644 "${srcdir}/${pkgname}-${pkgver}"/LICENSE \
    "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
}

# vim:set ts=2 sw=2 et:
