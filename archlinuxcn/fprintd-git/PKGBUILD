# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Nikolay Rysev <mad.f3ka@gmail.com>

pkgname=fprintd-git
pkgver=1.90.9.r21.gdbf0139
pkgrel=1
pkgdesc="D-Bus service to access fingerprint readers"
url="https://fprint.freedesktop.org/"
arch=(x86_64)
license=(GPL)
depends=(glib2 libfprint polkit dbus dbus-glib libsystemd)
provides=(fprintd)
conflicts=(fprintd)
makedepends=(gtk-doc git meson pam_wrapper python-cairo python-dbus
             python-dbusmock)

source=("${pkgname}::git+https://gitlab.freedesktop.org/libfprint/fprintd.git"
        dirmode.diff)

sha256sums=('SKIP'
            'f57bd61e28a8c5cb183f27d3aba5847ae2a5e3ba9d9796bfd4a967caa173bb49')

pkgver() {
  cd $pkgname
  git describe --long | sed -r 's/^v//g;s/([^-]*-g)/r\1/;s/-/./g'
}

prepare() {
  cd $pkgname

  # Fix StateDirectoryMode https://bugs.archlinux.org/task/68932
  git apply -3 ../dirmode.diff
}

build() {
  arch-meson $pkgname build \
    -D pam_modules_dir=/usr/lib/security \
    -D gtk_doc=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  depends+=(libfprint-2.so)

  DESTDIR="$pkgdir" meson install -C build
}
