# Maintainer: Butui Hu <hot123tea123@gmail.com>

_pkgname=KWIML
pkgname=kwiml-git
pkgver=r60.500bc25
pkgrel=1
pkgdesc='The Kitware Information Macro Library'
arch=('any')
url='https://gitlab.kitware.com/utils/kwiml'
license=('BSD-3-Clause')
makedepends=(
  cmake
  git
)
provides=(kwiml)
conflicts=(kwiml)
source=("${_pkgname}::git+https://gitlab.kitware.com/utils/kwiml.git")
sha512sums=('SKIP')

pkgver() {
  cd "${_pkgname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cmake -B build -S ${srcdir}/${_pkgname} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make -C build
}

package() {
  make -C build DESTDIR="${pkgdir}" install
  install -Dm644 "${srcdir}/${_pkgname}/Copyright.txt" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:

