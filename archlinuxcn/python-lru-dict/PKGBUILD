# Maintainer: Xeonacid <h.dwwwwww@gmail.com>

_name=lru-dict
pkgname=python-${_name}
pkgver=1.3.0
pkgrel=2
pkgdesc="A fast and memory efficient LRU cache for Python"
arch=(x86_64)
url="https://github.com/amitdev/${_name}"
license=(MIT)
depends=(glibc python)
makedepends=(python-build python-installer python-setuptools python-wheel)
checkdepends=(python-pytest)
source=(${_name}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz)
sha512sums=('59cca8e1cea79b68728591f40f78066817d0a00a7ebbb3fffd22b2929399c846e873d5d7fe5902f5b104b05ad6e89fc17bf8f18867fc0c272f1fa867fdf4e5e2')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check(){
  cd $_name-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest -vv test/
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm644 README.rst -t "$pkgdir/usr/share/doc/$pkgname"
}
