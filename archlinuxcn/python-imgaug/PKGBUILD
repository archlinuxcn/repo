# Maintainer: Butui Hu <hot123tea123@gmail.com>

_pkgname=imgaug
pkgname=python-imgaug
pkgver=0.4.0
pkgrel=15
pkgdesc='Image augmentation for machine learning experiments'
arch=('any')
url='https://github.com/aleju/imgaug'
license=('MIT')
depends=(
  python-matplotlib
  python-numpy
  python-opencv
  python-pillow
  python-scikit-image
  python-scipy
  python-shapely
  python-six
)
makedepends=(python-setuptools)
source=("${_pkgname}-${pkgver}.tar.gz"::"https://github.com/aleju/imgaug/archive/${pkgver}.tar.gz")
sha512sums=('abec5ece42d38b455312154c04641d1c1373a6ed16b879050dcc754c9feb8704c9cea124cc1e990c787fcc7cdbf5894fe37339c4d84f65efd7cfbd23cba7a1e7')

build() {
  cd "${_pkgname}-${pkgver}"
  python setup.py build
}

package() {
  cd "${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
