# Maintainer: Butui Hu <hot123tea123@gmail.com>

_pkgname=transformers
pkgname=python-transformers
pkgver=4.57.3
pkgrel=1
pkgdesc='State-of-the-art Machine Learning for Pytorch, TensorFlow, and JAX'
arch=('any')
url='https://github.com/huggingface/transformers'
license=('Apache')
depends=(
  python-av
  python-accelerate
  python-filelock
  python-huggingface-hub
  python-numpy
  python-regex
  python-requests
  python-sacremoses
  python-safetensors
  python-scikit-learn
  python-tokenizers
  python-tqdm
  python-yaml
)
makedepends=(
  python-setuptools
)
optdepends=(
  python-pytorch
  python-tensorflow
)

source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/huggingface/transformers/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('acf212de3a30f2abf83c1ae3c63326f918a6144d9b91d928bdbeee91715a068f244d8c387dfe55f5be67f232205d9c7fb5d1c6c8dcd656157aa400c90722431b')

build() {
  cd "${_pkgname}-${pkgver}"
  python setup.py build
}

package() {
  cd "${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
# vim:set ts=2 sw=2 et:
