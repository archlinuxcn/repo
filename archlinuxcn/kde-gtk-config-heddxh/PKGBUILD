# Maintainer: Heddxh <g311571057 at gmail dot com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kde-gtk-config-heddxh
_pkgname=kde-gtk-config
pkgver=6.4.5
_dirver=$(echo $pkgver | cut -d. -f1-3)
pkgrel=1
pkgdesc='Syncs KDE settings to GTK applications with patch from Heddxh'
arch=(x86_64)
url='https://kde.org/plasma-desktop/'
license=(LGPL-2.0-or-later)
provides=("$_pkgname=$pkgver")
conflicts=("$_pkgname")
depends=(gcc-libs
    glib2
    glibc
    kcolorscheme
    kconfig
    kcoreaddons
    kdbusaddons
    kdecoration
    kguiaddons
    kwindowsystem
    qt6-base
    qt6-svg
    sh
    xsettingsd)
makedepends=(extra-cmake-modules
    gtk3
    sassc
    git)
optdepends=('gtk3: GTK3 apps support')
source=(https://download.kde.org/stable/plasma/$_dirver/$_pkgname-$pkgver.tar.xz{,.sig}
    0001-feat-customize-what-options-to-sync.patch)
sha256sums=('f7a81c1efd539b608cc640a999fdb184bbb83cfc9600d407d1a4f332c3880ec2'
            'SKIP'
            'afe854f49b46b76a825e73427addf150c08f47f34bf53c3b90c562ba30f73f0a')
validpgpkeys=('E0A3EB202F8E57528E13E72FD7574483BB57B18D' # Jonathan Esk-Riddell <jr@jriddell.org>
    '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'           # Bhushan Shah <bshah@kde.org>
    'D07BD8662C56CB291B316EB2F5675605C74E02CF'           # David Edmundson <davidedmundson@kde.org>
    '1FA881591C26B276D7A5518EEAAF29B42A678C20')          # Marco Martin <notmart@gmail.com>

prepare() {
    cd $_pkgname-$pkgver
    git apply ../0001-feat-customize-what-options-to-sync.patch
}

build() {
    cmake -B build -S $_pkgname-$pkgver \
        -DCMAKE_INSTALL_LIBEXECDIR=lib \
        -DBUILD_TESTING=OFF
    cmake --build build
}

package() {
    DESTDIR="$pkgdir" cmake --install build
}
