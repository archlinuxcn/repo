# Maintainer: Daniel Landau <daniel@landau.fi>
# Contributor: Maxqia <contrib@maxqia.com>

# The following people have contributed to keepassx-git package
# Contributor: Lev Lybin <aur@devtrue.net>
# Contributor: Jamie Macdonald <jamie.alban@gmail.com>
# Contributor: Alucryd <alucryd at gmail dot com>
# Contributor: Paolo Stivanin <admin at polslinux dot it>

pkgname=keepassxc-git
_gitname=keepassxc
pkgver=2.5.4.r195.g1ad01844
pkgrel=2
pkgdesc="Cross-platform community-driven port of KeePass Password Safe"
arch=('i686' 'x86_64')
url="https://github.com/keepassxreboot/keepassxc"
license=('GPL2')
depends=('qt5-base' 'qt5-x11extras' 'qt5-translations' 'qt5-imageformats'
         'libxtst' 'shared-mime-info' 'hicolor-icon-theme' 'desktop-file-utils'
         'yubikey-personalization' 'qt5-svg' 'qrencode' 'quazip'
         'libsodium' 'libgcrypt' 'argon2' 'zlib')
install=keepassxc.install
makedepends=('git' 'intltool' 'cmake' 'qt5-tools')
provides=(org.freedesktop.secrets)
conflicts=('keepassxc')
replaces=('keepassx-http' 'keepassx-reboot-git')
options=(!emptydirs)
source=(git+https://github.com/keepassxreboot/keepassxc.git#branch=develop)
md5sums=('SKIP')

pkgver() {
    cd "${_gitname}"
    git describe --long | sed 's/^FOO-//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    cd "${_gitname}"
    mkdir -p build
    sed -i'' -e 's:/usr/local/share/man/:${CMAKE_INSTALL_MANDIR}/:' src/cli/CMakeLists.txt
}

build() {
    cd "${_gitname}/build"
    cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_BINDIR=/usr/bin \
        -DCMAKE_INSTALL_LIBDIR=/usr/lib \
        -DCMAKE_VERBOSE_MAKEFILE=OFF \
        -DWITH_GUI_TESTS=ON \
        -DWITH_XC_ALL=ON \
        -DWITH_XC_KEESHARE_SECURE=ON \
        -DCMAKE_BUILD_TYPE=RelWithDebInfo ..
    make
}

#check() {
#    cd "${_gitname}/build"
#    make test
#}

package() {
    cd "${_gitname}/build"
    make DESTDIR="${pkgdir}" install
}

