# Maintainer: Butui Hu <hot123tea123@gmail.com>

_pkgname=datasets
pkgname=python-datasets
pkgver=3.2.0
pkgrel=1
pkgdesc='The largest hub of ready-to-use datasets for ML models with fast, easy-to-use and efficient data manipulation tools'
arch=('any')
url='https://github.com/huggingface/datasets'
license=('Apache-2.0')
depends=(
  python-aiohttp
  python-dill
  python-filelock
  python-fsspec
  python-huggingface-hub
  python-multiprocess
  python-numpy
  python-packaging
  python-pandas
  python-pyarrow
  python-requests
  python-tqdm
  python-xxhash
  python-yaml
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/huggingface/datasets/archive/refs/tags/${pkgver}.tar.gz")
sha512sums=('2e381d6919ea7f2e934e832d6bff30a4b23cfe929c0b4345703ce39c7882daf8a234fe71ca89d1d9bcd75000e35845156f7036f9604504ac584feb1b8d3239b1')

prepare() {
  # pyarrow_hotfix is only used for pyarrow < 14.0.1
  sed -i "/pyarrow_hotfix/d" "${srcdir}/${_pkgname}-${pkgver}/src/datasets/features/features.py"
  sed -i "/pyarrow-hotfix/d" "${srcdir}/${_pkgname}-${pkgver}/setup.py"
}

build() {
  cd "${_pkgname}-${pkgver}"
  python -m build --wheel --no-isolation
}

package() {
  cd "${_pkgname}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
}
# vim:set ts=2 sw=2 et:
