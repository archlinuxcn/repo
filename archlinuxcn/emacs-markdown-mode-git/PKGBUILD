# Maintainer: Noel Maersk <veox wemakethings net>
# Contributor: emhs <emhs08 gmail com>
# Contributor: Nils <mail n-sch de>

pkgname=emacs-markdown-mode-git
pkgver=2.5.r46.gb094ae0
pkgrel=1
pkgdesc='Emacs markdown-mode'
arch=('any')
url='http://jblevins.org/projects/markdown-mode/'
license=('GPL')
makedepends=('git')
depends=('emacs')
install=${pkgname}.install
provides=('emacs-markdown-mode')
conflicts=('emacs-markdown-mode')
source=('git+https://github.com/jrblevin/markdown-mode.git')
md5sums=('SKIP')

pkgver() {
  cd markdown-mode

  git describe --tag | sed -e 's/^[^0-9]*//' -e 's/_/./g' -e 's/-/.r/' -e 's/-/./g'
}

build() {
  cd markdown-mode

  emacs -batch -q -f batch-byte-compile *.el
}

package() {
  cd markdown-mode
  mkdir -p "$pkgdir/usr/share/emacs/site-lisp/markdown-mode"
  install -Dm644 *.el{,c} "$pkgdir/usr/share/emacs/site-lisp/markdown-mode"
}
