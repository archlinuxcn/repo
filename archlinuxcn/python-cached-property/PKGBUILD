# Maintainer: Xeonacid <h.dwwwwww@gmail.com>

_name=cached-property
pkgname=python-${_name}
pkgver=2.0.1
pkgrel=3
pkgdesc="A decorator for caching properties in classes."
arch=(any)
url="https://github.com/pydanny/${_name}"
license=(BSD-3-Clause)
depends=(python)
makedepends=(python-build python-installer python-setuptools)
checkdepends=(python-pytest python-freezegun)
source=(${_name}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz)
sha512sums=('9dbb5bf5ea33a0da1eb60e138884ee69e2412922bdfbc61b267ca2cb740a937ea390945975dca79a061c1ad6c87e9ca44dda7b32fecf9c65bb3928fed3f0131b')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check(){
  cd $_name-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest -vv tests/
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
}
