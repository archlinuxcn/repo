_name=httpx
pkgname=python-httpx-latest
pkgver=0.20.0
pkgrel=1
pkgdesc="The next generation HTTP client."
arch=(any)
url="https://github.com/encode/httpx"
license=('BSD')
depends=('python-charset-normalizer' 'python-sniffio' 'python-rfc3986' 'python-httpcore-latest' 'python-idna')
makedepends=('python-setuptools')
optdepends=('python-h2: HTTP/2 support' 'python-brotli: decoding for "brotli" compressed responses' 'python-brotlicffi: decoding for "brotli" compressed responses')
conflicts=('python-httpx')
provides=('python-httpx')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/httpx-0.20.0.tar.gz")
sha256sums=('09606d630f070d07f9ff28104fbcea429ea0014c1e89ac90b4d8de8286c40e7b')

prepare() {
  cd "$srcdir/httpx-0.20.0"
  sed -i '/certifi/d' setup.py
  sed -e '/import certifi/d' \
      -e 's|certifi.where()|"/etc/ssl/certs/ca-certificates.crt"|' \
      -i httpx/_config.py
}

build() {
  cd "$srcdir/httpx-0.20.0"
  python3 setup.py build
}

package() {
  cd "$srcdir/httpx-0.20.0"
  python3 setup.py install --root=$pkgdir --optimize=1 --skip-build

  # make sure we don't install any world-writable or root-readable-only files
  # we shouldn't need to fix ownership as we extract tarballs as a non-root user
  # https://github.com/pypa/setuptools/issues/1328
  # https://github.com/LonamiWebs/Telethon/issues/1605
  chmod u=rwX,go=rX -R "$pkgdir"
  # make sure we don't install annoying files
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  rm -rf "$pkgdir/$_site_packages/tests/"
}

