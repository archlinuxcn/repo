# Maintainer: Joffrey <j-off@live.fr>
# Contributor: eolianoe <eolianoe [at] gmail [DoT] com>
# Contributor: Edvinas Valatka <edacval@gmail.com>
# Contributor: Aaron Lindsay <aaron@aclindsay.com>

pkgname=seafile-server
pkgver=7.1.1
pkgrel=1
pkgdesc='Seafile server core'
arch=('i686' 'x86_64' 'armv7h' 'armv6h' 'aarch64')
url='https://github.com/haiwen/seafile-server'
license=('AGPL3')
depends=(
    "ccnet-server>=$pkgver"
    'fuse2'
    'libevhtp-seafile'
    'libarchive'
)
makedepends=('vala')
conflicts=('seafile')
source=(
    "$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver-server.tar.gz"
    'fix_seafile-controller_paths.diff'
    'fix_seafile-admin.diff'
    'fix_mysql_support.diff'
    'seafile-server@.service'
)
sha256sums=('6f8ea0204c322b3f1675e73f2e639d1167e880bc53596e9fe5fbb6c501de6bee'
            '8069df2e84e5142a030c4598e410eeece1aaed2fdce3b8abe82b4752d257ffb9'
            '5a3254ca0af6c4cd9ac39604c8c723248e493f470ae97e8aa457861c2a5be593'
            '4596350a73025b63ad8189488bff896c09a4b1e2855e25ee5bbc111d25b7cfe7'
            'da31d1b61031cbacc42e1ab708c67c83dba933ff391b07677dabab7ab79729f4')

prepare() {
    cd "$srcdir/$pkgname-$pkgver-server"
    # Remove scripts for tests and others OS
    rm -rf "./scripts/"{build,upgrade/win32,*.bat,*.md} "./integration-tests"

    # Apply patchs
    sed -i "s|(DESTDIR)@prefix@|@prefix@|" "./lib/libseafile.pc.in"
    patch -Np1 --no-backup-if-mismatch < ../fix_seafile-controller_paths.diff
    patch -Np1 --no-backup-if-mismatch < ../fix_seafile-admin.diff
    patch -Np1 --no-backup-if-mismatch < ../fix_mysql_support.diff
    2to3 -nw --no-diffs tools/seafile-admin

    sed -i -e 's:\${INSTALLPATH}/seafile/bin/:/usr/bin/:' scripts/*.sh
}

build() {
    cd "$srcdir/$pkgname-$pkgver-server"
    ./autogen.sh
    ./configure \
        --enable-fuse \
        --enable-python \
        --prefix=/usr \
        PYTHON='/usr/bin/python'
    make
}

package() {
    cd "$srcdir/$pkgname-$pkgver-server"
    make DESTDIR="$pkgdir" install

    # Prepare directories layout for deploying
    # These makes setup-seafile*.sh happy, whether we use them or not
    mkdir -p "$pkgdir/usr/share/$pkgname/"{runtime,seafile}
    cp -r -p "./scripts"/* "$pkgdir/usr/share/$pkgname/"

    install -Dm644 \
        "$srcdir/seafile-server@.service" \
        "$pkgdir/usr/lib/systemd/system/seafile-server@.service"
}
