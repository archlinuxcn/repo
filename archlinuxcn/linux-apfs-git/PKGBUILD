# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>
# Contributor: ManU
# Forked from aur/linux-can-dkms
# Contributor: Kyle Manna <kyle(at)kylemanna(dot)com>

pkgname=linux-apfs-git
epoch=1
pkgver=r6.2a598f1
pkgrel=65
pkgdesc="Experimental APFS kernel module (DKMS)"
arch=('any')
url="https://github.com/eafer/linux-apfs-oot"
license=('GPL2')
depends=('linux')
makedepends=('git' 'linux-headers' 'xz')
source=("git+https://github.com/eafer/linux-apfs-oot")
sha256sums=('SKIP')

pkgver() {
  cd linux-apfs-oot
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  _kernver=$(</usr/src/linux/version)

  cd linux-apfs-oot
  make KERNELRELEASE=$_kernver

  xz apfs.ko
}

package() {
  _kernver=$(</usr/src/linux/version)

  cd linux-apfs-oot
  install -Dm644 apfs.ko.xz -t "$pkgdir"/usr/lib/modules/$_kernver/extramodules
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
