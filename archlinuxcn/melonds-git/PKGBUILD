# Maintainer: Zion Nimchuk <zionnimchuk@gmail.com>

## links
# http://melonds.kuribo64.net/
# https://github.com/melonDS-emu/melonDS

: ${_use_clang=true}

pkgname=melonds-git
_gitname=melonDS
pkgver=2641.e6aa4bdff
pkgrel=1
pkgdesc='DS emulator, sorta'
url="https://github.com/melonDS-emu/melonDS"
license=('GPL-3.0-or-later')
arch=('i686' 'x86_64' 'arm' 'armv6h' 'armv7h' 'aarch64')
depends=('enet' 'libarchive' 'libslirp' 'qt6-base' 'qt6-multimedia' 'qt6-svg' 'sdl2' 'faad2' 'vulkan-icd-loader')
makedepends=('cmake' 'extra-cmake-modules' 'git' 'ninja' 'pkg-config' 'vulkan-headers')

if [[ "${_use_clang::1}" == "t" ]]; then
  makedepends+=('clang' 'lld')
else
  options=('!lto')
fi

provides=('melonds')
conflicts=('melonds')

source=("${_gitname}::git+https://github.com/melonDS-emu/melonDS.git")
sha256sums=('SKIP')

pkgver()
{
  cd "${_gitname}"
  printf "%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${_gitname}"

  # allow Arch build system to control build
  sed -e '/CMAKE_INTERPROCEDURAL_OPTIMIZATION/d' -i CMakeLists.txt
  truncate -s 0 cmake/DefaultBuildFlags.cmake

  # fix for Qt 6.10
  sed -E -e 's&\b(Qt6::)?(Widgets)\b&\1\2 \1GuiPrivate&' -i src/frontend/qt_sdl/CMakeLists.txt
}

build() {
  export CC CXX CFLAGS CXXFLAGS LDFLAGS
  CFLAGS+=" -DNDEBUG"
  CXXFLAGS+=" -DNDEBUG"

  local _ldflags=(${LDFLAGS})

  if [[ "${_use_clang::1}" == "t" ]]; then
    export CC=clang
    export CXX=clang++
    export LDFLAGS="${_ldflags[@]//*fuse-ld*/} -fuse-ld=lld"
  fi

  local _cmake_options=(
    -B build
    -S "$_gitname"
    -G Ninja
    -DCMAKE_BUILD_TYPE=None
    -DCMAKE_INSTALL_PREFIX='/usr'
    -DUSE_QT6=ON
    -DUSE_SYSTEM_LIBSLIRP=ON
    -Wno-dev
  )

  cmake "${_cmake_options[@]}"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}

# vim: ts=2 sw=2 et:
