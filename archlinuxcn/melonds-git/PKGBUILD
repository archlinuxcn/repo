# Maintainer: Zion Nimchuk <zionnimchuk@gmail.com>

pkgname=melonds-git
_gitname=melonDS
pkgver=2093.d83172e5
pkgdesc='DS emulator, sorta. also 1st quality melon.'
pkgrel=1
arch=('i686' 'x86_64')
url="http://melonds.kuribo64.net/"
license=('GPLv3')
makedepends=('git' 'cmake' 'extra-cmake-modules')
depends=('qt6-base' 'qt6-multimedia' 'pkg-config' 'sdl2' 'libslirp' 'libepoxy' 'libarchive' 'hicolor-icon-theme')
source=("${_gitname}::git+https://github.com/Arisotura/${_gitname}.git")
provides=('melonds')
conflicts=('melonds')

md5sums=('SKIP')

pkgver()
{
  cd "${_gitname}"
  printf "%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build()
{
  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
  cd build
  cmake ../${_gitname} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DUSE_QT6=ON
  make VERBOSE=ON
}

package()
{
  cd build
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
