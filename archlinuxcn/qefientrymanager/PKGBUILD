# Maintainer: Integral <integral@member.fsf.org>

pkgname=qefientrymanager
_srcname=QEFIEntryManager
pkgver=0.3.1
pkgrel=1
pkgdesc="A userspace cross-platform EFI boot entry management GUI App based on Qt"
arch=('x86_64' 'aarch64' 'riscv64')
url="https://github.com/Inokinoki/${_srcname}"
license=('GPL-3.0-or-later')
depends=('qt6-base' 'qt6-wayland' 'efivar' 'hicolor-icon-theme')
makedepends=('git' 'cmake')
source=(
	"git+${url}.git#tag=v${pkgver}"
	"git+https://github.com/Inokinoki/qefivar.git"
)
sha256sums=('6a717d88841b6906be6ee951984ebb7d00d1950845af248b533535bc3febabba'
            'SKIP')

prepare() {
	cd "${_srcname}/"

	git submodule init
	git config submodule.qefivar.url "${srcdir}/qefivar/"
	git -c protocol.file.allow=always submodule update

	mkdir -p build
}

build() {
	cd "${_srcname}/build/"
	cmake -DCMAKE_BUILD_TYPE=None -DCMAKE_INSTALL_PREFIX=/usr ..
	cmake --build .
}

package() {
	cd "${_srcname}/"

	DESTDIR="${pkgdir}" cmake --install build

	# License
	install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${_pkgname}/"

	# Documentation
	install -Dm644 README.md -t "${pkgdir}/usr/share/doc/${_pkgname}/"
}
