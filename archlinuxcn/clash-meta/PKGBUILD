# Maintainer: sukanka <su975853527 at gmail dot com>

pkgname=clash-meta
pkgver=1.18.3
pkgrel=1
pkgdesc="Another Clash Kernel by MetaCubeX"
arch=(x86_64)
url="https://github.com/MetaCubeX/mihomo"
license=(GPL-3.0-or-later)
depends=(clash-geoip glibc)
makedepends=(go)
backup=('etc/clash-meta/config.yaml')
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('d070c4b464650093c6a29ef475f1cf5beec7c4b93e7704ca01eaf01f6a56a020')

prepare() {
    mv "mihomo-$pkgver"       "$pkgname"
    cd "$pkgname"
    mv .github/mihomo.service "$pkgname.service"
}

build() {
    export CGO_CPPFLAGS="${CPPFLAGS}"
    export CGO_CFLAGS="${CFLAGS}"
    export CGO_CXXFLAGS="${CXXFLAGS}"
    export CGO_LDFLAGS="${LDFLAGS}"
    export CGO_ENABLED=1
    cd "$pkgname"
    NAME=clash-meta
    BUILDTIME=$(date -u --rfc-3339=seconds)
    LDFLAGS="-linkmode external -extldflags \"${LDFLAGS}\""
    OTHER_FLAGS="-X \"github.com/metacubex/mihomo/constant.Version=${pkgver}\" \
                 -X \"github.com/metacubex/mihomo/constant.BuildTime=${BUILDTIME}\""
    GOOS=linux go build \
        -trimpath \
        -buildmode=pie \
        -mod=readonly \
        -modcacherw \
        -ldflags "$LDFLAGS $OTHER_FLAGS" \
        -tags with_gvisor \
        -o "$pkgname-$pkgver"

    echo "u $pkgname - \"Clash-Meta Service\" - -"      >  "$pkgname.sysusers"
    echo "d /etc/$pkgname     0755 $pkgname $pkgname -" >  "$pkgname.tmpfiles"
    echo "d /var/log/$pkgname 0700 $pkgname $pkgname -" >> "$pkgname.tmpfiles"
    sed -i -e '/Description/s/mihomo/Clash-Meta/' \
           -e "s/mihomo/$pkgname/g"               \
           -e "/^Type=simple/a User=$pkgname"     \
           -e "/^Type=simple/a Group=$pkgname"    "$pkgname.service"
}

check() {
    cd "$pkgname"
    go test ./...
}

package() {
    cd "$pkgname"
    install -Dm755 "$pkgname-$pkgver"      "$pkgdir/usr/bin/clash-meta"
    install -Dm644 "$pkgname.sysusers"     "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"
    install -Dm644 "$pkgname.tmpfiles"     "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"
    install -Dm644 docs/config.yaml        "$pkgdir/etc/clash-meta/config.yaml"
    install -Dm644 "$pkgname.service"      "$pkgdir/usr/lib/systemd/system/$pkgname.service"
    ln -s          /etc/clash/Country.mmdb "$pkgdir/etc/$pkgname/Country.mmdb"
}
