# Maintainer: Jonathan Steel <jsteel at archlinux.org>
# Contributor: Benjamin Klettbach <b.klettbach@gmail.com>
# Contributor: Maciek Marciniak <mm2pl at kotmisia.pl>

pkgname=obs-studio-lily
pkgver=31.0.0
pkgrel=4
pkgdesc="Free, open source software for live streaming and recording (with websocket support)"
arch=('x86_64')
url="https://obsproject.com"
license=('GPL2')
depends=('ffmpeg' 'jansson' 'libxinerama' 'libxkbcommon-x11' 'mbedtls' 'rnnoise' 'pciutils'
         'qt6-svg' 'curl' 'jack' 'gtk-update-icon-cache' 'pipewire' 'libxcomposite'
         'libdatachannel' 'uthash')
makedepends=('cmake' 'libfdk-aac' 'x264' 'swig' 'python' 'luajit' 'sndio' 'nlohmann-json' 'websocketpp' 'asio' 'qrcodegencpp-cmake')
provides=(obs-studio=$pkgver)
conflicts=(obs-studio)
optdepends=('libfdk-aac: FDK AAC codec support'
            'libva-intel-driver: hardware encoding'
            'libva-mesa-driver: hardware encoding'
            'luajit: scripting support'
            'python: scripting support'
            'sndio: Sndio input client'
            'v4l2loopback-dkms: virtual camera support'
            'qrcodegencpp-cmake: websocket support')
source=(obs-studio-$pkgver.tar.gz::https://github.com/obsproject/obs-studio/releases/download/$pkgver/OBS-Studio-$pkgver-Sources.tar.gz
        obs-studio-11618.patch::https://patch-diff.githubusercontent.com/raw/obsproject/obs-studio/pull/11618.patch
        nv-codec-headers.tar.gz::https://github.com/FFmpeg/nv-codec-headers/releases/download/n12.1.14.0/nv-codec-headers-12.1.14.0.tar.gz)
# XXX nv-codec-headers are kept back at version n12.1.14.0 due to OBS not supporting any newer version
sha256sums=('23d45d8ffdf9e673aa0ef4ee7b8d030a63f21151c709b6e4657d20bc230db178'
            '81680e20c3b517d051f738043079a58c8e8be516afaa7f4c5f9c5ec5f80bf026'
            '62b30ab37e4e9be0d0c5b37b8fee4b094e38e570984d56e1135a6b6c2c164c9f')

prepare() {
  cd obs-studio-$pkgver-sources

  patch -Np1 -i "$srcdir"/obs-studio-11618.patch
}

build() {
  export CFLAGS+=" -Wno-error=deprecated-declarations"
  cmake -B build -S obs-studio-$pkgver-sources \
    -DFFnvcodec_INCLUDE_DIR="nv-codec-headers-12.1.14.0/include/" \
    -DCMAKE_INSTALL_PREFIX="/usr" \
    -DENABLE_BROWSER=OFF \
    -DENABLE_VST=ON \
    -DENABLE_VLC=OFF \
    -DENABLE_NEW_MPEGTS_OUTPUT=OFF \
    -DENABLE_AJA=OFF \
    -DENABLE_JACK=ON \
    -DENABLE_LIBFDK=ON \
    -DENABLE_WEBRTC=ON \
    -DOBS_VERSION_OVERRIDE="$pkgver" \
    -DCALM_DEPRECATION=ON \
    -DENABLE_WEBSOCKET=ON \
    -Wno-dev
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
