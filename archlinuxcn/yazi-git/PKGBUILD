# Maintainer: Pig Fang <g-plane@hotmail.com>

pkgname=yazi-git
_pkgname=yazi
pkgver=0.2.3.r50.gfa2632e
pkgrel=1
pkgdesc="Blazing fast terminal file manager written in Rust, based on async I/O."
url="https://github.com/sxyazi/yazi"
arch=("x86_64" "aarch64" "riscv64")
license=("MIT")
optdepends=(
	'ttf-nerd-fonts-symbols: for full display of icons'
	'jq: for JSON preview'
	'unarchiver: for archive preview'
	'ffmpegthumbnailer: for video thumbnails'
	'fd: for file searching'
	'ripgrep: for file content searching'
	'fzf: for directory jumping'
	'zoxide: for directory jumping')
makedepends=("git" "cargo" "imagemagick")
conflicts=("${_pkgname}")
provides=("${_pkgname}")
source=("git+${url}.git")
sha256sums=('SKIP')
options=('!lto')

pkgver() {
	cd "${_pkgname}/"
	git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/v//'
}

build() {
	cd "${_pkgname}/"
	YAZI_GEN_COMPLETIONS=true cargo build --release
}

package() {
	cd "${_pkgname}/"
	install -Dm644 "LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}/"
	install -Dm755 "target/release/${_pkgname}" -t "${pkgdir}/usr/bin/"
	install -Dm644 "assets/yazi.desktop" -t "${pkgdir}/usr/share/applications/"

	local r
	for r in 16 24 32 48 64 128 256; do
		install -dm755 "${pkgdir}/usr/share/icons/hicolor/${r}x${r}/apps/"
		convert assets/logo.png -resize "${r}x${r}" "$pkgdir/usr/share/icons/hicolor/${r}x${r}/apps/yazi.png"
	done

	cd "${_pkgname}-boot/completions"
	install -Dm644 "${_pkgname}.bash" "${pkgdir}/usr/share/bash-completion/completions/${_pkgname}"
	install -Dm644 "${_pkgname}.fish" -t "${pkgdir}/usr/share/fish/vendor_completions.d/"
	install -Dm644 "_${_pkgname}" -t "${pkgdir}/usr/share/zsh/site-functions/"
}
