# Maintainer: Pig Fang <g-plane@hotmail.com>

pkgname=yazi-git
_pkgname=yazi
pkgver=0.1.4.r7.ga2ecc9f
pkgrel=1
pkgdesc="Blazing fast terminal file manager written in Rust, based on async I/O."
url="https://github.com/sxyazi/yazi"
arch=("x86_64" "aarch64" "riscv64")
license=("MIT")
depends=("ttf-nerd-fonts-symbols")
optdepends=(
	'jq: for JSON preview'
	'unarchiver: for archive preview'
	'ffmpegthumbnailer: for video thumbnails'
	'fd: for file searching'
	'ripgrep: for file content searching'
	'fzf: for directory jumping'
	'zoxide: for directory jumping')
makedepends=("git" "cargo")
conflicts=("${_pkgname}")
provides=("${_pkgname}")
source=("git+${url}.git")
sha256sums=('SKIP')
options=('!lto')

pkgver() {
	cd "${_pkgname}/"
	git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/v//'
}

build() {
	cd "${_pkgname}/"
	cargo build --release
}

package() {
	cd "${_pkgname}/"
	install -Dm644 "LICENCE" -t "${pkgdir}/usr/share/licenses/${pkgname}/"
	install -Dm755 "target/release/${_pkgname}" -t "${pkgdir}/usr/bin/"
}
