# Maintainer: Integral <integral@member.fsf.org>
# Contributor: Pig Fang <g-plane@hotmail.com>

pkgname=yazi-git
_pkgname=yazi
pkgver=25.5.31.r97.g021d86f
pkgrel=1
pkgdesc="Blazing fast terminal file manager written in Rust, based on async I/O."
url="https://github.com/sxyazi/${_pkgname}"
arch=("x86_64" "aarch64" "riscv64")
license=("MIT")
optdepends=(
	'ttf-nerd-fonts-symbols: for full display of icons'
	'ffmpeg: for video thumbnails'
	'7zip: for archive extraction and preview'
	'jq: for JSON preview'
	'poppler: for PDF preview'
	'fd: for file searching'
	'ripgrep: for file content searching'
	'fzf: for quick file subtree navigation'
	'zoxide: for historical directories navigation'
	'resvg: for SVG preview'
	'imagemagick: for image and font preview'
	'xclip: for X11 clipboard support'
	'xsel: for X11 clipboard support'
	'wl-clipboard: for Wayland clipboard support'
	'chafa: for ASCII image preview as fallback'
)
makedepends=("git" "cargo" "imagemagick")
conflicts=("${_pkgname}")
provides=("${_pkgname}")
source=("git+${url}.git")
sha256sums=('SKIP')
options=('!lto')

pkgver() {
	cd "${_pkgname}/"
	git describe --long --tags --match="v[0-9]*" --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/v//'
}

prepare() {
	cd "${_pkgname}/"
	cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
	cd "${_pkgname}/"
	YAZI_GEN_COMPLETIONS=true cargo build --frozen --release --all-features
}

check() {
	cd "${_pkgname}/"
	cargo test --frozen --all-features
}

package() {
	cd "${_pkgname}/"
	install -Dm644 "LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}/"
	install -Dm755 target/release/{${_pkgname},ya} -t "${pkgdir}/usr/bin/"
	install -Dm644 "assets/${_pkgname}.desktop" -t "${pkgdir}/usr/share/applications/"

	local r
	for r in 16 24 32 48 64 128 256; do
		install -dm755 "${pkgdir}/usr/share/icons/hicolor/${r}x${r}/apps/"
		magick assets/logo.png -resize "${r}x${r}" "$pkgdir/usr/share/icons/hicolor/${r}x${r}/apps/${_pkgname}.png"
	done

	cd "${_pkgname}-boot/completions"
	_install_completions "${_pkgname}"

	cd "../../${_pkgname}-cli/completions"
	_install_completions ya
}

_install_completions() {
	install -Dm644 "$1.bash" "${pkgdir}/usr/share/bash-completion/completions/$1"
	install -Dm644 "$1.fish" -t "${pkgdir}/usr/share/fish/vendor_completions.d/"
	install -Dm644 "_$1" -t "${pkgdir}/usr/share/zsh/site-functions/"
}
