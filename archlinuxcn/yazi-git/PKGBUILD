# Maintainer: Integral <integral@member.fsf.org>
# Contributor: Pig Fang <g-plane@hotmail.com>

pkgname=yazi-git
_pkgname=yazi
pkgver=26.1.22.r32.ge49cdfe
pkgrel=1
pkgdesc="Blazing fast terminal file manager written in Rust, based on async I/O."
url="https://github.com/sxyazi/${_pkgname}"
arch=("x86_64" "aarch64" "riscv64")
license=("MIT")
depends=('glibc' 'libgcc' 'hicolor-icon-theme' 'ttf-nerd-fonts-symbols' 'oniguruma')
optdepends=(
	'ttf-nerd-fonts-symbols: for full display of icons'
	'ffmpeg: for video thumbnails'
	'7zip: for archive extraction and preview'
	'jq: for JSON preview'
	'poppler: for PDF preview'
	'fd: for file searching'
	'ripgrep: for file content searching'
	'fzf: for quick file subtree navigation'
	'zoxide: for historical directories navigation'
	'resvg: for SVG preview'
	'imagemagick: for image and font preview'
	'xclip: for X11 clipboard support'
	'xsel: for X11 clipboard support'
	'wl-clipboard: for Wayland clipboard support'
	'chafa: for ASCII image preview as fallback'
	'git: for Yazi package management'
)
makedepends=("git" "cargo" "imagemagick")
conflicts=("${_pkgname}")
provides=("${_pkgname}")
source=("git+${url}.git")
sha256sums=('SKIP')
options=('!lto')

pkgver() {
	cd "${_pkgname}/"
	git describe --long --tags --match="v[0-9]*" --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/v//'
}

prepare() {
	cd "${_pkgname}/"
	cargo fetch --locked --target host-tuple
}

build() {
	cd "${_pkgname}/"
	YAZI_GEN_COMPLETIONS=true RUSTONIG_DYNAMIC_LIBONIG=1 cargo build --frozen --release --all-features
}

check() {
	cd "${_pkgname}/"
	RUSTONIG_DYNAMIC_LIBONIG=1 cargo test --frozen --all-features --workspace
}

package() {
	cd "${_pkgname}/"
	install -Dm644 "LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}/"
	install -Dm755 target/release/{${_pkgname},ya} -t "${pkgdir}/usr/bin/"
	install -Dm644 "assets/${_pkgname}.desktop" -t "${pkgdir}/usr/share/applications/"

	for r in 16 24 32 48 64 128 256 512; do
		install -dm755 "${pkgdir}/usr/share/icons/hicolor/${r}x${r}/apps/"
		magick assets/logo.png -resize "${r}x${r}" "$pkgdir/usr/share/icons/hicolor/${r}x${r}/apps/${_pkgname}.png"
	done

	_install_completions "${_pkgname}-boot" "${_pkgname}"
	_install_completions "${_pkgname}-cli" "ya"
}

_install_completions() {
	pushd "$1/completions"
	install -Dm644 "$2.bash" "${pkgdir}/usr/share/bash-completion/completions/$2"
	install -Dm644 "$2.fish" -t "${pkgdir}/usr/share/fish/vendor_completions.d/"
	install -Dm644 "_$2" -t "${pkgdir}/usr/share/zsh/site-functions/"
	install -Dm644 "$2.elv" -t "${pkgdir}/usr/share/elvish/lib/"
	popd
}
