pkgname=miniupnpd-nft-git
pkgver=2.3.7.r86.gdaefbb3
pkgrel=1
pkgdesc="Lightweight UPnP IGD daemon with nftables support"
arch=('x86_64')
url="https://miniupnp.tuxfamily.org"
license=('BSD-3-Clause')
depends=('iptables' 'util-linux' 'util-linux-libs' libuuid.so 'libcap-ng' libcap-ng.so 'libnftnl')
makedepends=('git' 'lsb-release' 'procps-ng')
provides=('miniupnpd')
conflicts=('miniupnpd')
backup=(etc/miniupnpd/miniupnpd.conf)
source=("git+https://github.com/miniupnp/miniupnp.git"
        "miniupnpd.service")
sha256sums=('SKIP'
            'e4fb18c389feb752fd844ffee534775d67e643e854202483922381672c299d28')


pkgver() {
  cd "miniupnp"

  git describe --long --tags --match 'miniupnpd_*' | sed 's/^miniupnpd_//;s/\([^-]*-g\)/r\1/;s/[_-]/./g'
}

build() {
  cd "miniupnp/miniupnpd"
  ./configure --ipv6 --leasefile --disable-fork --firewall=nftables
  make
}

package() {
  cd "miniupnp/miniupnpd"

  make DESTDIR="$pkgdir" PREFIX=/ INSTALLPREFIX=/usr SBINDIR=/usr/bin install

  rm -r "$pkgdir/etc/init.d"

  install -Dm644 "LICENSE" "$pkgdir/usr/share/licenses/miniupnpd/LICENSE"
  install -Dm644 "$srcdir/miniupnpd.service" "$pkgdir/usr/lib/systemd/system/miniupnpd.service"
  install -Dm755 "netfilter_nft/scripts/miniupnpd_functions.sh" "$pkgdir/etc/miniupnpd/miniupnpd_functions.sh"
  install -Dm755 "netfilter_nft/scripts/nft_init.sh" "$pkgdir/etc/miniupnpd/nft_init.sh"
  install -Dm755 "netfilter_nft/scripts/nft_flush.sh" "$pkgdir/etc/miniupnpd/nft_flush.sh"
  install -Dm755 "netfilter_nft/scripts/nft_delete_chain.sh" "$pkgdir/etc/miniupnpd/nft_delete_chain.sh"
  install -Dm755 "netfilter_nft/scripts/nft_removeall.sh" "$pkgdir/etc/miniupnpd/nft_removeall.sh"
  install -Dm755 "netfilter_nft/scripts/nft_display.sh" "$pkgdir/etc/miniupnpd/nft_display.sh"

  sed -i -e "s/^uuid=0\{8\}\(-0000\)\{3\}-0\{12\}$/uuid=`uuidgen`/g" "$pkgdir/etc/miniupnpd/miniupnpd.conf"
}
