# Maintainer: dawfukfr <dawfukfr@gmail.com>

pkgname=aquamarine-git
_pkgname=${pkgname%-git}
pkgver=0.9.3.r2.g50637ed
pkgrel=2
pkgdesc="A very light linux rendering backend library"
arch=('x86_64' 'aarch64' 'riscv64')
url="https://github.com/hyprwm/${_pkgname}"
license=('BSD-3-Clause')
depends=(
	'pugixml'
	'seatd'
	'wayland-protocols'
	'pixman'
	'libdisplay-info'
	'opengl-driver'
	'hyprutils-git' 'libhyprutils.so'
	'libinput'
)
makedepends=('git' 'cmake' 'hyprwayland-scanner-git')
source=("git+${url}.git")
provides=("${_pkgname}")
conflicts=("${_pkgname}")
sha256sums=('SKIP')

pkgver() {
	cd "${_pkgname}/"
	git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/v//'
}

build() {
	cmake -S "${_pkgname}" \
		-B build \
		-DCMAKE_BUILD_TYPE=None \
		-DCMAKE_INSTALL_PREFIX=/usr

	cmake --build build
}

package() {
	DESTDIR="${pkgdir}" cmake --install build
	install -Dm644 "${_pkgname}/LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
