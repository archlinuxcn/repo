# Maintainer: Kiri <kiri@vern.cc> 
# Contributor: Igor Dyatlov <dyatlov.igor@protonmail.com>

pkgname=amberol
pkgver=0.10.3
pkgrel=1
pkgdesc="Plays music, and nothing else"
arch=('x86_64' 'aarch64')
url="https://apps.gnome.org/app/io.bassi.Amberol"
license=('GPL-3.0-or-later')
depends=('libadwaita'
         'gstreamer' 
         'gcc-libs'
         'gdk-pixbuf2'
         'dbus'
         'hicolor-icon-theme'
         'dconf'
         'glibc'
         'pango'
         'glib2'
         'cairo'
         'graphene'
         'gtk4'
         'gst-plugins-base-libs'
         'gst-plugins-bad-libs')
makedepends=('meson'
             'cargo')
checkdepends=('appstream-glib'
              'reuse')
source=("https://gitlab.gnome.org/World/amberol/-/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
options=('!lto')
sha512sums=("12684a222e06d0abbab03f6196021c262416ca9ed48526dd5343432a314c2615866ad6b97032c45d93003bb198a2bc90cb23baaf7debe3d3581cb3fd925021cf")

prepare() {
  cd ${pkgname}-${pkgver}
  export RUSTUP_TOOLCHAIN=stable
  cargo update
  cargo fetch --target "$CARCH-unknown-linux-gnu"
}

build() {
  local meson_options=(
    --buildtype release
  )

  export RUSTUP_TOOLCHAIN=stable
  arch-meson ${pkgname}-${pkgver} build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs || :
}

package() {
  meson install -C build --destdir "$pkgdir"
}

