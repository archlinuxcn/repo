# Maintainer: Integral <integral@archlinuxcn.org>
# Contributor: Antoine Viallon <antoine+aur@lesviallon.fr>

pkgname=video2x
pkgver=6.1.0
pkgrel=1
epoch=1
pkgdesc="Machine learning video/GIF/image upscaling"
url="https://${pkgname}.org"
arch=('x86_64' 'aarch64' 'riscv64')
license=("AGPL-3.0-or-later")
depends=('ffmpeg' 'ncnn' 'vulkan-driver' 'spdlog' 'opencv')
makedepends=('git' 'cmake' 'vulkan-headers' 'boost')
optdepends=(
	"waifu2x-ncnn-vulkan: for anime/cartoon upscaling and JPEG denoising - fast"
	"realsr-ncnn-vulkan: real photos upscaling and denoising - slow"
	"srmd-ncnn-vulkan: general purpose upscaling and image restoration"
	"waifu2x-converter-cpp: C++ implemenation of waifu2x working on all platforms"
)
source=(
	"git+https://github.com/k4yt3x/${pkgname}.git#tag=${pkgver}"
	"git+https://github.com/k4yt3x/libreal-esrgan-ncnn-vulkan.git"
)
sha256sums=('56f67c37f608fd96e6826c297e0a6869d931b5e97137100dac4b1639708ea190'
            'SKIP')

prepare() {
	cd "${pkgname}/"
	git submodule init
	git config submodule.third_party/libreal_esrgan_ncnn_vulkan.url "${srcdir}/libreal-esrgan-ncnn-vulkan"
	git -c protocol.file.allow=always submodule update
}

build() {
	cmake -B build -S "${pkgname}" -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr
	cmake --build build
}

package() {
	DESTDIR="${pkgdir}" cmake --install build
	install -Dm644 "${pkgname}/LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
