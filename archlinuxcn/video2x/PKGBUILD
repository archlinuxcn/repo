# Maintainer: Integral <i7arch@member.fsf.org>
# Contributor: Antoine Viallon <antoine+aur@lesviallon.fr>

pkgname=video2x
pkgver=6.0.0.beta.1
_pkgver=${pkgver/.beta/-beta}
pkgrel=1
pkgdesc="Machine learning video/GIF/image upscaling"
url="https://${pkgname}.org"
arch=('x86_64' 'aarch64' 'riscv64')
license=("AGPL-3.0-or-later")
depends=('ffmpeg' 'ncnn' 'vulkan-driver')
makedepends=('git' 'cmake' 'vulkan-headers')
optdepends=(
	"waifu2x-ncnn-vulkan: for anime/cartoon upscaling and JPEG denoising - fast"
	"realsr-ncnn-vulkan: real photos upscaling and denoising - slow"
	"srmd-ncnn-vulkan: general purpose upscaling and image restoration"
	"waifu2x-converter-cpp: C++ implemenation of waifu2x working on all platforms"
)
source=(
	"git+https://github.com/k4yt3x/${pkgname}.git#tag=${_pkgver}"
	"git+https://github.com/k4yt3x/libreal-esrgan-ncnn-vulkan.git"
	"git+https://github.com/Tencent/ncnn.git"
)
sha256sums=('a2d7e4074faa0ef551250f047e3c2e704120b3375e057bb38699f3f418247058'
            'SKIP'
            'SKIP')

prepare() {
	cd "${pkgname}/"
	git submodule init
	git config submodule.third_party/libreal_esrgan_ncnn_vulkan.url "${srcdir}/libreal-esrgan-ncnn-vulkan"
	git config submodule.third_party/ncnn.url "${srcdir}/ncnn"
	git -c protocol.file.allow=always submodule update
}

build() {
	cmake -B build -S "${pkgname}" -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr
	cmake --build build
}

package() {
	DESTDIR="${pkgdir}" cmake --install build
	install -Dm644 "${pkgname}/LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
