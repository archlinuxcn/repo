# Maintainer: Integral <integral@archlinuxcn.org>
# Contributor: Antoine Viallon <antoine+aur@lesviallon.fr>

pkgname=video2x
pkgver=6.4.0
pkgrel=6
epoch=1
pkgdesc="Machine learning video/GIF/image upscaling"
url="https://${pkgname}.org"
arch=('x86_64')
license=("AGPL-3.0-only")
depends=('ffmpeg' 'ncnn' 'vulkan-driver' 'spdlog' 'boost-libs')
makedepends=('git' 'cmake' 'vulkan-headers' 'boost' 'openmp')
source=(
	"git+https://github.com/k4yt3x/${pkgname}.git#tag=${pkgver}"
	"git+https://github.com/k4yt3x/libreal-esrgan-ncnn-vulkan.git"
	"git+https://github.com/k4yt3x/librealcugan-ncnn-vulkan.git"
	"git+https://github.com/k4yt3x/librife-ncnn-vulkan.git"
)
sha256sums=('7140f1da2f34606b904d273c8cf48f29a928409730f63146f2a044081261e9b2'
            'SKIP'
            'SKIP'
            'SKIP')

prepare() {
	cd "${pkgname}/"
	git rm third_party/{ncnn,spdlog,boost}

	git submodule init
	git config submodule.third_party/libreal_esrgan_ncnn_vulkan.url "${srcdir}/libreal-esrgan-ncnn-vulkan"
	git config submodule.third_party/librealcugan_ncnn_vulkan.url "${srcdir}/librealcugan-ncnn-vulkan"
	git config submodule.third_party/librife_ncnn_vulkan.url "${srcdir}/librife-ncnn-vulkan"
	git -c protocol.file.allow=always submodule update

	cd third_party/librife_ncnn_vulkan
	git cherry-pick -n a60b30ade45b48b4f5df5f223f3a8674def93275 # Fix build with ncnn 20250916
}

build() {
	cmake -B build -S "${pkgname}" -DCMAKE_BUILD_TYPE=None -DCMAKE_INSTALL_PREFIX=/usr
	cmake --build build
}

package() {
	DESTDIR="${pkgdir}" cmake --install build
}
