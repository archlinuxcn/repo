# Maintainer: DuckSoft <realducksoft at gmail dot com>
pkgname=trojan-r-git
pkgver=0.1.0.r33
pkgrel=1
pkgdesc="Lightweight and blazing fast Trojan-GFW/Trojan-Go implementation"
arch=(aarch64 x86_64)
url=https://github.com/p4gefau1t/trojan-r
license=(GPL)
depends=(gcc-libs)
makedepends=(cargo git)
provides=(trojan-r)
conflicts=(trojan-r)
source=("${pkgname%-git}::git+$url" trojan-r@.service)
b2sums=('SKIP'
        '7ca5980b41e4b69b883a58197c43e0106f8a17f74783a1e8316e7ecdd6442a562a948065d0a476e29aaba44ec07a7f3c9321e292d807df16a985ef9ddd9a4e64')

pkgver() {
    cd "$srcdir"/"${pkgname%-git}"
    printf "%s.r%s" $(cargo pkgid | cut -d# -f2 | cut -d: -f2) $(git rev-list --count HEAD)
}

build() {
    cd "$srcdir"/"${pkgname%-git}"
    cargo build --release --locked --all-features --target-dir=.
}

package() {
    cd "$srcdir"/"${pkgname%-git}"
    install -Dm755 release/trojan-r -t "$pkgdir"/usr/bin/
    install -Dm644 config/*.toml -t "$pkgdir"/etc/trojan-r/examples
    install -Dm644 ../trojan-r@.service -t "$pkgdir"/usr/lib/systemd/system/
}
