# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=nginx-mainline-mod-brotli
epoch=1
pkgver=1.0.0rc
pkgrel=8

_modname="ngx_${pkgname#nginx-mainline-mod-}"

pkgdesc="Brotli compression filter module for mainline nginx"
arch=('x86_64' 'aarch64')
depends=("nginx-mainline" 'brotli')
makedepends=('nginx-mainline-src')
url="https://github.com/google/ngx_brotli"
license=('CUSTOM')

source=(https://github.com/google/$_modname/archive/v$pkgver/$_modname-$pkgver.tar.gz)
sha256sums=('c85cdcfd76703c95aa4204ee4c2e619aa5b075cac18f428202f65552104add3b')

prepare() {
	mkdir -p build
	cd build
	ln -sf /usr/src/nginx/auto
	ln -sf /usr/src/nginx/src

	cd "$srcdir"/$_modname-$pkgver
	sed 's@/usr/local@/usr@' -i config
}

build() {
	cd build
	/usr/src/nginx/configure --with-compat --add-dynamic-module=../$_modname-$pkgver
	make modules
}

package() {
	depends=("nginx-mainline=$(pacman -Q nginx-mainline | awk '{print $NF}' | sed 's/-.*//')" 'brotli')
	install -Dm644 "$srcdir"/$_modname-$pkgver/LICENSE \
		"$pkgdir"/usr/share/licenses/$pkgname/LICENSE

	cd "$srcdir"/build/objs
	for mod in ngx_*.so; do
		install -Dm755 $mod "$pkgdir"/usr/lib/nginx/modules/$mod
	done
}
