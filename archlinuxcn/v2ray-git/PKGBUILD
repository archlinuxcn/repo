# Maintainer: Dct Mei <dctxmei@yandex.com>
# Contributor: m8D2 <omui (at) proton mail (dot) com>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: pandada8 <pandada8@gmail.com>

pkgname=v2ray-git
_pkgname=v2ray-core
pkgver=5.1.0.r45.g3631053c2
pkgrel=1
pkgdesc="A platform for building proxies to bypass network (git version)"
arch=('x86_64')
url="https://github.com/v2fly/v2ray-core"
license=(MIT)
depends=('glibc' 'v2ray-domain-list-community' 'v2ray-geoip')
makedepends=('git' 'go')
provides=("v2ray=${pkgver%%.r*}")
conflicts=('v2ray')
source=("${_pkgname}::git+${url}.git"
        "v2ray.service"
        "v2ray@.service"
        "v2ray-dir.service")
sha256sums=('SKIP'
            '1ee35a51baed909fc2252fa7606a9f5ac2cbde5522ffe4cd5f4a31019eabb990'
            '6bd3ef3111b0684f5796984baba029ac0a803c6e8a2fba297cefc9f240791140'
            'ed2fa062a2b3a82bac9c77ce62bbb0d3d549c226eb720d9ffaf0994b0b530817')

pkgver() {
    cd "${srcdir}"/"${_pkgname}"/
    # cutting off 'v' prefix that presents in the git tag
    git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    cd "${srcdir}"/"${_pkgname}"/
    export CGO_CFLAGS="${CFLAGS}"
    export CGO_CPPFLAGS="${CPPFLAGS}"
    export CGO_CXXFLAGS="${CXXFLAGS}"
    export CGO_LDFLAGS="${LDFLAGS}"
    export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw -ldflags=-linkmode=external"
    go build -o v2ray ./main
}

check() {
    cd "${srcdir}"/"${_pkgname}"/
    go test -p 1 -tags json -v -timeout 30m ./...
}

package() {
    cd "${srcdir}"/"${_pkgname}"/
    install -dm0755 "${pkgdir}"/etc/v2ray/
    install -Dm0755 v2ray -t "${pkgdir}"/usr/bin/
    install -Dm0644 LICENSE -t "${pkgdir}"/usr/share/licenses/v2ray/
    install -Dm0644 "${srcdir}"/v2ray.service -t "${pkgdir}"/usr/lib/systemd/system/
    install -Dm0644 "${srcdir}"/v2ray@.service -t "${pkgdir}"/usr/lib/systemd/system/
    install -Dm0644 "${srcdir}"/v2ray-dir.service -t "${pkgdir}"/usr/lib/systemd/system/
}
