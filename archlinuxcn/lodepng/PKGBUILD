# Maintainer: Jens Carl <jc [dot] archlinux [at] jens-carl [dot] de>

pkgname=lodepng
pkgver=20241228
pkgrel=1
pkgdesc="PNG encoder and decoder in C and C++, without dependecies."
url="https://lodev.org/lodepng"
arch=('x86_64')
license=('ZLIB')
makedepends=('cmake' 'git')
source=('git+https://github.com/lvandeve/lodepng.git'
        'CMakeLists.txt')
sha256sums=('SKIP'
            '6fda51d0ff9ee41e25dea11f148775eda5b54f325491bbbe5c002b414ec4a946')

pkgver() {
    cd lodepng
    git grep 'LODEPNG_VERSION_STRING =' | cut -d\" -f2
}

prepare() {
    cp ${srcdir}/CMakeLists.txt ${srcdir}/lodepng/
    rm -rf ${srcdir}/lodepng/build && mkdir -p ${srcdir}/lodepng/build
}

build() {
    cd ${srcdir}/lodepng/build
    cmake .. \
        -DCMAKE_INSTALL_PREFIX=/usr
    make
}

package() {
    cd ${srcdir}/lodepng
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

    cd build
    make DESTDIR="${pkgdir}/" install
}
