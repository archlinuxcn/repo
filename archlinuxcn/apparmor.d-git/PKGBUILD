# Maintainer: Roald Clark <roaldclark@gmail.com>
# Contributor: Alexandre Pujol <alexandre@pujol.io>

_pkgname=apparmor.d
pkgname="${_pkgname}-git"
pkgver=0.3644
pkgrel=1
pkgdesc="Full set of apparmor profiles"
arch=("x86_64")
url="https://github.com/roddhjav/${_pkgname}"
license=('GPL-2.0-or-later')
makedepends=('go' 'git')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=("${pkgname}::git+${url}.git")
sha512sums=('SKIP')

pkgver() {
    cd "${srcdir}/${pkgname}"
    echo "0.$(git rev-list --count HEAD)"
}

prepare() {
    cd "${srcdir}/${pkgname}"

    # https://github.com/roddhjav/apparmor.d/commit/e4a7e16ec03d4d5d330834efb5e6617c599c02b1
    remove=(
        "abstractions/devices-usb-read"
        "abstractions/devices-usb"
        "abstractions/nameservice-strict"
        "tunables/multiarch.d/base"
        "groups/network/wg"
    )

    cd "${_pkgname}"
    for path in "${remove[@]}"; do
        rm -vf "${path}"
    done
}

build() {
    cd "${srcdir}/${pkgname}"
    export CGO_CPPFLAGS="${CPPFLAGS}"
    export CGO_CFLAGS="${CFLAGS}"
    export CGO_CXXFLAGS="${CXXFLAGS}"
    export CGO_LDFLAGS="${LDFLAGS}"
    export GOPATH="${srcdir}"
    export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
    make DISTRIBUTION=arch
}

package() {
    depends+=('apparmor>=4.1')

    cd "${srcdir}/${pkgname}"

    # Applying test development changes leads to conflicts with the
    # abstractions/authentication file in the apparmor.d-git package
    sed -i '/unix_chkpwd/s/rpx/rPx/' .build/apparmor.d/abstractions/authentication.d/complete
    #

    make install DESTDIR="${pkgdir}"
}
