# Maintainer: Ikosse <andrelofgren@hotmail.co.uk>

pkgname=harbour-amazfish
pkgver=2.5.0
pkgrel=1
pkgdesc="Companion application for Huami Devices and the Pinetime Infinitime"
arch=('x86_64' 'aarch64')
url="https://github.com/piggz/${pkgname}"
provides=('amazfish')
license=('GPL-3.0-or-later')
depends=(
	'qt5-base'
	'karchive5'
	'kdb'
	'kcontacts5'
	'qt5-location'
	'qt5-connectivity'
	'qtmpris'
	'nemo-qml-plugin-dbus'
	'kirigami2'
	'bluez-qt5'
)
makedepends=('git' 'qt5-tools')
source=("${pkgname}-${pkgver}::${url}/archive/refs/tags/${pkgver}.tar.gz")
sha512sums=('2a2253b10b8bd5c147a71daf7f50ce33c6c1d2621453b9eb2e9331134865f664055159dd9b5c5d5190d3fd6f65874f515ac72d7ac5aeec10be1a4ea79e9614de')

prepare() {
	cd "${pkgname}-${pkgver}/"
	rm -rf qble daemon/libwatchfish
	git init
	git submodule add https://github.com/piggz/qble.git qble
	git submodule add https://github.com/piggz/libwatchfish.git daemon/libwatchfish
}

build() {
	cd "${pkgname}-${pkgver}/"
	qmake FLAVOR=kirigami PREFIX=/usr .
	make
}

package() {
	cd "${pkgname}-${pkgver}/"
	make INSTALL_ROOT="${pkgdir}" install
}
