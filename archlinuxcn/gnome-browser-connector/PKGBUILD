# Contributor: Andrew Querol <andrew@querol.me>
# Maintainer: Rafael Fontenelle <rafaelff@gnome.org>

pkgname=gnome-browser-connector
pkgver=42.1
pkgrel=4
pkgdesc='Native browser connector for integration with extensions.gnome.org'
arch=(any)
url="https://wiki.gnome.org/Projects/GnomeShellIntegration"
license=(GPL3)
depends=(gnome-shell python-gobject)
makedepends=(meson git)
provides=(chrome-gnome-shell)
replaces=(chrome-gnome-shell gs-chrome-connector)
conflicts=(chrome-gnome-shell gs-chrome-connector)
_commit=1ed2008a39acceaed153b8b3b865081ceba98ab2 # tags/v42.1
source=("git+https://gitlab.gnome.org/GNOME/$pkgname.git#commit=$_commit")
md5sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $pkgname
  # Enforce Arch python package's binary, to avoid different python binary in users' PATH
  sed -i "s|'python3'|'/usr/bin/python3'|" meson.build
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir="$pkgdir"
}
