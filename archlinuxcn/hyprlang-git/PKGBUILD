# Maintainer: Integral <integral@member.fsf.org>

pkgname=hyprlang-git
_pkgname="${pkgname%-git}"
pkgver=0.5.2.r11.g5b175c9
pkgrel=1
pkgdesc="The official implementation library for the hypr config language"
arch=('x86_64' 'aarch64' 'riscv64')
url="https://github.com/hyprwm/${_pkgname}"
license=('GPL-3.0-or-later')
depends=('hyprutils')
makedepends=('git' 'cmake')
source=("git+${url}.git")
conflicts=("${_pkgname}")
provides=("${_pkgname}=${pkgver%%.r*}" "lib${_pkgname}.so")
sha256sums=('SKIP')

pkgver() {
	cd "${_pkgname}/"
	git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/v//'
}

build() {
	cd "${_pkgname}/"
	cmake --no-warn-unused-cli -DCMAKE_BUILD_TYPE:STRING=Release -DCMAKE_INSTALL_PREFIX:PATH=/usr -S . -B ./build
	cmake --build ./build --config Release --target "${_pkgname}" -j$(nproc 2>/dev/null || getconf NPROCESSORS_CONF)
}

package() {
	cd "${_pkgname}/"
	DESTDIR="${pkgdir}" cmake --install ./build
	install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${_pkgname}/"
	install -Dm644 "${_pkgname}-docs" -t "${pkgdir}/usr/share/doc/${_pkgname}/"
}
