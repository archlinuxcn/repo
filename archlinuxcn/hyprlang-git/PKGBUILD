# Maintainer: Integral <integral@member.fsf.org>

pkgname=hyprlang-git
_pkgname="${pkgname%-git}"
pkgver=0.5.3.r0.gf054f2e
pkgrel=1
pkgdesc="The official implementation library for the hypr config language"
arch=('x86_64' 'aarch64' 'riscv64')
url="https://github.com/hyprwm/${_pkgname}"
license=('GPL-3.0-or-later')
depends=('hyprutils-git')
makedepends=('git' 'cmake')
source=("git+${url}.git")
conflicts=("${_pkgname}")
provides=("${_pkgname}=${pkgver%%.r*}" "lib${_pkgname}.so")
sha256sums=('SKIP')

pkgver() {
	cd "${_pkgname}/"
	git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/v//'
}

build() {
	cmake -DCMAKE_BUILD_TYPE=None -DCMAKE_INSTALL_PREFIX=/usr -S "${_pkgname}/" -B build
	cmake --build build
}

package() {
	DESTDIR="${pkgdir}" cmake --install build

	cd "${_pkgname}/"
	install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${_pkgname}/"
	install -Dm644 "${_pkgname}-docs" -t "${pkgdir}/usr/share/doc/${_pkgname}/"
}
