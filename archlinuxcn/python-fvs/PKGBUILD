# Maintainer: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix

_pkgname=FVS
pkgname=python-fvs
pkgver=0.3.4
pkgrel=4
pkgdesc="A File Versioning System with hash comparison and data storage to create unlinked states that can be deleted"
arch=('any')
url="https://github.com/mirkobrombin/${_pkgname}"
license=('MIT')
depends=(
    'python'
    'python-orjson'
)
makedepends=(
    'python-build'
    'python-installer'
    'python-setuptools'
)
provides=('fvs')
conflicts=('fvs')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha256sums=('c57bcda81cd7500bc54b8f93c81405cccbc4c54b794209f8316ae27f43372696')

build() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    python -m build --wheel --no-isolation
}

package() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    install -Dm0644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
    python -m installer --destdir="${pkgdir}" dist/*.whl
}
