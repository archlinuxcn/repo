# Maintainer: Eric DeStefano <eric at ericdestefano dot com>

pkgbase=sheepshaver-git
pkgname=(basiliskii-git sheepshaver-git sheepnet-dkms-git)
pkgver=r2623.g5cbf07e9
pkgrel=1
pkgdesc="An Open Source PowerMac and Classic Macintosh Emulator"
arch=('x86_64')
url="http://sheepshaver.cebix.net"
license=('GPL')
depends=('gtk2' 'sdl' 'vde2')
source=('git+https://github.com/cebix/macemu'
        'SheepShaver.sysctl'
        'SheepShaver.desktop'
        'SheepShaver.png'
        'BasiliskII.desktop'
        'BasiliskII.png')
makedepends=('git')
sha256sums=('SKIP'
            'a4aa858b95d29906873693988d5db42d5a4da8aa94a72c79374f59fc488efd51'
            'cb002e73e864a6ca271e0bbaa6df6eb89dc1df86856fc575c20bf79367458c39'
            'b7f67b1f8424f3e0ffa1a5e57597f368c4c4f93ea1f871ec0a76700b7519b241'
            '9adf6c8a64191ef54c98db071685743ef0badd0deb58204b8ca4ab256e326444'
            '66635f63471e314fcc587d1a14b6caf87ed582ba80525e12462da0dba87eec60')

pkgver() {
  cd macemu
  echo "r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

build() {
  cd macemu/SheepShaver/src/Unix
  ./autogen.sh \
    --prefix=/usr \
    --enable-sdl-audio \
    --enable-sdl-video \
    --enable-tuntap \
    --with-bincue \
    --with-vdeplug \
    ;
  make -j1

  cd $srcdir/macemu/BasiliskII/src/Unix
  sed -i 's/I_PUSH/TIOCPKT/g' sshpty.c

  ./autogen.sh \
		--prefix=/usr \
		--enable-sdl-video \
		--enable-sdl-audio \
		--enable-jit-compiler \
		--with-bincue \
		--with-vdeplug \
		;
	make -j1
}

package_basiliskii-git() {
  cd macemu/BasiliskII/src/Unix
	make DESTDIR="$pkgdir/" install
  cd ${srcdir}
  install -Dm644 BasiliskII.desktop "$pkgdir"/usr/share/applications/BasiliskII.desktop
  install -Dm644 BasiliskII.png     "$pkgdir"/usr/share/pixmaps/BasiliskII.png
}

package_sheepshaver-git() {
  provides=("sheepshaver=$pkgver")
  conflicts=("sheepshaver")

  install -Dm755 macemu/SheepShaver/src/Unix/SheepShaver    "$pkgdir"/usr/bin/SheepShaver

  mkdir -p "$pkgdir"/usr/share/doc
  cp -a macemu/SheepShaver/doc/Linux "$pkgdir"/usr/share/doc/SheepShaver

  install -Dm644 SheepShaver.desktop "$pkgdir"/usr/share/applications/SheepShaver.desktop
  install -Dm644 SheepShaver.png     "$pkgdir"/usr/share/pixmaps/SheepShaver.png
  install -Dm644 SheepShaver.sysctl  "$pkgdir"/etc/sysctl.d/90-SheepShaver.conf
}

package_sheepnet-dkms-git() {
  depends=('dkms')
  provides=("sheepnet-dkms=$pkgver")
  conflicts=("sheepnet-dkms")

  mkdir -p "$pkgdir"/usr/src
  cp -rL macemu/SheepShaver/src/Unix/Linux/NetDriver "$pkgdir"/usr/src/sheepnet-$pkgver

  cat > "$pkgdir"/usr/src/sheepnet-$pkgver/dkms.conf <<EOF
PACKAGE_NAME="sheepnet"
PACKAGE_VERSION="$pkgver"
AUTOINSTALL=yes
BUILT_MODULE_NAME="sheep_net"
DEST_MODULE_LOCATION="/kernel/net"
EOF
}

# vim: ts=2:sw=2:et:
