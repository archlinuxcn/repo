# Maintainer: Butui Hu <hot123tea123@gmail.com>

pkgname=python-gluoncv-torch-git
_pkgver=0.0.3
pkgver=0.0.3.r9.8cab8da
pkgrel=2
pkgdesc='PyTorch API for GluonCV Models'
arch=('any')
url='https://github.com/StacyYang/gluoncv-torch'
license=('MIT')
depends=(
  python-numpy
  python-pillow
  python-requests
  python-torchvision
  python-tqdm
)
makedepends=(
  git
  python-setuptools
)
provides=(python-gluoncv-torch=${pkgver})
conflicts=(python-gluoncv-torch)
source=("${pkgname}::git+https://github.com/StacyYang/gluoncv-torch.git")
md5sums=('SKIP')

pkgver() {
  cd "${pkgname}"
  ver=$(printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)")
  echo "${_pkgver}.${ver}"
}

build() {
  cd "${pkgname}"
  python setup.py build
}

package() {
  cd "${pkgname}"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
