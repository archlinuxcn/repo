# Maintainer: oldherl
# Contributor: Chris Severance aur.severach aATt spamgourmet dott com
# Contributor: ajs124

# Tested on linux 6.13.6 with 03f0:0867 HP USB-C Universal Dock
pkgname='evdi-dkms'
pkgver=1.14.9
_pkgver=$pkgver
pkgrel=1
pkgdesc='kernel module that enables management of multiple screens, primarily for DisplayLink USB VGA DVI HDMI DisplayPort video'
arch=('x86_64')
url='https://github.com/DisplayLink/evdi'
license=('GPL-2.0-only AND LGPL-2.1-only AND MIT')
depends=('dkms' 'libdrm')
makedepends=('git')
provides=("evdi=${_pkgver}")
conflicts=('evdi' 'evdi-git')
_srcdir="evdi"
source=(
  "git+https://github.com/DisplayLink/evdi#tag=v${pkgver}"
)
sha256sums=('b8a4ea5e5f344242ca7ae4a0fbfb31d2040dffd22ef67f993bf7e73ed14bc2e5')

build() {
  cd "${_srcdir}"
  CFLAGS="${CFLAGS/-fno-plt/}"
  CFLAGS="${CFLAGS/-fexceptions/}"
  make -j1 -C 'library'
}

package() {
  cd "${_srcdir}"
  make -C 'library' -j1 install DESTDIR="${pkgdir}" PREFIX='/usr'

  local _DKMS="${pkgdir}/usr/src/evdi-${_pkgver}"
  #install -Dpm644 module/* -t "${_DKMS}/"
  install -d "${_DKMS}/"
  cp -pr 'module/'* "${_DKMS}/"
  make -j1 -C "${_DKMS}" clean
  rm -f "${_DKMS}/evdi.mod"
}
