# Maintainer: oldherl
# Contributor: Chris Severance aur.severach aATt spamgourmet dott com
# Contributor: ajs124

# Tested on linux 6.13.6 with 03f0:0867 HP USB-C Universal Dock
pkgname='evdi-dkms'
pkgver=1.14.13
_pkgver=$pkgver
pkgrel=1
pkgdesc='kernel module that enables management of multiple screens, primarily for DisplayLink USB VGA DVI HDMI DisplayPort video'
arch=('x86_64')
url='https://github.com/DisplayLink/evdi'
license=('GPL-2.0-only AND LGPL-2.1-only AND MIT')
depends=('dkms' 'libdrm')
makedepends=('git')
provides=("evdi=${_pkgver}")
conflicts=('evdi' 'evdi-git')
_srcdir="evdi"
source=(
  "git+https://github.com/DisplayLink/evdi#tag=v${pkgver}"
)
sha256sums=('00b24ce5cf2a3b89b490192e172a74f2ff3823177ef6d29f8eef2cc35901807c')

build() {
  cd "${_srcdir}"
  make -C 'library'
}

package() {
  cd "${_srcdir}"
  make -C 'library' install DESTDIR="${pkgdir}" PREFIX='/usr'

  local _DKMS="${pkgdir}/usr/src/evdi-${_pkgver}"
  install -d "${_DKMS}/"
  cp -pr 'module/'* "${_DKMS}/"
  make -C "${_DKMS}" clean
  rm -f "${_DKMS}/evdi.mod"
}
