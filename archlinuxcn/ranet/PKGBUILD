# Maintainer: Coelacanthus <uwu@coelacanthus.name>
# Contributor: huggy <arch@huggy.moe>
# Contributor: Nick Cao <nickcao@nichi.co>

pkgname=ranet
pkgver=0.12.0
pkgrel=1
pkgdesc="ranet, acronym for redundant array of networks, forms full mesh IPSec VPN"
arch=('x86_64' 'aarch64')
url="https://github.com/NickCao/ranet"
license=('MIT')
depends=(
  gcc-libs
  glibc
  openssl
)
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::https://github.com/NickCao/ranet/archive/refs/tags/v${pkgver}.tar.gz")
b2sums=('191581a2a6127706ce120d943e06d9f1cdef0e984c9b2428eff3a212db1c1a98f37d5cc2f7061a03bd79823a397a59ba261f6d4553aeb3fe3964b5849b1924ea')

prepare() {
  cd "$pkgname-$pkgver"
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-$pkgver"
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm0755 -t "$pkgdir/usr/bin/" target/release/"$pkgname"
  install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}

# vim :set ts=2 sw=2 et:
