# Maintainer: Popolon <popolon@popolon.org>

_module='mozjpeg-lossless-optimization'
_module_underscore="${_module//-/_}"
pkgname=python-${_module}
pkgver=1.3.1
pkgrel=1
pkgdesc="Optimize JPEGs losslessly using MozJPEG"
arch=('x86_64')
url="https://github.com/wanadev/mozjpeg-lossless-optimization"
license=('BSD-3-Clause')
depends=(
    'glibc'
    'python'
    'python-cffi'
)
makedepends=(
    'cmake'
    'python-build'
    'python-installer'
    'python-setuptools'
)
source=("https://files.pythonhosted.org/packages/source/${_module::1}/${_module}/${_module_underscore}-${pkgver}.tar.gz")
sha256sums=('15658b0b5083d9105db37ba7f8fe3b8bb0c184bec683e563f8986e870650a9c0')

build() {
    cd "${srcdir}/${_module_underscore}-${pkgver}"
    python -m build --wheel --no-isolation
}

package() {
    cd "${srcdir}/${_module_underscore}-${pkgver}"
    install -Dm0644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    python -m installer --destdir="${pkgdir}" dist/*.whl
}
