# Maintainer: Coelacanthus <CoelacanthusHex at gmail dot com>
# Contributor: DuckSoft <realducksoft at gmail dot com>

pkgname=mpv-git
_gitname=mpv
pkgver=0.34.0_278_gbb5b4b1ba6
pkgrel=1
pkgdesc='A free, open source, and cross-platform media player (with all possible libs)'
arch=('x86_64')
license=('GPL3')
url='https://mpv.io/'
depends=('cmocka' 'ffmpeg' 'lcms2' 'libcdio-paranoia' 'libgl' 'libxss'
        'libxinerama' 'libxv' 'libxkbcommon' 'libva' 'wayland' 'libcaca'
        'desktop-file-utils' 'hicolor-icon-theme' 'xdg-utils' 'lua52' 'mujs'
        'libdvdnav' 'libxrandr' 'jack' 'rubberband' 'uchardet' 'libarchive'
        'smbclient' 'zlib' 'vapoursynth' 'sndio' 'openal' 'vulkan-icd-loader'
        'shaderc' 'libplacebo' 'zimg' 'sdl2'
)
makedepends=('git' 'mesa' 'python-docutils' 'ladspa' 'vulkan-headers'
             'wayland-protocols' 'ffnvcodec-headers' 'meson' 'pipewire' 'pulseaudio')
optdepends=('youtube-dl: for video-sharing websites playback'
            'nvidia-utils: for hardware accelerated video decoding with CUDA')
provides=('mpv')
conflicts=('mpv')
options=('!emptydirs')
source=("mpv::git+https://github.com/mpv-player/mpv")
sha256sums=('SKIP')
options=('debug' 'strip')

pkgver() {
  cd "$srcdir/mpv"
  ./version.sh | sed s/-/_/g
}


build() {
    local _opt_extra_flags=(
        '-Dgpl=true'
        '-Ddvdnav=enabled'
        '-Dcdda=enabled'
        '-Dlua=enabled' '-Dlua=lua52'
        '-Djavascript=enabled'
        '-Ddvbin=enabled'
        '-Dsdl2=enabled'
        '-Dopenal=enabled'
        '-Dpulse=enabled'
        '-Dpipewire=enabled'
        '-Dmanpage-build=enabled'
    )
    
    cd "$srcdir/mpv"
    arch-meson . build \
        --auto-features auto \
        -Dlibmpv=true \
        -Ddefault_library=shared \
        "${_opt_extra_flags[@]}"
    meson compile -C build
}

package() {
    cd "$srcdir/mpv"
    
    meson install -C build --no-rebuild --destdir="$pkgdir"
    
    install -D -m644 DOCS/{encoding.rst,tech-overview.txt} "${pkgdir}/usr/share/doc/mpv"
    install -D -m644 TOOLS/lua/* -t "${pkgdir}/usr/share/mpv/script"
}
