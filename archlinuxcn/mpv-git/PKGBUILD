# Maintainer: Coelacanthus <CoelacanthusHex at gmail dot com>
# Contributor: DuckSoft <realducksoft at gmail dot com>

pkgname=mpv-git
_gitname=mpv
pkgver=0.37.0_338_g3fd840f85d
pkgrel=2
pkgdesc='A free, open source, and cross-platform media player (with all possible libs)'
arch=('x86_64')
license=('GPL-3.0-or-later AND LGPL-2.1-or-later')
url='https://mpv.io/'
depends=(
        'libx11' 'libxext' 'libxrandr' 'libxss' 'libxinerama' 'libxv' 'libxpresent'
        'desktop-file-utils' 'hicolor-icon-theme' 'xdg-utils' 'luajit' 'mujs'
        'libgl' 'libdvdnav' 'uchardet' 'mesa' 'libdrm' 'libcaca'
        'zlib' 'vapoursynth' 'sdl2' 'libsixel' 'libcdio-paranoia' 'libcdio'

        'libva' 'libva.so' 'libva-x11.so' 'libva-wayland.so' 'libva-drm.so'
        'libxkbcommon' 'libxkbcommon.so'
        'wayland' 'libwayland-client.so' 'libwayland-cursor.so' 'libwayland-egl.so'
        'libjpeg' 'libjpeg.so'
        'sndio' 'libsndio.so'
        'libjack.so'
        'alsa-lib' 'libasound.so'
        'zimg' 'libzimg.so'
        'rubberband' 'librubberband.so'
        'libbluray' 'libbluray.so'
        'libarchive' 'libarchive.so'
        'libass' 'libass.so'
        'lcms2' 'liblcms2.so'
        'vulkan-icd-loader' 'libvulkan.so'
        'ffmpeg-with-libplacebo-git' 'libavcodec.so' 'libavdevice.so' 'libavfilter.so' 'libavformat.so' 'libavutil.so' 'libswresample.so' 'libswscale.so'
        'libplacebo-git' 'libplacebo.so'
        'libpipewire'
)
makedepends=('git' 'python-docutils' 'vulkan-headers' 'wayland-protocols'
             'ffnvcodec-headers' 'meson' 'pulseaudio' 'openal'
)
optdepends=('youtube-dl: for video-sharing websites playback'
            'yt-dlp: for video-sharing websites playback'
            'openal: OpenAL audio output'
            'pulseaudio: PulseAudio audio output'
            'pipewire: PipeWire audio output'
            'smbclient: Samba support'
            'nvidia-utils: for hardware accelerated video decoding with CUDA')
provides=('mpv' 'libmpv.so')
conflicts=('mpv')
options=('!emptydirs')
source=("mpv::git+https://github.com/mpv-player/mpv")
sha256sums=('SKIP')
options=('debug' 'strip')

pkgver() {
    cd "$srcdir/mpv"
    git describe --always --tags | sed 's/-/_/g;s/^v//g'
}

prepare() {
    cd "$srcdir/mpv"
}

build() {
    local _opt_extra_flags=(
        '-Dgpl=true'
        '-Dtests=true'
        '-Dcdda=enabled'
        '-Dlua=enabled' '-Dlua=luajit'
        '-Ddvbin=enabled'
        '-Dsdl2=enabled'
    )
    
    local _other_os_flags=(
        '-Dmacos-cocoa-cb=disabled' # macOS
        '-Dmacos-media-player=disabled' # macOS
        '-Dmacos-touchbar=disabled' # macOS
        '-Dswift-build=disabled' # macOS
        '-Dswift-flags=disabled' # macOS
        '-Dgl-cocoa=disabled' # macOS
        '-Dcocoa=disabled' # macOS
        '-Daudiounit=disabled' # macOS
        '-Dvideotoolbox-gl=disabled' # macOS and iOS
        '-Dvideotoolbox-pl=disabled' # macOS and iOS
        '-Dcoreaudio=disabled' # iOS
        '-Dios-gl=disabled' # iOS
        '-Dgl-dxinterop=disabled' # Windows
        '-Dd3d-hwaccel=disabled' # Windows
        '-Dd3d9-hwaccel=disabled' # Windows
        '-Dgl-dxinterop-d3d9=disabled' # Windows
        '-Dgl-win32=disabled' # Windows
        '-Degl-angle-win32=disabled' # Windows
        '-Degl-angle-lib=disabled' # Windows
        '-Degl-angle=disabled' # Windows
        '-Ddirect3d=disabled' # Windows
        '-Dd3d11=disabled' # Windows
        '-Dwasapi=disabled' # Windows
        '-Dwin32-threads=disabled' # Windows
        '-Duwp=disabled' # Windows
        '-Dvaapi-win32=disabled' # Windows
        '-Dspirv-cross=disabled' '-Dshaderc=disabled' # Windows, see https://github.com/mpv-player/mpv/commit/60a31a8a284e539043c47a33ff90f3d55aade6a8
        '-Dopensles=disabled' # Android
        '-Degl-android=disabled' # Android
        '-Dandroid-media-ndk=disabled' # Android
        '-Doss-audio=disabled' # Not Linux
    )
    
    cd "$srcdir/mpv"
    arch-meson . build \
        --buildtype=debugoptimized \
        -Dlibmpv=true \
        -Ddefault_library=shared \
        "${_opt_extra_flags[@]}" \
        "${_other_os_flags[@]}"
    meson compile -C build
}

package() {
    cd "$srcdir/mpv"
    
    meson install -C build --no-rebuild --destdir="$pkgdir"
    
    install -D -m644 DOCS/{encoding.rst,tech-overview.txt} "${pkgdir}/usr/share/doc/mpv"
    install -D -m644 TOOLS/lua/* -t "${pkgdir}/usr/share/mpv/script"
}

check() {
    cd "$srcdir/mpv"
    meson test -C build --print-errorlogs --no-suite ffmpeg
}
