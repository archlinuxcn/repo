# Maintainer: DuckSoft <realducksoft at gmail dot com>

pkgname=mpv-git
_gitname=mpv
pkgver=0.32.0_581_gc82abb3a65
pkgrel=1
pkgdesc='A free, open source, and cross-platform media player (with all possible libs)'
arch=('x86_64')
license=('GPL3')
url='https://mpv.io/'
depends=('cmocka' 'ffmpeg' 'lcms2' 'libcdio-paranoia' 'libgl' 'libxss'
        'libxinerama' 'libxv' 'libxkbcommon' 'libva' 'wayland' 'libcaca'
        'desktop-file-utils' 'hicolor-icon-theme' 'xdg-utils' 'lua52' 'mujs'
        'libdvdnav' 'libxrandr' 'jack' 'rubberband' 'uchardet' 'libarchive'
        'smbclient' 'zlib' 'vapoursynth' 'sndio' 'openal' 'vulkan-icd-loader'
        'shaderc' 'libplacebo' 'zimg' 'sdl2'
)
makedepends=('git' 'mesa' 'python-docutils' 'ladspa' 'vulkan-headers'
             'wayland-protocols' 'ffnvcodec-headers')
optdepends=('youtube-dl: for video-sharing websites playback'
            'nvidia-utils: for hardware accelerated video decoding with CUDA')
provides=('mpv')
conflicts=('mpv')
options=('!emptydirs')
source=("mpv::git+https://github.com/mpv-player/mpv")
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/mpv"
  ./version.sh | sed s/-/_/g
}


build() {
    local _common_opts=(
        '--color=yes'
        '--prefix=/usr'
        '--progress'
        '--confdir=/etc/mpv'
        
        '--disable-lgpl'
        '--enable-libmpv-shared'
        '--enable-dvdnav'
        '--enable-cdda'
        '--enable-dvbin'
        '--enable-sdl2'
        '--enable-openal'
    )
    
    cd "mpv"
    ./bootstrap.py
    
    printf '%s\n' ' -> Building the release files (without tests)...'
    ./waf configure --disable-tests "${_common_opts[@]}"
    ./waf build
}

package() {
    cd "mpv"
    
    export WAFLOCK='.lock-waf_linux_build'
    ./waf install --destdir="$pkgdir"
    
    install -D -m644 DOCS/{encoding.rst,tech-overview.txt} "${pkgdir}/usr/share/doc/mpv"
    install -D -m644 TOOLS/lua/* -t "${pkgdir}/usr/share/mpv/script"
}
