# Maintainer: Rodrigo Bezerra <rodrigobezerra21 at gmail dot com>
# Contributor: GordonGR <ntheo1979@gmail.com>
# Contributor: josephgbr <rafael.f.f1@gmail.com>

_basename=libdvdread
pkgname=lib32-libdvdread
pkgver=6.1.3
pkgrel=1
pkgdesc="Provides a simple foundation for reading DVD video disks (32 bit)"
arch=(x86_64)
url="https://www.videolan.org/developers/libdvdnav.html"
license=(GPL)
depends=(lib32-glibc libdvdread)
makedepends=(lib32-libdvdcss)
source=(https://download.videolan.org/pub/videolan/$_basename/$pkgver/$_basename-$pkgver.tar.bz2{,.asc})
sha256sums=('ce35454997a208cbe50e91232f0e73fb1ac3471965813a13b8730a8f18a15369'
            'SKIP')
validpgpkeys=(65F7C6B4206BD057A7EB73787180713BE58D1ADC)

build() {
    cd $_basename-$pkgver

    export CC='gcc -m32'
    export CXX='g++ -m32'
    export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

    ./configure \
        --build=i686-pc-linux-gnu \
        --prefix=/usr \
        --libdir=/usr/lib32

    make
}

package() {
    cd $_basename-$pkgver

    make DESTDIR="${pkgdir}" install

    rm -rf "${pkgdir}/usr"/{include,share}
}
provides=("libdvdread.so")
