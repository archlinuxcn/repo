# Maintainer: Alexander GÃ¶rtz <aur@nyloc.de>
# Contributer: Dylan Cali <calid1984@gmail.com>

pkgname='google-authenticator-libpam-git'

pkgver=1.09.r13.gfdd8289
pkgver() {
  cd "$srcdir/$pkgname-2"
  local count=$(git rev-list --count HEAD)
  count=$(($count + 200)) # Avoid conflicting with the original repo's count
  ( set -o pipefail
    git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$count" "$(git rev-parse --short HEAD)"
  )
}

pkgrel=1
pkgdesc='PAM module for google authenticator app'
arch=('i686' 'x86_64' 'armv7h')
url='https://github.com/google/google-authenticator'
license=('APACHE')
depends=('pam')
makedepends=('git')
optdepends=('qrencode: scannable QR codes for google auth phone app')
provides=('google-authenticator-libpam')
conflicts=('google-authenticator-libpam')
source=("$pkgname-2"::'git+https://github.com/google/google-authenticator-libpam.git')
md5sums=(SKIP)

build() {
  cd "${srcdir}/${pkgname}-2"
  ./bootstrap.sh
  ./configure --prefix=/usr
  make
}


package() {
  cd "${srcdir}/${pkgname}-2"
  make DESTDIR="${pkgdir}/" install
}
