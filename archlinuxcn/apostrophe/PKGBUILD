# Maintainer: Mark Wagie <mark dot wagie at tutanota dot com>
pkgname=apostrophe
pkgver=2.4
pkgrel=1
_commit="7a69b54589c52197b3cf0d98a8bd10ec01741653"
pkgdesc="A distraction free Markdown editor for GNU/Linux made with GTK+"
arch=('x86_64')
url="https://gitlab.gnome.org/World/apostrophe"
license=('GPL3')
depends=('webkit2gtk' 'gspell' 'python-pypandoc' 'python-regex' 'python-levenshtein'
         'python-pyenchant' 'python-gobject' 'python-cairo' 'otf-fira-mono' 'libhandy'
         'python-chardet')
makedepends=('meson' 'gobject-introspection')
optdepends=('texlive-latexextra: for the pdftex module'
            'mathjax: for formula preview'
            'hspell: Hebrew spell-checker'
            'appstream: Provides a standard for creating app stores across distributions')
source=("${pkgname}-${pkgver}.tar.gz::https://gitlab.gnome.org/World/apostrophe/-/archive/v${pkgver}/apostrophe-v{pkgver}.tar.gz")
sha256sums=('f4878e880a4fda0460a821a0c7ff5020401d944da296cd91c3dd2710fc38ae14')

build() {
	arch-meson "${pkgname}-v${pkgver}-${_commit}" build
	meson compile -C build
}

check() {
	meson test -C build --print-errorlogs
}

package() {
	DESTDIR="$pkgdir" meson install -C build
}
