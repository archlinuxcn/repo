# $Id $
# Maintainer: John K. Luebs <jkl@johnluebs.com>
# Contributor: Eric Le Bras <eric.lebras@gmail.com>
# Contributor: Uwe Koloska <kolewu@koloro.de>

pkgname=mma
pkgver=21.09
pkgrel=2
pkgdesc="Musical MIDI Accompaniment (MMA) is an accompaniment generator"
url="http://www.mellowood.ca/mma/"
depends=('python')
optdepends=('timidity++: Synthesize MIDI files')
license=('GPL')
arch=('any')
install=mma.install
DLAGENTS=('http::/usr/bin/curl -fLC - --retry 3 --retry-delay 3 --user-agent Mozilla/4.0 -o %o %u')
source=("http://www.mellowood.ca/mma/mma-bin-$pkgver.tar.gz"
  mmarc)
sha1sums=('0d7a2a46b5807351dd14f62ef119516ca0b09f43'
          '3c03107c2a1b380f66ed68458d9b9def8371c0f6')
options=(!strip)
backup=(etc/mmarc)

package () {
  cd "$srcdir/$pkgname-bin-$pkgver"

  mkdir -p "$pkgdir"/usr/{bin,share/mma,share/man/man1,share/man/man8}
  mkdir -p "$pkgdir"/etc

  cp -p mma.py "$pkgdir"/usr/bin/mma
  cp -p mma-gb "$pkgdir"/usr/bin/mma-gb
  cp -p mma-libdoc "$pkgdir"/usr/bin/mma-libdoc
  cp -p mma-renum "$pkgdir"/usr/bin/mma-renum
  cp -p mma-splitrec "$pkgdir"/usr/bin/mma-splitrec
  cp -p util/mma-mnx.py "$pkgdir"/usr/bin/mma-mnx
  cp -p util/mma-rm2std.py "$pkgdir"/usr/bin/mma-rm2std
  cp -p util/mmatabs.py "$pkgdir"/usr/bin/mmatabs
  cp -p util/mup2mma.py "$pkgdir"/usr/bin/mup2mma
  cp -p util/pg2mma.py "$pkgdir"/usr/bin/pg2mma
  cp -p util/synthsplit.py "$pkgdir"/usr/bin/mma-synthsplit
  cp -pr {docs,egs,includes,lib,MMA,plugins,text} "$pkgdir"/usr/share/mma

  cp -p util/README.* "$pkgdir"/usr/share/mma/docs
  mv "$pkgdir"/usr/share/mma/docs/man/mma-libdoc.8 "$pkgdir"/usr/share/man/man8
  mv "$pkgdir"/usr/share/mma/docs/man/mma-renum.1 "$pkgdir"/usr/share/man/man1
  mv "$pkgdir"/usr/share/mma/docs/man/mma.1 "$pkgdir"/usr/share/man/man1
  mv "$pkgdir"/usr/share/mma/docs/man/mma-gb.1 "$pkgdir"/usr/share/man/man1
  rm -rf "$pkgdir"/usr/share/mma/docs/man
  cp -p "$srcdir"/mmarc "$pkgdir"/etc
}
