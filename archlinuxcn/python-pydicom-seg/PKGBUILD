# Maintainer: Butui Hu <hot123tea123@gmail.com>

_pkgname=pydicom-seg
pkgname=python-pydicom-seg
pkgver=0.4.1
pkgrel=2
pkgdesc='Python package for DICOM-SEG medical segmentation file reading and writing'
arch=('any')
url='https://github.com/razorx89/pydicom-seg'
license=('MIT')
depends=(
  python-attrs
  python-jsonschema
  python-numpy
  python-pydicom
  python-simpleitk
)
makedepends=(
  python-build
  python-installer
  python-poetry
)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/razorx89/pydicom-seg/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('0a2a839357625235f8aad1f326499f0ba213494e764cd67dfefacdeae13b49cd53c1e915837b02478b8fa545ab4d236a92afb68096011bf655f3fce3fdc9353d')

build() {
  cd "${_pkgname}-${pkgver}"
  python -m build --wheel --no-isolation -x
}

package() {
  cd "${_pkgname}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
