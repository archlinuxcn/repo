# Maintainer: Solomon Choina <shlomochoina@gmail.com>
pkgname=wayfire-git
pkgver=r1054.225a262
pkgrel=1
pkgdesc="3D wayland compositor"
arch=('x86_64')
url="https://github.com/ammen99/wayfire"
license=('MIT')
depends=('wlroots' 'cairo' 'glm' 'libjpeg' 'wf-config-git')
makedepends=('git' 'meson' 'ninja' 'wayland-protocols')
optdepends=('wf-shell-git: GTK3-based panel for the Wayfire compositor'
            'wf-sound-control-git: Small utility for the Wayfire compositor to control sound volume')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
replaces=()
options=()
source=('git+https://github.com/ammen99/wayfire')
sha256sums=('SKIP')

prepare() {
  cd "$srcdir/wayfire"
  git submodule update --init --recursive
}
pkgver() {
	cd "$srcdir/wayfire"

# Git, no tags available
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"

}

build() {
	cd "$srcdir/wayfire/"
  arch-meson build
  ninja -C build
}


package() {
	cd "$srcdir/wayfire"
	DESTDIR="$pkgdir/" ninja -C build install
  install -Dm644 wayfire.desktop $pkgdir/usr/share/wayland-sessions/wayfire.desktop 
  cp wayfire.ini.default $pkgdir/usr/share
}
