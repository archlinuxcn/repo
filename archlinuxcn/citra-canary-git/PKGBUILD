# Maintainer: Brendan Szymanski <bscubed@pm.me>

_pkgname=citra
pkgname=$_pkgname-canary-git
pkgver=r9030.7ee5bf952
pkgrel=2
pkgdesc="An experimental open-source Nintendo 3DS emulator/debugger"
arch=('i686' 'x86_64')
url="https://github.com/citra-emu/citra-canary/tree/master"
license=('GPL2')
depends=('boost-libs' 'libfdk-aac' 'shared-mime-info' 'desktop-file-utils' 'ffmpeg' 'libusb' 'sdl2' 'qt5-base' 'qt5-multimedia' 'libxkbcommon-x11')
makedepends=('git' 'rapidjson' 'cmake' 'python' 'clang' 'llvm' 'boost' 'qt5-tools')
optdepends=('qt5-wayland: for Wayland support')
provides=('citra')
conflicts=('citra')
source=("$_pkgname::git+https://github.com/citra-emu/citra-canary#branch=master")
md5sums=('SKIP')

pkgver() {
	cd "$srcdir/$_pkgname"
	echo "r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

prepare() {
	cd "$srcdir/$_pkgname"

	git submodule init
	git submodule update --init --recursive
  sed -i 's|libusb.h|libusb-1.0/libusb.h|g' src/input_common/gcadapter/gc_adapter.cpp
}

build() {
	# Trick the compiler into thinking we're building from a continuous
	# integration tool so the build number is correctly shown in the title
	cd "$srcdir/$_pkgname"
	export CI=true
	export TRAVIS=true
	export TRAVIS_REPO_SLUG=citra-emu/citra-canary
	export TRAVIS_TAG=$(git describe --tags)
	
	# Hopefully temporary fix for a compilation error involving fmt
	CXXFLAGS+=" -DFMT_USE_USER_DEFINED_LITERALS=0"
	
	mkdir -p build && cd build
	cmake .. \
	  -DCMAKE_INSTALL_PREFIX=/usr \
	  -DCMAKE_BUILD_TYPE=Release \
	  -DENABLE_QT_TRANSLATION=ON \
	  -DCITRA_ENABLE_COMPATIBILITY_REPORTING=ON \
	  -DENABLE_COMPATIBILITY_LIST_DOWNLOAD=ON \
	  -DUSE_DISCORD_PRESENCE=ON \
    -DUSE_SYSTEM_BOOST=ON \
	  -DENABLE_FFMPEG_VIDEO_DUMPER=ON \
    -DENABLE_MF=OFF \
	  -DENABLE_FDK=ON
	make
}

check() {
	cd "$srcdir/$_pkgname/build"
	make test
}

package() {
	cd "$srcdir/$_pkgname/build"
	make DESTDIR="$pkgdir/" install
}
