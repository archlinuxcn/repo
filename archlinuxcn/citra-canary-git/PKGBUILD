# Maintainer: Brendan Szymanski <bscubed@pm.me>

_pkgname=citra
pkgname=$_pkgname-canary-git
epoch=2
pkgver=2792.r0.gb3eff81fb
pkgrel=1
pkgdesc="An experimental open-source Nintendo 3DS emulator/debugger"
arch=('i686' 'x86_64')
url="https://github.com/citra-emu/citra-canary/tree/master"
license=('GPL2')
depends=('boost-libs' 'shared-mime-info' 'ffmpeg' 'libusb' 'sdl2' 'qt6-multimedia' 'libxkbcommon-x11' 'cubeb' 'enet' 'libinih' 'discord-rpc' 'fmt' 'dynarmic' 'openal' 'faad2' 'crypto++' 'soundtouch')
makedepends=('git' 'rapidjson' 'cmake' 'python' 'clang' 'llvm' 'boost' 'qt6-tools' 'xbyak' 'cpp-jwt' 'qt6-wayland' 'desktop-file-utils' 'robin-map' 'glslang' 'spirv-headers' 'vulkan-memory-allocator' 'catch2' 'spirv-tools' 'cpp-httplib')
provides=('citra')
conflicts=('citra')
source=("$_pkgname::git+https://github.com/citra-emu/citra-canary"
ext-lib.patch
gc.patch)
md5sums=('SKIP'
         'cd9e6be75ae839618f747ea6d653f349'
         'f151510cd1fc233992710405cbf44323')
options=('!debug')

pkgver() {
	cd "$srcdir/$_pkgname"
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/canary.//g'
}

prepare() {
	cd "$srcdir/$_pkgname"
    patch -p1 -i ../ext-lib.patch
    patch -p1 -i ../gc.patch

	mkdir -p build
    sed -i 's/OUTVAR ${BUILD_TAG}/OUTVAR "${BUILD_TAG}"/g' CMakeModules/GenerateSCMRev.cmake
    sed -i 's/OUTVAR ${BUILD_REPOSITORY}/OUTVAR "${BUILD_REPOSITORY}"/g' CMakeModules/GenerateBuildInfo.cmake
    sed -i 's/cpp-httplib::cpp-httplib/httplib::httplib/g' externals/CMakeLists.txt
    sed -i 's/CppHttp/httplib/g' externals/CMakeLists.txt
    sed -i 's/0.14.1/0.15.3/g' externals/CMakeLists.txt
    sed -i 's/SPIRV/SPIRV SPIRV-Tools/' src/video_core/CMakeLists.txt

    git submodule update --init --remote externals/nihstro
    git submodule update --init --remote externals/sirit
    git submodule update --init --remote externals/teakra
    git submodule update --init --remote externals/dds-ktx
    git submodule update --init --remote externals/lodepng/lodepng
}

build() {
	# Trick the compiler into thinking we're building from a continuous
	# integration tool so the build number is correctly shown in the title
	cd "$srcdir/$_pkgname"
	export CI=true
	export TRAVIS=true
	export TRAVIS_REPO_SLUG=citra-emu/citra-canary
	export TRAVIS_TAG=$(git describe --tags)

    CXXFLAGS+=" -I/usr/include/libusb-1.0"
	mkdir -p build && cd build
	cmake .. \
	  -DCMAKE_INSTALL_PREFIX=/usr \
	  -DCMAKE_BUILD_TYPE=Release \
	  -DENABLE_QT_TRANSLATION=ON \
      -DCITRA_ENABLE_COMPATIBILITY_REPORTING=ON \
      -DENABLE_COMPATIBILITY_LIST_DOWNLOAD=ON \
      -DUSE_DISCORD_PRESENCE=ON \
      -DUSE_SYSTEM_LIBS=ON \
      -DENABLE_TESTS=OFF \
      -DCITRA_CHECK_SUBMODULES=OFF \
      -DSIRIT_USE_SYSTEM_SPIRV_HEADERS=ON \
      -DDISABLE_SYSTEM_LODEPNG=ON

	make
}

package() {
	cd "$srcdir/$_pkgname/build"
	make DESTDIR="$pkgdir/" install
}
