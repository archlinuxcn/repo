# Maintainer: lilydjwg <lilydjwg@gmail.com>
# Contribuitor: SÃ©bastien Luttringer
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname='virtualbox-host-modules-lily'
pkgver=6.1.12
pkgrel=1
pkgdesc='Virtualbox host kernel modules for linux-lily Kernel'
arch=('x86_64')
url='https://virtualbox.org/'
license=('GPL')
depends=("linux-lily=5.4.42-1")
makedepends=('linux-lily-headers'
             "virtualbox-host-dkms>=$pkgver")
conflicts=('virtualbox-host-dkms')
provides=('VIRTUALBOX-HOST-MODULES')

build() {
  _kernver="$(</usr/src/linux-lily/version)"

  dkms build --dkmstree "$srcdir" -m vboxhost/${pkgver}_OSE -k ${_kernver}
}

package(){
  _kernver="$(</usr/src/linux-lily/version)"

  install -Dt "$pkgdir/usr/lib/modules/$_kernver/extramodules" -m0644 \
    vboxhost/${pkgver}_OSE/${_kernver}/${CARCH}/module/*

  # compress each module individually
  find "$pkgdir" -name '*.ko' -exec xz -T1 {} +

  # systemd module loading
  printf '%s\n' vboxdrv vboxnetadp vboxnetflt |
    install -D -m0644 /dev/stdin "$pkgdir/usr/lib/modules-load.d/$pkgname.conf"
}



