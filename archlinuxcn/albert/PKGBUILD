# Maintainer: Evgenii Alekseev
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Manuel Schneider  <manuelschneid3r at googles mail>

pkgname=albert
pkgver=0.26.2
pkgrel=2
pkgdesc="A sophisticated standalone keyboard launcher"
arch=('x86_64')
url="https://github.com/albertlauncher"
license=('LicenseRef-Albert')
depends=('hicolor-icon-theme' 'qt6-5compat' 'qt6-declarative' 'qt6-scxml' 'qt6-shadertools' 'qt6-svg')
makedepends=('cmake' 'git' 'libqalculate' 'pybind11' 'python' 'qt6-tools')
optdepends=('ddgr: duckduckgo search plugin'
	'libarchive: documentation plugin'
	'libqalculate: calculator plugin'
	'python: python extension'
	'python-docker: docker plugin'
	'python-googletrans: google translate plugin'
	'python-pylatexenc: TeX plugin'
	'python-urllib3: python web plugins'
	'python-virtualbox: virtualbox plugin')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/albertlauncher/${pkgname}/releases/download/v${pkgver}/v${pkgver}.tar.gz")
sha512sums=('dba6a850bbc787aae8e1ca7c65b98a84f892405e75042c153255756f85c0b1b552be199416dd65e555291b5d731823c290639d8903c87d1b5af1a0b7dc96765b')

build() {
	mkdir -p build
	cd build

	cmake "../${pkgname}" \
		-DCMAKE_BUILD_TYPE=MinSizeRel \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_INSTALL_LIBDIR=lib \
		-DCMAKE_FIND_PACKAGE_RESOLVE_SYMLINKS=ON \
		-DQHOTKEY_INSTALL=OFF \
		-DBUILD_APPLICATIONS_XDG=ON \
		-DBUILD_CALCULATOR_QALCULATE=ON \
		-DBUILD_CHROMIUM=ON \
		-DBUILD_CLIPBOARD=ON \
		-DBUILD_DATETIME=ON \
		-DBUILD_DEBUG=OFF \
		-DBUILD_FILES=ON \
		-DBUILD_HASH=ON \
		-DBUILD_PYTHON=ON \
		-DBUILD_QMLBOXMODEL=ON \
		-DBUILD_SNIPPETS=ON \
		-DBUILD_SSH=ON \
		-DBUILD_SYSTEM=ON \
		-DBUILD_TEMPLATE=OFF \
		-DBUILD_TERMINAL=ON \
		-DBUILD_URLHANDLER=ON \
		-DBUILD_WEBSEARCH=ON \
		-DBUILD_WIDGETSBOXMODEL=ON

	make
}

package() {
	cd build

	make DESTDIR="${pkgdir}/" install
	install -Dm644 "../${pkgname}/LICENSE.md" -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
