# Maintainer: xiretza <xiretza+aur@xiretza.xyz>
# Contributor: Bruce Zhang

_pkgname=SPIRV-Headers
pkgname=spirv-headers-git
epoch=2
pkgver=1.3.261.0.r15.g88bc5e3
pkgrel=1
pkgdesc='SPIR-V header files Git version'
arch=('any')
url='https://www.khronos.org/registry/spir-v/'
license=('MIT')
source=("git+https://github.com/KhronosGroup/$_pkgname.git")
sha1sums=('SKIP')
makedepends=('git' 'cmake')
conflicts=('spirv-headers')
provides=("spirv-headers=2:$pkgver")

pkgver() {
    cd "$_pkgname"
    git describe --long --tags | sed 's/^[^0-9]*//; s/\([^-]*-g\)/r\1/; s/-/./g'
}

build() {
	cmake -B build -S "$_pkgname" \
		-DCMAKE_INSTALL_PREFIX=/usr
	make -C build
}

package() {
	make -C build DESTDIR="$pkgdir" install
	install -Dm644 "$_pkgname/LICENSE" -t "$pkgdir/usr/share/licenses/$pkgname"
}
