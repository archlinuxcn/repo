# Maintainer: Zhijun Zhang <zhangzhijun23@mails.ucas.ac.cn>

pkgname=ds9
pkgver=8.6
pkgrel=3
pkgdesc="SAOImage DS9: Astronomical Data Visualization Application"
url="https://sites.google.com/cfa.harvard.edu/saoimageds9"
arch=('x86_64')
license=('GPL3')
depends=(libx11 zlib libxslt libxml2 libxft tcl tk)
options=(!strip !lto)
makedepends=(git gcc make automake autoconf zip xml2-config-compat)
conflicts=('ds9' 'ds9-bin')
source=("$pkgname::git+https://github.com/SAOImageDS9/SAOImageDS9.git#tag=v$pkgver"
        "ds9.desktop"
        )
sha256sums=('SKIP'
         'cf41109f31b9be057ab04a96613a59a87fb6cb3ed405fa93545f6bfd8fd98975'
         )

build() {
    cd ${srcdir}/${pkgname}
    export PATH=$PWD/compat:$PATH
    which xml2-config
    unix/configure
    # workaround for unsatisfying build order in upstream
    # "-j 1" doesn't work, need to change JOBS in Makefile
    sed -i 's/^JOBS.*/JOBS = 1/' Makefile
    make
}

package() {
    install -Dm644 ds9.desktop ${pkgdir}/usr/share/applications/ds9.desktop
    install -Dm644 ${srcdir}/${pkgname}/ds9/doc/sun.png ${pkgdir}/usr/share/pixmaps/ds9.png
    install -Dm644 ${srcdir}/${pkgname}/ds9/doc/sun.png ${pkgdir}/usr/share/pixmaps/"SAOImage ds9".png

    cd ${srcdir}/${pkgname}
    install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
    install -Dm755 ./bin/ds9 ${pkgdir}/usr/bin/ds9

}
