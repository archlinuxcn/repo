# Maintainer: Roald Clark <roaldclark@gmail.com>
# Contributor: LizardByte (https://github.com/LizardByte/Sunshine/blob/master/packaging/linux/Arch/PKGBUILD)

_gcc_version=14
_pkgname="sunshine"
pkgname="${_pkgname}-roaldclark"
pkgver=2025.717.152211
pkgrel=1
pkgdesc="A self-hosted game stream host for Moonlight (Dynamic User Sandbox)"
arch=('x86_64' 'aarch64')
url="https://github.com/LizardByte/Sunshine"
license=('GPL-3.0-only')
depends=(
    'curl'
    'gcc-libs'
    'glib2'
    'glibc'
    'gtk3'
    'hicolor-icon-theme'
    'icu'
    'libayatana-appindicator'
    'libcap'
    'libdrm'
    'libevdev'
    'libnotify'
    'libpulse'
    'libva'
    'miniupnpc'
    'numactl'
    'openssl'
    'opus'
    'wayland'
)
makedepends=(
    "gcc${_gcc_version}"
    'cmake'
    'cuda'
    'git'
    'make'
    'nodejs'
    'npm'
)
optdepends=(
    'cuda: Nvidia GPU encoding support'
    'libva-mesa-driver: AMD GPU encoding support'
    'xorg-server-xvfb: Virtual X server for headless testing'
)
provides=("${_pkgname}=${pkgver}")
conflicts=("${_pkgname}")
install="${_pkgname}.install"
source=("${_pkgname}::git+${url}.git#tag=v${pkgver}"
        "${_pkgname}.desktop"
        "${_pkgname}.service"
        "60-${_pkgname}.rules")
sha256sums=('e4b403a91a72819ef06ad26cb9afdaee1ed2a17f0176ff81ae8e1a9d38972a1e'
            '9e2174f0b879db4abf6b0b301b35dc60b177b588c8eaa1d21e7b1348346d6eeb'
            'aadb37577564e83ccd13e905da63f8fffa6f694ac2c452f4d47c8a70d4c060de'
            'c4af80597aa84eb735f9ce215cfb76332604edf8fe8562d79b8d5bae10bb8e6a')

prepare() {
    cd "${srcdir}/${_pkgname}"
    git submodule update --init --recursive
}

build() {
    cd "${srcdir}/${_pkgname}"
    export BRANCH="master"
    export BUILD_VERSION="${pkgver}"
    export COMMIT="$(git rev-parse HEAD)"

    export CC="gcc-${_gcc_version}"
    export CXX="g++-${_gcc_version}"

    export CFLAGS="${CFLAGS/-Werror=format-security/}"
    export CXXFLAGS="${CXXFLAGS/-Werror=format-security/}"

    cmake \
        -B build \
        -Wno-dev \
        -D BUILD_DOCS=OFF \
        -D BUILD_TESTS=OFF \
        -D BUILD_WERROR=ON \
        -D CMAKE_INSTALL_PREFIX='/usr' \
        -D CMAKE_INSTALL_BINDIR="/usr/lib/${_pkgname}" \
        -D SUNSHINE_ENABLE_CUDA=ON \
        -D SUNSHINE_ENABLE_X11=ON \
        -D SUNSHINE_EXECUTABLE_PATH="/usr/lib/${_pkgname}/${_pkgname}" \
        -D SUNSHINE_ASSETS_DIR="share/${_pkgname}" \
        -D SUNSHINE_PUBLISHER_NAME='LizardByte' \
        -D SUNSHINE_PUBLISHER_WEBSITE='https://app.lizardbyte.dev' \
        -D SUNSHINE_PUBLISHER_ISSUE_URL='https://app.lizardbyte.dev/support'

    make -C build
}

package() {
    cd "${srcdir}/${_pkgname}"
    make -C build install DESTDIR="${pkgdir}"
    rm --recursive --verbose "${pkgdir}/usr/lib/systemd/user/"
    rm --verbose "${pkgdir}/usr/share/applications/dev.lizardbyte.app.Sunshine.desktop"
    rm --verbose "${pkgdir}/usr/share/applications/dev.lizardbyte.app.Sunshine.terminal.desktop"
    install -Dm0644 "${srcdir}/${_pkgname}.desktop" "${pkgdir}/usr/share/applications/dev.lizardbyte.app.Sunshine.desktop"
    install -Dm0644 "${srcdir}/${_pkgname}.service" -t "${pkgdir}/usr/lib/systemd/system/"
    install -Dm0644 "${srcdir}/60-${_pkgname}.rules" -t "${pkgdir}/usr/share/polkit-1/rules.d/"
}
