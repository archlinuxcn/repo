pkgname='python-pyglm'
pkgver='2.8.2'
pkgrel=3
pkgdesc="OpenGL Mathematics library for Python"
url="https://github.com/Zuzu-Typ/PyGLM"
depends=('glm' 'python')
makedepends=(
	'git'
	'python-build'
	'python-installer'
	'python-setuptools'
	'python-wheel'
)
license=('LicenseRef-Zlib')
arch=('x86_64')
options=(!strip !lto)
source=("$pkgname::git+https://github.com/Zuzu-Typ/PyGLM.git#tag=${pkgver}")
sha256sums=('d26ecc224b11b12fc4d90465917dbac966448bec6245bebe09d004b0e0ae2c67')

build() {
    cd "$srcdir/$pkgname"
    CFLAGS="$CFLAGS -Wno-all" python -m build --wheel --no-isolation
}

package() {
    cd "$srcdir/$pkgname"
    python -m installer --destdir="$pkgdir" --compile-bytecode=1 dist/*.whl
}
