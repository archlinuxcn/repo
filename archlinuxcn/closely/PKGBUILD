# Maintainer: George Hu <integral@archlinux.org>

pkgname=closely
pkgver=0.1.24
pkgrel=1
pkgdesc="Subscribe to updates from people you follow, from any platform to any platform"
arch=('x86_64' 'aarch64')
url="https://github.com/SpriteOvO/${pkgname}"
license=('AGPL-3.0-only')
depends=('glibc' 'libgcc' 'openssl')
makedepends=('cargo')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('07add8492e4c7ae211bcf92773395f499a9e41e7502f858cd05325e0c38a09a9')

prepare() {
	cd "${pkgname}-${pkgver}/"
	export RUSTUP_TOOLCHAIN=stable
	cargo fetch --locked --target host-tuple
}

build() {
	cd "${pkgname}-${pkgver}/"
	export RUSTUP_TOOLCHAIN=stable
	export CARGO_TARGET_DIR=target
	CFLAGS+=" -ffat-lto-objects" cargo build --frozen --release --all-features
}

package() {
	cd "${pkgname}-${pkgver}/"
	install -Dm755 "target/release/${pkgname}" -t "${pkgdir}/usr/bin/"
	install -Dm644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
