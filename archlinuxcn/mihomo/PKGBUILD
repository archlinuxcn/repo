# Maintainer: Kimiblock Moe

pkgname=mihomo
pkgver=1.19.13
pkgrel=2
pkgdesc="Mihomo Kernel by MetaCubeX, formerly known as Clash.Meta"
arch=("x86_64" 'aarch64')
url="https://github.com/MetaCubeX/mihomo"
license=("MIT")
depends=('glibc' 'clash-geoip')
makedepends=('go')
conflicts=('clash-meta')
provides=('clash-meta')
replaces=('clash-meta')
backup=('etc/mihomo/config.yaml')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz"
	"${pkgname}.service"
	"config.yaml"
	start.sh)
sha256sums=('dcbdcfb84bde1d70758247e7f3a4fbd4e4771655d01b23aabbdf3cafcaf749b1'
            '434f5bfab25b7185c002cb0338cbd8c528b6e24fa12c857dfa7f1a7178a76ac6'
            '90f7fdacecd5928e37865b4f841517f925c8bedc769f16f7a7a1e89b923f1fb9'
            '9cb9b070715082c18c0d0150ec01fdfc8d3558c1a63d338e0663986ca9a21fbf')

build(){
	cd "${srcdir}"/mihomo-${pkgver}
	BUILDTIME=$(date -u --rfc-3339=seconds)
	GOOS=linux go build \
		-trimpath \
		-buildmode=pie \
		-mod=readonly \
		-modcacherw \
		-ldflags "-linkmode external -extldflags \"${LDFLAGS}\" \
		-X \"github.com/metacubex/mihomo/constant.Version=${pkgver}\" \
		-X \"github.com/metacubex/mihomo/constant.BuildTime=${BUILDTIME}\" \
		" \
		-tags with_gvisor -o ${pkgname}-${pkgver}
}
package() {
	install -vDm755 "${srcdir}/start.sh" "${pkgdir}/usr/lib/mihomo/start"
	install -vDm600 "${srcdir}/config.yaml" "${pkgdir}/etc/mihomo/config.yaml"
	install -vDm644 "${srcdir}/${pkgname}.service"  -t "${pkgdir}/usr/lib/systemd/system"
	cd "${srcdir}"/mihomo-${pkgver}
	install -vDm755 "${pkgname}-${pkgver}" "${pkgdir}/usr/bin/${pkgname}"
	ln -sf /etc/clash/Country.mmdb ${pkgdir}/etc/${pkgname}/Country.mmdb
	# Compability with applications executing /usr/bin/clash-meta
	ln -sf /usr/bin/mihomo ${pkgdir}/usr/bin/clash-meta
}
