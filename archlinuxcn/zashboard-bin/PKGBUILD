# Maintainer: Roald Clark <roaldclark at gmail dot com>
# Contributor: whf920521 <whf920521 at gmail dot com>

_pkgname=zashboard
pkgname="${_pkgname}-bin"
pkgver=1.95.0
pkgrel=1
pkgdesc="A dashboard using clash api"
arch=('any')
url="https://github.com/Zephyruso/zashboard"
license=('MIT')
optdepends=(
    'clash-meta: Another Clash Kernel by MetaCubeX'
    'mihomo: Another Clash Kernel by MetaCubeX'
    'clash: A rule-based tunnel in Go'
    'sing-box: The universal proxy platform'
)
provides=("${_pkgname}=${pkgver}")
conflicts=("${_pkgname}")
source=("${pkgname}-${pkgver}.zip::${url}/releases/download/v${pkgver}/dist.zip"
        "${url}/raw/v${pkgver}/LICENSE")
sha256sums=('8050c261029291e8656dfa867a58e3cc9da1f878947665a30b636ec25925af4a'
            'ffc390eec51f16b58506b1a42c99fe0cd9ffea1e3081aab443100fd8c20182d9')

package() {
    install -Dm0644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"

    cd "${srcdir}/dist"
    find . -type f -exec install -Dm0644 {} "${pkgdir}/usr/share/${_pkgname}/"{} \;
}
