# Contributor: Pedro H Lara Campos <root@pedrohlc.com>
_pkgname=haxm
pkgname=haxm-dkms-git
epoch=1
pkgver=413.aa7df4f
pkgrel=1
pkgdesc="IntelÂ® Hardware Accelerated Execution Manager - a hardware-assisted virtualization engine (DKMS git version)"
arch=('x86_64' 'i686')
url="https://software.intel.com/en-us/android/articles/intel-hardware-accelerated-execution-manager"
license=('BSD')
depends=('dkms')
makedepends=(git linux linux-headers)
provides=(haxm)
conflicts=(haxm)
source=("git+https://github.com/intel/haxm.git" 'dkms.conf')
md5sums=('SKIP' 'SKIP')
install='haxm-git.install'

pkgver() {
  cd "$srcdir/$_pkgname"

  printf "%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir/$_pkgname/platforms/linux"
  sed -i 's|#include <linux/slab.h>|#include <linux/slab.h>\n #include <asm/io.h>|g' hax_mem_alloc.c
}

package() {
  mkdir -p "${pkgdir}"/usr/src/${_pkgname}-${pkgver}/
  chmod 755 "${pkgdir}"/usr/src/${_pkgname}-${pkgver}/

  cd "$srcdir"

  cp -R $_pkgname*/* "${pkgdir}"/usr/src/${_pkgname}-${pkgver}/

  install -Dm644 dkms.conf "${pkgdir}"/usr/src/${_pkgname}-${pkgver}/dkms.conf

    sed -e "s/@PKGVER@/${pkgver}/" \
      -i "${pkgdir}"/usr/src/${_pkgname}-${pkgver}/dkms.conf

  mkdir -p "$pkgdir/usr/lib/modules-load.d"

  echo "haxm" >> "$pkgdir/usr/lib/modules-load.d/haxm.conf"

  mkdir -p "$pkgdir/usr/lib/udev/rules.d"

  echo "KERNEL==\"HAX\", GROUP=\"haxm\", MODE=\"0660\"" \
    > $pkgdir/usr/lib/udev/rules.d/99-haxm.rules
}
