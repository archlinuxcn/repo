# Maintainer: oldherl <oldherl@gmail.com>

pkgname=skychart
_pkgver=4.3-4991
pkgver=${_pkgver/-/.}
pkgrel=1
pkgdesc="Free software to draw sky charts, also known as Cartes du Ciel. Beta version"
arch=('x86_64')
license=('GPL-2.0-or-later')
depends=('qt6-base' 'xplanet' 'libpasastro' 'qt6pas')
makedepends=('fpc' 'lazarus-qt6' 'wget')
optdepends=(
"xplanet: for displaying textures on planets"
)
url="http://www.ap-i.net/skychart/start"

# Using my own copy of source code because upstream deletes beta tarballs regularly.
source=(
"https://build.archlinuxcn.org/~oldherl/files/skychart/skychart-${_pkgver}-src.tar.xz"
)

sha256sums=('2d58f801872804b75ac7df3d585a447023768cce7fab8817602f3eea4f066906')

prepare() {
  cd "skychart-$_pkgver-src"
  # Do not strip binaries when installing. Let makepkg do so.
  # makepkg will produce a -debug package of the debug symbols.
  sed -i 's/-m 755 -s/-m 755/g' install.sh
}

build() {
  cd "skychart-$_pkgver-src"
  fpc="/usr/lib/fpc/""`fpc -iV`""/units/x86_64-linux/"
  echo fpc=$fpc
  # Keep debug symbols and do not strip
  export fpcopts="-g -gl -O3 -CX -XX"
  echo fpcopts="$fpcopts"
  echo ./configure fpc="$fpc" lazarus=/usr/lib/lazarus prefix="$pkgdir/usr" target=x86_64-linux 
  ./configure fpc="$fpc" lazarus=/usr/lib/lazarus prefix="$pkgdir/usr" target=x86_64-linux 
  make CPU_TARGET=x86_64 OS_TARGET=linux LCL_PLATFORM=qt6 clean
  make CPU_TARGET=x86_64 OS_TARGET=linux LCL_PLATFORM=qt6 -j 1
}

package() {
  cd "skychart-$_pkgver-src"
  echo pkgdir $pkgdir
  mkdir -p "$pkgdir/usr"
  make install
  make install_data
  make install_doc
  make install_nonfree
}
