# Maintainer: ThatOneCalculator <kainoa@t1c.dev>
# Based on the xdg-desktop-portal-wlr-git PKGBUILD

pkgname=xdg-desktop-portal-hyprland-git
_pkgname="${pkgname%-git}"
pkgver=r291.57a3a41
pkgrel=1
epoch=1
pkgdesc="xdg-desktop-portal backend for hyprland"
url="https://github.com/hyprwm/xdg-desktop-portal-hyprland"
arch=('x86_64')
license=('MIT')
provides=("${_pkgname}" "xdg-desktop-portal-impl" "xdg-desktop-portal-wlr")
conflicts=("${_pkgname}" "xdg-desktop-portal-wlr")
depends=('xdg-desktop-portal' 'pipewire' 'libinih' 'qt6-base' 'qt6-wayland')
makedepends=('git' 'meson' 'wayland-protocols' 'wayland' 'scdoc' 'cmake' 'sdbus-cpp')
optdepends=(
	"grim: required for the screenshot portal to function"
	"slurp: support for interactive mode for the screenshot portal; one of the built-in chooser options for the screencast portal"
	"bemenu: one of the built-in chooser options for the screencast portal"
	"wofi: one of the built-in chooser options for the screencast portal"
	"hyprland: the Hyprland compositor"
)
source=("git+${url}.git")
sha256sums=('SKIP')
options=('!makeflags' '!buildflags' '!strip')

pkgver() {
	cd "${_pkgname}/"
	git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/v//'
}

build() {
	cd "${_pkgname}/"
	git submodule update --init
	meson build --prefix=/usr --libexecdir lib
	ninja -C build
	cd hyprland-share-picker && make all && cd ..
}

package() {
	cd "${_pkgname}/"
	DESTDIR="${pkgdir}" ninja -C build install
	install -Dm755 hyprland-share-picker/build/hyprland-share-picker -t "${pkgdir}/usr/bin/"
	install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${_pkgname}/"
}
