# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Maintainer: Integral <integral@member.fsf.org>

pkgname=xdg-desktop-portal-hyprland-git
_pkgname="${pkgname%-git}"
pkgver=1.2.6.r1.g54a3025
pkgrel=1
epoch=1
pkgdesc="xdg-desktop-portal backend for hyprland"
url="https://github.com/hyprwm/${_pkgname}"
arch=('x86_64')
license=('MIT')
depends=('hyprlang'
	'libinih'
	'pipewire'
	'qt6-base'
	'qt6-wayland'
	'sdbus-cpp'
	'util-linux-libs'
	'wlroots'
	'xdg-desktop-portal')
makedepends=('git'
	'cmake'
	'wayland'
	'wayland-protocols')
optdepends=('grim: required for the screenshot portal to function'
	'slurp: support for interactive mode for the screenshot portal')
provides=("${_pkgname}" "xdg-desktop-portal-impl" "xdg-desktop-portal-wlr")
conflicts=("${_pkgname}" "xdg-desktop-portal-wlr")
source=("git+${url}.git"
	"git+https://github.com/hyprwm/hyprland-protocols.git"
	"hyprland-portals.conf") # TODO move this to hyprland where it belongs
sha256sums=('SKIP'
            'SKIP'
            '20bc215211f16a361086d59fa051df7337d95f91c695a29d8c5d23d40407fad5')

pkgver() {
	cd "${_pkgname}/"
	git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/v//'
}

prepare() {
	cd "${_pkgname}/subprojects/"
	rm -rf hyprland-protocols sdbus-cpp
	ln -sfT "${srcdir}/hyprland-protocols/" hyprland-protocols
}

build() {
	cd "${_pkgname}/"
	cmake -B build \
		-D CMAKE_INSTALL_PREFIX=/usr \
		-D CMAKE_INSTALL_LIBEXECDIR=/usr/lib \
		-D CMAKE_BUILD_TYPE=Release

	cmake --build build
}

package() {
	cd "${_pkgname}/"
	DESTDIR="${pkgdir}" cmake --install build
	install -Dm644 "${srcdir}/hyprland-portals.conf" -t "${pkgdir}/usr/share/xdg-desktop-portal/"
	install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
