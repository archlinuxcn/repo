# Maintainer: Hu Butui <hot123tea123@gmail.com>
# Contributor: Kyle Manna <kyle[at]kylemanna[d0t]com>

_pkgname=affine
pkgname=python-affine
pkgver=3.0rc1
pkgrel=1
pkgdesc="Python package for matrices describing affine transformation of the plane"
arch=('any')
url='https://github.com/rasterio/affine'
license=('BSD-3-Clause')
makedepends=(
  python-build
  python-installer
  python-wheel
  python-flit-core
)
depends=(
  python
)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/rasterio/affine/archive/refs/tags/${pkgver}.tar.gz")
sha512sums=('7b84614aad7589bf54a05ce72a273ddac9e5fc5da7c48aa7f48642ca1e1617fcbfdd41cc4d0c3b8a42bfd7b11b5455ff6f99c150d9c8eae368f0b84f837d050c')

build() {
  cd "${_pkgname}-${pkgver}"
  python -m build --wheel --no-isolation
}

package() {
  cd "${_pkgname}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 -t "${pkgdir}/usr/share/licenses/${pkgname}" LICENSE.txt
  # delete tests dir
  local site_packages=$(python -c 'import site; print(site.getsitepackages()[0])')
  rm -rfv "${pkgdir}${site_packages}/affine/tests"
}
# vim:set ts=2 sw=2 et:
