# Maintainer: Joe Baker < JoeBlakeB at protonmail dot com >

pkgname=ttf-twemoji-mozilla
pkgver=0.7.0
pkgrel=3
pkgdesc="Twitter Emoji with COLRv1 with Mozilla."
url="https://github.com/mozilla/twemoji-colr"
arch=(any)
license=('CCPL' 'MIT' 'Apache')
makedepends=('npm' 'nodejs' 'python-fonttools' 'fontforge')
provides=('emoji-font')
source=("twemoji-colr-v$pkgver.tar.gz::https://github.com/mozilla/twemoji-colr/archive/refs/tags/v$pkgver.tar.gz"
        )
b2sums=('8acf3756bbf0244c73ffeeb8a9c34916dac1c446e8aee1c1cf39dd5f8a09e22bf881a67d678803800ed756a35b4e080ad190be75265944a282bf3539dd834061')

build() {
  cd "$srcdir/twemoji-colr-$pkgver"
  sed -i 's/resolutions/overrides/g' package.json
  npm install
  make -j1
}

package() {
  cd "$srcdir/twemoji-colr-$pkgver"
  install -Dm644 "build/Twemoji Mozilla.ttf" \
    "${pkgdir}/usr/share/fonts/TwemojiMozilla.ttf"
  install -Dm644 -t "${pkgdir}/usr/share/licenses/${pkgname}" \
    LICENSE.md
}
