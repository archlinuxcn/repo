# Maintainer: Coelacanthus <uwu@coelacanthus.name>
# Contributor: Joe Baker < JoeBlakeB at protonmail dot com >

pkgname=ttf-twemoji-mozilla
pkgver=0.7.0
pkgrel=3
pkgdesc="Twitter Emoji with COLRv1 with Mozilla."
url="https://github.com/mozilla/twemoji-colr"
arch=(any)
license=('CC-BY-SA-4.0 AND Apache-2.0')
makedepends=(
  'fontforge'
  'git'
  'nodejs'
  'npm'
  'python-fonttools'
  'python-setuptools'
)
provides=('emoji-font')
source=(
  "twemoji-colr-v$pkgver.tar.gz::https://github.com/mozilla/twemoji-colr/archive/refs/tags/v$pkgver.tar.gz"
  "$pkgname-twemoji-15.0.3.patch::https://github.com/mozilla/twemoji-colr/commit/3ab46d5f06862d2fb4d6b112ca7b3cb5b856c63c.patch"
  "$pkgname-twemoji-15.1.0.patch::https://github.com/mozilla/twemoji-colr/commit/0dccb7e5da29654918e73697a3e8d0d4d33e21fb.patch"
)
b2sums=('8acf3756bbf0244c73ffeeb8a9c34916dac1c446e8aee1c1cf39dd5f8a09e22bf881a67d678803800ed756a35b4e080ad190be75265944a282bf3539dd834061'
        '9ac440f68096c466429ed6ca5fa94b990d79258e459a58f0c34059bc867aeb502c4cb936907310fb819aa8ec49c40da614f65b4ea5548e148e20d0fc52f9d0e0'
        '168a0548c548e8601bc6e82aa8916fa4c9a9869f4d98ab8ad5b40cf5db42ccaa018bf819ce46662b7f7959575c6671f9486aafc101c8562890ec57e0c0a587e8')

prepare() {
  cd "twemoji-colr-$pkgver"
  git apply -p1 ../"$pkgname-twemoji-15.0.3.patch"
  git apply -p1 ../"$pkgname-twemoji-15.1.0.patch"
}

build() {
  cd "$srcdir/twemoji-colr-$pkgver"
  npm install
  make -j1
}

package() {
  cd "$srcdir/twemoji-colr-$pkgver"
  install -Dm644 "build/Twemoji Mozilla.ttf" \
    "${pkgdir}/usr/share/fonts/TwemojiMozilla.ttf"
  install -Dm644 -t "${pkgdir}/usr/share/licenses/${pkgname}" \
    LICENSE.md
}
# vim: set ts=2 sw=2 et:
