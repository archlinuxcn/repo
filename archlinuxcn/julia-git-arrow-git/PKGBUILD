pkgname=julia-git-arrow-git
pkgver=2.4.1.0.0.rc0.9.g94ee070
epoch=1
pkgrel=1
pkgdesc="Arrow.jl"
url="https://github.com/JuliaData/Arrow.jl"
arch=('any')
license=('MIT')
makedepends=(git julia-pkg-scripts)
depends=(julia-git)
provides=(julia-git-arrow "julia-git-arrowtypes-git=${pkgver}-${pkgrel}" julia-git-arrowtypes)
source=(git+https://github.com/JuliaData/Arrow.jl)
md5sums=('SKIP')

pkgver() {
  cd Arrow.jl

  git describe --tags | sed -e 's/^[^0-9]*//' -e 's/-/.0.0./' -e 's/-/./g'
}

package() {
  cd Arrow.jl

  . /usr/lib/julia/julia-install-pkg.sh Arrow "${pkgdir}" "${pkgname}" julia-git
  ln -s Arrow/src/ArrowTypes "${pkgdir}/${site_dir}/ArrowTypes"
  old_depends=("${depends[@]}")
  _depends=() # use a different name to fool makepkg's stupic static check
  for dep in "${old_depends[@]}"; do
    if [[ "$dep" != julia-git-arrowtypes ]]; then
      _depends+=("${dep}")
    fi
  done
  depends=("${_depends[@]}")
}
