# Maintainer:  Peter Mattern <pmattern at arcor dot de>

_pkgname=qps
pkgname=$_pkgname-git
pkgver=2.6.0.15.g40d0172
pkgrel=1
pkgdesc='Qt process manager'
arch=('i686' 'x86_64')
url='https://github.com/lxqt/qps'
license=('GPL')
depends=('qt5-x11extras' 'gtk-update-icon-cache' 'liblxqt-git')
makedepends=('git' 'cmake' 'qt5-tools' 'lxqt-build-tools-git')
provides=("$_pkgname")
conflicts=("$_pkgname")
source=("git+https://github.com/lxqt/qps.git")
sha256sums=("SKIP")

pkgver() {
    cd $_pkgname
    git describe --always | sed 's:-:.:g'
}

build() {
    mkdir build ; cd build
    cmake ../$_pkgname -DCMAKE_INSTALL_PREFIX=/usr
    make
}

package() {
    cd build
    make DESTDIR=$pkgdir install
}
