# Maintainer: Trent Palmer trenttdually at gmail

###########################################################################################################
#                                          Build Options
###########################################################################################################
_vim="y"

# neovim in Arch Linux seems to load vim extensions from /usr/share/vim/vimfiles by
# default now, (or you can add 'set rtp^=/usr/share/vim/vimfiles/' to .vimrc)
# but in any event, you may not need or want to set _neovim="y"
_neovim="n"
###########################################################################################################

_name='deoplete'
pkgname="${_name}-git"
pkgver=r2060.b3a03b2
pkgrel=1
pkgdesc="deoplete completion plugin for neovim"
# i.e. seems to build and work fine on raspberrypi
arch=('any')
url="https://github.com/Shougo/deoplete.nvim"
license=('MIT')
groups=()
# python-msgpack>=1.0.0 apparently no longer needed
# depends=('python-pynvim' 'python-msgpack>=1.0.0')
depends=('python-pynvim')
optdepends=('vim: vim support' 
            'neovim: neovim support' 
            'nvim-yarp: vim support' 
            'vim-hug-neovim-rpc: vim support')
makedepends=('git') 
provides=('deoplete')
conflicts=('deoplete')
replaces=('')
backup=()
options=()
install="${_name}.install"
source=("${_name}::git+${url}")
noextract=()
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_name}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
  if [ "$_neovim" = "y" ]; then
    mkdir -p "$pkgdir/usr/share/nvim/runtime"
    cp -r "${srcdir}/${_name}/"{autoload,doc,plugin,rplugin} \
      "$pkgdir/usr/share/nvim/runtime"
  fi

  if [ "$_vim" = "y" ]; then
    mkdir -p "$pkgdir/usr/share/vim/vimfiles"
    cp -r "${srcdir}/${_name}/"{autoload,doc,plugin,rplugin} \
      "$pkgdir/usr/share/vim/vimfiles"
  fi

  install -D -m644 "${srcdir}/${_name}/README.md" "$pkgdir"/usr/share/doc/$pkgname/README.md
  install -D -m644 "${srcdir}/${_name}/LICENSE" "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
