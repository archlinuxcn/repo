# Maintainer: DuckSoft <realducksoft at gmail dot com>
# Contributor: rustemb <rustemb at systemli dot org>
pkgname=shadowsocks-rust-opt-git
pkgver=1.11.0.r12.g2a5cdb0
pkgrel=2
provides=(shadowsocks-rust)
conflicts=(shadowsocks-rust)
pkgdesc='Rust port of Shadowsocks (with optimizations for x86-64-v3)'
arch=('x86_64')
url='https://github.com/shadowsocks/shadowsocks-rust'
license=('MIT')
depends=('openssl')
makedepends=('cargo' 'libsodium' 'git')
source=("git+$url.git" shadowsocks-rust{,-server}@.service)

pkgver() {
    cd "${pkgname%-opt-git}"
    git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    cd "${pkgname%-opt-git}"
    RUSTFLAGS="-C opt-level=2 -C target-cpu=x86-64-v3" cargo build --release --features "local-http-rustls local-redir local-dns dns-over-tls dns-over-https"
}

package() {
    install -Dm644 shadowsocks-rust{,-server}@.service -vt "$pkgdir"/usr/lib/systemd/system/

    cd "${pkgname%-opt-git}"
    install -Dm644 LICENSE                      -vt "$pkgdir"/usr/share/licenses/shadowsocks-rust/
    install -Dm644 examples/config{,_ext}.json  -vt "$pkgdir"/etc/shadowsocks-rust/

    cd target/release
    for name in sslocal ssserver ssurl ssmanager; do
        install -Dm755 -v $name "$pkgdir"/usr/bin/${name}-rust
    done
}

b2sums=('SKIP'
        '3fa3c6864981206df83511fa32acfef9a16788b903516d70476dca19aae321e168a2dc8c427c87b53b501d51a2f7353258cf2e8701b1962a3c8a2c844d8ecfa7'
        'f0b0e4f3d0b0f260f8a3908ff2b30c3dbf97326925c9919dd730216cf544b14175c47efc93d00c70bf5fe08cba3ccb89d20c6bf46d4e4bb5f787cb1013701404')
