# Maintainer: Butui Hu <hot123tea123@gmail.com>

_pkgname=hf_transfer
pkgname=python-hf-transfer
pkgver=0.1.9
pkgrel=2
pkgdesc='Speed up file transfers with the Hugging Face Hub'
arch=('x86_64')
url='https://github.com/huggingface/hf_transfer'
license=('Apache-2.0')
depends=(
  gcc-libs
  glibc
  openssl
  python
)
makedepends=(
  python-build
  python-maturin
  python-installer
  python-wheel
)
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/huggingface/hf_transfer/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('074801626fdf7bf9c83451f6d7567ef3f9cebc390ccf4724f1f3fdbb1d5f56e49d179ef7baecbd53d89b317c13ac6df7459ee47cb9a9f18aa23f52fbfba36a84')

build() {
  cd "${_pkgname}-${pkgver}"
  # use system openssl
  OPENSSL_NO_VENDOR=1 \
  python -m build --wheel --no-isolation
}

package() {
  cd "${_pkgname}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
}
# vim:set ts=2 sw=2 et:
