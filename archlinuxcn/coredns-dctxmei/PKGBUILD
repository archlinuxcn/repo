# Maintainer: Lorenzo Fontana <lo@linux.com>
# Maintainer: Xuanwo <xuanwo@archlinuxcn.org>
# Maintainer: Dct Mei <dctxmei@yandex.com>

pkgname=coredns-dctxmei
_pkgname=coredns
pkgver=1.10.1
pkgrel=1
pkgdesc="A DNS server that chains plugins"
arch=('x86_64')
url="https://github.com/coredns/coredns"
license=('Apache')
depends=('glibc')
makedepends=('git' 'go' 'make')
provides=("coredns=${pkgver}")
conflicts=('coredns')
backup=('etc/coredns/Corefile')
source=("${_pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz"
        "Corefile"
        "coredns.service")
sha256sums=('f47186452e5f2925e2c71135669afd9e03b9b55831417d33d612ef2fa69924a7'
            '8996ce8a79d5d4d2015fe69325f9116533d1fc18368198b05ac63af632a51eee'
            '351e88aed0d57e0777ecf7d4ba8d6ae77aa9020c8b7c25ea6d45541abeef11bf')

build() {
    export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
    export CGO_CPPFLAGS="${CPPFLAGS}"
    export CGO_CXXFLAGS="${CXXFLAGS}"
    export CGO_CFLAGS="${CFLAGS}"
    export CGO_LDFLAGS="${LDFLAGS}"

    cd "${srcdir}"/"${_pkgname}-${pkgver}"/
    go build -ldflags="-linkmode=external" -o coredns
}

package() {
    cd "${srcdir}"/"${_pkgname}-${pkgver}"/
    # Install binary
    install -Dm 755 coredns -t "${pkgdir}"/usr/bin/
    # Install default coredns config
    install -Dm 644 "${srcdir}"/Corefile -t "${pkgdir}"/etc/coredns/
    # Install systemd service
    install -Dm 644 "${srcdir}"/coredns.service -t "${pkgdir}"/usr/lib/systemd/system/
}
