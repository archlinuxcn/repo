# Maintainer: Hu Butui <hot123tea123@gmail.com>

_pkgname=formatR
_pkgver=1.12
pkgname=r-${_pkgname,,}
pkgver=1.12
pkgrel=1
pkgdesc='Format R Code Automatically'
arch=('any')
url="https://cran.r-project.org/package=${_pkgname}"
license=('GPL')
depends=(
  r
)
optdepends=(
  r-codetools
  r-knitr
  r-rmarkdown
  r-shiny
  r-testit
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
sha256sums=('8b52efbf43cbef25d837bb99a793a590c0958b753052c032b52304724e808c8b')

build() {
  R CMD INSTALL ${_pkgname}_${_pkgver}.tar.gz -l "${srcdir}"
}

package() {
  install -dm0755 "${pkgdir}/usr/lib/R/library"
  cp -a --no-preserve=ownership "${_pkgname}" "${pkgdir}/usr/lib/R/library"
}
# vim:set ts=2 sw=2 et:
