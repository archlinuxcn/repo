# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgbase=qt5-base-24h
_origpkgname=qt5-base
pkgname=(qt5-base-24h)
_basever=5.15.16
pkgver=5.15.16+kde+r130
pkgrel=3.14
_commit=2529f7f0c2333d437089c775c9c30f624d1fd5bc
arch=('x86_64')
url='https://www.qt.io'
license=('GPL3' 'LGPL3' 'FDL' 'custom')
pkgdesc='A cross-platform application and UI framework. This package uses 24-hour notation in all locales.'
depends=(libicui18n.so 'libjpeg-turbo' 'xcb-util-keysyms' 'xcb-util-renderutil' 'libgl' 'fontconfig' 'xdg-utils'
         'shared-mime-info' 'xcb-util-wm' 'libxrender' 'libxi' 'sqlite' 'xcb-util-image' 'mesa'
         'tslib' 'libinput' 'libxkbcommon-x11' 'libproxy' 'libcups' 'double-conversion' 'md4c')
makedepends=('python2' 'libfbclient' 'mariadb-libs' 'unixodbc' 'postgresql-libs' 'alsa-lib' 'gst-plugins-base-libs'
             'gtk3' 'libpulse' 'cups' 'freetds' 'vulkan-headers' 'git')
optdepends=('qt5-svg: to use SVG icon themes'
            'qt5-wayland: to run Qt applications in a Wayland session'
            'postgresql-libs: PostgreSQL driver'
            'mariadb-libs: MariaDB driver'
            'unixodbc: ODBC driver'
            'libfbclient: Firebird/iBase driver'
            'freetds: MS SQL driver'
            'gtk3: GTK platform plugin'
            'perl: for fixqt4headers and syncqt')
conflicts=("qt5-base" "qt5-3d>$_basever.99" "qt5-charts>$_basever.99" "qt5-connectivity>$_basever.99" "qt5-datavis3d>$_basever.99" "qt5-declarative>$_basever.99" "qt5-doc>$_basever.99" "qt5-examples>$_basever.99" "qt5-gamepad>$_basever.99" "qt5-graphicaleffects>$_basever.99" "qt5-imageformats>$_basever.99" "qt5-location>$_basever.99" "qt5-lottie>$_basever.99" "qt5-multimedia>$_basever.99" "qt5-networkauth>$_basever.99" "qt5-purchasing>$_basever.99" "qt5-quick3d>$_basever.99" "qt5-quickcontrols>$_basever.99" "qt5-quickcontrols2>$_basever.99" "qt5-remoteobjects>$_basever.99" "qt5-scxml>$_basever.99" "qt5-sensors>$_basever.99" "qt5-serialbus>$_basever.99" "qt5-serialport>$_basever.99" "qt5-speech>$_basever.99" "qt5-svg>$_basever.99" "qt5-tools>$_basever.99" "qt5-translations>$_basever.99" "qt5-virtualkeyboard>$_basever.99" "qt5-wayland>$_basever.99" "qt5-webchannel>$_basever.99" "qt5-webglplugin>$_basever.99" "qt5-websockets>$_basever.99" "qt5-webview>$_basever.99" "qt5-x11extras>$_basever.99" "qt5-xmlpatterns>$_basever.99" 'qtchooser')

provides=("qt5-base=$pkgver")

_pkgfqn=${_origpkgname/5-/}
source=(
      oldherl-24h.patch
      'https://build.archlinuxcn.org/~oldherl/files/cldr/42/core.zip'
      kde-$_pkgfqn::git+https://invent.kde.org/qt/qt/$_pkgfqn#commit=$_commit
        qmake-cflags.patch
        qmake-config.patch)
sha256sums=(
      '901a764c896559bb472d5ecee4af9ee006b235f73e56959de9ded477a10e5fc6'
      '53cd4fd1ac2ee4d4cbcae362e7af5d02e98e5e39c826ce9d723d41ca836fc846'
      'c0190253f5a3326bbc04614f5f9bbe94df6900bb60c865ccdecc71ee7e137d19'
            '5411edbe215c24b30448fac69bd0ba7c882f545e8cf05027b2b6e2227abc5e78'
            '4abc22150fa3e06b2fdcec32146abc9be4e316692aa4d5bd5aa53b4b726783fa')

pkgver() {
  cd kde-$_pkgfqn
  echo "$_basever+kde+r"`git rev-list --count v$_basever-lts-lgpl..$_commit` | sed -e 's|+kde+r0||'
}

prepare() {
  cd kde-$_pkgfqn
  patch -p1 < ../qmake-cflags.patch # Use system CFLAGS in qmake
  patch -p1 < ../qmake-config.patch # Don't strip binaries with qmake and use -ltcg, cf. QTBUG-73834

  patch -p1 -i ../oldherl-24h.patch
  cd util/locale_database
  echo "This is slow. It takes about 4 minutes on my desktop."
  ./cldr2qlocalexml.py ../../../ > ./24h.xml
  ./qlocalexml2cpp.py ./24h.xml ../..
}

build() {
  cd kde-$_pkgfqn

  ./configure -confirm-license -opensource -v \
    -prefix /usr \
    -docdir /usr/share/doc/qt \
    -headerdir /usr/include/qt \
    -archdatadir /usr/lib/qt \
    -datadir /usr/share/qt \
    -sysconfdir /etc/xdg \
    -examplesdir /usr/share/doc/qt/examples \
    -plugin-sql-{psql,mysql,sqlite,odbc,ibase} \
    -system-sqlite \
    -openssl-linked \
    -nomake examples \
    -no-rpath \
    -dbus-linked \
    -system-harfbuzz \
    -journald \
    -libproxy \
    -no-mimetype-database \
    -no-use-gold-linker \
    -no-reduce-relocations \
    -no-strip \
    -ltcg
# No configure flag for fat static libs with lto
  bin/qmake CONFIG+=fat-static-lto -- -redo
  make
}

package() {
install -dm755 "$pkgdir"/usr/share/licenses/
ln -s /usr/share/licenses/${pkgname} "$pkgdir"/usr/share/licenses/qt5-base

  pkgdesc='A cross-platform application and UI framework'
  depends+=(qt5-translations)

  cd kde-$_pkgfqn
  make INSTALL_ROOT="${pkgdir}" install

  install -Dm644 LICENSE* -t "$pkgdir"/usr/share/licenses/$pkgbase

  # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
  find "${pkgdir}/usr/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;

  # Fix wrong qmake path in pri file
  sed -i "s|${srcdir}/${_pkgfqn}|/usr|" \
    "${pkgdir}"/usr/lib/qt/mkspecs/modules/qt_lib_bootstrap_private.pri

  # Symlinks for backwards compatibility
  for b in "${pkgdir}"/usr/bin/*; do
    ln -s $(basename $b) "${pkgdir}"/usr/bin/$(basename $b)-qt5
  done
}

nopackage_qt5-xcb-private-headers() {
  pkgdesc='Private headers for Qt5 Xcb'

  depends=("qt5-base=$pkgver")
  optdepends=()
  groups=()
  conflicts=()

  cd kde-$_pkgfqn
  install -d -m755 "$pkgdir"/usr/include/qtxcb-private
  cp -r src/plugins/platforms/xcb/*.h "$pkgdir"/usr/include/qtxcb-private/
}
