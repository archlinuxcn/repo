# Maintainer: Integral <integral@member.fsf.org>

pkgname=danxi-git
_pkgname=${pkgname%-git}
pkgver=1.4.9.r14.g0999997
pkgrel=1
pkgdesc="Maybe the best all-rounded service app for Fudan University students | 可能是复旦学生最好的第三方校园服务 APP"
url="https://github.com/DanXi-Dev/DanXi"
license=('GPL-3.0-or-later')
arch=('x86_64')
depends=('gtk3' 'libsecret' 'gnome-keyring')
makedepends=('git' 'clang' 'cmake' 'ninja' 'fvm' 'imagemagick')
conflicts=("${_pkgname}")
provides=("${_pkgname}")
source=("git+${url}.git")
sha256sums=('SKIP')

pkgver() {
	git -C DanXi describe --long --tags --abbrev=7 | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
	cd DanXi
	fvm install stable
	fvm use stable
	fvm flutter --disable-analytics
	fvm flutter --no-version-check pub get
}

build() (
	cd DanXi
	export CXXFLAGS+=" -Wno-error=unused-result"
	fvm dart --disable-analytics
	fvm dart run intl_utils:generate
	fvm dart build_release.dart --target linux --versionCode dummy
)

package() {
	cd DanXi

	pushd build/linux/x64/release
	install -Dm755 bundle/dan_xi -t "${pkgdir}/usr/lib/${_pkgname}/"
	cmake -DCMAKE_INSTALL_PREFIX="${pkgdir}/usr/lib/${_pkgname}" .
	cmake -P cmake_install.cmake
	popd

	# Symlink
	install -dm755 "${pkgdir}/usr/bin"
	ln -s "/usr/lib/${_pkgname}/dan_xi" "${pkgdir}/usr/bin/dan_xi"

	# Icon
	for r in 16 24 32 48 64 128 256 512; do
		install -dm755 "${pkgdir}/usr/share/icons/hicolor/${r}x${r}/apps/"
		magick packaging/dan_xi.png -resize "${r}x${r}" "${pkgdir}/usr/share/icons/hicolor/${r}x${r}/apps/dan_xi.png"
	done

	# Desktop Launcher
	install -Dm644 "packaging/io.github.danxi_dev.dan_xi.desktop" "${pkgdir}/usr/share/applications/dan_xi.desktop"
}
