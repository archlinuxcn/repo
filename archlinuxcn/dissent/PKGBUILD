# Maintainer: txtsd <aur.archlinux@ihavea.quest>

pkgname=dissent
pkgver=0.0.20
_pkgver=$(sed 's/\./-/3' <<<${pkgver})
pkgrel=1
pkgdesc="Discord client written in go and gtk4"
arch=('x86_64' 'aarch64')
url="https://github.com/diamondburned/${pkgname}"
license=('GPL-3.0-or-later')
depends=('gtk4>=4.10.3' 'gobject-introspection' 'libadwaita>=1.3.2')
makedepends=('git' 'go>=1.20.3')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${_pkgver}.tar.gz")
sha256sums=('a0dc5af6f36dc2c26874ebabbced075b6272b0aad1abc09e18069c7c76bf68cb')

prepare() {
	mkdir -p "${pkgname}-${_pkgver}/build/"
}

build() {
	cd "${pkgname}-${_pkgver}/"

	export CGO_CPPFLAGS="${CPPFLAGS}"
	export CGO_CFLAGS="${CFLAGS}"
	export CGO_CXXFLAGS="${CXXFLAGS}"
	export CGO_LDFLAGS="${LDFLAGS}"
	export GOPATH="${srcdir}/go"
	export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
	go build -o build
}

package() {
	cd "${pkgname}-${_pkgver}/build/"
	install -Dm755 "${pkgname}" -t "${pkgdir}/usr/bin/"

	cd ..
	install -Dm644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}/"
	install -Dm644 "nix/so.libdb.${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
	install -Dm644 "internal/icons/hicolor/scalable/apps/so.libdb.Dissent.Devel.svg" -t "${pkgdir}/usr/share/icons/hicolor/scalable/apps/"
}
