# Maintainer: Rocket Aaron
# Contributor: huyz

pkgname=plasma-login-manager-git
_pkgname=plasma-login-manager
pkgver=6.5.90.r61.gbbf9b88
pkgrel=1
pkgdesc='Plasma Login Manager (git version)'
url='https://invent.kde.org/plasma/plasma-login-manager'
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
    kpackage
    layer-shell-qt
    qt6-declarative
    libplasma
    kdbusaddons
    kio
    kconfig
    kwindowsystem
    kirigami
    plasma-workspace
    kcoreaddons
    qt6-base
    kservice
    kauth
    kcmutils
    ki18n
)
makedepends=(extra-cmake-modules
    cmake
    ninja
    git)
source=('git+https://invent.kde.org/plasma/plasma-login-manager.git/'
        'PROJECT_DEP_VERSION.patch')
sha256sums=('SKIP'
            'd7f17a4f4cf26a369c0e355c4a519de3df15d55715354579ae2f721edacc3bdc')
provides=(${_pkgname})
conflicts=(${_pkgname})

pkgver() {
    cd $_pkgname
    git describe --long --abbrev=7 | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    cd $_pkgname
    # allow build for older version of plasma
    patch -Np1 -i $srcdir/PROJECT_DEP_VERSION.patch
}

build() {
    cmake -B build -S $_pkgname -GNinja \
      -DCMAKE_INSTALL_LIBEXECDIR=lib \
      -DDBUS_CONFIG_FILENAME=plasma_org.freedesktop.DisplayManager.conf
    cmake --build build
}

package() {
    DESTDIR="$pkgdir" cmake --install build
}
