# Maintainer:  Iwan Timmer <irtimmer@gmail.com>

pkgname=process-cpp
pkgdesc="C++11 library for handling processes"
pkgver=3.0.1
pkgrel=5
arch=('x86_64')
url="https://launchpad.net/process-cpp"
license=('LGPL3')
depends=('boost-libs' 'gcc-libs')
makedepends=('cmake' 'doxygen' 'boost' 'properties-cpp')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/process-cpp_3.0.1.orig.tar.gz")
md5sums=('95e187de74037b70b105679f85047c12')

prepare() {
  cd "$srcdir/${pkgname}-${pkgver}"

  # Don't build tests
  truncate -s 0 tests/CMakeLists.txt
}

build() {
  mkdir -p "$srcdir/${pkgname}-${pkgver}/build"
  cd "$srcdir/${pkgname}-${pkgver}/build"

  cmake .. -DCMAKE_INSTALL_LIBDIR=/usr/lib -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd "$srcdir/${pkgname}-${pkgver}/build"

  make DESTDIR="$pkgdir/" install
}
