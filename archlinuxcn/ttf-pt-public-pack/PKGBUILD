# Maintainer: Serhii Starovoitov <drommer.94@gmail.com>

pkgbase=ttf-pt-public-pack
pkgname=('ttf-pt-astra-sans' 'ttf-pt-astra-serif' 'ttf-pt-mono' 'ttf-pt-root_ui' 'ttf-pt-sans' 'ttf-pt-serif')
pkgver=2024.09.02
pkgrel=2
epoch=1
arch=('any')
url='https://www.paratype.ru'
license=('custom:OFL')
source=("PT-Public-Pack.ottt.zip"::"https://api.paratype.com/api/editions/ofl/download/59384")
DLAGENTS=('https::/usr/bin/curl -gqb "" -fLC - --retry 3 --retry-delay 3 -o %o %u --referer https://www.paratype.com/ --user-agent Mozilla')
sha256sums=('SKIP')

package_ttf-pt-astra-sans() {
  pkgdesc='A sans-serif font designed to be metrically equivalent to Times New Roman'
  provides=('ttf-font' 'ttf-pt-astra-sans')
  conflicts=('ttf-pt-astra-sans' 'ttf-astra-sans')
  install -Dm644 $srcdir/PT/PT/PT-Astra-Sans/*.ttf -t $pkgdir/usr/share/fonts/paratype/pt-astra-sans
  install -Dm644 $srcdir/OFL.txt -t $pkgdir/usr/share/licenses/${pkgname}
}

package_ttf-pt-astra-serif() {
  pkgdesc='A serif font designed to be metrically equivalent to Times New Roman'
  provides=('ttf-font' 'ttf-pt-astra-serif')
  conflicts=('ttf-pt-astra-serif' 'ttf-astra-serif')
  install -Dm644 $srcdir/PT/PT/PT-Astra-Serif/*.ttf -t $pkgdir/usr/share/fonts/paratype/pt-astra-serif
  install -Dm644 $srcdir/OFL.txt -t $pkgdir/usr/share/licenses/${pkgname}
}

package_ttf-pt-mono() {
  pkgdesc='A monospace font for use in forms, tables, worksheets and other scenarios where equal character widths are advantageous'
  provides=('ttf-font' 'ttf-pt-mono')
  conflicts=('ttf-pt-mono' 'ttf-paratype')
  install -Dm644 $srcdir/PT/PT/PT-Mono/*.ttf -t $pkgdir/usr/share/fonts/paratype/pt-mono
  install -Dm644 $srcdir/OFL.txt -t $pkgdir/usr/share/licenses/${pkgname}
}

package_ttf-pt-root_ui() {
  pkgdesc='A sans-serif typeface designed specifically for user interfaces and web design'
  provides=('ttf-font' 'ttf-pt-root-ui')
  conflicts=('ttf-pt-root-ui')
  install -Dm644 $srcdir/PT/PT/PT-Root-UI/*.ttf -t $pkgdir/usr/share/fonts/paratype/pt-root-ui
  install -Dm644 $srcdir/OFL.txt -t $pkgdir/usr/share/licenses/${pkgname}
}

package_ttf-pt-sans() {
  pkgdesc='A humanist sans-serif font inspired by the mid-20th century types suitable for applications and website'
  provides=('ttf-font' 'ttf-pt-sans')
  conflicts=('ttf-pt-sans' 'ttf-paratype')
  install -Dm644 $srcdir/PT/PT/PT-Sans/*.ttf -t $pkgdir/usr/share/fonts/paratype/pt-sans
  install -Dm644 $srcdir/OFL.txt -t $pkgdir/usr/share/licenses/${pkgname}
}

package_ttf-pt-serif() {
  pkgdesc='A transitional serif typeface with humanistic terminals designed for use together with PT Sans'
  provides=('ttf-font' 'ttf-pt-serif')
  conflicts=('ttf-pt-serif' 'ttf-paratype')
  install -Dm644 $srcdir/PT/PT/PT-Serif/*.ttf -t $pkgdir/usr/share/fonts/paratype/pt-serif
  install -Dm644 $srcdir/OFL.txt -t $pkgdir/usr/share/licenses/${pkgname}
}
