# Maintainer: jason ryan <jasonwryan@gmail.com>
# Contributor:  Bart≈Çomiej Piotrowski <nospam@bpiotrowwski.pl>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: evr <evanroman at gmail>
# Contributor: Luiz Ribeiro <luizribeiro@gmail.com>

pkgname=mpdscribble
pkgver=0.23
pkgrel=4
pkgdesc='MPD client which submits track info to {Libre,Last}.fm'
url='https://github.com/MusicPlayerDaemon/mpdscribble'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('GPL')
depends=('curl' 'libmpdclient')
makedepends=('boost' 'systemd' 'meson' 'ninja')
install=$pkgname.install
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/MusicPlayerDaemon/${pkgname}/archive/v${pkgver}.tar.gz")
md5sums=('6b3b325c82f6b6eff38123960d7d91dc')

prepare() {
  cd "${pkgname}"-"${pkgver}"
  DESTDIR="${pkgdir}" meson build \
     --prefix=/usr \
     --sysconfdir=/etc
}

build() {
  cd "${pkgname}"-"${pkgver}"
  DESTDIR="${pkgdir}" meson build \
  --prefix=/usr \
  --buildtype=plain
  meson compile -C build
}

package() {
  cd ${pkgname}-"${pkgver}"
  DESTDIR="${pkgdir}" meson install -C build

  install -Dm644 build/systemd/system/mpdscribble.service \
    "${pkgdir}"/usr/lib/systemd/system/mpdscribble@.service
  install -Dm644 build/systemd/user/mpdscribble.service \
    "${pkgdir}"/usr/lib/systemd/user/mpdscribble.service

  # example config
  install -Dm644 doc/mpdscribble.conf \
    "${pkgdir}"/usr/share/mpdscribble/mpdscribble.conf.example
  rm -r "${pkgdir}"/etc

  install -d "${pkgdir}"/var/cache/mpdscribble
    touch "${pkgdir}"/var/cache/mpdscribble/mpdscribble.cache
}

# vim:set ts=2 sw=2 et:
