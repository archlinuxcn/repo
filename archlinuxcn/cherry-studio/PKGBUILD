# Maintainer: Gavin Luo <lunt.luo@gmail.com>

pkgname=cherry-studio
_name="Cherry Studio"
pkgver=1.7.8
pkgrel=1
pkgdesc='ðŸ’ Cherry Studio is a desktop client that supports for multiple LLM providers'
arch=(x86_64)
url=https://github.com/CherryHQ/cherry-studio
license=(AGPL-3.0-only)
_electron=electron38
depends=("$_electron"
         'bash'
         'hicolor-icon-theme'
         'glibc'
         'gcc-libs'
         'python'
         'python-defusedxml'
         'python-numpy'
         'python-lxml'
         'python-six'
         'python-yaml'
         'python-pillow'
         'python-playwright')
optdepends=('ollama: Use your local LLM')
makedepends=('gendesk'
             'asar'
             'nodejs'
             'npm'
             'yarn'
             'python'
             'python-setuptools'
             'libcrypt.so=1')
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz")
b2sums=('9789059bb31d166ab1cf74e6ec296ee8f98f35505fa3d2a96904b7bc617b50c355499c1f72c4696f41db601a1e00a179b0454f0857093e5192f40fe5f67c3e8c')

prepare() {
    gendesk -q -f -n \
        --pkgname="${pkgname}" \
        --pkgdesc="A powerful AI assistant for producer." \
        --categories="Utility" \
        --name="${_name}" \
        --exec="/usr/bin/${pkgname} --no-sandbox %U" \
        --custom="StartupWMClass=${_name/ /}" \
        --mimetypes="x-scheme-handler/cherrystudio"

    cat >"${pkgname}.sh" <<EOD
#!/bin/bash
XDG_CONFIG_HOME=\${XDG_CONFIG_HOME:-~/.config}
FLAT_FILE=\${XDG_CONFIG_HOME}/${pkgname}-flags.conf

# Allow users to override command-line options
if [[ -f \${FLAT_FILE} ]]; then
    mapfile -t _USER_FLAGS < <(sed 's/#.*//' "\${FLAT_FILE}" | tr '\n' ' ')
fi

exec /usr/bin/${_electron} /usr/lib/${pkgname}/app.asar "$@" "\${_USER_FLAGS[@]}"
EOD
}

build() {
    cd "$pkgname-$pkgver"

    local electronVer="$(</usr/lib/${_electron}/version)"
    export NODE_ENV=production
    export ELECTRON_SKIP_BINARY_DOWNLOAD=1
    HOME="$srcdir/.electron-gyp" yarn install --frozen-lockfile
    yarn build
    yarn run electron-builder --dir \
        -c.electronDist=/usr/lib/"$_electron" \
        -c.electronVersion=${electronVer}

    find ./dist/linux-unpacked/resources/app.asar.unpacked \
        -type d \( -name darwin \) -print -exec rm -r {} +
}

package() {
    install -Dm644 "${pkgname}.desktop" -t "${pkgdir}/usr/share/applications"
    install -Dm755 "${pkgname}.sh"         "${pkgdir}/usr/bin/${pkgname}"

    cd "${srcdir}/${pkgname}-${pkgver}"
    install -Dm644 "build/logo.png" "$pkgdir/usr/share/icons/${pkgname}.png"

    cd "${srcdir}/${pkgname}-${pkgver}/dist/linux-unpacked/resources"
    install -Dm644                 app.asar            -t "${pkgdir}/usr/lib/${pkgname}/"
    cp -Pr --no-preserve=ownership app.asar.unpacked      "${pkgdir}/usr/lib/${pkgname}/"
    cp -Pr --no-preserve=ownership claude-code-plugins    "${pkgdir}/usr/lib/${pkgname}/"
}
