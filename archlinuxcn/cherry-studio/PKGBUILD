# Maintainer: Gavin Luo <lunt.luo@gmail.com>

pkgname=cherry-studio
_name="Cherry Studio"
pkgver=1.7.15
pkgrel=2
pkgdesc='ðŸ’ Cherry Studio is a desktop client that supports for multiple LLM providers'
arch=(x86_64)
url=https://github.com/CherryHQ/cherry-studio
license=(AGPL-3.0-only)
_electron=electron38
depends=("$_electron"
         'bash'
         'glibc'
         'gcc-libs'
         'python'
         'python-defusedxml'
         'python-numpy'
         'python-lxml'
         'python-six'
         'python-yaml'
         'python-pillow'
         'python-playwright')
optdepends=('ollama: Use your local LLM')
makedepends=('npm' 'pnpm' 'python' 'libcrypt.so=1')
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz")
b2sums=('430b4393dda6bb5cc6ceb0bfdf220070cac99fd02eacc9e2708313c24889da2c527062a150eca985f47eadc8e2387fbf21507bb3f336166e6f268a20389fd4ec')

prepare() {
    cat >"${pkgname}.desktop" <<EOD
[Desktop Entry]
Name=${_name}
Exec=/usr/bin/${pkgname} --no-sandbox %U
Terminal=false
StartupNotify=false
Type=Application
Icon=${pkgname}
StartupWMClass=${_name/ /}
Comment=A powerful AI assistant for producer.
MimeType=x-scheme-handler/cherrystudio;
Categories=Utility;
EOD

    cat >"${pkgname}.sh" <<EOD
#!/bin/bash
XDG_CONFIG_HOME=\${XDG_CONFIG_HOME:-~/.config}
FLAT_FILE=\${XDG_CONFIG_HOME}/${pkgname}-flags.conf

# Allow users to override command-line options
if [[ -f \${FLAT_FILE} ]]; then
    mapfile -t _USER_FLAGS < <(sed 's/#.*//' "\${FLAT_FILE}" | tr '\n' ' ')
fi

exec /usr/bin/${_electron} /usr/lib/${pkgname}/app.asar "$@" "\${_USER_FLAGS[@]}"
EOD
}

build() {
    cd "$pkgname-$pkgver"

    local electronVer="$(</usr/lib/${_electron}/version)"
    npm pkg set devDependencies.electron=${electronVer}

    export ELECTRON_SKIP_BINARY_DOWNLOAD=1
    export NPM_CACHE_DIR=${srcdir}/.npmcache
    HOME="$srcdir/.electron-gyp" pnpm install --ignore-scripts

    export NODE_ENV=production
    export ELECTRON_DIST=/usr/lib/"$_electron"
    export ELECTRON_VERSION=${electronVer}
    pnpm build:unpack

    find ./dist/linux-unpacked/resources/app.asar.unpacked \
        -type d \( -name darwin \) -print -exec rm -r {} +
}

package() {
    install -Dm644 "${pkgname}.desktop" -t "${pkgdir}/usr/share/applications"
    install -Dm755 "${pkgname}.sh"         "${pkgdir}/usr/bin/${pkgname}"

    cd "${srcdir}/${pkgname}-${pkgver}"
    install -Dm644 "build/logo.png" "$pkgdir/usr/share/icons/${pkgname}.png"

    cd "${srcdir}/${pkgname}-${pkgver}/dist/linux-unpacked/resources"
    install -Dm644                 app.asar            -t "${pkgdir}/usr/lib/${pkgname}/"
    cp -Pr --no-preserve=ownership app.asar.unpacked      "${pkgdir}/usr/lib/${pkgname}/"
    cp -Pr --no-preserve=ownership claude-code-plugins    "${pkgdir}/usr/lib/${pkgname}/"
}
