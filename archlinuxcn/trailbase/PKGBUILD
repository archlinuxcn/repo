# Maintainer: everyx <lunt.luo@gmail.com>

pkgname=trailbase
pkgdesc='A blazingly fast, open-source application server with type-safe APIs, built-in WebAssembly runtime, realtime, auth, and admin UI built on Rust, SQLite & Wasmtime.'
pkgver=0.22.9
pkgrel=1
url='https://trailbase.io/'
arch=('x86_64')
license=("OSL-3.0")

depends=('gcc-libs' 'glibc')
makedepends=('git' 'cargo' 'pnpm' 'openssl' 'sqlite' 'clang' 'protobuf' 'zstd')
_repo='https://github.com/trailbaseio/trailbase'
source=("git+${_repo}.git#tag=v${pkgver}")
b2sums=('7f607c6e7210de9f1d077a29694d86a956424f3d8e84fdc627b13433f2f621190830f0eaaca0edb190683e559f8cbf4397baba643ccf3f183d053a648a6f15eb')

prepare() {
    cd ${srcdir}/${pkgname}
    git submodule update --init --recursive
    pnpm install

    cd "${srcdir}/${pkgname}/crates/cli"
    export RUSTUP_TOOLCHAIN=stable
    cargo fetch --locked --target $(rustc --print host-tuple)
}

build(){
    cd "${srcdir}/${pkgname}/crates/cli"

    export RUSTUP_TOOLCHAIN=stable
    export CARGO_TARGET_DIR="${srcdir}/target"
    export PNPM_OFFLINE=TRUE
    # Add -ffat-lto-objects to CFLAGS to fix building with LTO
    export CFLAGS+=' -ffat-lto-objects'
    export CARGO_PROFILE_RELEASE_LTO=fat
    export CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1
    cargo build --frozen --release --bin trail
}

package() {
    install -Dm0755 ${srcdir}/target/release/trail -t "$pkgdir/usr/bin/" 
    install -Dm0644 ${srcdir}/${pkgname}/LICENSE   -t "$pkgdir/usr/share/licenses/$pkgname"
}
