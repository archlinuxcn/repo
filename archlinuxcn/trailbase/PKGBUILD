# Maintainer: everyx <lunt.luo@gmail.com>

pkgname=trailbase
pkgdesc='A blazingly fast, open-source application server with type-safe APIs, built-in WebAssembly runtime, realtime, auth, and admin UI built on Rust, SQLite & Wasmtime.'
pkgver=0.22.13
pkgrel=1
url='https://trailbase.io/'
arch=('x86_64')
license=("OSL-3.0")

depends=('gcc-libs' 'glibc')
makedepends=('git' 'cargo' 'pnpm' 'openssl' 'sqlite' 'clang' 'protobuf' 'zstd')
_repo='https://github.com/trailbaseio/trailbase'
source=("git+${_repo}.git#tag=v${pkgver}")
b2sums=('539afa94c884ade3beb4fb553aca691e4bb86ed0eed5746f4fb441e66791131e4c3e23e80800a6a910eba4735fdfb77538c8cb7ab0416c46e367f67771a41a98')

prepare() {
    cd ${srcdir}/${pkgname}
    git submodule update --init --recursive
    pnpm install

    cd "${srcdir}/${pkgname}/crates/cli"
    export RUSTUP_TOOLCHAIN=stable
    cargo fetch --locked --target $(rustc --print host-tuple)
}

build(){
    cd "${srcdir}/${pkgname}/crates/cli"

    export RUSTUP_TOOLCHAIN=stable
    export CARGO_TARGET_DIR="${srcdir}/target"
    export PNPM_OFFLINE=TRUE
    # Add -ffat-lto-objects to CFLAGS to fix building with LTO
    export CFLAGS+=' -ffat-lto-objects'
    export CARGO_PROFILE_RELEASE_LTO=fat
    export CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1
    cargo build --frozen --release --bin trail
}

package() {
    install -Dm0755 ${srcdir}/target/release/trail -t "$pkgdir/usr/bin/" 
    install -Dm0644 ${srcdir}/${pkgname}/LICENSE   -t "$pkgdir/usr/share/licenses/$pkgname"
}
