# Maintainer: Moeta Yuko <loli at yuko dot moe>

_pkgname=fcitx5-hallelujah
pkgname=$_pkgname-git
pkgver=r20.d5c544a
pkgrel=1
pkgdesc='Fcitx5 port of HallelujahIM'
arch=('x86_64' 'aarch64')
url='https://github.com/fcitx-contrib/fcitx5-hallelujah'
license=('LGPL-2.1-or-later')
depends=('fcitx5' 'marisa' 'fmt' 'json-c' 'glibc' 'gcc-libs')
makedepends=('git' 'extra-cmake-modules')
provides=("$_pkgname")
conflicts=("$_pkgname")
source=("$_pkgname::git+$url.git")
sha256sums=('SKIP')

pkgver() {
	cd $_pkgname
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	cd $_pkgname
	cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib .
	make
}

package() {
	cd $_pkgname
	make DESTDIR="$pkgdir" install
}
