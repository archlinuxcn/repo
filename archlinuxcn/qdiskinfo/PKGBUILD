# Maintainer: Integral <integral@member.fsf.org>

pkgname=qdiskinfo
_pkgname=QDiskInfo
pkgver=0.4
pkgrel=2
pkgdesc="CrystalDiskInfo alternative for Linux"
arch=("x86_64")
url="https://github.com/edisionnano/${_pkgname}"
license=("GPL-3.0-only")
depends=(
	"qt6-base"
	"qt6-svg"
	"smartmontools"
	"polkit"
	"hicolor-icon-theme"
)
makedepends=("cmake")
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/${pkgver}.tar.gz")
sha256sums=('33762f494f2da4b59e770207ad5bacca4394774c76509c15d3e3fa23fbf76d33')

build() {
	cmake -B build \
		-S "${_pkgname}-${pkgver}" \
		-DCMAKE_BUILD_TYPE=None \
		-DCMAKE_INSTALL_PREFIX=/usr

	cmake --build build
}

package() {
	DESTDIR="${pkgdir}" cmake --install build
	install -Dm644 "${_pkgname}-${pkgver}/LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
