# Maintainer: RocketDev <ma2014119@outlook.com>

_pkgbase=pbtk
pkgname=$_pkgbase-git
pkgver=1.0.7.r0.ge8a90a4
pkgrel=1
pkgdesc='A toolset for reverse engineering and fuzzing Protobuf-based apps'
url="https://github.com/marin-m/$_pkgbase"
arch=('any')
license=('GPL-3.0-only')
depends=(
    python
    pyside6
    python-protobuf
    python-requests
    python-websocket-client
    qt6-webengine
    protobuf
    sh
)
optdepends=(
    'java-runtime: jar extractor'
    'jad: jar extractor'
    'chromium: web extractor'
)
makedepends=(
    git
    python-build
    python-setuptools
    python-installer
    python-wheel
)
source=(
    "$pkgname::git+$url.git"
    use-system-binaries.patch
)
b2sums=('SKIP'
        'a5533e09847e7c7722c676a1c0ebc22c116cb649cf86aca0c61b8cf3351c5f56cdfe18fa470a574cd304a932bae3c6e0212dff941cbab684869bd4e4df8cab34')

pkgver() {
    cd "$pkgname"
    git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    cd "$pkgname"
    patch -p1 < ../use-system-binaries.patch
}

build() {
    cd "$pkgname"
    python -m build --wheel --no-isolation
}

package() {
    cd "$pkgname"
    python -m installer --destdir="$pkgdir" dist/*.whl

    pushd "$pkgdir$(python -c 'import site; print(site.getsitepackages()[0])')/$_pkgbase"
    rm -r utils/external/jad utils/external/protoc utils/external/dex2jar/*.bat
    popd

    install -Dm 0644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
}
