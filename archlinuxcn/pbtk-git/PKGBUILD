# Maintainer: RocketDev <ma2014119@outlook.com>

pkgname=pbtk-git
pkgver=1.0.5.r14.g5721302
pkgrel=1
pkgdesc='A toolset for reverse engineering and fuzzing Protobuf-based apps'
url='https://github.com/marin-m/pbtk'
arch=('any')
license=('GPL-3.0-only')
depends=(
    'python'
    'python-pyqt5'
    'python-protobuf'
    'python-requests'
    'python-websocket-client'
    'qt5-webengine'
    'python-pyqtwebengine'
    'chromium'
    'java-runtime'
    'bash'
    'jad'
    'protobuf'
)
makedepends=(
    'git'
)
source=(
    "$pkgname::git+https://github.com/marin-m/pbtk"
    use-system-binaries.patch
)
b2sums=('SKIP'
        '6a2115edd6af10f929ea477f4dbb9f5018d649ef7b8b8b0993245bb0af8052ea31d4014de0f18946252606441d002b00fcdb0fc2006fbeef209499673ab42a36')

pkgver() {
    cd "$pkgname"
    git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    cat > pbtk.sh << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec /usr/bin/python gui.py "\$@"
EOF
    cd "$pkgname"
    patch -p1 < ../use-system-binaries.patch
}

build() {
    cd "$pkgname"
    rm -r utils/external/jad utils/external/protoc utils/external/dex2jar/*.bat
    chmod 0644 *.py */*.py
    chmod 0755 utils/external/dex2jar/*.sh
}

package() {
    install -Dm 0755 pbtk.sh "$pkgdir/usr/bin/pbtk"
    cd "$pkgname"
    install -Dm 0755 -t "$pkgdir/usr/share/$pkgname" gui.py
    cp -r utils extractors views "$pkgdir/usr/share/$pkgname"
    install -Dm 0644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
}
