# Maintainer: Coelacanthus <CoelacanthusHex@gmail.com>
# Contributor: Fancy Zhang <springzfx@gmail.com>

pkgname=cgproxy-git
pkgver=0.20.r0.g86fe42e
pkgrel=3
pkgdesc="A transparent proxy program powered by cgroup2 and tproxy"
arch=('x86_64')
url="https://github.com/springzfx/cgproxy"
license=('GPL-2.0-only')
makedepends=('git' 'cmake' 'ninja' 'nlohmann-json' 'clang' 'bpf')
depends=('libbpf' 'libbpf.so' 'iproute2' 'which' 'glibc' 'gcc-libs' 'bash')
provides=('cgproxy')
conflicts=('cgproxy')
backup=('etc/cgproxy/config.json')

source=("${pkgname}::git+https://github.com/springzfx/cgproxy#branch=master")
b2sums=('SKIP')

pkgver() {
  cd "$pkgname"
  ( set -o pipefail
    git describe --long --tags 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/v//' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

prepare() {
  cd "${srcdir}/${pkgname}"
  # PR 52
  git cherry-pick -n d7990c0c2f1a1add5f863d35c670ec6aa720f1d3^..0b2c9a4c8264c2c4464ac38b12a60b96adf364f6
  # PR 53
  #git cherry-pick -n 3e68415864bacfe7fdbb73c08f403f867b440253^..4a76b6834a04bd28d4a16651ac05891aad48a615
}

build(){
  cd "${srcdir}/${pkgname}"

  cmake -S . -B build \
    -G Ninja \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -Dbuild_execsnoop_dl=ON \
    -Dbuild_static=OFF
  ninja -C build
}

package_cgproxy-git(){
  cd "${srcdir}/$pkgname"
  DESTDIR=$pkgdir ninja -C build install
}


