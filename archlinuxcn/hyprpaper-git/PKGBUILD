# Maintainer: ThatOneCalculator <kainoa@t1c.dev>

pkgname=hyprpaper-git
_pkgname="${pkgname%-git}"
pkgver=0.7.1.r4.g1c18ad6
pkgrel=1
pkgdesc="A blazing fast wayland wallpaper utility with IPC controls."
arch=('x86_64' 'aarch64' 'riscv64')
url="https://github.com/hyprwm/${_pkgname}"
license=('BSD-3-Clause')
depends=(
	wayland
	libglvnd
	pango
	libjpeg
	libpng
	libwebp
	hyprlang
	hyprutils
)
makedepends=(
	git
	cmake
	ninja
	meson
	wayland-protocols
	xorgproto
	hyprwayland-scanner-git
)
source=("git+${url}.git")
conflicts=("${_pkgname}")
provides=("${_pkgname}")
sha256sums=('SKIP')
options=(!makeflags !buildflags !strip)

pkgver() {
	cd "${_pkgname}/"
	git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/v//'
}

build() {
	cd "${_pkgname}/"
	cmake --no-warn-unused-cli -DCMAKE_BUILD_TYPE:STRING=Release -DCMAKE_INSTALL_PREFIX:PATH=/usr -S . -B ./build
	cmake --build ./build --config Release --target "${_pkgname}"
}

package() {
	cd "${_pkgname}/"
	DESTDIR="${pkgdir}" cmake --install ./build
	install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
	install -Dm644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
