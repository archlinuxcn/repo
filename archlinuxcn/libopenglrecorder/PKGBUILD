# Maintainer:  Daniel Mensinger daniel@mensinger-ka.de
# Contributor: Daniel Mensinger daniel@mensinger-ka.de

pkgname=libopenglrecorder
pkgver=0.1.0
pkgrel=5
pkgdesc="A library allowing optional async readback OpenGL frame buffer with optional audio recording"
arch=('i686' 'x86_64')
url="https://github.com/Benau/libopenglrecorder"
license=('BSD')
groups=()
depends=('libjpeg-turbo' 'libvpx' 'openh264' 'libpulse')
makedepends=('git' 'cmake' 'make')
optdepends=()
provides=('libopenglrecorder')
install=
source=("${url}/archive/v${pkgver}.tar.gz")
sha256sums=('a90a99c23f868636f77003a8dc6ffe6c3699fc2759c47df5dbd44ff8b42d2e4f')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  [ -d build ] && rm -rf build
  mkdir build
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}/build"

  cmake \
    -DBUILD_SHARED_LIBS=ON          \
    -DBUILD_WITH_VPX=ON             \
    -DBUILD_WITH_H264=ON            \
    -DBUILD_RECORDER_WITH_SOUND=ON  \
    -DCMAKE_INSTALL_PREFIX=/usr     \
    -DCMAKE_INSTALL_LIBDIR=lib      \
    -DCMAKE_BUILD_TYPE=Release      \
    ..

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  cd build
  make DESTDIR="${pkgdir}" install
}
