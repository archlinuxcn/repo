# Maintainer : Xeonacid <h.dwwwwww@gmail.com>
# Maintainer : Leon MÃ¶ller <jkhsjdhjs at totally dot rip>
# Contributor: Asger Hautop Drewsen <asgerdrewsen@gmail.com>

pkgname='perl-compress-raw-lzma'
pkgver=2.217
pkgrel=1
pkgdesc="Low-Level Perl Interface to lzma compression library"
_dist=Compress-Raw-Lzma
arch=('i686' 'x86_64')
url="https://metacpan.org/release/$_dist"
license=('GPL-1.0-or-later' 'Artistic-1.0-Perl')
options=('!emptydirs' 'purge')
depends=('glibc' 'perl' 'perl-pod-markdown' 'xz')
source=("https://cpan.metacpan.org/authors/id/P/PM/PMQS/$_dist-$pkgver.tar.gz")
md5sums=('bd368c8ab5fb221d498415220e1a3701')
sha512sums=('96ac659782d731e2fd8d53a265a9560e03fcecbcf136553d23cf6f5f378df0c3b3ef64959dc3942f8201b9a6aed1a9c77541bcb8727c6ebc536288c75bc9366b')

build() {
  cd $_dist-$pkgver
  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  /usr/bin/perl Makefile.PL
  make
}

check() {
  cd $_dist-$pkgver
  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1
  make test
}

package() {
  cd $_dist-$pkgver
  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
