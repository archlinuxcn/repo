# Maintainer : Xeonacid <h.dwwwwww@gmail.com>
# Maintainer : Leon MÃ¶ller <jkhsjdhjs at totally dot rip>
# Contributor: Asger Hautop Drewsen <asgerdrewsen@gmail.com>

pkgname='perl-compress-raw-lzma'
pkgver=2.219
pkgrel=1
pkgdesc="Low-Level Perl Interface to lzma compression library"
_dist=Compress-Raw-Lzma
arch=('i686' 'x86_64')
url="https://metacpan.org/release/$_dist"
license=('GPL-1.0-or-later' 'Artistic-1.0-Perl')
options=('!emptydirs' 'purge')
depends=('glibc' 'perl' 'perl-pod-markdown' 'xz')
source=("https://cpan.metacpan.org/authors/id/P/PM/PMQS/$_dist-$pkgver.tar.gz")
md5sums=('9436b79123eb8e0645ff8ee98d1a5214')
sha512sums=('b9c50707a6a1e0e04f79950bc9c118e759bb435734f066e88c191e44d7b8c22af13199817b97b593af413c43050e5a09fe5423d767124d410b1e5ff89d3a9ed1')

build() {
  cd $_dist-$pkgver
  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  /usr/bin/perl Makefile.PL
  make
}

check() {
  cd $_dist-$pkgver
  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1
  make test
}

package() {
  cd $_dist-$pkgver
  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
