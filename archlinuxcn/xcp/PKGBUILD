# Maintainer: Integral <integral@member.fsf.org>
# Contributor: tarball <bootctl@gmail.com>
# Contributor: √Årni Dagur <arnidg@protonmail.ch>

pkgname=xcp
pkgver=0.24.3
pkgrel=1
pkgdesc="An extended 'cp'"
arch=('i686' 'x86_64' 'armv7h' 'aarch64')
url="https://github.com/tarka/${pkgname}"
license=('GPL-3.0-only')
depends=('gcc-libs')
makedepends=('cargo')
source=("${url}/archive/${pkgname}-v${pkgver}.tar.gz")
sha256sums=('cbb70f85b9be693cfc3b6b6f2d5ec070ba4ebfcb5ea60fd49e9329e62afebaf3')

prepare() {
	cd "${pkgname}-${pkgname}-v${pkgver}/"
	cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
	cd "${pkgname}-${pkgname}-v${pkgver}/"
	cargo build --frozen --release --all-features
}

# check() {
# 	cd "${pkgname}-${pkgname}-v${pkgver}/"
# 	cargo test --frozen --all-features
# }

package() {
	cd "${pkgname}-${pkgname}-v${pkgver}/"
	install -Dm755 "target/release/${pkgname}" -t "${pkgdir}/usr/bin/"

	cd completions
	install -Dm644 "${pkgname}.bash" "$pkgdir/usr/share/bash-completion/completions/${pkgname}"
	install -Dm644 "${pkgname}.fish" -t "$pkgdir/usr/share/fish/vendor_completions.d/"
	install -Dm644 "${pkgname}.zsh" "$pkgdir/usr/share/zsh/site-functions/_${pkgname}"
}
