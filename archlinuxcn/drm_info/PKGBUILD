
pkgname=drm_info
pkgver=2.9.0
pkgrel=2
license=(MIT)
pkgdesc='Small utility to dump info about DRM devices'
makedepends=(meson scdoc git)
depends=("libdrm>=2.4.115" libdisplay-info json-c pciutils)
arch=(x86_64)
url=https://gitlab.freedesktop.org/emersion/drm_info
conflicts=(drm_info)
source=("source::git+https://gitlab.freedesktop.org/emersion/drm_info.git")
sha512sums=('SKIP')

build() {
	rm -rf build
	arch-meson build source -Dlibpci=enabled -Dman-pages=enabled
	meson compile -C build
}

check () {
	meson test -C build
}

package() {
	meson install -C build --destdir="${pkgdir}"
	install -vDm644 "source/LICENSE" \
		"${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
