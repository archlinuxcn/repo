
pkgname=drm_info
pkgver=2.9.0
pkgrel=3
license=(MIT)
pkgdesc='Small utility to dump info about DRM devices'
makedepends=(meson scdoc git)
depends=("libdrm>=2.4.115" libdisplay-info json-c pciutils)
arch=(x86_64)
url=https://gitlab.freedesktop.org/emersion/drm_info
conflicts=(drm_info)
source=("source::git+https://gitlab.freedesktop.org/emersion/drm_info.git#tag=v${pkgver}")
sha512sums=('466023037d2f8ac502f00cec64beaa50124e6aff3189563fa3d8cc126526b8b8143b5fdbbf0ecfaa266cafc0e01819ef3d35fdbbfe7e60b179d227b8e9f84a85')

build() {
	rm -rf build
	arch-meson build source -Dlibpci=enabled -Dman-pages=enabled
	meson compile -C build
}

check () {
	meson test -C build
}

package() {
	meson install -C build --destdir="${pkgdir}"
	install -vDm644 "source/LICENSE" \
		"${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
