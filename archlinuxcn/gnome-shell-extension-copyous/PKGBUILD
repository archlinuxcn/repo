# Maintainer: Gavin Luo <lunt.luo@gmail.com>

pkgname="gnome-shell-extension-copyous"
_uuid=copyous@boerdereinar.dev
pkgver=1.2.0
pkgrel=1
pkgdesc="Modern Clipboard Manager for GNOME"
arch=('any')
url="https://github.com/boerdereinar/copyous"
license=('GPL-3.0+')
depends=('gnome-shell>=48'
         'libgda6'
         'gsound')
makedepends=('pnpm' 'git')
source=("${pkgver}::git+${url}.git#tag=v${pkgver}")
b2sums=('5da8238cd6c2cb1ce7971f021756a22072ac51dbf90e0aaf1c948d3cb8ae41b1c66691d5d0c791d4f22fed9c8d16f31e586b849a7e3d425a1e65a96ba2060d5e')

prepare() {
  cd "${srcdir}/${pkgver}"

  git submodule update --init --recursive --depth=1
}

build() {
  cd "${srcdir}/${pkgver}"

  pnpm install --ignore-scripts
  env RELEASE=1 make build
}

package() {
  cd "${srcdir}/${pkgver}"

  local extdir="${pkgdir}"/usr/share/gnome-shell/extensions/"${_uuid}"

  install -dm755 "${extdir}"

  bsdtar xf "dist/"*.zip -C "${extdir}"

  install -Dm644 "${extdir}"/schemas/*.gschema.xml -t "${pkgdir}"/usr/share/glib-2.0/schemas/
  rm -rf "${extdir}"/schemas

  mv "${extdir}/locale" "${pkgdir}"/usr/share/
}
