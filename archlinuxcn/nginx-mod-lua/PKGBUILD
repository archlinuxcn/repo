# Maintainer: lilydjwg <lilydjwg@gmail.com>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# from: github
# what: openresty/lua-nginx-module
# match! rc[0-9]*$

pkgname=nginx-mod-lua
_modname="${pkgname#nginx-mod-}"
pkgver=0.10.29
pkgrel=1
_ndkver=0.3.4
pkgdesc='Lua script engine module for nginx'
arch=('x86_64')
depends=("nginx=1.28.0" 'luajit' 'lua-resty-core' 'pcre')
makedepends=('nginx-src')
url="https://github.com/openresty/lua-nginx-module"
license=('BSD-2-Clause')
source=(https://github.com/openresty/$_modname-nginx-module/archive/v$pkgver/$_modname-$pkgver.tar.gz
        https://github.com/simpl/ngx_devel_kit/archive/v$_ndkver/ngx_devel_kit-$_ndkver.tar.gz)
sha256sums=('ca2c2122b909529bf9d1a89e9a5763835a2bd2629def8cb279c550f638f0a78f'
            '14a28063294f645d457b1eb10e3c23bbba44398f1c5f021421b58b6f8ab31662')
provides=(nginx-mod-ndk=$_ndkver)
conflicts=(nginx-mod-ndk)

prepare() {
	mkdir -p build
	cd build
	ln -sf /usr/src/nginx/auto
	ln -sf /usr/src/nginx/src
}

build() {
	cd build
	export LUAJIT_INC=$(pkg-config luajit --variable=includedir)
	export LUAJIT_LIB=$(pkg-config luajit --variable=libdir)
	/usr/src/nginx/configure \
		--with-compat \
		--with-ld-opt='-lpcre -Wl,-E'  \
		--add-dynamic-module=../ngx_devel_kit-$_ndkver \
		--add-dynamic-module=../$_modname-nginx-module-$pkgver
	make modules
}

package() {
	cd build/objs
	for mod in *.so; do
		install -Dm755 $mod "$pkgdir"/usr/lib/nginx/modules/$mod
	done
        install -dm0755 "$pkgdir"/etc/nginx/modules.d
        echo 'load_module "/usr/lib/nginx/modules/ndk_http_module.so";' >> "$pkgdir/etc/nginx/modules.d/20-ndk.conf"
        echo 'load_module "/usr/lib/nginx/modules/ngx_http_lua_module.so";' >> "$pkgdir/etc/nginx/modules.d/30-lua.conf"
}
