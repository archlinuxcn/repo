# Contributor: Simo Huhtiranta <simo_huhtirantaATpp_inet_fi>  
# Contributor: Tilmann Becker <tilmann.becker@web.de>
# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=scribus-svn
pkgver=24852
pkgrel=1
pkgdesc="A desktop publishing program - Version from SVN"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL')
url="http://www.scribus.net"
depends=(boost-libs cairo fontconfig freetype2 harfbuzz-icu
         hunspell lcms2 libcdr libcups libfreehand libjpeg libmspub
         libpagemaker libpng libqxp librevenge libtiff libvisio libxml2 libzmf
         openscenegraph openssl podofo poppler python3 qt6-5compat zlib graphicsmagick)
makedepends=('subversion' 'cmake' 'qt6-tools' 'boost')
optdepends=('lib2geom: for mesh distortion')
conflicts=('scribus')
provides=('scribus')
source=('scribus::svn://scribus.net/trunk')
sha256sums=('SKIP')
_svnmod='scribus'

pkgver() {
  cd ${_svnmod}
  local ver="$(svnversion)"
  printf "%s" "${ver//[[:alpha:]]}"
}

prepare() {
  cd $_svnmod/Scribus
  # fix "error: reference to 'byte' is ambiguous" with C++17 mode
  sed -i 's/\bbyte\b/prc_&/g' scribus/third_party/prc/oPRCFile.cc
}

build() {
  cd $_svnmod/Scribus
  cmake . -DCMAKE_INSTALL_PREFIX:PATH=/usr \
	-DWANT_GRAPHICSMAGICK:BOOL=YES \
	-DCMAKE_LIBRARY_PATH:PATH=/usr/lib \
    -DCMAKE_SKIP_RPATH=ON \
	-DCMAKE_BUILD_WITH_INSTALL_RPATH:BOOL=FALSE \
    -DWANT_HUNSPELL=ON \
    -DWITH_BOOST=ON \
    -DWITH_PODOFO=ON \
    -DWANT_CPP17=ON \
	-DWANT_SVNVERSION:BOOL=YES
  make
}

package () {
  cd "$srcdir"/$_svnmod/Scribus
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
  install -Dm644 scribus.desktop $pkgdir/usr/share/applications/scribus.desktop
  for i in 16x16 32x32 128x128 256x256 512x512 1024x1024
  do
    install -Dm644 resources/iconsets/artwork/icon_${i}.png "${pkgdir}"/usr/share/icons/hicolor/${i}/apps/scribus.png
  done
  # Use system hyphen
  rm -rf "${pkgdir}"/usr/share/scribus/dicts/hyph
  ln -sf /usr/share/hyphen "${pkgdir}"/usr/share/scribus/dicts/hyph
}
