# Maintainer: Butui Hu <hot123tea123@gmail.com>

pkgname=python-medpy
_pkgname=medpy
pkgver=0.4.0
pkgrel=8
pkgdesc='Medical image processing in Python'
arch=('x86_64')
url='https://github.com/loli/medpy'
license=('GPL')
depends=(
  boost-libs
  python-numpy
  python-scipy
  python-simpleitk
)
makedepends=(
  boost
  python-setuptools
)
checkdepends=(
  python-nose
)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/loli/medpy/archive/${pkgver}.tar.gz")
sha512sums=('539570d99fc786b5c27f76046999b9d87e343f32c7442b34bc49c56831d2e93efe13a72f1f3577f96e8026ad4d4380994ede7882c9219d6ef8f5673bdb2ab91e')

build() {
  cd "${_pkgname}-${pkgver}"
  python setup.py build
  python setup.py build_ext --inplace
}

check() {
  local python_version=$(python -c 'import sys; print("".join(map(str, sys.version_info[:2])))')
  cd "${_pkgname}-${pkgver}"
  PYTHONPATH="${PWD}/build/lib.linux-${CARCH}-cpython-${python_version}" nosetests -v
}

package() {
  cd "${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
