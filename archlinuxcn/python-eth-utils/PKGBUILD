# Maintainer: Xeonacid <h.dwwwwww@gmail.com>

_name=eth-utils
pkgname=python-${_name}
pkgver=5.2.0
pkgrel=1
pkgdesc="Utility functions for working with ethereum related codebases."
arch=(any)
url="https://github.com/ethereum/${_name}"
license=(MIT)
depends=(python python-eth-hash python-eth-typing python-cytoolz)
makedepends=(python-build python-installer python-setuptools python-wheel python-sphinx python-sphinx_rtd_theme)
checkdepends=(python-pytest mypy python-hypothesis)
source=(${_name}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz)
sha512sums=('312e0b5b5b51df74b0a26220008c98b5c19e77e56228a29ad84b80f8c0ad1bb38feb41d185f6393a2c76df69a8e0f3e7bca9cfc1ae97e0675f6aa8bb2cd74fdf')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
  make -C docs man
}

check(){
  cd $_name-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest -vv --showlocals tests/
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 docs/_build/man/eth_utils.1 -t "$pkgdir/usr/share/man/man1"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
