pkgname=mosh-git
epoch=1
pkgver=1.4.0.r19.g1105d48
pkgrel=23
pkgdesc='Mobile shell, surviving disconnects with local echo and line editing. Git version'
arch=(x86_64 aarch64)
url='https://mosh.org/'
license=(GPL-3.0-or-later)
depends=(libutempter openssh openssl perl-io-tty protobuf)
makedepends=(git)
conflicts=(mosh)
provides=(mosh)
options=(!emptydirs)
source=("git+https://github.com/mobile-shell/mosh"
        https://github.com/mobile-shell/mosh/pull/1292.diff)
sha512sums=('SKIP'
            '10cda1d4601985e059cebdbfbb03b7e0cc042c00d912f8c58ef8bc7425b243a3e8a29db6449fa57b17a1a3fcd51eb093ceb0c579802d337094b789d42856d58b')

pkgver() {
  cd "$srcdir/mosh"
  git describe | sed -E 's/([^-]*-g)/r\1/;s/-/./g;s/^mosh\.//'
}

prepare() {
  cd "$srcdir/mosh"
  patch -Np1 < ../1292.diff
}

build() {
  cd "$srcdir/mosh"
  ./autogen.sh
  ./configure --enable-ufw --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "$srcdir/mosh"
  DESTDIR="$pkgdir" make install
  install -Dm644 "conf/bash-completion/completions/mosh" \
    "$pkgdir/usr/share/bash-completion/completions/mosh"
}
