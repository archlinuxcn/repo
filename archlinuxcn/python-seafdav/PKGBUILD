# Maintainer: Klemens Sch√∂lhorn <klemens@schoelhorn.eu>

pkgname=python-seafdav
pkgver=7.1.4
pkgrel=1
pkgdesc="Seafile webdav server"
arch=('any')
url="https://github.com/haiwen/seafdav"
license=('MIT')
depends=(python
         python-seafobj
         python-defusedxml
         python-six
         python-jinja
         python-yaml
         python-jsmin
         python-sqlalchemy)
makedepends=('python-setuptools')
source=("https://github.com/haiwen/seafdav/archive/v${pkgver}-server.tar.gz"
        0001-Log-gunicorn-output.-Increase-worker-count-and-timeo.patch)
sha256sums=('43f5201658c862e9c32dc6617d5045fe47d099e637e4ac15d0725f7640f912fe'
            '255e2daf094e1910520d2b564972c3903e740911a9fb27b4a5365473c699a5d9')

prepare() {
  cd seafdav-$pkgver-server

  patch -Np1 < ../0001-Log-gunicorn-output.-Increase-worker-count-and-timeo.patch
}

package() {
  cd seafdav-$pkgver-server
  python ./setup.py install --root="$pkgdir/" --optimize=1 \
         --install-lib "usr/share/seafile-server/seahub/thirdpart" \
         --install-scripts "usr/share/seafile-server/seahub/bin"

  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
