# Maintainer: Butui Hu <hot123tea123@gmail.com>
# Contributor: Leo Mao <leomaoyw at gmail dot com>

pkgname=python-fastrlock
_pkgname=fastrlock
pkgver=0.8.3
pkgrel=1
pkgdesc='A fast RLock implementation for CPython'
url='https://github.com/scoder/fastrlock'
arch=('x86_64')
license=('MIT')
depends=(
  glibc
)
makedepends=(
  cython
  python-setuptools
)
checkdepends=(
  python-pytest
)
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/scoder/fastrlock/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('ea2196b118d8ddcdc899171b56b725e5a7fcc6fb8a3427c0005c793e85c308ed8c9d8f8e04ab682c94567f9a3b4e32ebff9d226aa223db552ccfbb2758c8ab07')

build() {
  cd "${_pkgname}-${pkgver}"
  python setup.py build
  python setup.py build_ext --inplace
}

check() {
  local python_version=$(python -c 'import sys; print("".join(map(str, sys.version_info[:2])))')
  cd "${_pkgname}-${pkgver}"
  PYTHONPATH="${PWD}/build/lib.linux-${CARCH}-cpython-${python_version}" pytest -v
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
# vim:set ts=2 sw=2 et:
