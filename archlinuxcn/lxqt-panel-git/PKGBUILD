# Maintainer:  Peter Mattern <pmattern@arcor.de>
# Contributor: Yen Chi Hsuan <yan12125@gmail.com>
# Contributor: Jerome Leclanche <jerome@leclan.ch>

_pkgname=lxqt-panel
pkgname=$_pkgname-git
pkgver=1.2.0.12.gf26cba96
pkgrel=1
pkgdesc='The LXQt desktop panel'
arch=('i686' 'x86_64')
url='https://github.com/lxqt/lxqt-panel'
license=('GPL2')
depends=("libxtst"
      "alsa-lib" "libpulse" "lm_sensors" "libstatgrab" "libsysstat-git" "libxtst"
      "solid"
      "menu-cache" "libxcomposite" "lxmenu-data" "libdbusmenu-qt5"
      "lxqt-globalkeys-git"
)
makedepends=("git" "cmake" "qt5-tools" "lxqt-build-tools-git")
provides=("$_pkgname")
conflicts=("$_pkgname")
source=("git+${url}.git")
sha256sums=('SKIP')

pkgver() {
      cd "$srcdir/$_pkgname"
      git describe --always | sed "s/-/./g"
}

build() {
      mkdir -p build
      cd build
      cmake "$srcdir/$_pkgname" \
            -DCMAKE_INSTALL_PREFIX=/usr \
            -DCMAKE_INSTALL_LIBDIR=/usr/lib
      make
}

package() {
      cd build
      make DESTDIR="$pkgdir" install
}
