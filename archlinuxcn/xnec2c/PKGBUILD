# Maintainer: Coelacanthus <uwu@coelacanthus.name>
# Contributor: Kyle Keen <keenerd@gmail.com>
# Contributor: Vitaliy Berdinskikh ur6lad[at]i.ua

pkgname=xnec2c
pkgver=4.4.17
pkgrel=1
pkgdesc="GTK+ Antenna EM Modeling Client"
arch=('i686' 'x86_64')
url="https://www.xnec2c.org/"
license=('GPL-2.0-or-later')
depends=(
  'cairo'
  'gdk-pixbuf2'
  'glib2'
  'glibc'
  'gtk3'
  'hicolor-icon-theme'
  'pango'
)
makedepends=('openblas' 'intel-oneapi-mkl')
optdepends=(
  'cblas: to use OpenBLAS'
  'gnuplot: to use plotted output/data files'
  'intel-oneapi-mkl: to use Intel MKL'
  'lapacke: to use OpenBLAS'
)
source=("$pkgname-$pkgver.tgz::https://github.com/KJ7LNW/xnec2c/archive/refs/tags/v$pkgver.tar.gz")
b2sums=('2a5527acc0d54d9a7daf0375086496235fd964281e89983dcd99a7142d948629ac82d39cccad354a8afb37fd96a43aee909bb4eeb37d5594d5ff4501a3dd894a')

prepare() {
  cd "$pkgname-$pkgver"
  ./autogen.sh
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr
    make
}

package() {
  depends+=(
    'libcairo.so'
    'libgdk_pixbuf-2.0.so'
    'libgio-2.0.so' 'libgobject-2.0.so' 'libglib-2.0.so' 'libgmodule-2.0.so'
    'libgtk-3.so' 'libgtk-3.so'
    'libpango-1.0.so' 'libpangocairo-1.0.so'
  )
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}
# vim:set ts=2 sw=2 et:
