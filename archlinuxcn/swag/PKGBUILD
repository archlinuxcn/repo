pkgname=swag
pkgver=2.0.0_rc5
pkgrel=1
pkgdesc='Automatically generate RESTful API documentation with Swagger 2.0 for Go.'
arch=('x86_64')
url="https://github.com/swaggo/swag"
license=('MIT')
makedepends=('go' git)
source=(source::"git+https://github.com/swaggo/swag#tag=v$(echo ${pkgver} | sed "s/_/-/g")")
sha256sums=('a5c678c87f19d755f2223fe89bad78aae01ccc0e515a055b1fc431dbf8446577')

build() {
  cd source
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -ldflags=-linkmode=external -mod=readonly -modcacherw"
  make build
}

check() {
  cd source
  make test
}

package() {
  cd source
  install -Dm755 swag "$pkgdir/usr/bin/swag"
}
