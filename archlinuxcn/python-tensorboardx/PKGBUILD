# Maintainer: Butui Hu <hot123tea123@gmail.com>

_pkgname=tensorboardX
pkgname=python-tensorboardx
pkgver=2.6.2.2
pkgrel=1
pkgdesc='Tensorboard for pytorch (and chainer, mxnet, numpy, ...)'
arch=('any')
url=https://github.com/lanpa/tensorboardX
license=(MIT)
depends=(python-numpy python-protobuf python-six)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-setuptools-scm
  python-wheel
)
checkdepends=(flake8 python-future python-matplotlib python-pytest python-pytorch python-torchvision)
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/lanpa/tensorboardX/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('219b7129fbe7bb6a6c53947a71b0d4bd3dc88739f9a9679c831b53c8d7c68875d39e6e036f8adf5632986ce6b8eb05671a43847cf86224068040a65c556f76ab')

prepare() {
  cd "${_pkgname}-${pkgver}"
  _ver=$(protoc --version|cut -d ' ' -f 2)
  # using protobuf from official repo
  sed -i "s/DESIRED_PROTO_VERSION=.*/DESIRED_PROTO_VERSION=${_ver}/" compile.sh
  # fix Deprecated config in `setup.cfg`
  sed -i "s/license_file /license_files /" setup.cfg
  # dirty hack to skip pip install command
  sed -i "s/pip install/echo/" setup.py
}


build() {
  cd "${_pkgname}-${pkgver}"
  SETUPTOOLS_SCM_PRETEND_VERSION=${pkgver} \
  python -m build --wheel --no-isolation
}

package() {
  cd "${_pkgname}-${pkgver}"
  SETUPTOOLS_SCM_PRETEND_VERSION=${pkgver} \
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
