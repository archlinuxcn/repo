# Maintainer: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix
# Contributor: Igor Dyatlov <dyatlov.igor@protonmail.com>

pkgname=upscaler
pkgver=1.5.1
pkgrel=1
pkgdesc="Upscale and enhance images"
arch=('x86_64')
url="https://gitlab.gnome.org/World/Upscaler"
license=(GPL-3.0-only)
depends=(libadwaita
         python-gobject
         python-pillow
         python-vulkan
         upscayl-ncnn
         python
         gtk4
         pango
         glib2)
makedepends=(git
             blueprint-compiler
             meson)
checkdepends=(appstream-glib)
source=("git+https://gitlab.gnome.org/World/Upscaler.git#tag=${pkgver}")
b2sums=('158b2d2cc470eae909a2c214bdb2414e35a45039dc21d1c72ab21041354819d85e8ec5e0781e65ba6dd27cfe05f0bd109abb091efbf18594a29a0fac046d0148')

build() {
  arch-meson Upscaler build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs || :
}

package() {
  meson install -C build --destdir "$pkgdir"
}
