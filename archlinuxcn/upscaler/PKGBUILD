# Maintainer: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix
# Contributor: Igor Dyatlov <dyatlov.igor@protonmail.com>

pkgname=upscaler
pkgver=1.6.0
pkgrel=1
pkgdesc="Upscale and enhance images"
arch=('x86_64')
url="https://gitlab.gnome.org/World/Upscaler"
license=(GPL-3.0-only)
depends=(libadwaita
         python-gobject
         python-pillow
         python-vulkan
         upscayl-ncnn
         python
         gtk4
         pango
         glib2)
makedepends=(git
             blueprint-compiler
             meson)
checkdepends=(appstream-glib)
source=("git+https://gitlab.gnome.org/World/Upscaler.git#tag=${pkgver}")
b2sums=('e944cf4e67ea5acb3645258d2aed7b9703eda01d1232a22913d860b588f73e82c01f4f042aa25ef92237fc9f9680dd49aca75f8034ebbca49243aa07209066fc')

build() {
  arch-meson Upscaler build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs || :
}

package() {
  meson install -C build --destdir "$pkgdir"
}
