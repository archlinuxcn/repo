# Maintainer: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix
# Contributor: Igor Dyatlov <dyatlov.igor@protonmail.com>

pkgname=upscaler
pkgver=1.6.1
pkgrel=1
pkgdesc="Upscale and enhance images"
arch=('x86_64')
url="https://gitlab.gnome.org/World/Upscaler"
license=(GPL-3.0-only)
depends=('libadwaita>=1.8.0'
         python-gobject
         python-pillow
         python-vulkan
         upscayl-ncnn
         python
         gtk4
         pango
         glib2)
makedepends=(git
             blueprint-compiler
             meson)
checkdepends=(appstream-glib)
source=("git+https://gitlab.gnome.org/World/Upscaler.git#tag=${pkgver}")
b2sums=('aa823497a9df7b7fa0ac84b61cc01f240c687c08ded6fc58ae781bd2a81940893ec30d4bc09d445401a697ab7a5e9177c236a371543ec2da10b25b7ff8b2dbc6')

build() {
  arch-meson Upscaler build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs || :
}

package() {
  meson install -C build --destdir "$pkgdir"
}
