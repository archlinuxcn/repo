pkgname=julia-git-enzyme_jll-src
pkgver=0.0.207+0
_commit=b9f6424046fb8d7d931a68ea3d064457cd02a1ae
pkgrel=1
pkgdesc="Enzyme_jll.jl"
url="https://github.com/JuliaBinaryWrappers/Enzyme_jll.jl.git"
arch=(aarch64 armv7h x86_64)
license=('MIT')
makedepends=(git julia-pkg-scripts llvm)
depends=(julia-git enzyme-julia)
source=("git+https://github.com/JuliaBinaryWrappers/Enzyme_jll.jl.git#commit=$_commit"
        jll.toml.in)
sha256sums=('0829e869372c468de1d9384f2c321d5fd2b119d2195eb15d94b7e249a58ddc29'
            '7fe88d60082a7d6de7f775f36bef4247c04be3c9bd3f3c923851ebcf5ef86a84')

build() {
  _llvm_ver=$(llvm-config --version)
  [[ $_llvm_ver =~ ^([0-9]*).* ]]
  llvm_maj_ver=${BASH_REMATCH[1]}

  sed -e "s/@LLVM_VER@/${llvm_maj_ver}/g" jll.toml.in > jll.toml

  cd Enzyme_jll.jl

  julia /usr/lib/julia/julia-gen-jll.jl Enzyme_jll ../jll.toml
}

package() {
  cd Enzyme_jll.jl

  JULIA_INSTALL_SRCPKG=1 . /usr/lib/julia/julia-install-pkg.sh Enzyme_jll "${pkgdir}" "${pkgname}" julia-git
}
