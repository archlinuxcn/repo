# Maintainer: Kiri <kiri@vern.cc>
# Contributor: Rustmilian Rustmilian@proton.me

pkgname=('calamares')
pkgver=3.3.12
pkgrel=1
pkgdesc='Distribution-independent installer framework'
arch=('x86_64')
url="https://github.com/calamares/calamares"
license=('BSD-2-Clause'
         'MIT'
         'CC-BY-4.0'
         'CC0-1.0'
         'GPL-3.0-or-later'
         'LGPL-2.1-only'
         'LGPL-3.0-or-later')
_qt_version=6

depends=('sh'
         'icu'
         'kpmcore'
         'libpwquality'
         'yaml-cpp'
         'qt6-base'
         'kcoreaddons'
         'qt6-base'
         'qt6-svg'
         'kirigami'
         'python'
         'gcc-libs'
         'glibc'
         'qt6-declarative'
         'libxcrypt'
         'kcrash'
         'hicolor-icon-theme'
         'parted'
         'kio'
         'ckbcomp'
         'efibootmgr'
         'gtk-update-icon-cache'
         'hwinfo'
         'mkinitcpio-openswap'
         'squashfs-tools'
         'kconfig'
         'ki18n'
         'kiconthemes'
         'polkit-qt6'
         'solid'
         'kparts')

makedepends=('extra-cmake-modules'
             'qt6-tools')

backup=('usr/share/calamares/modules/bootloader.conf'
        'usr/share/calamares/modules/displaymanager.conf'
        'usr/share/calamares/modules/initcpio.conf'
        'usr/share/calamares/modules/unpackfs.conf')

source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz"
        "calamares.desktop"
        "calamares_polkit"
        "49-nopasswd-calamares.rules")

sha256sums=('f44110a91a4394f34728042136af4b906541babc37e72b3b33664888b3220f2d'
            'b9e65ab87f69b2d3f8f8eaea60c78625aef57dd336314ab75389f31a447445be'
            'c176b28007bd1c1f23d8dbb2c936fa54d0c01bacfb67290ddad597606c129df3'
            '56d85ff6bf860b9559b8c9f997ad9b1002f3fccc782073760eca505e3bddd176')

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"
        sed -i 's/"Install configuration files" OFF/"Install configuration files" ON/' "${srcdir}/${pkgname}-${pkgver}/CMakeLists.txt"
        sed -i "s|\${CALAMARES_VERSION_MAJOR}.\${CALAMARES_VERSION_MINOR}.\${CALAMARES_VERSION_PATCH}|${pkgver}-${pkgrel}|g" CMakeLists.txt
        sed -i "s|CALAMARES_VERSION_RC 1|CALAMARES_VERSION_RC 0|g" CMakeLists.txt
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    mkdir -p build
    cd build
    cmake .. \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DWITH_QT"${_qt_version}"=ON \
        -DWITH_PYTHONQT=OFF \
        -DWITH_KF5DBus=OFF \
        -DBoost_NO_BOOST_CMAKE=ON \
        -DWEBVIEW_FORCE_WEBKIT=OFF \
        -DSKIP_MODULES="webview \
                        tracking \
                        interactiveterminal \
                        initramfs \
                        initramfscfg \
                        dracut \
                        dracutlukscfg \
                        dummyprocess \
                        dummypython \
                        dummycpp \
                        dummypythonqt \
                        services-openrc \
                        keyboardq \
                        localeq \
                        welcomeq"
    make
}

package_calamares() {
    cd "${srcdir}/${pkgname}-${pkgver}/build"
    make DESTDIR="${pkgdir}" install
    install -Dm644 "${srcdir}/calamares.desktop" "${pkgdir}/etc/xdg/autostart/calamares.desktop"
    install -Dm755 "${srcdir}/calamares_polkit" "${pkgdir}/usr/bin/calamares_polkit"
    install -Dm644 "${srcdir}/49-nopasswd-calamares.rules" "${pkgdir}/etc/polkit-1/rules.d/49-nopasswd-calamares.rules"
    chmod 750 "${pkgdir}"/etc/polkit-1/rules.d

    cd "${srcdir}/${pkgname}-${pkgver}/LICENSES"
    install -Dm644 "BSD-2-Clause.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE-BSD"
    install -Dm644 "MIT.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE-MIT"
}
