# Maintainer: Y7n05h <Y7n05h(aT)protonmail--d0t--com>
# Contributor: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Contributor: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Ding Xiao <tinocodfcdsa10@mails.tsinghua.edu.cn>
# Contributor: Firmy <firmianay@gmail.com>

pkgname=python-pwntools-git
_pyname=pwntools
pkgver=4.10.0beta0.r6.gba7c7e55
pkgrel=1
pkgdesc='CTF framework and exploit development library'
url='https://github.com/Gallopsled/pwntools'
arch=('any')
license=('MIT' 'GPL2' 'BSD')
depends=('python'
  'python-mako'
  'python-pyelftools'
  'python-capstone'
  'python-pyserial'
  'python-requests'
  'python-pip'
  'python-pygments'
  'python-pysocks'
  'python-dateutil'
  'python-packaging'
  'python-psutil'
  'python-intervaltree'
  'python-sortedcontainers'
  'python-unicorn'
  'python-paramiko'
  'python-colored-traceback'
  'python-rpyc'
  'ropgadget')
makedepends=('pandoc' 'python-setuptools' 'git')
source=("$pkgname::git+$url"
  pwn.conf)
sha256sums=('SKIP'
  'SKIP')
provides=('pwntools')
conflicts=('python-pwntools' 'pwntools' 'pwntools-git')
pkgver() {
  cd ${pkgname}
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}
build() {
  cd ${pkgname}
  python setup.py build
}

check() {
  cd "${pkgname}"
  PYTHONPATH=. PWNLIB_NOTERM=true python -c 'import pwn'
}

package() {
  cd "${pkgname}"
  python setup.py install -O1 --root="${pkgdir}" --skip-build --only-use-pwn-command
  install -Dm 644 "${srcdir}/pwn.conf" -t "${pkgdir}/etc"
  install -Dm 644 LICENSE-pwntools.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  rm -f "${pkgdir}"/usr/lib/python*/site-packages/*.{txt,md}

  install -Dm 644 extra/bash_completion.d/pwn -t  "${pkgdir}/usr/share/bash-completion/completions/"
  install -Dm 644 extra/zsh_completion/_pwn -t "${pkgdir}/usr/share/zsh/site-functions/"
}
