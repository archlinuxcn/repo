# Maintainer: taotieren <admin@taotieren.com>

pkgname=crm-git
pkgver=0.1.8.r16.g7d4f255
pkgrel=1
pkgdesc="crm (Cargo registry manager)"
arch=('any')
url="https://github.com/wtklbm/crm"
license=('MIT' 'Apache-2.0')
provides=(${pkgname%-git})
conflicts=(${pkgname%-git})
replaces=()
depends=('cargo')
makedepends=('git' 'rust')
backup=()
options=('!strip')
install=
source=("${pkgname%-git}::git+${url}.git")
sha256sums=('SKIP')

pkgver() {
    cd "${srcdir}/${pkgname%-git}/"
    git describe --long --tags | sed 's/v//g;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
# build crm
    cd "${srcdir}/${pkgname%-git}/"
    cargo build --release
}

check() {
    cd "${srcdir}/${pkgname%-git}/"
    cargo test --release
}

package() {
# install crm
    install -Dm0755 "${srcdir}/${pkgname%-git}/target/release/${pkgname%-git}" "${pkgdir}/usr/share/${pkgname%-git}/${pkgname%-git}"

    install -Dm0755 /dev/stdin "${pkgdir}/usr/bin/${pkgname%-git}" << EOF
#!/bin/env bash
cd /usr/share/${pkgname%-git}/
./${pkgname%-git} "\$@"
EOF

}
