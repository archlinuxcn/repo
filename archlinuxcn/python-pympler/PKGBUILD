# Maintainer: Butui Hu <hot123tea123@gmail.com>

_pkgname=pympler
pkgname=python-pympler
pkgver=1.1
pkgrel=2
pkgdesc='Development tool to measure, monitor and analyze the memory behavior of Python objects in a running Python application'
arch=('any')
url='https://github.com/pympler/pympler'
license=('Apache-2.0')
depends=(
  python
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)

source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/pympler/pympler/archive/refs/tags/${pkgver}.tar.gz"
)
sha512sums=('366d7d492ab62e79ba89be28a05cfa3b399a27bf2fd2cdd41ea347077a6a9190d5639cc3881afdf52943ac12ab6673059be44cfc4183164005b68faa867b3dc3')

build() {
  cd "${_pkgname}-${pkgver}"
  python -m build --wheel --no-isolation
}

package() {
  cd "${_pkgname}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
}
# vim:set ts=2 sw=2 et:
