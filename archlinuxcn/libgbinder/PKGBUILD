# Maintainer: Puqns67 <me@puqns67.icu>
# Contributor: Danct12 <danct12@disroot.org>

# This PKGBUILD is base on the AUR repo libgbinder, by Danct12 <danct12@disroot.org>

pkgname='libgbinder'
pkgver=1.1.40
pkgrel=1
pkgdesc='GLib-style interface to binder'
arch=('i686' 'x86_64' 'armv7h' 'aarch64')
url='https://github.com/mer-hybris/libgbinder'
license=('BSD-3-Clause')
depends=('libglibutil' 'glib2')
source=("${pkgname}-${pkgver}.tar.gz"::"${url}/archive/refs/tags/${pkgver}.tar.gz")
sha512sums=('1bace60c31344b06ce6a935c045e14c1f29728621d7940df8e5e8d9a62592c21632a7a2172508a193e0b5fa8dfcea95bd42d3a6df34de6d1ebd2f5b2d592779e')

build() {
  KEEP_SYMBOLS=1 make -C "${srcdir}/${pkgname}-${pkgver}" release pkgconfig
}

package() {
  DESTDIR="${pkgdir}" make -C "${srcdir}/${pkgname}-${pkgver}"  install-dev

  # license
  install -Dm644 "${srcdir}/${pkgname}-${pkgver}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
