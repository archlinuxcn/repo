# Maintainer: Puqns67 <me@puqns67.icu>
# Contributor: Danct12 <danct12@disroot.org>

# This PKGBUILD is base on the AUR repo libgbinder, by Danct12 <danct12@disroot.org>

pkgname='libgbinder'
pkgver=1.1.31
pkgrel=2
pkgdesc='GLib-style interface to binder'
arch=('i686' 'x86_64' 'armv7h' 'aarch64')
url='https://github.com/mer-hybris/libgbinder'
license=('BSD')
depends=('libglibutil' 'glib2')
source=("${pkgname}-${pkgver}.tar.gz"::"${url}/archive/refs/tags/${pkgver}.tar.gz")
sha512sums=('519cc015a97d6ebcb410efcbff54f01afc401de85efb0a5c1128b57f996f23b352fa5dd70f06ac5b9205c3f0770ffabfd82f367c85ca18a5012f715d09bce690')

build() {
  make -C "${srcdir}/${pkgname}-${pkgver}/" KEEP_SYMBOLS=1 release pkgconfig
}

package() {
  make -C "${srcdir}/${pkgname}-${pkgver}/" DESTDIR="${pkgdir}" install-dev

  # license
  install -Dm 644 "${srcdir}/${pkgname}-${pkgver}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
