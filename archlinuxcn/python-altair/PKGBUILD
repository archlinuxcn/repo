_name=altair
pkgname=python-altair
pkgver=4.1.0
pkgrel=3
pkgdesc="Altair: A declarative statistical visualization library for Python."
arch=(any)
url="http://altair-viz.github.io"
license=('BSD 3-clause')
depends=('python-entrypoints' 'python-jinja' 'python-jsonschema' 'python-numpy' 'python-pandas' 'python-toolz')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/altair-4.1.0.tar.gz")
sha256sums=('3edd30d4f4bb0a37278b72578e7e60bc72045a8e6704179e2f4738e35bc12931')

build() {
  cd "$srcdir/altair-4.1.0"
  python3 setup.py build
}

package() {
  cd "$srcdir/altair-4.1.0"
  python3 setup.py install --root=$pkgdir --optimize=1 --skip-build

  # make sure we don't install any world-writable or root-readable-only files
  # we shouldn't need to fix ownership as we extract tarballs as a non-root user
  # https://github.com/pypa/setuptools/issues/1328
  # https://github.com/LonamiWebs/Telethon/issues/1605
  chmod u=rwX,go=rX -R "$pkgdir"
  # make sure we don't install annoying files
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  rm -rf "$pkgdir/$_site_packages/tests/"
}

