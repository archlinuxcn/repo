# Maintainer: bgme <i@bgme.me>
# Contributor: gilcu3
# Contributor: Y7n05h <Y7n05h@y7n05h.dev>

pkgname=ecapture
pkgver=1.4.1
pkgrel=1
pkgdesc="capture SSL/TLS text content without CA cert using eBPF"
arch=("x86_64" "aarch64")
url="https://github.com/gojue/ecapture"
license=("Apache-2.0")
provides=("ecapture")
conflicts=("ecapture-bin")
source=("${pkgname}::git+${url}#tag=v${pkgver}")
sha256sums=('452870f2f3173f4e444ae04b9a3722eb94d682c09264e76a9de3342028bbca7d')

depends=("glibc")
makedepends=("clang" "go" "git" "bpf" "linux" "linux-headers" "libelf" "llvm" "pkgconf")

build() {
    cd "${srcdir}/${pkgname}"
    UNAME_R=$(cat "/usr/src/linux/version")
    HOST_VERSION_SHORT=$(echo "${UNAME_R}" | cut -d'-' -f 1)
    make "UNAME_R=${UNAME_R}" "HOST_VERSION_SHORT=${HOST_VERSION_SHORT}"
}

package() {
    cd "${srcdir}/${pkgname}"
    install -Dm755 "bin/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
    install -Dm644 "README.md" "README_CN.md" "README_JA.md" -t "${pkgdir}/usr/share/doc/${pkgname}"
    install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
