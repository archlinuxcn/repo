# Maintainer: Iwan Timmer <irtimmer@gmail.com>

pkgname=dbus-cpp
pkgdesc="A header-only dbus-binding leveraging C++-11"
pkgver=5.0.0
_pkgver=5.0.0+18.04.20171031
pkgrel=4
arch=('x86_64')
url="https://code.launchpad.net/dbus-cpp"
license=('LGPL3')
depends=('process-cpp' 'dbus' 'libxml2')
makedepends=('cmake' 'gcovr' 'gmock' 'properties-cpp' 'boost')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/dbus-cpp_${_pkgver}.orig.tar.gz"
	"boost-asio-1-66.patch")
md5sums=('61e9568f3c799214c5bc8b5e3a498496'
         '127dde628ffb8296516090228975cfc2')

prepare() {
  cd "$srcdir"

  # Don't build tests
  sed -i 's/-Werror//g' CMakeLists.txt
  truncate -s 0 tests/CMakeLists.txt

  # Fix boost asio 1.66 API changes
  # https://github.com/anbox/anbox/issues/637#issuecomment-373361974
  patch -Np1 -i boost-asio-1-66.patch
}

build() {
  mkdir -p "$srcdir/build"
  cd "$srcdir/build"

  cmake .. -DCMAKE_INSTALL_LIBDIR=/usr/lib -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release -DDBUS_CPP_VERSION_MAJOR=5 -DDBUS_CPP_VERSION_MINOR=0 -DDBUS_CPP_VERSION_PATCH=0
  make
}

package() {
  cd "$srcdir/build"

  make DESTDIR="$pkgdir/" install
}
