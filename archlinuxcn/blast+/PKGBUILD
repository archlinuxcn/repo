# Maintainer: Hu Butui <hot123tea123@gmail.com>
# Contributor: Nathaniel Stickney <nstickney@gmail.com>
# Contributor: Christian Krause ("wookietreiber") <kizkizzbangbang@googlemail.com>

pkgname=blast+
pkgver=2.15.0
pkgrel=4
pkgdesc='BLAST tool suite from NCBI (blastn, blastp, blastx, psiblast, etc)'
arch=('x86_64')
url='http://blast.ncbi.nlm.nih.gov'
license=('custom')
depends=(
  grpc
  libnghttp2
  libuv
  lmdb
  lzo
  pcre
  perl
  python
  sqlite
)
makedepends=(
  arrow
  boost
  chrpath
  cpio
  fcgi
  freeglut
  freetype2
  gcc12
  giflib
  gl2ps
  glew
  gnutls
  gtest
  hdf5
  hiredis
  lapacke
  leveldb
  libjpeg
  libpng
  librdkafka
  libtiff
  libx11
  libxext
  libxmu
  libxpm
  libxslt
  libxt
  mariadb
  mesa
  microsoft-gsl
  mimetic
  muparser
  procps-ng
  protobuf
)
source=("${pkgname}-${pkgver}.tar.gz::https://ftp.ncbi.nlm.nih.gov/blast/executables/LATEST/ncbi-blast-${pkgver}+-src.tar.gz")
sha256sums=('6918c370524c8d44e028bf491e8f245a895e07c66c77b261ce3b38d6058216e0')

build() {
  export CC=gcc-12
  export CXX=g++-12
  cd "${srcdir}/ncbi-blast-${pkgver}+-src/c++"
  ./configure \
    --prefix=/usr \
    --with-dll
  make
}

package() {
  cd "${srcdir}/ncbi-blast-${pkgver}+-src/c++"
  make prefix="${pkgdir}/usr" install
  install -Dm644 "scripts/projects/blast/LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}"
  # delete conflict files
  rm -vf "${pkgdir}/usr/lib/libproj.so"
  # delete unused files
  rm -vf ${pkgdir}/usr/bin/test_*
  rm -vf ${pkgdir}/usr/bin/*_test
  rm -vf ${pkgdir}/usr/lib/*.a
  rm -vf ${pkgdir}/usr/lib/libtest_*
  rm -vf ${pkgdir}/usr/lib/libseqtest.so
  rm -vf ${pkgdir}/usr/lib/libxregexp_template_tester.so
  # fix permission
  chmod 0755 ${pkgdir}/usr/lib/*.so
  # fix rpath
  chrpath --delete ${pkgdir}/usr/lib/*.so ${pkgdir}/usr/bin/* || true
}
# vim:set ts=2 sw=2 et:
