# Maintainer: Robin Lange <robin dot langenc at gmail dot com>
# Contributor: Robin Lange <robin dot langenc at gmail dot com>
pkgname=optimus-manager
pkgver=0.7.1
pkgrel=2
pkgdesc="Management utility to handle GPU switching for Optimus laptops."
arch=('any')
url="https://github.com/Askannz/optimus-manager"
license=('MIT')
depends=('python' 'python-pyqt5' 'python-setuptools' 'nvidia' 'mesa-demos' 'xorg-xrandr')
makedepends=('python-setuptools' 'git')
backup=('etc/optimus-manager/xorg-intel.conf'
        'etc/optimus-manager/xorg-nvidia.conf')
source=("https://github.com/Askannz/optimus-manager/archive/v${pkgver}.tar.gz")
sha256sums=('9d1df080349e95bb8dce4a055d1eec8c335feb84f8cd2597dc10ba26939fa6bb')
 
build() {
 
  cd "${srcdir}/${pkgname}-${pkgver}/"
  python setup.py build
 
}
 
 
package() {
 
  cd "${srcdir}/${pkgname}-${pkgver}/"
 
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 modules/optimus-manager.conf "$pkgdir/etc/modprobe.d/optimus-manager.conf"
  install -Dm755 xorg/optimus-manager_Xsetup "$pkgdir/usr/bin/optimus-manager_Xsetup"
  install -Dm644 systemd/optimus-manager.service "$pkgdir/usr/lib/systemd/system/optimus-manager.service"
  install -Dm644 systemd/99-optimus-manager.conf "$pkgdir/usr/lib/systemd/system/display-manager.service.d/99-optimus-manager.conf"
  install -Dm644 optimus-manager.conf "$pkgdir/usr/share/optimus-manager.conf"
  
  install -Dm644 config/xorg-intel.conf "$pkgdir/etc/optimus-manager/xorg-intel.conf"
  install -Dm644 config/xorg-nvidia.conf "$pkgdir/etc/optimus-manager/xorg-nvidia.conf"
  
  install -Dm644 desktop/optimus-manager-systray.desktop "$pkgdir/usr/share/applications/optimus-manager-systray.desktop"
  
  mkdir -p "$pkgdir/var/lib/optimus-manager/"
 
  python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
 
} 
