# $Id: PKGBUILD 226039 2017-04-27 13:52:30Z felixonmars $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: csslayer <wengxt AT gmail com>

_pkgname=fcitx5-gtk
pkgname=${_pkgname}-git
pkgver=5.0.21.r7.g57e72e0
pkgrel=1
pkgdesc="Gtk im module for fcitx5 and glib based dbus client library"
arch=('i686' 'x86_64')
url="https://github.com/fcitx/fcitx5-gtk"
license=('GPL')
depends=('glib2')
makedepends=('extra-cmake-modules' 'gobject-introspection' 'gtk2' 'gtk3' 'gtk4' 'vulkan-headers' 'git')
provides=(${_pkgname})
conflicts=(${_pkgname})
source=("git+$url.git")
sha512sums=('SKIP')

pkgver() {
  cd fcitx5-gtk
  git describe --long | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build(){
  cd fcitx5-gtk

  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_LIBDIR=/usr/lib .
  make
}

package() {
  cd fcitx5-gtk
  make DESTDIR="$pkgdir" install
}
