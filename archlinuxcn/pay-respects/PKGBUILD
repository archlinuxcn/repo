# Maintainer: Integral <integral@member.fsf.org>
# Contributor: iff <iff@ik.me>

pkgname=pay-respects
pkgver=0.7.9
pkgrel=1
pkgdesc="Terminal command suggestion, alternative to thefuck written in Rust with AI support"
arch=('x86_64' 'aarch64')
url="https://codeberg.org/iff/${pkgname}"
license=('AGPL-3.0-or-later')
makedepends=('cargo')
optdepends=('curl: for fetching AI suggestions')
replaces=('pay_respects')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha256sums=('bb4b649373cb52e001a949952d3e7e4762311379179ed5ac9310684610445bef')

prepare() {
	cd "${pkgname}/"
	cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
	cd "${pkgname}/"
	export CFLAGS+=" -ffat-lto-objects"
	export _DEF_PR_LIB="/usr/lib/${pkgname}/"
	cargo build --frozen --release --all-features --workspace
}

package() {
	cd "${pkgname}/target/release"
	install -Dm755 "${pkgname}" -t "${pkgdir}/usr/bin/"
	install -Dm755 _${pkgname}-{module-100-runtime-rules,fallback-100-request-ai} -t "${pkgdir}/usr/lib/${pkgname}/"
}
