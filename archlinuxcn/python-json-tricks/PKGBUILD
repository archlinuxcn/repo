# Maintainer: Hu Butui <hot123tea123@gmail.com>
# Contributor:  Liu Zhe <cruise.pas@gmail.com>

_pkgname=pyjson_tricks
pkgname=python-json-tricks
pkgver=3.17.3
pkgrel=2
pkgdesc="Extra features for Python's JSON"
arch=('any')
url='https://github.com/mverleg/pyjson_tricks'
license=('BSD-3-Clause')
depends=(
  python
)
optdepends=(
  python-numpy
  python-pandas
  python-pytz
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/mverleg/pyjson_tricks/archive/v${pkgver}.tar.gz")
sha1sums=('884eba1efe60f0bf4953a3df58b27fd7cf8ca9d5')

build() {
  cd "${_pkgname}-${pkgver}"
  python -m build --wheel --no-isolation
}

package() {
  cd "$srcdir"/pyjson_tricks-$pkgver
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
# vim:set ts=2 sw=2 et:
