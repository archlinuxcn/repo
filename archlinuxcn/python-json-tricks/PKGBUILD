# Maintainer: Hu Butui <hot123tea123@gmail.com>
# Contributor:  Liu Zhe <cruise.pas@gmail.com>

_pkgname=pyjson_tricks
pkgname=python-json-tricks
pkgver=3.17.2
pkgrel=1
pkgdesc="Extra features for Python's JSON"
arch=('any')
url='https://github.com/mverleg/pyjson_tricks'
license=('BSD')
depends=(
  python
)
optdepends=(
  python-numpy
  python-pandas
  python-pytz
)
makedepends=(
  python-setuptools
)
checkdepends=(
  python-pytest
  python-numpy
  python-pytz
  python-pandas
)
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/mverleg/pyjson_tricks/archive/v${pkgver}.tar.gz")
sha1sums=('e41147e0bc73c7d3c7e4e4046482b7e78fdee98e')

build() {
  cd "${_pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${_pkgname}-${pkgver}"
  pytest -v
}

package() {
  cd "$srcdir"/pyjson_tricks-$pkgver
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
# vim:set ts=2 sw=2 et:
