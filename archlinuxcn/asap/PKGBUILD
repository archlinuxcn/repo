# Maintainer: Butui Hu <hot123tea123@gmail.com>

_pkgname=ASAP
pkgname=asap
pkgver=2.1
pkgrel=3
pkgdesc='Program for the analysis and visualization of whole-slide images in digital pathology'
arch=('x86_64')
url='https://github.com/computationalpathologygroup/ASAP'
license=('GPL-2.0-or-later')
depends=(
  boost
  gcc-libs
  glibc
  libjpeg
  libtiff.so
  opencv
  openjpeg2
  openslide
  pugixml
  python
  python-numpy
  qt5-base
  qt5-tools
)
makedepends=(
  swig
  clang
  cmake
  gendesk
  git
)
source=(
  "${pkgname}-${pkgver}.tar.gz::https://github.com/computationalpathologygroup/ASAP/archive/refs/tags/v${pkgver}.tar.gz"
  "pugixml::git+https://github.com/zeux/pugixml.git"
)
sha256sums=('22bc8d97e231a4c3aea5e05d574b6add1f11127efc7621ecaeb6a84f0f18fd9d'
            'SKIP')

prepare() {
  # fix https://github.com/computationalpathologygroup/ASAP/issues/244
  sed -i "34i#include <utility>" "${srcdir}/${_pkgname}-${pkgver}/core/argparse.hpp"

  echo "Creating desktop file"
  gendesk -f -n --pkgname ${pkgname} \
    --pkgdesc "${pkgdesc}" \
    --categories "Graphics;MedicalSoftware;Science;" \
    --icon "${pkgname}" \
    --exec "${_pkgname}"
}

build() {
  cmake \
    -B build \
    -DBUILD_ASAP=ON \
    -DBUILD_EXECUTABLES=ON \
    -DBUILD_IMAGEPROCESSING=ON \
    -DBUILD_MULTIRESOLUTIONIMAGEINTERFACE_DICOM_SUPPORT=OFF \
    -DBUILD_MULTIRESOLUTIONIMAGEINTERFACE_VSI_SUPPORT=OFF \
    -DCMAKE_CXX_COMPILER=clang++ \
    -DCMAKE_C_COMPILER=clang \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_INSTALL_RPATH=ON \
    -DOPENSLIDE_INCLUDE_DIR=/usr/include/openslide \
    -DPugiXML_INCLUDE_DIR=${srcdir}/pugixml/src \
    -DWRAP_MULTIRESOLUTIONIMAGEINTERFACE_PYTHON=ON \
    -DWRAP_WHOLESLIDEFILTERS_PYTHON=ON \
    -S "${srcdir}/${_pkgname}-${pkgver}"
  make -C build
}

package() {
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  make -C build DESTDIR="${pkgdir}" install
  install -Dm644 "${_pkgname}-${pkgver}/Copyright.txt" -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -d \
    "${pkgdir}/usr/cmake" \
    "${pkgdir}/usr/lib/cmake/${pkgname}" \
    "${pkgdir}${site_packages}"
  # move python-related files to site-packages dir
  mv -vf "${pkgdir}/usr/bin/_multiresolutionimageinterface.so" "${pkgdir}${site_packages}"
  mv -vf "${pkgdir}/usr/bin/_wholeslidefilters.so" "${pkgdir}${site_packages}"
  mv -vf "${pkgdir}/usr/bin/multiresolutionimageinterface.py" "${pkgdir}${site_packages}"
  mv -vf "${pkgdir}/usr/cmake" "${pkgdir}/usr/lib/cmake/${pkgname}"
  install -Dm644 "${srcdir}/${pkgname}.desktop" -t "${pkgdir}/usr/share/applications"
  install -Dm644 "${srcdir}/${_pkgname}-${pkgver}/ASAP/resources/application.png" "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
}
# vim:set ts=2 sw=2 et:
