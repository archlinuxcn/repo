# Maintainer: Butui Hu <hot123tea123@gmail.com>

_pkgname=ASAP
pkgname=asap
pkgver=2.0
pkgrel=2
pkgdesc='Program for the analysis and visualization of whole-slide images in digital pathology'
arch=('x86_64')
url='https://github.com/computationalpathologygroup/ASAP'
license=('GPL')
depends=(
  boost
  libtiff
  libjpeg
  gcc-libs
  opencv
  openslide
  qt5-base
  qt5-tools
  pugixml
  python
  swig
)
makedepends=(
  cmake
  gendesk
  git
)
source=(
  "${pkgname}-${pkgver}.tar.gz::https://github.com/computationalpathologygroup/ASAP/archive/refs/tags/ASAP-${pkgver}-(Nightly).tar.gz"
  "pugixml::git+https://github.com/zeux/pugixml.git"
)
noextract=(${pkgname}-${pkgver}.tar.gz)
sha256sums=('830a9874b143c5bf738eabf594fc91465df9cb5482391caf51cc4a2b945352cc'
            'SKIP')

get_pyver() {
  python -c 'import sys; print(str(sys.version_info[0]) + "." + str(sys.version_info[1]))'
}

prepare() {
  mkdir "${srcdir}/${pkgname}-${pkgver}"
  tar xvf "${srcdir}/${pkgname}-${pkgver}.tar.gz" -C "${srcdir}/${pkgname}-${pkgver}" --strip-components 1
  # fix for CMAKE_DL_LIBS
  sed -i 's/target_link_libraries(multiresolutionimageinterface PUBLIC core PRIVATE jpeg2kcodec libtiff Boost::disable_autolinking Boost::thread)/target_link_libraries(multiresolutionimageinterface PUBLIC core PRIVATE jpeg2kcodec libtiff Boost::disable_autolinking Boost::thread ${CMAKE_DL_LIBS})/' \
    "${srcdir}/${pkgname}-${pkgver}/multiresolutionimageinterface/CMakeLists.txt"
  echo "Creating desktop file"
  gendesk -f -n --pkgname ${pkgname} \
    --pkgdesc "${pkgdesc}" \
    --categories "Graphics;MedicalSoftware;Science;" \
    --icon "${pkgname}" \
    --exec "${_pkgname}"
}

build() {
  cmake -B build -S "${srcdir}/${pkgname}-${pkgver}" \
    -DBUILD_ASAP=ON \
    -DBUILD_EXECUTABLES=ON \
    -DBUILD_IMAGEPROCESSING=ON \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_INSTALL_RPATH=ON \
    -DOPENSLIDE_INCLUDE_DIR=/usr/include/openslide \
    -DPugiXML_INCLUDE_DIR=${srcdir}/pugixml/src \
    -DWRAP_WHOLESLIDEFILTERS_PYTHON=ON
  make -C build
}

package() {
  make -C build DESTDIR="${pkgdir}" install
  install -Dm644 "${pkgname}-${pkgver}/Copyright.txt" -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -d \
    "${pkgdir}/usr/cmake" \
    "${pkgdir}/usr/lib/cmake/${pkgname}" \
    "${pkgdir}/usr/lib/python$(get_pyver)/site-packages"
  mv -vf "${pkgdir}/usr/bin/_wholeslidefilters.so" "${pkgdir}/usr/lib/python$(get_pyver)/site-packages"
  mv -vf "${pkgdir}/usr/cmake" "${pkgdir}/usr/lib/cmake/${pkgname}"
  install -Dm644 "${srcdir}/${pkgname}.desktop" -t "${pkgdir}/usr/share/applications"
  install -Dm644 "${srcdir}/${pkgname}-${pkgver}/ASAP/resources/application.png" "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
}
# vim:set ts=2 sw=2 et:
