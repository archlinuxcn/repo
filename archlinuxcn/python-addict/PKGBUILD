# First Linux Arch packager: Serge Victor <arch@random.re>

_pkgname=addict
pkgname=python-addict
pkgver=2.4.0
pkgrel=5
pkgdesc="the Python Dict that's better than heroin"
arch=('any')
depends=(python)
makedepends=(python-setuptools)
checkdepends=(python-pytest)
url='https://github.com/mewwts/addict'
license=('MIT')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/mewwts/addict/archive/v${pkgver}.tar.gz")
sha512sums=('aa8248b6ba695392d3d87761e2e72675221e22adb99d5d9afccc47ae6763dc840aac610c660201110817704917967f68dc1f65fbebdd52a3b619204d85056a15')

build() {
  cd "${_pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${_pkgname}-${pkgver}"
  pytest -v
}

package() {
  cd "${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
