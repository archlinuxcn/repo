# Maintainer: FadeMind <fademind@gmail.com>
# Contributor: Alexey D. <lq07829icatm@rambler.ru>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=extra-cmake-modules-git
_pkgname=${pkgname%-git}
pkgver=6.0.0.r3675.b4d3206e
pkgrel=1
pkgdesc="Extra modules and scripts for CMake. (GIT version)"
arch=('any')
url="https://projects.kde.org/projects/kdesupport/${_pkgname}"
license=('LGPL')
groups=('kf6-git')
depends=('cmake')
makedepends=(
	'git'
	'python-sphinx'
	'python-requests'
)
checkdepends=(
	'reuse'
	'qt5-tools'
	'qt5-base'
	'qt5-declarative'
)
conflicts=("${_pkgname}")
provides=("${_pkgname}=${pkgver%%.r*}")
optdepends=(
	'python-pyxdg: to generate fastlane metadata for Android apps'
	'python-requests: to generate fastlane metadata for Android apps'
	'python-yaml: to generate fastlane metadata for Android apps'
	'clang: for clang support'
)
source=("git+https://invent.kde.org/frameworks/${_pkgname}.git")
sha256sums=('SKIP')

pkgver() {
	cd "${_pkgname}/"
	_ver="$(cat CMakeLists.txt | grep -m1 'set(VERSION' | grep -o "[[:digit:]]*" | paste -sd'.')"
	echo "${_ver}.r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

prepare() {
	sed 's|frameworks/||g' -i "${_pkgname}/tests/KDEFetchTranslations/CMakeLists.txt"
}

build() {
	cmake -S "${_pkgname}" -B build \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DBUILD_QTHELP_DOCS=ON \
		-DBUILD_TESTING=ON

	cmake --build build
}

check() {
	cd build
	ctest --output-on-failure -E ECMPoQmToolsTest
}

package() {
	DESTDIR="${pkgdir}" cmake --install build
}
