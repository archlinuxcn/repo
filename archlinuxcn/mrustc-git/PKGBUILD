# Maintainer: poscat

pkgname=mrustc-git
pkgver=v0.11.2.r78.gaeb58f3
pkgrel=1
pkgdesc='Alternative rust compiler written in C++'
arch=('x86_64')
url='https://github.com/thepowersgang/mrustc'
license=('MIT')
depends=(gcc zlib python)
makedepends=(cmake git)
provides=(mrustc)
conflicts=(mrustc)
source=('git+https://github.com/thepowersgang/mrustc.git'
        'mrustc.sh')
sha256sums=('SKIP'
            '836cbffc4be7c5267465e4887e5336918a5757397405271d9fc6a6e0e6240d5b')

pkgver() {
	cd "$srcdir/mrustc"
        git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
	cd "$srcdir/mrustc"
        export PARLEVEL=$(nproc)
        export RUSTC_VERSION=1.74.0 MRUSTC_TARGET_VER=1.74 OUTDIR_SUF=-1.74.0
        unset MRUSTC_LIBDIR
        make -f minicargo.mk bin/mrustc bin/minicargo LIBS
}

package() {
	cd "$srcdir/mrustc"
	install -Dt "$pkgdir/usr/bin" bin/mrustc bin/minicargo

        install -d "$pkgdir"/usr/share/mrustc
        cp -r output-1.74.0/*rlib{,.o,.hir} "$pkgdir"/usr/share/mrustc

        install -Dt "$pkgdir"/etc/profile.d "$srcdir"/mrustc.sh
}
