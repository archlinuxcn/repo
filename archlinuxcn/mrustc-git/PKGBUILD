# Maintainer: poscat

pkgname=mrustc-git
pkgver=v0.10.1.r259.g1827564
pkgrel=1
pkgdesc='Alternative rust compiler written in C++'
arch=('x86_64')
url='https://github.com/thepowersgang/mrustc'
license=('MIT')
depends=(gcc zlib python)
makedepends=(cmake git)
provides=(mrustc)
conflicts=(mrustc)
source=('git+https://github.com/thepowersgang/mrustc.git'
        'mrustc.sh')
sha256sums=('SKIP'
            'fdec13fa9f2b75e3950d2874ab1e1d121af556609a4ebca290394d4674530746')

pkgver() {
	cd "$srcdir/mrustc"
        git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
	cd "$srcdir/mrustc"
        export PARLEVEL=$(nproc)
        export RUSTC_VERSION=1.54.0 MRUSTC_TARGET_VER=1.54
        make -f minicargo.mk bin/mrustc bin/minicargo LIBS
}

package() {
	cd "$srcdir/mrustc"
	install -Dt "$pkgdir/usr/bin" bin/mrustc bin/minicargo

        install -d "$pkgdir"/usr/share/mrustc
        cp -r output-1.54.0/*rlib{,.o,.hir} "$pkgdir"/usr/share/mrustc

        install -Dt "$pkgdir"/etc/profile.d "$srcdir"/mrustc.sh
}
