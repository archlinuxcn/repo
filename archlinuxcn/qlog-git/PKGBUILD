# Maintainer: edward-p <edward at edward-p dot xyz>

_pkgname=qlog
pkgname="$_pkgname-git"
pkgver=0.46.0.r0.g6148b971
pkgrel=1
pkgdesc="Amateur radio logbook software"
arch=("x86_64")
url="https://github.com/foldynl/QLog"
license=('GPL-3.0-only')
depends=(
  'qt6-base'
  'qt6-charts'
  'qt6-webengine'
  'qtkeychain-qt6'
  'qt6-serialport'
  'qt6-websockets'
  'hamlib'
)
makedepends=(
  'git'
  'qt6-tools'
)
optdepends=(
  'org.freedesktop.secrets: keyring/password support'
)

provides=("$_pkgname")
conflicts=(${provides[@]})

source=(
  "$_pkgname"::"git+$url"

)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  git describe --long | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "$srcdir/$_pkgname"
  qmake6 PREFIX="$pkgdir/usr" QLog.pro
  make
}

package() {
  cd "$srcdir/$_pkgname"
  make install
}
