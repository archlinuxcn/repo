# Maintainer: edward-p <edward at edward-p dot xyz>

_pkgname=qlog
pkgname="$_pkgname-git"
pkgver=0.32.0.r0.g4c1699b
pkgrel=1
pkgdesc="Amateur radio logbook software"
arch=("x86_64")
url="https://github.com/foldynl/QLog"
license=('GPL-3.0-only')
depends=(
  'qt6-base'
  'qt6-charts'
  'qt6-webengine'
  'qtkeychain-qt6'
  'qt6-serialport'
  'hamlib'
)
makedepends=(
  'git'
  'qt6-tools'
)
optdepends=(
  'org.freedesktop.secrets: keyring/password support'
)

provides=("$_pkgname")
conflicts=(${provides[@]})

source=(
  "$_pkgname"::"git+$url"

  # submodule
  "flags"::"git+https://github.com/foldynl/QLog-Flags"
)
sha256sums=('SKIP'
            'SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  git describe --long | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  # prepare submodules
  cd "$srcdir/$_pkgname"
  _submodules=(
    'res/flags'
  )
  for submodule in ${_submodules[@]} ; do
    mkdir -p "$submodule"
    git submodule init ${submodule}
    git submodule set-url ${submodule} "${srcdir}/${submodule##*/}"
    git -c protocol.file.allow=always submodule update ${submodule}
  done
}

build() {
  cd "$srcdir/$_pkgname"
  qmake6 PREFIX="$pkgdir/usr" QLog.pro
  make
}

package() {
  cd "$srcdir/$_pkgname"
  make install
}
