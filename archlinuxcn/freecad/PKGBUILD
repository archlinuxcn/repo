# Maintainer: Yichao Yu <yyc1992@gmail.com>
# Contributor: Florian Pritz
# Contributor: Jonas Heinrich <onny@project-insanity.org>
# Contributor: Jordi De Groof <jordi (dot) degroof (at) gmail (dot) com>
# Contributor: mickele
# Contributor: manwithgrenade
# Contributor: bricem13
# Contributor: gborzi

pkgname=freecad-oce
epoch=1
pkgver=0.18.5
pkgrel=4
pkgdesc='General purpose 3D CAD modeler'
arch=(x86_64)
url='https://freecadweb.org/'
license=(LGPL)
depends=(boost-libs glew jsoncpp libspnav med netcdf curl oce openmpi
         pyside2-tools python-matplotlib python-pivy python-pyside2 qt5-svg
         qt5-tools qt5-webkit qt5-x11extras shared-mime-info xerces-c
         hicolor-icon-theme vtk)
makedepends=(boost cmake coin eigen gcc-fortran git ninja pyside2 desktop-file-utils
             python-shiboken2 shiboken2 swig)
optdepends=(python-matplotlib graphviz openscad)
options=(!libtool !staticlibs debug strip)
provides=("freecad=$pkgver")
conflicts=(freecad)
source=("git+https://github.com/FreeCAD/FreeCAD.git#tag=$pkgver"
        "freecad-qt-5.14.patch"
        "freecad-qt-5.14-version.patch"
        "freecad-qt-5.15.patch"
        "freecad-py38.patch"
        "freecad-py39.patch"
        "freecad-boost-1.73.patch"
        "freecad.desktop" "freecad.xml")
sha256sums=('SKIP'
            '9af1f0b85a58acad3035fd5abfaf60f57f6a5424101cb328f16468a9e4aebfa5'
            'a74b2f0a48056f73d4b5adaf37bb71750b140bb05a4e417b8c7ed35f294facde'
            '820d863794a0dbf0ca1dbe5a419d1823202b2459fee1cc8751a61af67a735bb6'
            '3d2570c143825acd01b8eeb645fc450566e9ac20d6333de9e17cbc10fb5a13e0'
            'de1d61c18d329e0903d5c1bc27b9c4e28c4911380bdd4b6c49da5b63740862f1'
            '99a55f06e9d3be3385cb41d747b1a6c9515b010d867bdeeda000a45c0014a89e'
            '7ef6f240a82a861cbeca128e453da1a0c37f548876894f409a780c55e9909add'
            '4ee1daf47c8371a3e17173d85a0dd4a106dacd7899d783ccd97a6f3e0dd1a21f')

prepare() {
  cd FreeCAD

  patch -Np1 -i ../freecad-qt-5.14.patch
  patch -Np1 -i ../freecad-qt-5.14-version.patch
  patch -Np1 -i ../freecad-qt-5.15.patch
  patch -Np1 -i ../freecad-py38.patch
  patch -Np1 -i ../freecad-py39.patch
  patch -Np1 -i ../freecad-boost-1.73.patch

  cd src

  # Fix missing "std::" qualifiers, sledge hammer style
  find . \
       ! -wholename "*/Mod/Sketcher/*" \
       ! -wholename "*/Mod/PartDesign/*" \
       -name "*.cpp" -type f \
       -exec sed -i '1i using namespace std;' {} \;

  # Add missing include
  sed -i '1i \#include <Inventor\/SbByteBuffer.h>' \
      Gui/Quarter/QuarterWidget.cpp
}

build() {
  mkdir -p build
  cd build

  cmake ../FreeCAD \
        -Wno-dev \
        -D BUILD_ENABLE_CXX_STD=C++14 \
        -D BUILD_QT5=ON \
        -D CMAKE_BUILD_TYPE=RelWithDebInfo \
        -D CMAKE_C_FLAGS="$CFLAGS -fPIC -w" \
        -D CMAKE_CXX_FLAGS="$CXXFLAGS -fPIC -w" \
        -D CMAKE_INSTALL_DATADIR="/usr/share/freecad" \
        -D CMAKE_INSTALL_DOCDIR="/usr/share/freecad/doc" \
        -D CMAKE_INSTALL_PREFIX="/usr/lib/freecad" \
        -D CMAKE_INSTALL_LIBDIR="/usr/lib/freecad/lib" \
        -D CMAKE_INSTALL_INCLUDEDIR="/usr/include" \
        -D FREECAD_USE_EXTERNAL_PIVY=ON \
        -D FREECAD_USE_OCC_VARIANT="Official Version" \
        -D FREECAD_USE_QT_FILEDIALOG=ON \
        -D PYTHON_EXECUTABLE=/usr/bin/python \
        -D OCC_INCLUDE_DIR=/usr/include/oce/ \
        -D OCE_DIR=/usr/lib/oce-0.18-dev/ \
        -G Ninja
  ninja
}

package() {
  DESTDIR="$pkgdir" ninja -C build install

  # Package desktop shortcut
  desktop-file-install \
    --dir="${pkgdir}/usr/share/applications" \
    "${srcdir}/freecad.desktop"

  # Package MIME info
  install -Dm644 freecad.xml "$pkgdir/usr/share/mime/packages/freecad.xml"

  cd FreeCAD/src/Gui/Icons

  # Package icons
  for i in 16 32 48 64; do
    install -Dm644 "freecad-icon-$i.png" \
            "$pkgdir/usr/share/icons/hicolor/${i}x$i/apps/freecad.png"
  done
  install -Dm644 freecad.svg \
          "$pkgdir/usr/share/icons/hicolor/scalable/apps/freecad.svg"


  # Package symlinks in /usr/bin
  install -dm755 "${pkgdir}/usr/bin"
  ln -sf /usr/lib/freecad/bin/FreeCAD "$pkgdir/usr/bin/freecad"
  ln -sf /usr/lib/freecad/bin/FreeCAD "$pkgdir/usr/bin/FreeCAD"
  ln -sf /usr/lib/freecad/bin/FreeCADCmd "$pkgdir/usr/bin/freecadcmd"
  ln -sf /usr/lib/freecad/bin/FreeCADCmd "$pkgdir/usr/bin/FreeCADCmd"

  boostver=$(objdump -p /usr/lib/libboost_system.so | \
               grep SONAME | sed -e 's/.*boost_system\.so\.//')
  [[ $boostver =~ ^([0-9]*)\.([0-9]*)\.([0-9]*) ]]
  maj_ver=${BASH_REMATCH[1]}
  min_ver=${BASH_REMATCH[2]}
  patch_ver=${BASH_REMATCH[3]}
  ((next_min=min_ver + 1))
  depends+=("boost-libs>=${maj_ver}.${min_ver}.${patch_ver}"
            "boost-libs<${maj_ver}.${next_min}.0")

  pymin_ver=$(python -c 'import sys; print(sys.version_info.minor)')
  ((pynext_min=pymin_ver + 1))
  depends+=("python>=3.${pymin_ver}.0" "python<3.${pynext_min}.0")
}
