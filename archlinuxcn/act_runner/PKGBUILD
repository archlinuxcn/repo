# Maintainer: Rocket Aaron <i at rocka dot me>
# Contributor: Manuel Gugger <mdgdot[at]tutanota[dot]com>

pkgname=act_runner
pkgver=0.2.13
pkgrel=1
pkgdesc="Runner for Gitea based on Gitea fork of act"
arch=('x86_64' 'aarch64')
url="https://gitea.com/gitea/act_runner"
license=('MIT')
optdepends=('docker' 'gitea')
makedepends=('go')
backup=('etc/act_runner/act_runner.yaml')
source=("act_runner-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz"
        'act_runner.install'
        'act_runner.service'
        'act_runner.sysusers'
        'act_runner.tmpfiles')
install="act_runner.install"
sha256sums=('69e6fe36ad9e9be188bf6dfe5fd55697eb92ef1aed6396c9a44c1d8e24611176'
            'd62614a114206e1e01e7cf7c259aac09a1cf3595f0f9fe43c41ee548a0a65031'
            '0211027d0fe34635b1ad7bc301c941a1ae40d2c9c72eef66b552c9bc2ded8cf2'
            'f0de2d8076ff59db8f5686addc096fc29e02190bfb7b44329979b3d9e1ad292f'
            '86885e9226ffb7bc3dbb105dc2e10630c41717212c804e19413acf3974c8b347')

build() {
    cd "${pkgname}"
    # https://wiki.archlinux.org/title/Go_package_guidelines#Supporting_debug_packages
    export CGO_CPPFLAGS="${CPPFLAGS}"
    export CGO_CFLAGS="${CFLAGS}"
    export CGO_CXXFLAGS="${CXXFLAGS}"
    export CGO_LDFLAGS="${LDFLAGS}"
    export GOPATH="${srcdir}"
    export GOFLAGS="-buildmode=pie -mod=readonly -modcacherw"
    go build -ldflags "-compressdwarf=false -linkmode external -X gitea.com/gitea/act_runner/internal/pkg/ver.version=$pkgver"
    "${srcdir}/act_runner/act_runner" generate-config > "$srcdir/act_runner.yaml"
}

package() {
    install -Dm755 "$srcdir/$pkgname/act_runner" "$pkgdir/usr/bin/act_runner"
    install -Dm644 "$srcdir/$pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
    install -Dm644 "$srcdir/act_runner.service" "$pkgdir/usr/lib/systemd/system/act_runner.service"
    install -Dm644 "$srcdir/act_runner.sysusers" "$pkgdir/usr/lib/sysusers.d/act_runner.conf"
    install -Dm644 "$srcdir/act_runner.tmpfiles" "$pkgdir/usr/lib/tmpfiles.d/act_runner.conf"
    install -Dm644 "$srcdir/act_runner.yaml" "$pkgdir/etc/act_runner/act_runner.yaml"
}
