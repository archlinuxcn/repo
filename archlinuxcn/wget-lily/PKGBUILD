# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Eric Bélanger <eric@archlinux.org>

pkgname=wget-lily
pkgver=1.20.3
pkgrel=3
pkgdesc='Network utility to retrieve files from the Web'
url='https://www.gnu.org/software/wget/wget.html'
arch=('x86_64')
license=('GPL3')
depends=('glibc' 'gnutls' 'libidn2' 'libutil-linux' 'libpsl' 'pcre2')
checkdepends=('perl-http-daemon' 'perl-io-socket-ssl' 'python')
optdepends=('ca-certificates: HTTPS downloads')
conflicts=('wget')
provides=("wget=$pkgver")
backup=('etc/wgetrc')
source=(https://ftp.gnu.org/gnu/wget/wget-${pkgver}.tar.lz{,.sig} wget.patch)
sha256sums=('69607ce8216c2d1126b7a872db594b3f21e511e660e07ca1f81be96650932abb'
            'SKIP' aa28cf8532f6b7cc1fe689eb253b0453102990eb725a6583704ca4f3e665c9b0)
validpgpkeys=('AC404C1C0BF735C63FF4D562263D6DF2E163E1EA'
              '7845120B07CBD8D6ECE5FF2B2A1743EDA91A35B6'
              '1CB27DBC98614B2D5841646D08302DB6A2670428') # Tim Rühsen <tim.ruehsen@gmx.de>

prepare() {
  cd wget-${pkgver}
  cat >> doc/sample.wgetrc <<EOF

# default root certs location
ca_certificate=/etc/ssl/certs/ca-certificates.crt
EOF
  patch -p1 -i "${srcdir}/wget.patch"
}

build() {
  cd wget-${pkgver}
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --disable-rpath \
    --enable-nls \
    --with-ssl=gnutls
  make
}

check() {
  cd wget-${pkgver}
  make check < /dev/null
}

package() {
  cd wget-${pkgver}
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
