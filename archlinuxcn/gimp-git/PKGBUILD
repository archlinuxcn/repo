# Maintainer: Filipe La√≠ns (FFY00) <filipe.lains@gmail.com>
# Contributor: Iru Cai <mytbk920423@gmail.com>
# Contributor: Alexander Hunziker <alex.hunziker@gmail.com>
# Contributor: Alessio Biancalana <dottorblaster@gmail.com>

pkgname=gimp-git
_pkgname=${pkgname%-git}
epoch=1
pkgver=45227.r07f95291a5.
pkgrel=1
pkgdesc="GNU Image Manipulation Program"
arch=('i686' 'x86_64')
url="https://www.gimp.org"
license=('GPL' 'LGPL')
depends=('pygtk' 'lcms2>=2.2' 'libwmf>=0.2.8' 'icu' 'aalib' 'enchant'
         'libgexiv2' 'librsvg>=2.16.1' 'desktop-file-utils'
         'libexif>=0.6.15' 'libart-lgpl>=2.3.19' 'dbus-glib' 'gtk-doc'
         'poppler-glib' 'poppler-data' 'openexr>=1.6.1' 'mypaint-brushes1>=1.3.0'
         'babl-git' 'gegl-git' 'cairo>=1.14' 'appstream-glib>=0.7.7' 'gjs')
makedepends=('git' 'intltool' 'gutenprint>=5.0.0' 'intltool>=0.40.1'
             'alsa-lib>=1.0.0' 'libxslt' 'glib-networking'
             'alsa-lib' 'curl' 'ghostscript' 'libxpm' 'webkit2gtk' 'python-gobject'
             'libheif' 'libwebp' 'libmng' 'iso-codes' 'zlib' 'gobject-introspection')
checkdepends=('xorg-server-xvfb')
optdepends=('gutenprint: for sophisticated printing only as gimp has built-in cups print support'
            'alsa-lib: for MIDI event controller module'
            'curl: for URI support'
            'ghostscript: for postscript support'
            'libxpm: XPM support'
            'webkit2gtk: HTML renderer and web content engine'
            'libheif: HEIF support'
            'libwebp: WebP support'
            'libmng: MNG support'
            'iso-codes: Language support'
            'aalib: ASCII art support'
            'zlib: Compression routines')
#provides=('gimp')
#conflicts=('gimp')
source=('git+https://gitlab.gnome.org/GNOME/gimp.git'
        'linux.gpl')
sha512sums=('SKIP'
            '6f33d57f242fa8ce04b65e06a712bd54677306a45b22cb853fbe348089cd4673bd4ed91073074fe067166fe8951c370f8bbbc386783e3ed5170d52e9062666fe')

pkgver() {
  cd $_pkgname

  printf "%s.r%s.%s" \
  	$(cat configure.ac | grep '^m4_define(\[gimp_api_version\], \[.*\])' | sed -e 's|m4_define(\[gimp_api_version\], \[||' -e 's|\])||') \
  	$(git rev-list --count HEAD) \
  	$(git rev-parse --short HEAD)
}

build() {
  cd $_pkgname

  _mypaintver=$(echo /usr/lib/libmypaint-*.so | grep -o -E '\-[0-9]+(\.[0-9]+)*' | head -1)
  sed -i "s|\\(libmypaint\\)\\( >= libmypaint_required_version\\)|\\1${_mypaintver}\\2|g" configure.ac

  export PYTHON=/usr/bin/python3

  ./autogen.sh

  ./configure \
  	--prefix=/usr \
  	--sysconfdir=/etc \
  	--libexecdir=/usr/bin \
  	--enable-mp \
  	--enable-gimp-console \
  	--enable-gimp-remote \
  	--enable-python \
  	--enable-gtk-doc \
  	--with-gif-compression=lzw \
  	--with-libcurl \
  	--with-openexr

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  PYTHONPATH=/usr/share/glib-2.0:$PYTHONPATH make
}

#check() {
#  cd $_pkgname
#
#  xvfb-run make check
#  xvfb-run make distcheck
#}

package() {
  cd $_pkgname

  make DESTDIR="$pkgdir" install

  install -Dm 644 "$srcdir"/linux.gpl "$pkgdir"/usr/share/gimp/2.99/palettes/Linux.gpl
  
  #fix gimp.desktop
  mv ${pkgdir}/usr/share/applications/gimp.desktop ${pkgdir}/usr/share/applications/gimp-2.99.desktop
  sed -i 's/Icon=gimp/&-2.99/' ${pkgdir}/usr/share/applications/gimp-2.99.desktop

  #fix icons
  for icon in $(find ${pkgdir}/usr/share/icons -type f); do
    mv ${icon} ${icon%.png}-2.99.png
  done

  #fix metainfo
  rm -rf ${pkgdir}/usr/share/metainfo
}

