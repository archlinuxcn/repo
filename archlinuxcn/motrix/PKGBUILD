# Maintainer: zhullyb <zhullyb [at] outlook dot com>
# Contributor: weearc <q19981121 [at] 163 dot com>
pkgname=motrix
_pkgname=Motrix
pkgver=1.6.11
pkgrel=3
epoch=
pkgdesc="A full-featured download manager (release version)"
arch=("x86_64")
url="https://github.com/agalwood/Motrix"
license=('MIT')
groups=()
depends=('gtk3' 'libxcb' 'electron11')
makedepends=('aria2-fast' 'npm' 'yarn' "nodejs<17")
checkdepends=()
optdepends=()
provides=()
conflicts=('motrix-git')
replaces=()
backup=()
options=()
install=
changelog=
source=("motrix.desktop"
    "motrix"
    "Motrix.tar.gz"::"https://github.com/agalwood/Motrix/archive/v${pkgver}.tar.gz"
    "https://raw.githubusercontent.com/agalwood/Motrix/732327f5a3516c875b06401ef0c9b8b5d2e3029c/package.json"
    "https://raw.githubusercontent.com/agalwood/Motrix/732327f5a3516c875b06401ef0c9b8b5d2e3029c/yarn.lock")
noextract=()
sha256sums=('af5092a2a599bd23c13303ad1e7b745992a7af141278d13abe4297ca50a77bd8'
            'becf35e632c6124792a2c8ee9233f3357ffbee08b81820c3f41bdbb5a2a7ead0'
            '9a1558063d32dd100aa289db601f01497d518c070f0ba570efd446830697089e'
            '13f0c3f8a785c368e2054b90c72129a0359943e001ba0113a2a3921ee467e14c'
            'c051a2cef0fadf678e239f69d34454c7cdecd0bd8bd9905437a827f833653fcd')
validpgpkeys=()

prepare() {
    mv ${_pkgname}-${pkgver} ${_pkgname}

    # a small fix for v1.6.11
    mv ${srcdir}/package.json ${_pkgname}/
    mv ${srcdir}/yarn.lock ${_pkgname}/
}

build() {
    cd ${_pkgname}/
    export YARN_CACHE_FOLDER="${srcdir}/yarn_cache"
    yarn
    yarn run build:dir
}

package() {

    install -Dm 644 ${srcdir}/${_pkgname}/release/linux-unpacked/resources/app.asar ${pkgdir}/usr/lib/${pkgname}/app.asar
    install -Dm 644 ${srcdir}/${_pkgname}/release/linux-unpacked/resources/engine/aria2.conf ${pkgdir}/usr/lib/${pkgname}/engine/aria2.conf

    # Copy aria2-fast from system. Motrix set 64 download threads as default, so if use original aria2c will get error.
    install -Dm 775 /usr/bin/aria2c ${pkgdir}/usr/lib/${pkgname}/engine/aria2c

    # binary wrapper
    install -Dm 775 ${srcdir}/motrix ${pkgdir}/usr/bin/${pkgname}

    # desktop enrty
    install -Dm 644 ${srcdir}/motrix.desktop ${pkgdir}/usr/share/applications/${pkgname}.desktop

    # icons
    install -Dm 644 ${srcdir}/${_pkgname}/build/256x256.png ${pkgdir}/usr/share/icons/${pkgname}.png
}
