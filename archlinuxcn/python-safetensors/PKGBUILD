# Maintainer: Butui Hu <hot123tea123@gmail.com>

_pkgname=safetensors
pkgname=python-safetensors
pkgver=0.5.3
pkgrel=1
epoch=1
pkgdesc='Simple, safe way to store and distribute tensors'
arch=('x86_64')
url='https://github.com/huggingface/safetensors'
license=('Apache-2.0')
depends=(
  gcc-libs
  glibc
  python
)
makedepends=(
  python-build
  python-installer
  python-maturin
  python-setuptools
  python-setuptools-rust
  python-wheel
)
optdepends=(
  python-numpy
  python-pytorch
  python-tensorflow
  python-huggingface-hub
)
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/huggingface/safetensors/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('1778ebe45e2b7ee181ae68171e197734d510cb312a686b476821ebcaa99225e0068a53f1f369a3335701d70bed0887b4bb9047997884353567c2692ea9784a49')

build() {
  cd "${_pkgname}-${pkgver}/bindings/python"
  python -m build --wheel --no-isolation
}

package() {
  cd "${_pkgname}-${pkgver}/bindings/python"
  python -m installer --destdir="${pkgdir}" dist/*.whl
}
# vim:set ts=2 sw=2 et:
