# Maintainer: Butui Hu <hot123tea123@gmail.com>

_pkgname=safetensors
pkgname=python-safetensors
pkgver=0.4.5
pkgrel=2
epoch=1
pkgdesc='Simple, safe way to store and distribute tensors'
arch=('x86_64')
url='https://github.com/huggingface/safetensors'
license=('Apache-2.0')
depends=(
  gcc-libs
  glibc
  python
)
makedepends=(
  python-build
  python-installer
  python-maturin
  python-setuptools
  python-setuptools-rust
  python-wheel
)
optdepends=(
  python-numpy
  python-pytorch
  python-tensorflow
  python-huggingface-hub
)
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/huggingface/safetensors/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('e465983d139d40c2924e5053ace21150b4321eb5e5145c2d1fcd80f2ec7f0709e14160a3959892cfc0953b16f94a1a79ec1648086cf8461dacf2ee8a7c1f75fd')

build() {
  cd "${_pkgname}-${pkgver}/bindings/python"
  python -m build --wheel --no-isolation
}

package() {
  cd "${_pkgname}-${pkgver}/bindings/python"
  python -m installer --destdir="${pkgdir}" dist/*.whl
}
# vim:set ts=2 sw=2 et:
