# Maintainer: Wesley Moore <wes@wezm.net>

pkgname=fnm
pkgver=1.38.1
pkgrel=1
pkgdesc="Fast and simple Node.js version manager, built with Rust"
arch=('x86_64')
url="https://github.com/Schniz/${pkgname}"
license=('GPL-3.0-or-later')
depends=('xz' 'bzip2' 'gcc-libs')
makedepends=('cargo')
conflicts=('fnm-bin')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha256sums=('c24e4c26183a4d88a33e343902ed2d45da23e78c66b2a696a7420eb86deddda9')
options=('!lto')

build() {
	cd "${pkgname}-${pkgver}/"
	cargo build --release --locked
}

package() {
	cd "${pkgname}-${pkgver}/"
	install -Dm755 "target/release/${pkgname}" -t "${pkgdir}/usr/bin/"

	mkdir -p \
		"${pkgdir}/usr/share/bash-completion/completions" \
		"${pkgdir}/usr/share/fish/vendor_completions.d" \
		"${pkgdir}/usr/share/zsh/site-functions"

	"${pkgdir}/usr/bin/${pkgname}" completions --shell bash >"${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
	"${pkgdir}/usr/bin/${pkgname}" completions --shell fish >"${pkgdir}/usr/share/fish/vendor_completions.d/${pkgname}.fish"
	"${pkgdir}/usr/bin/${pkgname}" completions --shell zsh >"${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"

	install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
	install -d "${pkgdir}/usr/share/doc/${pkgname}/"
	cp docs/* "${pkgdir}/usr/share/doc/${pkgname}/"
}
