# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>

pkgname=stellarium
pkgver=24.3
pkgrel=1
pkgdesc="A stellarium with great graphics and a nice database of sky-objects"
arch=('x86_64')
url="https://stellarium.org/"
license=('GPL-2.0-or-later')
depends=('libpng' 'libgl' 'freetype2' 'openssl' 'gpsd'
         'qt6-serialport' 'qt6-multimedia' 'qt6-positioning' 'qt6-charts' 'calcmysky-qt6'
         "qt6-webengine" "qxlsx-qt6" "nlopt")
makedepends=('cmake' 'mesa' 'qt6-tools')
source=(https://github.com/Stellarium/stellarium/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz{,.asc}
)
validpgpkeys=('79151C2E6351E7278DA1A730BF38D4D02A328DFF') # Alexander Wolf <alex.v.wolf@gmail.com>
sha256sums=('c3ffb56a049061c7754bafab176146a2c4474ecede108d650f3c7551e1eae50a'
            'SKIP')

build() {
  cd ${pkgname}-${pkgver}
  # Have to use vendor indilib because it does not support indi>2.0 yet
  cmake . -DENABLE_XLSX=1 -DCPM_USE_LOCAL_PACKAGES=yes -DCMAKE_INSTALL_PREFIX=/usr -DPREFER_SYSTEM_INDILIB=No
  make
}


package() {
  cd ${pkgname}-${pkgver}
 
  make DESTDIR="${pkgdir}" install
}
