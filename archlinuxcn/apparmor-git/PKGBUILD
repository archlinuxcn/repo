# Maintainer: Roald Clark <roaldclark@gmail.com>
# Contributor: nl6720 <nl6720@archlinux.org>
# Contributor: David Runge <dvzrv@archlinux.org>

pkgname=apparmor-git
_pkgname=${pkgname%-git}
pkgver=4.1.0.beta1.r213.g2b45586
pkgrel=1
pkgdesc="Mandatory Access Control (MAC) using Linux Security Module (LSM)"
arch=('x86_64')
url="https://gitlab.com/apparmor/apparmor"
license=(
    'GPL-2.0-only'
    'LGPL-2.0-only'
    'LGPL-2.1-only'
)
depends=(
    'audit'
    'bash'
    'gcc-libs'
    'glibc'
    'pam'
    'python'
)
makedepends=(
    'apache' # depend on 'libxcrypt'
    'autoconf-archive'
    'git'
    'libxcrypt'
    'perl'
    'python-setuptools'
    'ruby'
    'swig'
)
checkdepends=(
    'dejagnu'
    'flake8'
    #'perl-locale-gettext' # liblocale-gettext-perl by apparmor/apparmor/.gitlab-ci.yml
    'python-coverage'
    'python-gobject'
    'python-notify2'
    'python-psutil'
    'python-ttkthemes' # depend on 'tk'
    'tk'
)
optdepends=(
    'apparmor.d: full set of AppArmor profiles'
    'perl: for perl bindings'
    'python-notify2: for aa-notify'
    'python-psutil: for aa-notify'
    'ruby: for ruby bindings'
)
conflicts=("$_pkgname")
provides=(
    "${_pkgname}=${pkgver}"
    'libapparmor.so'
)
backup=(
    'etc/apparmor/easyprof.conf'
    'etc/apparmor/logprof.conf'
    'etc/apparmor/notify.conf'
    'etc/apparmor/parser.conf'
    'etc/apparmor/severity.db'
)
source=("git+${url}.git")
sha256sums=('SKIP')
validpgpkeys=('3ECDCBA5FB34D254961CC53F6689E64E3D3664BB') # AppArmor Development Team (AppArmor signing key) <apparmor@lists.ubuntu.com>

_core_perl="/usr/bin/core_perl"

pkgver() {
    cd "${srcdir}/${_pkgname}"

    git describe --long --abbrev=7 | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    cd "${srcdir}/${_pkgname}"

    cd libraries/libapparmor
    autoreconf --force --install --verbose # -fiv
}

build() {
    cd "${srcdir}/${_pkgname}"

    local configure_options=(
        --prefix=/usr
        --sbindir=/usr/bin
        --with-perl
        --with-python
        --with-ruby
    )

    # export required perl executable locations
    export MAKEFLAGS+=" POD2MAN=${_core_perl}/pod2man"
    export MAKEFLAGS+=" POD2HTML=${_core_perl}/pod2html"
    export MAKEFLAGS+=" PODCHECKER=${_core_perl}/podchecker"
    export MAKEFLAGS+=" PROVE=${_core_perl}/prove"
    (
        cd libraries/libapparmor/
        ./configure "${configure_options[@]}"
        make
    )
    make -C parser
    make -C binutils
    make -C utils
    make -C changehat/pam_apparmor
    make -C changehat/mod_apparmor
    make -C profiles

    # copy to test location as some tests render the resulting python bytecode
    # unreproducible: https://gitlab.com/apparmor/apparmor/-/issues/184
    cd ..
    cp -av "${_pkgname}" "${_pkgname}-test"
}

check() {
    cd "${srcdir}/${_pkgname}"

    cd "../${_pkgname}-test"

    echo "INFO: Running check libraries/libapparmor"
    make -C libraries/libapparmor check

    echo "INFO: Running check parser"
    make -C parser check

    echo "INFO: Running check binutils"
    make -C binutils check

    echo "INFO: Running check utils"
    make -C parser/tst gen_dbus
    make -C parser/tst gen_xtrans
    make -C utils check
    make -C utils/test coverage-regression

    echo "INFO: Running check mod_apparmor"
    make -C changehat/mod_apparmor check

    echo "INFO: Running check profiles"
    make -C profiles check-parser
    #make -C profiles check-abstractions.d
    #make -C profiles check-local
}

package() {
    cd "${srcdir}/${_pkgname}"

    make -C libraries/libapparmor DESTDIR="${pkgdir}" install
    make -C parser -j1 DESTDIR="${pkgdir}" SBINDIR="${pkgdir}/usr/bin" USR_SBINDIR="${pkgdir}/usr/bin" APPARMOR_BIN_PREFIX="${pkgdir}/usr/lib/apparmor" install install-systemd
    make -C binutils DESTDIR="${pkgdir}" SBINDIR="${pkgdir}/usr/bin" USR_SBINDIR="${pkgdir}/usr/bin" install
    make -C utils DESTDIR="${pkgdir}" SBINDIR="${pkgdir}/usr/bin" USR_SBINDIR="${pkgdir}/usr/bin" BINDIR="${pkgdir}/usr/bin" VIM_INSTALL_PATH="${pkgdir}/usr/share/vim/vimfiles/syntax" install
    make -C changehat/pam_apparmor DESTDIR="${pkgdir}/usr" install
    make -C changehat/mod_apparmor DESTDIR="${pkgdir}" install
    make -C profiles DESTDIR="${pkgdir}" install

    # set file mode to allow the perl library to be stripped:
    # https://gitlab.com/apparmor/apparmor/issues/34
    find "${pkgdir}/usr/lib/perl5/" -type f -iname "*.so" -exec chmod 755 {} \;

    # remove empty core_perl directory:
    # https://gitlab.com/apparmor/apparmor/issues/40
    rm -rv "${pkgdir}"/usr/lib/perl5/*/core_perl
    # move ruby bindings to vendor_ruby:
    # https://gitlab.com/apparmor/apparmor/issues/35
    mv -v "${pkgdir}"/usr/lib/ruby/{site,vendor}_ruby
    # adding files below /etc/apparmor.d to backup array
    cd "${pkgdir}"
    # trick extract_function_variable() in makepkg into not detecting the
    # backup array modification and adding remaining configuration files
    [[ /usr/bin/true ]] && backup=( ${backup[@]} $(find "etc/${_pkgname}.d/" -type f | LC_ALL=C sort) )

    # check if apparmor.d package is already installed
    if pacman -Qq apparmor.d &> /dev/null; then
        echo "apparmor.d is installed, checking for file conflicts..."

        APPARMORD_FILES=$(mktemp)
        pacman -Qlq apparmor.d > "${APPARMORD_FILES}"

        # Check for conflicts with files from apparmor.d package
        find "${pkgdir}/etc/apparmor.d/" -type f | while read -r file; do
            apparmor_file="${file#$pkgdir}"
            if grep -Fxq "${apparmor_file}" "${APPARMORD_FILES}"; then
                rm -v "$file"
            fi
        done
        rm "${APPARMORD_FILES}"
    else
        echo "apparmor.d is not installed, skipping conflict checks"
    fi
}
