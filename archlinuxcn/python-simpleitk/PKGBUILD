# Maintainer: Butui Hu <hot123tea123@gmail.com>

_name=SimpleITK
pkgname=python-simpleitk
pkgver=2.2.1
pkgrel=1
pkgdesc="A layer built on top of the Insight Toolkit (ITK), intended to simplify and facilitate ITK's use in rapid prototyping, education and interpreted languages. This is the python binding."
arch=('x86_64')
url='http://www.simpleitk.org'
license=('Apache')
depends=(
  python-numpy
)
makedepends=(
  doxygen
  git
  python-scikit-build
  python-setuptools
  python-pip
)
source=("${pkgname}-${pkgver}::git+https://github.com/SimpleITK/SimpleITKPythonPackage.git"
        "git+https://github.com/SimpleITK/SimpleITK.git#tag=v${pkgver}")
sha512sums=('SKIP'
            'SKIP')

prepare() {
  cd "${pkgname}-${pkgver}"
  git submodule init
  git config submodule.${_name}.url "${srcdir}/${_name}"
  git -c protocol.file.allow=always submodule update --init --recursive
}

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py bdist_wheel
}

package() {
  cd "${pkgname}-${pkgver}"
  PIP_CONFIG_FILE=/dev/null pip install --isolated --root="${pkgdir}" --ignore-installed --no-deps dist/*.whl
  python -O -m compileall "${pkgdir}"
}
# vim:set ts=2 sw=2 et:
