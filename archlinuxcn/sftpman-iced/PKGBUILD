# Maintainer: Roald Clark <roaldclark at gmail.com>
# Contributor: Slavi Pantaleev <slavi at devture.com>

_name=sftpman-iced-rs
pkgname=sftpman-iced
pkgver=2.1.0
pkgrel=1
pkgdesc="A GUI frontend to sftpman (an application for managing and mounting sshfs (SFTP) filesystems)"
arch=('x86_64' 'aarch64')
url="https://github.com/spantaleev/${_name}"
license=('AGPL-3.0-or-later')
# sftpman-iced requires libsftpman, but pulls it via cargo
# There's no runtime dependency on sftpman
depends=(
    'fuse3'
    'gcc-libs'
    'glibc'
    'sshfs'
)
makedepends=(
    'cargo'
    'git'
)
optdepends=(
    'sftpman: CLI version'
    'ksshaskpass: ssh-add helper and passphrase dialog for KDE'
    'seahorse: ssh-add helper and passphrase dialog for GNOME'
)
replaces=('sftpman-gtk')
install="${pkgname}.install"
source=("git+${url}.git#tag=v${pkgver}")
b2sums=('09018bac8466d6aff6c6c68ce986ba90887111ce85b6c013640fcce519790cd4307375248be0560affb6a4354143867d25b635644a890e5fc8f7bb217780dd20')

prepare() {
    cd "${srcdir}/${_name}"
    export RUSTUP_TOOLCHAIN=stable
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
    cd "${srcdir}/${_name}"
    export RUSTUP_TOOLCHAIN=stable
    export CARGO_TARGET_DIR=target
    cargo build --release --frozen --all-features
}

check() {
    cd "${srcdir}/${_name}"
    export RUSTUP_TOOLCHAIN=stable
    cargo test --frozen --all-features
}

package() {
    cd "${srcdir}/${_name}"
    install -Dm0755 "target/release/${pkgname}" -t "${pkgdir}/usr/bin/"
    install -Dm0644 "etc/${pkgname}.desktop" -t "${pkgdir}/usr/share/applications/"
    install -Dm0644 "assets/${pkgname}-512.png" "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
}
