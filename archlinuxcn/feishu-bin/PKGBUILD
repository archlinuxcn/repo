# Generated by debtap
# Maintainer: Allen Zhong <allen@moe.cat>
# Contributor: Feishu Linux Team <contact@feishu.cn>
pkgname=feishu-bin
pkgver=5.4.13
#_pkgrel=24
_pkgtyp=dev
_pkghash=e734d9e40e6a
pkgrel=1
pkgdesc="Linux client of Feishu (Lark) from Bytedance."
arch=('x86_64')
url="https://www.feishu.cn/"
license=('unknown')
depends=('ca-certificates' 'gtk3' 'nss' 'xdg-utils')
replaces=('bytedance-feishu-stable-bin')
conflicts=('feishu')
provides=('bytedance-feishu' 'feishu')
options=('!strip' '!emptydirs')
install=${pkgname}.install
source=("https://sf3-cn.feishucdn.com/obj/ee-appcenter/${_pkghash}/Feishu-linux_x64-${pkgver}.deb")
sha256sums=('26dddc20e13f733640dbf8669f4dc24f8c098d5db71f9f86ffe33546a68a5986')

package(){
  # Extract package data
  tar xpvf "${srcdir}/data.tar.xz" --xattrs-include='*' --numeric-owner -C "${pkgdir}"

  # Modify files
  cd "${pkgdir}"
  ln -s bytedance-feishu-${_pkgtyp} usr/bin/feishu
  sed -i "s/bytedance-feishu-${_pkgtyp}/feishu/g" "${pkgdir}/usr/share/applications/bytedance-feishu.desktop"
  sed -i "s/bytedance-feishu/feishu/g" "${pkgdir}/usr/share/menu/bytedance-feishu.menu"
  sed -i 's/bytedance-feishu/feishu/g' "${pkgdir}/usr/share/appdata/bytedance-feishu.appdata.xml"
  mv "${pkgdir}"/usr/share/menu/{bytedance-,}feishu.menu
  mv "${pkgdir}"/usr/share/applications/{bytedance-,}feishu.desktop
  mv "${pkgdir}"/usr/share/appdata/{bytedance-,}feishu.appdata.xml
  mv "${pkgdir}"/usr/share/man/man1/{bytedance-,}feishu.1.gz
  mv "${pkgdir}"/usr/share/doc/{bytedance-feishu-${_pkgtyp},feishu}

  # Fix directory permissions
  find "${pkgdir}" -type d | xargs chmod 755
}
