# Maintainer: Coelacanthus <uwu@coelacanthus.name>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: David Herrmann <dh.herrmann@gmail.com>

pkgname=libtsm
pkgver=4.3.0
pkgrel=1
pkgdesc='Terminal-emulator State Machine'
arch=('x86_64')
url="https://www.freedesktop.org/wiki/Software/kmscon/$pkgname"
license=('MIT')
depends=(
  'glibc'
)
makedepends=(
  'libxkbcommon'
  'meson'
)
checkdepends=(
  'check'
)
provides=('libtsm.so')
source=(
  "https://github.com/Aetf/libtsm/archive/v$pkgver/$pkgname-$pkgver.tar.gz"
)
b2sums=('fc8071011e4c8833c96e6e2aa09490ddd51f6468e11c87e3ae58f818db29b11c737d9c6440539be108a636f36dabee1148beaf6a24a59f4f102bc99b52eee95c')

prepare() {
  cd $pkgname-$pkgver
}

build() {
  local _meson_options=(
  )
  arch-meson $pkgname-$pkgver build "${_meson_options[@]}"
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --no-rebuild --destdir="$pkgdir"
  install -Dm644 $pkgname-$pkgver/COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
# vim:set ts=2 sw=2 et:
