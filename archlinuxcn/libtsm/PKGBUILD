# Maintainer: Coelacanthus <uwu@coelacanthus.name>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: David Herrmann <dh.herrmann@gmail.com>

pkgname=libtsm
pkgver=4.4.0
pkgrel=1
pkgdesc='Terminal-emulator State Machine'
arch=('x86_64')
url="https://www.freedesktop.org/wiki/Software/kmscon/$pkgname"
license=('MIT')
depends=(
  'glibc'
)
makedepends=(
  'libxkbcommon'
  'meson'
)
checkdepends=(
  'check'
)
provides=('libtsm.so')
source=(
  "https://github.com/kmscon/libtsm/archive/v$pkgver/$pkgname-$pkgver.tar.gz"
)
b2sums=('261e7a420aef465ddcee52685acc7809beea05d563ee42435e8f01d2a6f20dd1d16432e1274eeb919f121b9ee770e43af9c9755ec96234ee1c1cfae950b29089')

prepare() {
  cd $pkgname-$pkgver
}

build() {
  local _meson_options=(
  )
  arch-meson $pkgname-$pkgver build "${_meson_options[@]}"
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --no-rebuild --destdir="$pkgdir"
  install -Dm644 $pkgname-$pkgver/COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
# vim:set ts=2 sw=2 et:
