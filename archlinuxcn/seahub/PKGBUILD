# Maintainer: Joffrey <j-off@live.fr>
# Contributor: eolianoe <eolianoe [at] gmail [DoT] com>
# Contributor: Edvinas Valatka <edacval@gmail.com>
# Contributor: Aaron Lindsay <aaron@aclindsay.com>

pkgname=seahub
pkgver=7.1.3
pkgrel=1
pkgdesc='The web frontend for seafile server'
arch=('any')
url='https://github.com/haiwen/seahub'
license=('Apache')
makedepends=('python-setuptools')
source=("$pkgname-$pkgver-server.tar.gz::$url/archive/v$pkgver-server.tar.gz"
        "https://www.djangoproject.com/m/releases/1.11/Django-1.11.25.tar.gz"
        fix_shared_link.patch)
sha256sums=('a2cf1ad1ff357b06c112f3f80e2e4a2ef109813496c96afc309449f6915e8797'
            '5314e8586285d532b7aa5c6d763b0248d9a977a37efec86d30f0212b82e8ef66'
            'f2af699359155b0a303044e6027828950fb3eaf11795b9143896b0a5a4431b31')
options=('!strip')

prepare() {
    cd "$srcdir/$pkgname-$pkgver-server"
    patch -Np1 --no-backup-if-mismatch < ../fix_shared_link.patch
    patch -Np1 --no-backup-if-mismatch < ../fix_toolbar.patch

    # Remove useless files and directories
    rm -rf \
        './'{CONTRIBUTORS,HACKING,Makefile} \
        './'{*test*,*dev*,*sh*,README*,pylintrc*,LICENSE*}
}

build() {
    cd "$srcdir/$pkgname-$pkgver-server"
    for locale in ./locale/*/LC_MESSAGES/*.po; do
        msgfmt -vo "${locale%.po}.mo" "$locale"
    done

    cd "$srcdir/Django-1.11.25"
    python setup.py build
}

package() {
    depends=(
        "seafile-server>=$pkgver"
        # The following list is from seahub/requirements.txt
        # python-django # 1.11.25
        'python-future'
        # 'python-captcha' # This one seems to conflict with django-simple-captcha
        'python-django-statici18n'
        'python-django-post-office'
        'python-django-webpack-loader'
        'gunicorn'
        'python-pymysql'
        'python-django-picklefield'
        'python-openpyxl'
        'python-qrcode'
        'python-django-formtools'
        'python-django-simple-captcha'
        'python-django-rest-framework'
        'python-dateutil'
        'python-requests'
        'python-pillow'
        'python-pyjwt'
        'python-pycryptodome'
    )
    optdepends=(
      'ffmpeg: For video thumbnails'
      'python-seafdav: For WebDAV support'
    )
    cd "$srcdir/seahub-$pkgver-server/"
    install -dm755 "$pkgdir/usr/share/seafile-server/seahub"
    cp -r -p "./"* "$pkgdir/usr/share/seafile-server/seahub/"

    cd "$srcdir/Django-1.11.25"
    python setup.py install --root="$pkgdir" --optimize=1 \
        --install-lib "usr/share/seafile-server/seahub/thirdpart"
    rm -rf "$pkgdir/usr/bin/"
}
