# Maintainer: Kimiblock Moe

pkgname=fractal-portable
pkgver=1
pkgrel=1
epoch=1
pkgdesc="Fractal sandboxed by portable. No dGPU wakeups."
arch=('x86_64')
url="https://github.com/Kraftland/portable"
license=('GPL-3.0-or-later')
groups=()
provides=(fractal)
options=(!debug !strip)
conflicts=("fractal")

optdepends=()

makedepends+=(fractal coreutils)

checkdepends=()

source=(
	portable-config
	fractal.sh
	fractal.desktop
)


md5sums=('1ce4e3c8db9bb3f86bbb0aaf5fd583bf'
         '7b9045a876cb70cfce509dcef69d6561'
         '7c0b0bae889e55058c366fc47d9edd25')

function prepare() {
	pacman -Ql fractal >file.list
}

function package() {
	depends=("portable")
	depends+=(dconf  fontconfig  gcc-libs  gdk-pixbuf2  glib2  glibc  glycin1  graphene  gst-plugins-bad-libs  gst-plugins-base-libs  gst-plugin-gtk4  gstreamer
                  gtk4  gtksourceview5  hicolor-icon-theme  lcms2  libadwaita  libpipewire  libseccomp  libshumate  openssl  org.freedesktop.secrets  pango  sqlite)
	while IFS= read -r line; do
		file="$(echo "$line" | awk '{print $2}')"
		if [[ -d ${file} ]]; then
			echo "Omitting Directory"
		else
			if [[ -L "${file}" ]]; then
				mkdir -p "$(dirname "${pkgdir}/${file}")"
				ln -vsf "$(readlink -f "${file}")" "${pkgdir}/${file}"
			else
				install -vDm755 "${file}" "${pkgdir}/${file}"
			fi
		fi
	done < file.list
	install -vDm755 "${pkgdir}/usr/bin"/* -t "${pkgdir}/usr/lib/portable/overlay-usr"
	rm -f "${pkgdir}/usr/share/applications"/*
	rm -f "${pkgdir}/usr/bin"/*
	install -Dm644 portable-config \
		"${pkgdir}/usr/lib/portable/info/org.gnome.Fractal/config"
	install -Dm755 "fractal.sh" \
		"${pkgdir}/usr/bin/fractal"
	install -Dm644 "fractal.desktop" "${pkgdir}/usr/share/applications/org.gnome.Fractal.desktop"
}
