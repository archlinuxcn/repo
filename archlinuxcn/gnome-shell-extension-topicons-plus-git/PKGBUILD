# Maintainer: Justin Dray <justin@dray.be>
# Contributor: surefire@cryptomile.net

_srcname=TopIcons-plus
pkgname=gnome-shell-extension-topicons-plus-git
pkgver=22+27+ge51ac0a
pkgrel=1
pkgdesc="Shows legacy tray icons on top."
arch=('any')
url="https://extensions.gnome.org/extension/1031/topicons/"
license=('GPL')
depends=('gnome-shell')
makedepends=('git')
conflicts=('gnome-shell-extension-topicons-plus')
provides=('gnome-shell-extension-topicons-plus')
groups=('gnome-shell-extensions')
source=("git+https://github.com/phocean/${_srcname}.git")
sha256sums=('SKIP')

pkgver() {
	cd "${_srcname}"
	git describe --tags --long | sed 's/^v//; s/-/+/g'
}

build() {
	cd "${_srcname}"
	make build
}

package() {
	cd "${_srcname}"
	local uuid=$(grep -Po '(?<="uuid": ")[^"]*' _build/metadata.json)
	local destdir="$pkgdir/usr/share/gnome-shell/extensions/$uuid"

	install -dm755 "$destdir"

	cp -r _build/* "$destdir"
}
