# Maintainer: Lorenzo Fontana <lo@linux.com>
# Maintainer: Xuanwo <xuanwo@archlinuxcn.org>
pkgname=coredns
pkgver=1.6.7
pkgrel=1
pkgdesc='A DNS server that chains plugins'
makedepends=('go' 'make' 'git')
conflicts=('coredns-bin')
arch=('i686' 'x86_64')
url='https://github.com/coredns/coredns'
license=('Apache')
provides=('coredns')
backup=(etc/coredns/Corefile)
source=(coredns-${pkgver}.tar.gz::https://github.com/coredns/${pkgname}/archive/v${pkgver}.tar.gz
        coredns.service
        coredns-sysusers.conf
        coredns-tmpfiles.conf
        Corefile)
sha256sums=('36460bac70c04cdf9c89c2b930ec4faa2c0dc4cb6573097883f54670a707c818'
            '947b4838af48212ecd8e565155c97ca61ff7d408d0b8622499925b6e232656c4'
            'f4fd89dcb36f52466ebb1a2ae435dc960cdc828a071f3aaae1d83a39696bff30'
            '20052dded9a02e05013a602e597002ee2a003ecfab3309d30969c28207fc5b72'
            '1f05c9af9d9dae221388f2f89b8343d11515674914d2b7d9c2270b6c613d3dc2')

build() {
  export GOPATH="$srcdir/build"
  export PATH=$GOPATH/bin:$PATH

  cd $pkgname-${pkgver}
  # In order to build with PIE buildmode, we need to use CGO.
  # Currently, we will keep cgo disabled for better performance.
  GO111MODULE=on CGO_ENABLED=0 go build -v -ldflags="-s -w" -o coredns
}

package() {
  # Install binary
  install -Dm755 "$srcdir/$pkgname-${pkgver}/coredns" "$pkgdir/usr/bin/coredns"
  # Install systemd service
  install -Dm644 "$srcdir/coredns.service" "$pkgdir/usr/lib/systemd/system/coredns.service"
  # Install coredns sysusers
  install -Dm644 "$srcdir/coredns-sysusers.conf" "$pkgdir/usr/lib/sysusers.d/coredns.conf"
  # Install coredns tmpfiles
  install -Dm644 "$srcdir/coredns-tmpfiles.conf" "$pkgdir/usr/lib/tmpfiles.d/coredns.conf"
  # Install default coredns config
  install -Dm644 Corefile "$pkgdir/etc/coredns/Corefile"
}

# vim: ft=sh syn=sh et
