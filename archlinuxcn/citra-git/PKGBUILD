# Maintainer: Tim Schumacher <timschumi@gmx.de>
# Contributor: Emmanuel Gil Peyrot <linkmauve@linkmauve.fr>
# Contributor: ceri

_pkgbase='citra'
pkgname="$_pkgbase-git"
pkgver=r10031.f4768cd26
pkgrel=1
pkgdesc="An experimental open-source Nintendo 3DS emulator/debugger (Master branch)"
arch=('i686' 'x86_64')
url="https://github.com/citra-emu/citra/"
license=('GPL2')
depends=('boost-libs' 'shared-mime-info' 'ffmpeg' 'libusb' 'sdl2' 'qt6-multimedia' 'libxkbcommon-x11' 'cubeb' 'enet' 'libinih' 'discord-rpc' 'fmt' 'dynarmic' 'openal' 'crypto++' 'soundtouch' 'faad2')
makedepends=('git' 'rapidjson' 'cmake' 'python' 'clang' 'llvm' 'boost' 'qt6-tools' 'xbyak' 'cpp-jwt' 'qt6-wayland' 'desktop-file-utils' 'robin-map' 'glslang' 'spirv-headers' 'vulkan-memory-allocator' 'catch2' 'cpp-httplib')
provides=('citra')
conflicts=('citra')
source=("git+https://github.com/citra-emu/citra.git"
ext-lib.patch
gc.patch)
md5sums=('SKIP'
         'cd9e6be75ae839618f747ea6d653f349'
         'f151510cd1fc233992710405cbf44323')

pkgver() {
	cd "$srcdir/$_pkgbase"
	echo "r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

prepare() {
	cd "$srcdir/$_pkgbase"
    patch -p1 -i ../ext-lib.patch
    patch -p1 -i ../gc.patch
	mkdir -p build
    sed -i 's/OUTVAR ${BUILD_TAG}/OUTVAR "${BUILD_TAG}"/g' CMakeModules/GenerateSCMRev.cmake
    sed -i 's/OUTVAR ${BUILD_REPOSITORY}/OUTVAR "${BUILD_REPOSITORY}"/g' CMakeModules/GenerateBuildInfo.cmake
    sed -i 's/cpp-httplib::cpp-httplib/httplib::httplib/g' externals/CMakeLists.txt
    sed -i 's/CppHttp/httplib/g' externals/CMakeLists.txt
    sed -i 's/0.14.1/0.15.3/g' externals/CMakeLists.txt
    sed -i 's/SPIRV/SPIRV SPIRV-Tools/' src/video_core/CMakeLists.txt

    git submodule update --init --remote externals/lodepng/lodepng
    git submodule update --init --remote externals/nihstro
    git submodule update --init --remote externals/sirit
    git submodule update --init --remote externals/teakra
    git submodule update --init --remote externals/dds-ktx
}

build() {
	# Trick the compiler into thinking we're building from a continuous
	# integration tool so the build number is correctly shown in the title
	cd "$srcdir/$_pkgbase"
	export CI=true
	export TRAVIS=true
	export TRAVIS_REPO_SLUG=citra-emu/citra
	export TRAVIS_TAG=$(git describe --tags)

    CXXFLAGS+=" -I/usr/include/libusb-1.0"
	mkdir -p build && cd build
	cmake .. \
	  -DCMAKE_INSTALL_PREFIX=/usr \
	  -DCMAKE_BUILD_TYPE=Release \
	  -DENABLE_QT_TRANSLATION=ON \
      -DCITRA_ENABLE_COMPATIBILITY_REPORTING=ON \
      -DENABLE_COMPATIBILITY_LIST_DOWNLOAD=ON \
      -DUSE_DISCORD_PRESENCE=ON \
      -DUSE_SYSTEM_LIBS=ON \
      -DENABLE_TESTS=OFF \
      -DCITRA_CHECK_SUBMODULES=OFF \
      -DSIRIT_USE_SYSTEM_SPIRV_HEADERS=ON \
      -DDISABLE_SYSTEM_LODEPNG=ON
    
	make
}

package() {
	cd "$srcdir/$_pkgbase/build"
	DESTDIR="$pkgdir/" make install
}
