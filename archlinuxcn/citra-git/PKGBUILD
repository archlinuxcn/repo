# Maintainer: Tim Schumacher <timschumi@gmx.de>
# Contributor: Emmanuel Gil Peyrot <linkmauve@linkmauve.fr>
# Contributor: ceri

_pkgbase='citra'
pkgbase="$_pkgbase-git"
pkgname=("$_pkgbase-git" "$_pkgbase-qt-git")
pkgver=r8036.360e6cb51
pkgrel=1
pkgdesc="An experimental open-source Nintendo 3DS emulator/debugger"
arch=('i686' 'x86_64')
url="https://github.com/citra-emu/citra/"
license=('GPL2')
makedepends=('git' 'cmake' 'sdl2' 'qt5-base' 'shared-mime-info' 'desktop-file-utils' 'qt5-multimedia' 'ffmpeg' 'ninja' 'qt5-tools' 'clang' 'llvm')
source=("$_pkgbase::git+https://github.com/citra-emu/citra")
md5sums=('SKIP')

pkgver() {
	cd "$srcdir/$_pkgbase"
	echo "r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

prepare() {
	cd "$srcdir/$_pkgbase"
	mkdir -p build

	git submodule init
	git submodule update --init --recursive
}

build() {
	cd "$srcdir/$_pkgbase/build"
	CXXFLAGS+=" -DFMT_USE_USER_DEFINED_LITERALS=0" cmake .. \
	  -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
	  -DENABLE_QT_TRANSLATION=ON \
	  -DCITRA_ENABLE_COMPATIBILITY_REPORTING=ON \
	  -DENABLE_COMPATIBILITY_LIST_DOWNLOAD=ON \
	  -DUSE_DISCORD_PRESENCE=ON \
	  -DENABLE_FFMPEG=ON \
	  -DCMAKE_BUILD_TYPE=Release \
    -DDYNARMIC_USE_LLVM=TRUE \
    -DENABLE_NC=TRUE \
    -DFMT_INSTALL=TRUE \
    -DTEAKRA_BUILD_TOOLS=TRUE \
	  -GNinja
	ninja
}

check() {
	cd "$srcdir/$_pkgbase/build"
	ctest -C Release
}

package_citra-git() {
	depends=('sdl2' 'ffmpeg' 'llvm-libs')

	install -Dm755 "$srcdir/$_pkgbase/build/bin/citra" "$pkgdir/usr/bin/citra"
}

package_citra-qt-git() {
	depends=('qt5-multimedia' 'sdl2' 'shared-mime-info' 'desktop-file-utils' 'ffmpeg' 'llvm-libs')
	optdepends=('libxkbcommon-x11: for X11 support'
		    'qt5-wayland: for Wayland support')

	cd "$srcdir/$_pkgbase/build"
	DESTDIR="$pkgdir/" ninja install
	rm "$pkgdir/usr/bin/citra"
}
