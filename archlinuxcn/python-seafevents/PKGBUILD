# Maintainer: Klemens Sch√∂lhorn <klemens@schoelhorn.eu>

pkgname=python-seafevents
pkgver=13.0.18
pkgrel=1
pkgdesc="Python library for accessing seafile data model"
arch=('any')
url="https://github.com/haiwen/seafevents"
license=('Apache')
depends=('python')
makedepends=('python-setuptools')
source=("https://github.com/haiwen/seafevents/archive/v${pkgver}-server.tar.gz"
        setup.py.in)
sha256sums=('57dd2dc2a1788001f1b61c9fe402767c701a96e777ff0c7da40d54c3a558606d'
            'a8aeb5d2b62bb7b5d3468459730418936626f6cd748e35d99c07c6bb989656f7')

package() {
  depends+=(
    python-sqlalchemy
    python-redis
    python-pyjwt
    python-pymysql
    python-flask
    python-pyexiftool
    python-beautifulsoup4
    python-markdown-it-py
    python-mdit_py_plugins
    python-linkify-it-py
    python-scikit-learn
    python-waitress
    python-memcached
  )
  sed setup.py.in -e "s/@SEAFEVENTS_VERSION@/${pkgver}/g" > setup.py
  rm -rf build
  mkdir build/
  ln -sf ../seafevents-$pkgver-server build/seafevents
  find seafevents-$pkgver-server/seasearch -type d -exec touch {}/__init__.py \;
  find seafevents-$pkgver-server/wiki -type d -exec touch {}/__init__.py \;
  cd build
  python ../setup.py install --root="$pkgdir/" --optimize=1
}
