# maintainer: Kiri <kiri@vern.cc>
# Contributor: Decrypted Epsilon <decrypted.epsilon@gmail.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor:  Piotr Balwierz <parseByHuman(surname at email_service_of_google)>
# Co-maintainer: Rhinoceros <https://aur.archlinux.org/account/rhinoceros>

pkgname=igv
pkgver=2.16.2
pkgrel=1
pkgdesc="High-performance visualization tool for interactive exploration of large, integrated genomic datasets. From Broad Institute. https://doi.org/10.1093/bioinformatics/btac830"
arch=('any')
url="https://igv.org"
license=('MIT')
makedepends=('jdk11-openjdk'
             'gendesk')
depends=('java-runtime>=11')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/igvteam/igv/archive/v${pkgver}.tar.gz")
sha256sums=('d08e785d9d44f4e3c16dfe22514133fa0a80d067b677c0cc3c64d2e3d2c12d21')

prepare() {
    # generate /usr/bin entry
    cat <<'EOF' > igv
#!/usr/bin/sh
export PATH=/usr/lib/jvm/java-11-openjdk/bin/:$PATH
exec /usr/share/igv/igv.sh
EOF

    cat <<'EOF' > igvtools
#!/usr/bin/sh
export PATH=/usr/lib/jvm/java-11-openjdk/bin/:$PATH
exec /usr/share/igv/igvtools
EOF

    # generate desktop entry
    gendesk -f -n --pkgname "igv" \
        --pkgdesc "Integrative Genomics Viewer" \
        --exec="igv" \
        --icon="/usr/share/icons/${pkgname}.ico" \
        --name="IGV" \
        --categories="Science"
}

build() {
    export PATH=/usr/lib/jvm/java-11-openjdk/bin/:$PATH
    cd ${srcdir}/${pkgname}-${pkgver}
    ./gradlew createDist
}

package() {
    cd ${srcdir}
    install -Dm755 igv ${pkgdir}/usr/bin/igv
    install -Dm755 igvtools ${pkgdir}/usr/bin/igvtools
    install -Dm644 ${pkgname}.desktop ${pkgdir}/usr/share/applications/${pkgname}.desktop
    cd ${srcdir}/${pkgname}-${pkgver}
    install -Dm644 license.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
    install -Dm644 resources/IGV_64.ico ${pkgdir}/usr/share/icons/${pkgname}.ico
    cd ${srcdir}/${pkgname}-${pkgver}/build/IGV-dist
    install -Dm755 igv.sh ${pkgdir}/usr/share/${pkgname}/igv.sh
    install -Dm755 igvtools ${pkgdir}/usr/share/${pkgname}/igvtools
    install -Dm644 igv.args ${pkgdir}/usr/share/${pkgname}/igv.args
    install -Dm644 lib/*jar -t ${pkgdir}/usr/share/java/${pkgname}
    install -Dm644 lib/genomes/* -t ${pkgdir}/usr/share/${pkgname}/lib/genomes
    ln -s ${pkgdir}/usr/share/java/${pkgname}/*  ${pkgdir}/usr/share/${pkgname}/lib
}
