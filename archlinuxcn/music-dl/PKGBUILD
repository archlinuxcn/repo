# Maintainer: Dct Mei <dctxmei@yandex.com>

pkgname=music-dl
pkgver=3.0.0
pkgrel=3
pkgdesc="A command line tool which helps you search and download music from multiple sources"
arch=('any')
url="https://github.com/0xHJK/music-dl"
license=('MIT')
depends=('python-click' 'python-prettytable' 'python-pycryptodome' 'python-requests')
makedepends=('python-setuptools')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha256sums=('2c5c83976533a1896ae6028ad640ce5aed881ef2f23e7d9ec2c7b2ee5e43b48b')

build() {
    cd "${srcdir}"/"${pkgname}-${pkgver}"/
    python setup.py build
}

package() {
    cd "${srcdir}"/"${pkgname}-${pkgver}"/
    python setup.py install --root="${pkgdir}"/ --optimize=1 --skip-build
    rm "${pkgdir}"/usr/LICENSE
    rm "${pkgdir}"/usr/README.en.md
    install -Dm 644 LICENSE "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
    install -Dm 644 README.en.md "${pkgdir}"/usr/share/"${pkgname}"/README
}
