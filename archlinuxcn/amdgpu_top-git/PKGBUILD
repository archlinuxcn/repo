# Maintainer: Integral <integral@member.fsf.org>
# Contributor: Paul <pb.orzel@proton.me>

pkgname=amdgpu_top-git
_pkgname=${pkgname%-git}
pkgver=0.10.5.r47.g83a5d4f
pkgrel=1
pkgdesc="Tool that shows AMD GPU utilization"
arch=('i686' 'pentium4' 'x86_64' 'arm' 'armv7h' 'armv6h' 'aarch64')
url="https://github.com/Umio-Yasuno/${_pkgname}"
license=("MIT")
depends=("libdrm")
makedepends=("git" "cargo")
conflicts=("${_pkgname}")
provides=("${_pkgname}")
source=("git+${url}.git")
sha256sums=('SKIP')

pkgver() {
	cd "${_pkgname}/"
	git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/v//'
}

prepare() {
	cd "${_pkgname}/"
	cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
	cd "${_pkgname}/"
	cargo build --frozen --release --features package
}

package() {
	cd "${_pkgname}/"
	install -Dm755 "target/release/${_pkgname}" -t "${pkgdir}/usr/bin/"
	install -Dm644 assets/${_pkgname}{,-tui}.desktop -t "${pkgdir}/usr/share/applications/desktop/"

	install -d "${pkgdir}/usr/share/doc/${_pkgname}/"
	cp -r docs/* "${pkgdir}/usr/share/doc/${_pkgname}/"

	install -Dm644 "docs/${_pkgname}.1" -t "${pkgdir}/usr/share/man/man1/"
	install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
