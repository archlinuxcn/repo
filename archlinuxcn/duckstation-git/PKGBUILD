# Maintainer: katt <magunasu.b97@gmail.com>

pkgname=duckstation-git
pkgver=5600.4652c5fa
pkgdesc='A Sony PlayStation (PSX) emulator, focusing on playability, speed, and long-term maintainability (git version)'
pkgrel=1
epoch=1
arch=(x86_64 aarch64)
url=https://github.com/stenzek/duckstation
license=(GPL3)
makedepends=(git cmake extra-cmake-modules qt6-tools libdrm gtk3 ninja)
depends=(sdl2 qt6-base)
optdepends=()
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=(git+"$url".git)
sha256sums=('SKIP')

pkgver() {
    cd "${srcdir}/duckstation"
    printf "%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
   cmake -B build -S duckstation \
        -DBUILD_NOGUI_FRONTEND=OFF \
        -DUSE_WAYLAND=ON \
        -G Ninja \
        -Wno-dev
    ninja -C build
}

package() {
    # Main files
    install -m755 -d "${pkgdir}/opt"
    cp -rv build/bin "${pkgdir}/opt/${pkgname%-git}"

    # Symlink to /usr/bin
    install -m755 -d "${pkgdir}/usr/bin"
    ln -svt "${pkgdir}/usr/bin" /opt/"${pkgname%-git}"/"${pkgname%-git}"-qt

    # Desktop file
    install -Dm644 -t "${pkgdir}/usr/share/applications/" "${pkgname%-git}"/extras/linux-desktop-files/duckstation-qt.desktop
    sed -e 's/Icon=duckstation-qt/Icon=duckstation/' -i "${pkgdir}/usr/share/applications/duckstation-qt.desktop"
    #sed -e 's/Icon=duckstation-qt/Icon=duckstation/' -i "${pkgdir}/usr/share/applications/duckstation-nogui.desktop"
    install -Dm644 "${pkgname%-git}"/extras/icons/icon-256px.png "${pkgdir}/usr/share/pixmaps/duckstation.png"
}
