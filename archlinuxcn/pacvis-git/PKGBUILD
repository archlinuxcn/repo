# Maintainer: Jiachen Yang <farseerfc@gmail.com>

pkgname=pacvis-git
_pkgname=pacvis
pkgver=0.2.7.r12.g34f7494
pkgrel=0
pkgdesc="Visualize pacman local database using Vis.js, inspired by pacgraph"
arch=('any')
url="https://github.com/farseerfc/pacvis"
license=('MIT')
depends=(
    'pyalpm'
    'python'
    'python-tornado'
)
makedepends=(
    'git'
    'python-build'
    'python-installer'
    'python-setuptools'
    'python-wheel'
)
source=("git+https://github.com/farseerfc/pacvis.git")
provides=("${_pkgname}=${pkgver}")
conflicts=("${_pkgname}")
sha512sums=('SKIP')

pkgver() {
    cd "${srcdir}/${_pkgname}"
    ( set -o pipefail
        git describe --long --tags 2>/dev/null | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g' ||
        printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
    )
}

build () {
    cd "${srcdir}/${_pkgname}"
    python -m build --wheel --no-isolation
}

package () {
    cd "${srcdir}/${_pkgname}"
    install -Dm0644 LICENSE.md "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    install -Dm0644 pacvis@.service -t "${pkgdir}/usr/lib/systemd/system/"
    python -m installer --destdir="${pkgdir}" dist/*.whl
}
