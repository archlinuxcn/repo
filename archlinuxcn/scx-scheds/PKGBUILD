# Maintainer: Tejun Heo <tj@kernel.org>
# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>

_pkgname=scx
pkgname="${_pkgname}-scheds"
pkgver=1.0.2
pkgrel=1
pkgdesc="Sched_ext schedulers"
url="https://github.com/sched-ext/${_pkgname}"
arch=('x86_64')
license=('GPL-2.0-only')
depends=('libbpf' 'bpf' 'libelf' 'zlib' 'systemd')
makedepends=('git' 'python' 'meson' 'clang' 'llvm' 'cargo' 'jq')
backup=('etc/default/scx')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('aa40b39397046dc935daa898f53acfe02795d8b7bd49f1cf13b73f02768119b4471f745ca080e6beda06fc6585b514cef53dcceddfc28a4d0bdb8ead43ca29e2')
options=('!lto')

build() {
	cd "${_pkgname}-${pkgver}/"
	arch-meson . build -Dcargo_home=$HOME/cargo-deps --auto-features auto
	meson compile -C build
}

package() {
	cd "${_pkgname}-${pkgver}/"
	meson install -C build --destdir "${pkgdir}"
	install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
