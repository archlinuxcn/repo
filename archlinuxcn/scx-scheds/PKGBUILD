# Maintainer: Tejun Heo <tj@kernel.org>
# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>

_pkgname=scx
pkgname="${_pkgname}-scheds"
pkgver=0.1.6
pkgrel=1
pkgdesc="Sched_ext schedulers"
url="https://github.com/sched-ext/${_pkgname}"
arch=('x86_64')
license=('GPL-2.0-only')
depends=('libbpf' 'bpf' 'libelf' 'zlib' 'systemd')
makedepends=('python' 'meson' 'clang' 'llvm-libs' 'cargo')
source=("${url}/archive/refs/tags/v${pkgver}.tar.gz"
	"0001-scx-introduce-systemd-services.patch")
sha512sums=('caf969be909f2dfca7b8b82d528a50a147029e4ac788825ece3adad67fef41c6e43a37e74316238bcc13c1357a63503c940c76aeefa2e1e4f6787c1496833d05'
            '007acfb68275a5b3e61aed4b0582c239f73250b979d2b5e0820367071bd34510f6f1b6b8a3dabb606b02478653578a7f0b9efee3b1808692374afba7aecb27c3')
options=('!lto')

prepare() {
	cd "${_pkgname}-${pkgver}/"

	local src
	for src in "${source[@]}"; do
		src="${src%%::*}"
		src="${src##*/}"
		[[ ${src} = *.patch ]] || continue
		echo "Applying patch ${src}..."
		patch -Np1 <"../${src}"
	done
}

build() {
	cd "${_pkgname}-${pkgver}/"
	arch-meson . build
	meson compile -C build
}

package() {
	cd "${_pkgname}-${pkgver}/"
	meson install -C build --destdir "${pkgdir}"
	install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
