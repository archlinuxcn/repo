# Maintainer: kraxarn <kraxie@protonmail.com>

pkgname=spotify-qt
pkgver=3.12
pkgrel=1
pkgdesc="Lightweight Spotify client using Qt"
arch=('i686' 'x86_64' 'armv7h' 'aarch64')
url="https://github.com/kraxarn/${pkgname}"
license=("GPL-3.0-or-later")
depends=('qt6-base' 'qt6-svg' 'hicolor-icon-theme')
makedepends=('cmake')
optdepends=(
	"librespot: Recommended playback client"
	"spotifyd: Recommended playback client"
)
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha256sums=('5782ff9cd854aa92b4ba49837d3ed4b5ada0427fd440e16866e9727abcd9df62')

build() {
	cmake -B build -S "${pkgname}-${pkgver}" \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr

	cmake --build build
}

package() {
	DESTDIR="${pkgdir}" cmake --install build
}
