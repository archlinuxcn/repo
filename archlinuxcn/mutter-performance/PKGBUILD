# Patched package:
# Maintainer: Térence Clastres <t.clastres@gmail.com>
# Co-maintainer: Saren Arterius <saren@wtako.net>
# Co-maintainer: Mingi Sung <sungmg@saltyming.net>

# Official package:
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Michael Kanis <mkanis_at_gmx_dot_de>


### PACKAGE OPTIONS
## MERGE REQUESTS SELECTION
# Merge Requests List: ('579' '1441' '3567' '3751' '3934')
_merge_requests_to_use=('1441' '3751')


### IMPORTANT: Do no edit below this line unless you know what you're doing!

_pkgname=mutter
pkgname=mutter-performance
epoch=1
pkgver=46.4+r17+g0d60d07204
pkgrel=1
pkgdesc="A window manager for GNOME | Attempts to improve performances with non-upstreamed merge-requests and frequent stable branch resync"
url="https://gitlab.gnome.org/GNOME/mutter"
arch=(x86_64 aarch64)
license=('GPL-2.0-or-later')
depends=(
  at-spi2-core
  cairo
  colord
  dconf
  fontconfig
  fribidi
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  gnome-desktop-4
  gnome-settings-daemon
  graphene
  gsettings-desktop-schemas
  gtk4
  harfbuzz
  iio-sensor-proxy
  lcms2
  libcanberra
  libcolord
  libdisplay-info
  libdrm
  libei
  libglvnd
  libgudev
  libice
  libinput
  libpipewire
  libsm
  libsysprof-capture
  libwacom
  libx11
  libxau
  libxcb
  libxcomposite
  libxcursor
  libxdamage
  libxext
  libxfixes
  libxi
  libxinerama
  libxkbcommon
  libxkbcommon-x11
  libxkbfile
  libxrandr
  libxtst
  mesa
  pango
  pipewire
  pixman
  python
  startup-notification
  systemd-libs
  wayland
  xorg-xwayland

)
makedepends=(
  egl-wayland
  gi-docgen
  git
  glib2-devel
  gobject-introspection
  gtk3
  meson
  sysprof
  wayland-protocols
  xorg-server
  xorg-server-xvfb
)
if [ -n "$_enable_check" ]; then
  checkdepends=(gnome-session xorg-server-xvfb pipewire-session-manager python-dbusmock zenity)
fi
_commit=0d60d072048c8ce8ebb527284e5baa10df26bc7c  # tags/46.4^17
source=("git+$url.git#commit=$_commit"
        'mr1441.patch'
        'mr3567.patch'
        'mr3751.patch'
        'mr3934.patch')
sha256sums=('f72340218b99f0278ca358cfb94fc213ec3e91b5ff7ecae198b3d220829b7e4e'
            '60638078a3f3c46da61f868c04b2a3424488510702648452c3e8214a3790e37b'
            '2e474a574edad8dc047a53a920fc7cbe005cac0102907eef80a252fc556d0517'
            '4c15b0e6d0001c8c3feedf05a9140f7f17d52eb36ff7b8039e0281c1373ccdfe'
            'c0ae2855247cddde806a320d031021fd0f7c867c61ac95af99ea3765f290bbf5')
b2sums=('86db7a63e6dfd33baf5a6cebc30b96d57de1688de6cbc475a9b46fe21b5e02c50bcb56b4065ab8e2092c4fc7303d2d6d86c75f4d555fcef8a00a9cd243d30e25'
        '802cf82c0d4fa4514dc376bac57a65453df0ce789e6efac334d2f76a24349ad0a2b6e9ce02345839e8de719d7696dfcae911b1e66922103b2ec315e9fbaa8f01'
        '65302b1fa24a7aba26915269fb694c7a64b134ee2062ec7e649036a3a41efb2f2f4110cff6194808af34985855ade641c5fb01f0b97536cd3c912892ea3a0d4c'
        'f9f2284d7236d6072a818de3784a339c2d934490da8870876f49d4effd59ce807593ce8b8a3c5e06586fd5fd7e81fb31fedca84115acf1e71cfe23d4a5e727b3'
        '18e8d521336422f4c441979fe77ff22c60406e94cd2159c08bbfd127bf6b010dece55c7226b99a953c560b05194fe72e5c2140765577069d970c93479b573bcb')

for mr in "${_merge_requests_to_use[@]}"; do
  if [ '3567' = "$mr" ]; then
    depends+=(gnome-settings-daemon-xwayland-scaling)
  fi
done

pkgver() {
  cd $_pkgname
  git describe --tags | sed -r 's/\.([a-z])/\1/;s/([a-z])\./\1/;s/[^-]*-g/r&/;s/-/+/g'
}

pick_mr() {
  for mr in "${_merge_requests_to_use[@]}"; do
    if [ "$1" = "$mr" ]; then
      if [ "$2" = "merge" ] || [ -z "$2" ]; then
        msg2 "Downloading then Merging $1..."
        curl -O "https://gitlab.gnome.org/GNOME/mutter/-/merge_requests/$mr.diff"
        git apply "$mr.diff"
      elif [ "$3" = "revert" ]; then
        msg2 "Reverting $1..."
        git revert "$2" --no-commit
      elif [ "$3" = "patch" ]; then
	if [ -e ../"$2" ]; then 
          msg2 "Patching with $2..."
          patch -Np1 -i ../"$2"
        else
          msg2 "Downloading $mr as $2 then patching..."
          curl -O "https://gitlab.gnome.org/GNOME/mutter/-/merge_requests/$mr.diff" -o "$2"
          patch -Np1 -i "$2"
        fi
      else
        msg2 "ERROR: wrong argument given: $2"
      fi
      break
    fi
  done
}

prepare() {
  cd $_pkgname

  git reset --hard
  git cherry-pick --abort || true
  git clean -fd

  #git remote add vanvugt https://gitlab.gnome.org/vanvugt/mutter.git || true
  #git remote add verdre https://gitlab.gnome.org/verdre/mutter.git || true
  #git remote add 3v1no https://gitlab.gnome.org/3v1n0/mutter.git || true

  #git fetch vanvugt
  #git fetch verdre
  #git fetch 3v1no

  ### Merge Requests

  # Merge Request Prototype
  # Title:
  # Author:
  # URL:
  # Type:
  # Status:
  # Comment:
  # git cherry-pick -n first_commit^..last_commit
  #
  # Possible Type:
  #   1. Improvement: Makes an already existing feature behave better, more efficiently/reliably.
  #   2. Feature: Adds a new functionality.
  #   3. Fix: Regression/bug fix only available in master (not backported).
  #   4. Cleanup: Code styling improvement, function deprecation, rearrangement...
  #
  # Possible Status:
  #   1. Needs rebase: Conflicts with master branch.
  #   2. Needs review: Mutter maintainers needs to review the new/updated MR and provide feedback.
  #   3. Needs changes: MR needs to be adjusted based on maintainers feedback.
  #   4. Merged: MR approved and it changes commited to master.
  #
  # Generally, a MR status oscillate between 2 and 3 and then becomes 4.

  # Title: backends: Do not reload keymap on new keyboard notifications
  # Author: Carlos Garnacho <carlosg@gnome.org>
  # URL:  https://gitlab.gnome.org/GNOME/mutter/-/merge_requests/579
  # Type: 1
  # Status: 4
  # Comment: Was reverted: https://gitlab.gnome.org/GNOME/mutter/-/merge_requests/833
  #          If you use stenography software or play hardcore rhythm games like Lunatic Rave 2/osumania, use it.
  pick_mr '579' ce86f90efbaa51522ba14c5b4cad933c2106de42 'revert'

  # Title: Let scaling-aware Xwayland clients scale themselves with "scale-monitor-framebuffers"
  # Author: Jonas Dreßler <verdre@v0yd.nl>
  # URL:  https://gitlab.gnome.org/GNOME/mutter/-/merge_requests/3567
  # Type: 1
  # Status: 3
  # Comment: Backported to gnome-46. It needs more testing.
  #          Goes with https://gitlab.gnome.org/GNOME/gnome-settings-daemon/-/merge_requests/353
  pick_mr '3567' 'mr3567.patch' 'patch'

  # Title: wayland/text-input-v1: Implement basic text-input-v1 support
  # Author: Alynx Zhou <alynx.zhou@gmail.com>
  # URL:  https://gitlab.gnome.org/GNOME/mutter/-/merge_requests/3751
  # Type: 1
  # Status: Not gonna be merged considering the maintainers' feedbacks.
  # Comment: This commit makes input methods work in text-input-v1 only clients.
  #          (mostly Chromium/Electron based apps with Ozone Wayland.)
  pick_mr '3751' 'mr3751.patch' 'patch'

  # Title: kms/impl-device: Track dispatch duration in crtc_frame_deadline_dispatch
  # Author: Michel Dänzer <mdaenzer@redhat.com>
  # URL:  https://gitlab.gnome.org/GNOME/mutter/-/merge_requests/3934
  # Type: 1
  # Status: 2
  # Comment: This fixes cursor stutter.
  #          Do not enable this if !1441 is enabled since it already includes !3934.
  pick_mr '3934' 'mr3934.patch' 'patch'

  # Title: Draft: Dynamic triple/double buffering (v4)
  # Author: Daniel van Vugt <daniel.van.vugt@canonical.com>
  # URL:  https://gitlab.gnome.org/GNOME/mutter/-/merge_requests/1441
  # Type: 1
  # Status: 2 & 3
  # Comment: Helps GPU frequencies to scale up.
  #          Backported: !3934.
  pick_mr '1441' 'mr1441.patch' 'patch'

}

build() {
  local meson_options=(
    -D egl_device=true
    -D wayland_eglstream=true
    -D installed_tests=false
    -D libdisplay_info=enabled
    -D docs=false
    -D tests=false
  )

  CFLAGS="${CFLAGS/-O2/-O3} -fno-semantic-interposition"
  LDFLAGS+=" -Wl,-Bsymbolic-functions"

  arch-meson $_pkgname build "${meson_options[@]}"
  meson compile -C build
}

package() {
  provides=(mutter libmutter-14.so)
  conflicts=(mutter)
  

  meson install -C build --destdir "$pkgdir"
}
