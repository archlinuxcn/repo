# Maintainer: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix
# Contributor: Fixed Torres <aur_linuxero@outlook.com>

pkgname=sayonara-player-git
pkgver=1.11.0.stable2.r5.g1b3b44433
pkgrel=1
pkgdesc="Small, clear and fast audio player for Linux written in C++, supported by the Qt framework. It uses Gstreamer as audio backend."
arch=(x86_64 i686 aarch64)
url="https://sayonara-player.com/"
license=(GPL3)
depends=(qt5-base qt5-svg gst-plugins-base taglib python)
makedepends=(cmake git qt5-tools)
checkdepends=(xorg-server-xvfb gst-plugins-good)
optdepends=('gst-libav: additional codecs'
            'gst-plugins-good: additional codecs'
            'gst-plugins-bad: additional codecs'
            'gst-plugins-ugly: additional codecs')
provides=(sayonara-player)
conflicts=(sayonara-player)
source=("git+https://gitlab.com/luciocarreras/sayonara-player.git")
sha512sums=('SKIP')

pkgver() {
  cd "sayonara-player"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  local _flags=(

  )

  cmake -B build -S "sayonara-player" -Wno-dev \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    "${_flags[@]}"

  cmake --build build
}

check() {
  xvfb-run -s '-screen 0 1920x1080x24 -nolisten local' ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="${pkgdir}" cmake --install build
}

