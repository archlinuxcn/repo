# Maintainer: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix
# Contributor: Fixed Torres <aur_linuxero@outlook.com>

pkgname=sayonara-player-git
pkgver=1.11.0.beta1.r19.gd0f90a538
pkgrel=1
pkgdesc="Small, clear and fast audio player for Linux written in C++, supported by the Qt framework. It uses Gstreamer as audio backend."
arch=(x86_64 i686 aarch64)
url="https://sayonara-player.com/"
license=(GPL3)
depends=(qt5-base qt5-svg gst-plugins-base taglib python)
makedepends=(cmake git qt5-tools)
checkdepends=(xorg-server-xvfb gst-plugins-good)
optdepends=('gst-libav: additional codecs'
            'gst-plugins-good: additional codecs'
            'gst-plugins-bad: additional codecs'
            'gst-plugins-ugly: additional codecs')
provides=(sayonara-player)
conflicts=(sayonara-player)
source=("git+https://gitlab.com/luciocarreras/sayonara-player.git")
sha512sums=('SKIP')

pkgver() {
  cd "${srcdir}/sayonara-player"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "${srcdir}/sayonara-player"
  [[ -d build ]] || mkdir build
}

build() {
  cd "${srcdir}/sayonara-player"
  cmake -B build \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \


  cmake --build build
}

check() {
  cd "${srcdir}/sayonara-player"
  xvfb-run -s '-screen 0 1920x1080x24 -nolisten local' ctest --test-dir build --output-on-failure
}

package() {
  cd "${srcdir}/sayonara-player"
  DESTDIR="$pkgdir" cmake --install build
}

