# Maintainer: Alexandre Bouvier <contact@amb.tf>
_pkgname=xemu
pkgname=$_pkgname-git
pkgver=0.8.20.r0.g3bdb9e7fd4
pkgrel=1
pkgdesc="Original Xbox emulator (fork of XQEMU)"
arch=('x86_64')
url="https://xemu.app/"
license=('GPL-2.0-only')
depends=(
	'gcc-libs'
	'glibc'
	'glslang'
	'hicolor-icon-theme'
	'cpp-httplib'
	'sdl2'
)
makedepends=(
	'cmake'
	'dtc'
	'git'
	'glib2'
	'glu'
	'gtk3'
	'libepoxy'
	'libpcap'
	'libsamplerate'
	'libslirp'
	'meson'
	'nlohmann-json'
	'openssl'
	'python-yaml'
	'tomlplusplus>=3.1'
	'vulkan-headers'
	'vulkan-icd-loader'
	'xxhash>=0.8'
	'zlib'
)
provides=("$_pkgname")
conflicts=("$_pkgname")
install=$_pkgname.install
source=(
	"$_pkgname::git+https://github.com/xemu-project/xemu.git"
	'use-system-libs.patch'
)
b2sums=(
	'SKIP'
	'917b9eddb0e3d783ff228dfa97a6a0a5662c4451c69d9723938a8a26e83efa127943f6292bb65c18ebdfc05d282b01ae036771ac8f7f3b92e20a524f317fedf1'
)

pkgver() {
	cd $_pkgname
	git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
	cd $_pkgname
	mkdir -p ../build
	patch -Np1 < ../use-system-libs.patch
	python scripts/gen-license.py > XEMU_LICENSE
}

build() {
	cd build
	../$_pkgname/configure \
		--audio-drv-list="sdl" \
		--disable-werror \
		--extra-cflags="-DXBOX=1" \
		--ninja="$NINJA" \
		--target-list="i386-softmmu" \
		-Dbuildtype=plain
	make qemu-system-i386
}

package() {
	depends+=(
		'libepoxy.so'
		'libgdk-3.so'
		'libglib-2.0.so'
		'libgobject-2.0.so'
		'libgtk-3.so'
		'libpcap.so'
		'libsamplerate.so'
		'libslirp.so'
		'libtomlplusplus.so'
		'libxxhash.so'
		'libz.so'
	)
	cd $_pkgname
	# shellcheck disable=SC2154
	install -Dm755 ../build/qemu-system-i386 "$pkgdir"/usr/bin/$_pkgname
	install -Dm644 ui/xemu.desktop "$pkgdir"/usr/share/applications/$_pkgname.desktop
	for _size in 16 24 32 48 64 128 256 512; do
		install -Dm644 ui/icons/xemu_${_size}x${_size}.png "$pkgdir"/usr/share/icons/hicolor/${_size}x${_size}/apps/$_pkgname.png
	done
	install -Dm644 ui/icons/xemu.svg "$pkgdir"/usr/share/icons/hicolor/scalable/apps/$_pkgname.svg
	install -Dm644 XEMU_LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
