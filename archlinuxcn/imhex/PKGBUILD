# Maintainer: KokaKiwi <kokakiwi+aur@kokakiwi.net>
# Contributor: George Rawlinson <george@rawlinson.net.nz>

_pkgname=ImHex
pkgname=${_pkgname,,}
pkgver=1.28.0
pkgrel=3
pkgdesc='A Hex Editor for Reverse Engineers, Programmers and people that value their eye sight when working at 3 AM'
url='https://imhex.werwolv.net'
license=('GPL2')
arch=('x86_64')
depends=( 'glfw'
          'hicolor-icon-theme' 'xdg-desktop-portal'
          'dbus'
          'mbedtls' 'libmbedcrypto.so' 'libmbedtls.so' 'libmbedx509.so'
          'curl' 'libcurl.so'
          'freetype2' 'libfreetype.so'
          'fmt' 'libfmt.so'
          'yara' 'libyara.so'
          )
makedepends=('git' 'cmake' 'ninja' 'llvm' 'nlohmann-json' 'librsvg' 'python')
optdepends=(
  'imhex-patterns-git: ImHex base patterns'
)
source=("$pkgname::git+https://github.com/WerWolv/ImHex.git#tag=v$pkgver"
        "nativefiledialog::git+https://github.com/btzy/nativefiledialog-extended.git"
        "xdgpp::git+https://git.sr.ht/~danyspin97/xdgpp"
        "libromfs::git+https://github.com/WerWolv/libromfs"
        "capstone::git+https://github.com/capstone-engine/capstone"
        "pattern_language::git+https://github.com/WerWolv/PatternLanguage"
        "libwolv::git+https://github.com/WerWolv/libwolv"
        )
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP')
b2sums=('SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP')

prepare() {
  cd "$srcdir/$pkgname"

  git submodule init
  for name in nativefiledialog xdgpp libromfs capstone pattern_language libwolv; do
    git submodule set-url lib/external/$name "$srcdir/$name"
    git -c protocol.file.allow=always submodule update lib/external/$name
  done
  pushd lib/external/pattern_language
    git submodule init
    git submodule set-url external/libwolv "$srcdir"/libwolv
    git -c protocol.file.allow=always submodule update external/libwolv
  popd
  for name in fmt curl yara/yara; do
    git config --remove-section submodule.lib/external/$name
  done

  sed -E -e 's/-Werror //g' -i "$srcdir/$pkgname"/**/*.cmake "$srcdir/$pkgname"/**/CMakeLists.txt
}

build() {
  cd "$srcdir"
  cmake -B build -S "$pkgname" \
    -G Ninja \
    -Wno-dev \
    -D CMAKE_BUILD_TYPE=None \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D CMAKE_SKIP_RPATH=ON \
    -D IMHEX_OFFLINE_BUILD=ON \
    -D IMHEX_IGNORE_BAD_CLONE=ON \
    -D IMHEX_STRIP_RELEASE=OFF \
    -D USE_SYSTEM_LLVM=ON \
    -D USE_SYSTEM_YARA=ON \
    -D USE_SYSTEM_FMT=ON \
    -D USE_SYSTEM_CURL=ON \
    -D USE_SYSTEM_NLOHMANN_JSON=ON \
    -D USE_SYSTEM_CAPSTONE=OFF \
    -D LIBPL_ENABLE_CLI=OFF \
    -D IMHEX_VERSION="$pkgver"

  cmake --build build --parallel
}

package() {
  cd "$srcdir"
  DESTDIR="${pkgdir}" cmake --install build
  
  install -Dm0644 "$pkgname/resources/icon.svg" "$pkgdir/usr/share/icons/hicolor/scalable/apps/imhex.svg"
  for size in 32 48 64 128 256; do
    install -dm0755 "$pkgdir/usr/share/icons/hicolor/${size}x${size}/apps"
    rsvg-convert -a -f png -w $size -o "$pkgdir/usr/share/icons/hicolor/${size}x${size}/apps/imhex.png" \
      "$pkgname/resources/icon.svg"
  done

  # License
  install -Dm0644 "$pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # Documentation
  install -Dm0644 -t "$pkgdir/usr/share/doc/$pkgname" \
    "$pkgname/README.md"
}

