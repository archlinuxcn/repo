# Maintainer: Xeonacid <h.dwwwwww@gmail.com>
# Maintainer: KokaKiwi <kokakiwi+aur@kokakiwi.net>
# Contributor: George Rawlinson <george@rawlinson.net.nz>

_pkgname=ImHex
pkgname=${_pkgname,,}
pkgver=1.36.2
pkgrel=2
pkgdesc='A Hex Editor for Reverse Engineers, Programmers and people that value their eye sight when working at 3 AM'
url='https://imhex.werwolv.net'
license=('GPL-2.0-only')
arch=('x86_64')
depends=(
    'glfw'
    'hicolor-icon-theme' 'xdg-desktop-portal'
    'dbus'
    'mbedtls' 'libmbedcrypto.so'
    'curl' 'libcurl.so'
    'freetype2' 'libfreetype.so'
    'fmt' 'libfmt.so'
    'yara' 'libyara.so'
    'file' 'libmagic.so'
    'libglvnd'
    'libarchive'
    'glibc' 'gcc-libs' 'bash'
    'libxrandr'
    'fontconfig'
)
makedepends=('git' 'cmake' 'ninja' 'llvm' 'nlohmann-json' 'librsvg' 'python' 'dotnet-runtime')
optdepends=(
    'imhex-patterns-git: ImHex base patterns'
    'dotnet-runtime'
    'lz4: decompress plugin'
    'zlib: decompress plugin'
    'bzip2: decompress plugin'
    'xz: decompress plugin'
    'zstd: decompress plugin'
)
source=(
    "$pkgname::git+https://github.com/WerWolv/ImHex.git#tag=v$pkgver"
    "nativefiledialog::git+https://github.com/btzy/nativefiledialog-extended.git"
    "xdgpp::git+https://git.sr.ht/~danyspin97/xdgpp"
    "libromfs::git+https://github.com/WerWolv/libromfs"
    "capstone::git+https://github.com/capstone-engine/capstone"
    "pattern_language::git+https://github.com/WerWolv/PatternLanguage"
    "libwolv::git+https://github.com/WerWolv/libwolv"
    "HashLibPlus::git+https://github.com/WerWolv/HashLibPlus"
    "jthread::git+https://github.com/josuttis/jthread"
    "cli11::git+https://github.com/CLIUtils/CLI11"
    "lunasvg::git+https://github.com/WerWolv/lunasvg"
    "edlib::git+https://github.com/Martinsos/edlib"
    "throwing_ptr::git+https://github.com/rockdreamer/throwing_ptr"
    "plutovg::git+https://github.com/sammycage/plutovg"
    "ImHex-Patterns::git+https://github.com/WerWolv/ImHex-Patterns.git"
)
b2sums=('dc1617823fcd1deb348c756a451fbdc9ffa2b64369fe2d135d0da0c40eaa39a68a3d7980d2926160d5d721f80dab515675a7a11e9d970c0ad56f20629b069633'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP')

prepare() {
    cd "$pkgname"

    git submodule init
    for name in libromfs pattern_language libwolv; do
        git submodule set-url lib/external/"$name" "$srcdir/$name"
        git -c protocol.file.allow=always submodule update lib/external/$name
    done
    for name in nativefiledialog xdgpp capstone HashLibPlus edlib lunasvg; do
        git submodule set-url lib/third_party/"$name" "$srcdir/$name"
        git -c protocol.file.allow=always submodule update lib/third_party/"$name"
    done
    git submodule set-url lib/third_party/jthread/jthread "$srcdir/jthread"
    git -c protocol.file.allow=always submodule update lib/third_party/jthread/jthread
    pushd lib/external/pattern_language
        git submodule init
        for name in libwolv cli11 throwing_ptr; do
            git submodule set-url external/"$name" "$srcdir/$name"
            git -c protocol.file.allow=always submodule update external/"$name"
        done
    popd
    pushd lib/third_party/lunasvg
      git submodule init
      git submodule set-url plutovg "$srcdir/plutovg"
      git -c protocol.file.allow=always submodule update plutovg
    popd
    for name in fmt yara/yara; do
        git config --remove-section submodule.lib/third_party/"$name"
    done
}

build() {
    cmake -B build -S "$pkgname" \
        -G Ninja \
        -Wno-dev \
        -D CMAKE_BUILD_TYPE=None \
        -D CMAKE_INSTALL_PREFIX=/usr \
        -D CMAKE_SKIP_RPATH=ON \
        -D IMHEX_OFFLINE_BUILD=ON \
        -D IMHEX_IGNORE_BAD_CLONE=ON \
        -D IMHEX_STRIP_RELEASE=OFF \
        -D IMHEX_USE_GTK_FILE_PICKER=OFF \
        -D IMHEX_STRICT_WARNINGS=OFF \
        -D IMHEX_BUNDLE_DOTNET=OFF \
        -D USE_SYSTEM_LLVM=ON \
        -D USE_SYSTEM_YARA=ON \
        -D USE_SYSTEM_FMT=ON \
        -D USE_SYSTEM_CURL=ON \
        -D USE_SYSTEM_NLOHMANN_JSON=ON \
        -D USE_SYSTEM_CAPSTONE=OFF \
        -D LIBPL_ENABLE_CLI=ON \
        -D LIBPL_BUILD_CLI_AS_EXECUTABLE=OFF \
        -D IMHEX_VERSION="$pkgver"

    cmake --build build --parallel
}

package() {
    # FIXME: upstream install SDK in /usr/share and seems no way to disable it...
    DESTDIR="$pkgdir" cmake --install build
    
    # Documentation
    install -Dm0644 -t "$pkgdir/usr/share/doc/$pkgname" \
        "$pkgname/README.md"
}

# vim: set ts=4 sw=4 et:
