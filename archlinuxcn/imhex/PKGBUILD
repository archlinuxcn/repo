# Maintainer: Xeonacid <h.dwwwwww@gmail.com>
# Maintainer: KokaKiwi <kokakiwi+aur@kokakiwi.net>
# Contributor: George Rawlinson <george@rawlinson.net.nz>
# Contributor: RocketDev <ma2014119@outlook.com>

_pkgname=ImHex
pkgname=${_pkgname,,}
pkgver=1.38.0
pkgrel=1
pkgdesc='A Hex Editor for Reverse Engineers, Programmers and people that value their eye sight when working at 3 AM'
url='https://imhex.werwolv.net'
license=('GPL-2.0-only')
arch=('x86_64')
depends=(
    'glfw'
    'dbus'
    'mbedtls' 'libmbedcrypto.so'
    'curl' 'libcurl.so'
    'freetype2' 'libfreetype.so'
    'fmt' 'libfmt.so'
    'yara' 'libyara.so'
    'file' 'libmagic.so'
    'libglvnd' # needed in runtime
    'glibc' 'gcc-libs' 'bash'
    'libxrandr' # needed in runtime
    'fontconfig'
    'capstone'
    'md4c'
    # since these "optdepends" are depends of system core components,
    # it's ok to list them in depends
    'lz4'
    'zlib'
    'bzip2'
    'xz'
    'zstd'
    'libssh2'
)
makedepends=('git' 'cmake' 'ninja' 'llvm' 'nlohmann-json' 'librsvg' 'python' 'cli11' 'boost' 'dotnet-runtime')
optdepends=(
    'dotnet-runtime: dotnet script support'
)
replaces=('imhex-patterns-git')
source=(
    "$pkgname::git+https://github.com/WerWolv/ImHex.git#tag=v$pkgver"
    "nativefiledialog::git+https://github.com/btzy/nativefiledialog-extended.git"
    "xdgpp::git+https://git.sr.ht/~danyspin97/xdgpp"
    "libromfs::git+https://github.com/WerWolv/libromfs"
    "pattern_language::git+https://github.com/WerWolv/PatternLanguage"
    "libwolv::git+https://github.com/WerWolv/libwolv"
    "HashLibPlus::git+https://github.com/WerWolv/HashLibPlus"
    "lunasvg::git+https://github.com/sammycage/lunasvg"
    "edlib::git+https://github.com/Martinsos/edlib"
    "throwing_ptr::git+https://github.com/rockdreamer/throwing_ptr"
    "ImHex-Patterns::git+https://github.com/WerWolv/ImHex-Patterns.git"
    "disassembler::git+https://github.com/WerWolv/Disassembler.git"
)
b2sums=('843846fd2c5a0136d52771c11aece24fd3b69dc69e8f8a5f375faab17569ea830c49b43fcd7a66346cf70d0d1ec0f5602619006e244501144556c4041b8ce2cc'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP')

prepare() {
    cd "$pkgname"

    git submodule init
    for name in libromfs pattern_language libwolv disassembler; do
        git submodule set-url lib/external/"$name" "$srcdir/$name"
        git -c protocol.file.allow=always submodule update lib/external/$name
    done
    for name in nativefiledialog xdgpp HashLibPlus edlib lunasvg; do
        git submodule set-url lib/third_party/"$name" "$srcdir/$name"
        git -c protocol.file.allow=always submodule update lib/third_party/"$name"
    done
    pushd lib/external/pattern_language
        git submodule init
        for name in libwolv throwing_ptr; do
            git submodule set-url external/"$name" "$srcdir/$name"
            git -c protocol.file.allow=always submodule update external/"$name"
        done
    popd
}

build() {
    cmake -B build -S "$pkgname" \
        -G Ninja \
        -Wno-dev \
        -D CMAKE_BUILD_TYPE=RelWithDebInfo \
        -D CMAKE_INSTALL_PREFIX=/usr \
        -D CMAKE_SKIP_RPATH=ON \
        -D IMHEX_OFFLINE_BUILD=ON \
        -D IMHEX_IGNORE_BAD_CLONE=ON \
        -D IMHEX_STRIP_RELEASE=OFF \
        -D IMHEX_USE_GTK_FILE_PICKER=OFF \
        -D IMHEX_STRICT_WARNINGS=OFF \
        -D IMHEX_BUNDLE_DOTNET=OFF \
        -D IMHEX_BUNDLE_PLUGIN_SDK=OFF \
        -D USE_SYSTEM_LLVM=ON \
        -D USE_SYSTEM_YARA=ON \
        -D USE_SYSTEM_FMT=ON \
        -D USE_SYSTEM_NLOHMANN_JSON=ON \
        -D USE_SYSTEM_CAPSTONE=ON \
        -D USE_SYSTEM_MD4C=ON \
        -D USE_SYSTEM_CLI11=ON \
        -D USE_SYSTEM_BOOST=ON \
        -D LIBPL_ENABLE_CLI=ON \
        -D LIBPL_BUILD_CLI_AS_EXECUTABLE=OFF \
        -D IMHEX_VERSION="$pkgver" \
        -D IMHEX_ENABLE_UNIT_TESTS=OFF \
        -D IMHEX_ENABLE_PLUGIN_TESTS=OFF \
        -D IMHEX_ENABLE_IMGUI_TEST_ENGINE=OFF \
        -D CMAKE_POLICY_VERSION_MINIMUM=3.5

    cmake --build build --parallel
}

package() {
    DESTDIR="$pkgdir" cmake --install build
    
    # Documentation
    install -Dm0644 -t "$pkgdir/usr/share/doc/$pkgname" \
        "$pkgname/README.md"
}

# vim: set ts=4 sw=4 et:
