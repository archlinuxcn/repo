# Maintainer: Taiko2k <captain dot gxj at gmail dot com>

pkgname=tauon-music-box
_pkgname=tauonmb
_gitname=TauonMusicBox
pkgver=7.4.7
pkgrel=1
_kissfftver=131.1.0
_miniaudiocommit=1778a5e839514f35f41d31449f3573e3adffc51a
pkgdesc="A modern streamlined music player"
arch=('any')
url="https://tauonmusicbox.rocks"
license=('GPL3')

depends=('python-pillow'
         'python-pylast'
         'python-pysdl2' # AUR
         'python-send2trash'
         'python-musicbrainzngs'
         'python-mutagen'
         'python-isounidecode' # AUR
         'python-setproctitle'
         'python-gobject'
         'python-cairo'
         'python-beautifulsoup4'
         'python-requests'
         'python-dbus'
         'python-natsort'
         'python-websocket-client'
         'libnotify'
         'ffmpeg'
         'flac'
         'noto-fonts-extra'
         'noto-fonts'
         'sdl2_image'
         'xdg-utils'
         'mpg123'
         'opusfile'
         'wavpack'
         'libvorbis'
         'libappindicator-gtk3'
         'libopenmpt'
         'libsamplerate'
         'python-httpx' # Workaround for https://bugs.archlinux.org/task/74611
         )
         
optdepends=('noto-fonts-cjk: Matching font for CJK characters'
            'p7zip: 7z archive extraction support'
            'unrar: RAR archive extraction support'
            'python-plexapi: Plex streaming'
            'python-pypresence: Discord status support'
            'python-pychromecast: Chromecast stream support' 
            'picard: Recommended tag editor'
            'python-jxlpy: JPEG XL image support' # AUR
            )
            
source=("$pkgname-$pkgver.tar.gz::https://github.com/Taiko2k/TauonMusicBox/archive/v$pkgver.tar.gz"
	"kissfft.tar.gz::https://github.com/mborgerding/kissfft/archive/refs/tags/$_kissfftver.tar.gz"
	"miniaudio.tar.gz::https://github.com/mackron/miniaudio/archive/$_miniaudiocommit.tar.gz"
	)

b2sums=('6697891897bfe5ec3f8ab276bd4f78d25a2f870039b7defa66941edfaa50471fb2726e7d0c7458b513153bc1445b838451cb163c1773d30449f1f1929226577f'
        '74e6d2e7d132a311b31c28a468e13d9772a53f0ea0abed0e0f49d8db9c183fb0646f58fd38df3e797b8577285899daf6b80446b149ce2582bb828410656d96df'
        '4a1de7006beae269b7109dfd6e3b9f2402e2c408d85ec982184295746934aca32d8452ad39cc4fa3a615b8549bd75d4551fa885b42c282a4a0c8b9c98bb22ae6')

build() {
    mv kissfft-$_kissfftver/* $_gitname-$pkgver/src/phazor/kissfft/
    mv miniaudio-$_miniaudiocommit/* $_gitname-$pkgver/src/phazor/miniaudio/
    cd "$_gitname-$pkgver"
    python compile-translations.py
    bash compile-phazor.sh
}

package() {
    cd "$_gitname-$pkgver"
    install -Dm755 tauon.py -t "$pkgdir/opt/$pkgname"
    install -Dm644 input.txt -t "$pkgdir/opt/$pkgname"
    
    cp -r  assets templates theme t_modules lib "$pkgdir/opt/$pkgname"
 
    for t in de es fr_FR hu id ja_JP nb_NO pl pt pt_BR pt_PT ru sv zh_CN tr; do
        install -Dm644 locale/${t}/LC_MESSAGES/*.mo -t "$pkgdir/usr/share/locale/${t}/LC_MESSAGES"
    done
 
    install -Dm644 "extra/$_pkgname.desktop" -t "$pkgdir/usr/share/applications"
    install -Dm644 "extra/$_pkgname-symbolic.svg" -t "$pkgdir/usr/share/icons/hicolor/symbolic/apps"
    install -Dm644 "extra/$_pkgname.svg" -t "$pkgdir/usr/share/icons/hicolor/scalable/apps"
    install -Dm755 "extra/tauonmb.sh" "$pkgdir/opt/$pkgname/tauonmb.sh"
    install -Dm755 "extra/tauonmb.sh" "$pkgdir/usr/bin/tauon"
}
