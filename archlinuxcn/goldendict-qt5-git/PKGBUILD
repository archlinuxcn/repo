# Maintainer: Yuanji <self@gimo.me>
# Contributor: Mohammadreza Abdollahzadeh <morealaz at gmail dot com>
# Contributor:  VirtualTam <virtualtam@flibidi.net>
# Contributor: Eugene Yudin aka Infy <Eugene dot Yudin at gmail dot com>

pkgname=goldendict-qt5-git
pkgver=1.5.0.RC2.439.gdda311cf
pkgrel=1
pkgdesc="Feature-rich dictionary lookup program."
arch=('i686' 'x86_64')
url="http://goldendict.org/"
license=('GPL3')
depends=('ffmpeg' 'hunspell' 'libao' 'libeb' 'qt5-multimedia' 'qt5-svg' 'qt5-tools' 'qt5-webkit' 'qt5-x11extras')
makedepends=('git')
conflicts=('goldendict' 'goldendict-svn' 'goldendict-git-opt')
provides=('goldendict')
_gitname="goldendict"
source=("git+https://github.com/goldendict/goldendict.git")
sha256sums=(SKIP)

pkgver() {
  cd ${_gitname}
  git describe --always --tags | sed 's|-|.|g'
}

prepare() {
  cd ${_gitname}
  echo "QMAKE_CXXFLAGS_RELEASE = $CFLAGS" >> goldendict.pro
  echo "QMAKE_CFLAGS_RELEASE = $CXXFLAGS" >> goldendict.pro
}

build(){
  cd ${_gitname}
  PREFIX="/usr" qmake-qt5 "CONFIG+=zim_support"
  make
}

package() {
  cd ${_gitname}
  make INSTALL_ROOT="${pkgdir}" install
}
# vim:set ts=2 sw=2 et:
