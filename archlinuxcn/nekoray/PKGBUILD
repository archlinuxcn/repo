# Maintainer: Chi_Tang <me@chitang.dev>
# Maintainer: Integral <integral@member.fsf.org>

pkgname=nekoray
pkgver=4.2.12
_pkgver=${pkgver/.beta/-beta}
pkgrel=2
pkgdesc="Qt based cross-platform GUI proxy configuration manager (backend: sing-box)"
arch=('x86_64' 'aarch64' 'riscv64')
url="https://github.com/Mahdi-zarei/${pkgname}"
license=('GPL-3.0-or-later')
makedepends=('cmake' 'git' 'go')
depends=(
	'qt6-base' 'qt6-svg' 'qt6-tools'
	'protobuf' 'yaml-cpp' 'zxing-cpp'
	'abseil-cpp' 'cpr'
)
optdepends=(
	'sing-geoip-db: geoip database for NekoBox'
	'sing-geosite-db: geosite database for NekoBox'
)
source=(
	"git+${url}.git#tag=${_pkgver}"
	${pkgname}.{sh,desktop}
	"fix-geoasset.patch::https://github.com/Mahdi-zarei/nekoray/pull/320.patch"
)
install="${pkgname}.install"
sha512sums=('21ec6d5d5311bae60f657dfb472294c3cf1f3afb0529db0c13c43d1b521908a070f31cb2adbd8d0b39b53bbb6bff2bbfe023a099d9d619899580ed2a6de2828a'
            'b377f7e8c859ee0b5bc05f89e2dc6c0b8535e740e089e9afe5e5f145c38a05fccbbddfcb6eb8ced9a7478e4d9ccc3cbd3ffb6843c128189c75599c40c87737e8'
            'b0cfd99d7fd038d660af275a5f2fc7f9ebfe63d6751edd6eea66a8c5350f314b6dbc9eddaa5aaed134e97087290630d369b1bdf4ad59d12868c780103b33dbed'
            'ca2f1cc68f20c1a0a18bf6ee94835b78a70b4f13fd48a3913e90dacc74f5b6a33db18486082ed9f962f9952009bfaf9257a3055c2ebfea5cce21ba53829718fd')

prepare() {
	patch -d "${pkgname}" -p1 -i ../fix-geoasset.patch
}

build() {
	cd "${pkgname}/"

	cmake -B build \
		-DCMAKE_BUILD_TYPE=None \
		-DCMAKE_INSTALL_PREFIX=/usr

	cmake --build build

	export GOARCH

	case $CARCH in
	x86_64) GOARCH=amd64 ;;
	aarch64) GOARCH=arm64 ;;
	riscv64) GOARCH=riscv64 ;;
	esac

	GOOS=linux ./script/build_go.sh
}

package() {
	install -Dm644 "${pkgname}.desktop" -t "${pkgdir}/usr/share/applications/"
	install -Dm755 "${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"

	cd "${pkgname}/"
	install -Dm644 "res/public/nekobox.png" -t "${pkgdir}/usr/share/pixmaps/"

	# Core
	local core_srcdir=linux

	case $CARCH in
	x86_64) core_srcdir+=64 ;;
	aarch64) core_srcdir+=-arm64 ;;
	riscv64) core_srcdir+=-riscv64 ;;
	esac

	install -Dm755 "deployment/${core_srcdir}/nekobox_core" -t "${pkgdir}/usr/lib/${pkgname}/"

	# Binary file
	install -Dm755 "build/${pkgname}" -t "${pkgdir}/usr/lib/${pkgname}/"
}
