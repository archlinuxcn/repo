# Maintainer: Mark Wagie <mark dot wagie at proton dot me>
pkgname=python-uni-curses
_name=unicurses
pkgver=3.1.2
pkgrel=2
pkgdesc="Unified Curses Wrapper for Python"
arch=('any')
url="https://github.com/unicurses/unicurses"
license=('GPL-3.0-or-later')
depends=('ncurses' 'python' 'python-x256')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
source=("$_name-$pkgver.tar.gz::$url/archive/refs/tags/v.$pkgver.tar.gz")
sha256sums=('306d63739bfb47032f9c9d7c6ac00eea08667b09559fbac49d57d55e4cb9f676')

prepare() {
  cd "$_name-v.$pkgver"

  # Don't package Windows binaries
  sed -i '/package_data/d' setup.py
}

build() {
  cd "$_name-v.$pkgver"
  python -m build --wheel --no-isolation
}

package() {
  cd "$_name-v.$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl
}
