# Maintainer: Patrick Northon <northon_patrick3@yahoo.ca>

_name=pygame_sdl2
_pkgname="python-${_name//_/-}"
pkgname="${_pkgname}-git"
pkgver=2.1.0.r470.18b0d3f
pkgrel=1
pkgdesc="SDL2-based implementation of the Pygame API"
arch=('x86_64')
url="https://github.com/renpy/${_name}"
license=('LGPL-2.1-or-later' 'Zlib')
depends=(
    'glibc'
    'libjpeg-turbo'
    'libpng'
    'python'
    'sdl2'
    'sdl2_image'
    'sdl2_mixer'
    'sdl2_ttf'
)
makedepends=(
    'cython'
    'git'
    'python-build'
    'python-installer'
    'python-setuptools'
    'python-wheel'
)
provides=("${_pkgname}=${pkgver}")
conflicts=("${_pkgname}")
source=("git+${url}.git"
        "0001-Add-headers-argument-and-simplify-header-handling.patch")
sha256sums=('SKIP'
            'cfae1b9df49e065ffcf630ea0714e9c7628d433ea2464be4cb7ab1a8930613d4')

pkgver() {
    cd "${srcdir}/${_name}"
    version="$(cat 'src/pygame_sdl2/version.py' | grep '^vernum = ' | sed 's/^vernum = //; s/(//; s/)//; s/, /./g')"
    echo "${version}.r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

prepare() {
    cd "${srcdir}/${_name}"
    patch -Np1 -i ../0001-Add-headers-argument-and-simplify-header-handling.patch
}

build() {
    cd "${srcdir}/${_name}"
    python -m build --wheel --no-isolation
}

package() {
    cd "${srcdir}/${_name}"
    python -m installer --destdir="${pkgdir}" dist/*.whl
    install -Dm0644 'COPYING.LGPL21' "${pkgdir}/usr/share/licenses/${pkgname}/COPYING.LGPL-2.1"
    install -Dm0644 'COPYING.ZLIB' "${pkgdir}/usr/share/licenses/${pkgname}/COPYING.Zlib"
}
