# Maintainer: Jan Neumann <neum dot ja at gmail dot com>
# Contributor: Michael Straube <straubem@gmx.de>

pkgname=latte-dock-git
_gitname=latte-dock
pkgver=0.10.8.r6000.93c50a7e
pkgrel=1
pkgdesc='Latte is a dock based on plasma frameworks that provides an elegant and intuitive experience for your tasks and plasmoids'
arch=('i686' 'x86_64')
url='https://invent.kde.org/plasma/latte-dock'
license=('GPL')
depends=('plasma-framework>=5.38.0' 'knewstuff' 'hicolor-icon-theme' )
makedepends=('git' 'cmake' 'extra-cmake-modules' 'appstream' 'plasma-wayland-protocols')
optdepends=('libunity: quicklists, counters, and progress bars for apps using libunity')
conflicts=('latte-dock')
provides=('latte-dock')
source=("git+https://invent.kde.org/plasma/latte-dock.git")

sha256sums=('SKIP')

pkgver() {
    cd ${_gitname}
    printf "0.10.8.r%s.%s" $(git rev-list --count HEAD) $(git rev-parse --short HEAD)
}

prepare() {
    mkdir -p build
}

build() {
    cd build

    cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release ../${_gitname}
    make
}

package() {
    cd build
    make DESTDIR="${pkgdir}" install
}
