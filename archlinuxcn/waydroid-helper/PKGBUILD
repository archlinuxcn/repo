# Maintainer: Rikka <ayasa0520@gmail.com>

pkgname=waydroid-helper
pkgver=0.2.9
pkgrel=1
pkgdesc="A GUI application for Waydroid configuration and extension installation"
arch=('any')
url="https://github.com/ayasa520/${pkgname}"
license=('GPL-3.0-or-later')
depends=('gtk4' 'libadwaita' 'waydroid' 'python' 'python-httpx' 'python-gobject>=3.50.0' 'python-yaml' 'python-aiofiles' 'python-dbus')
makedepends=('git' 'python-setuptools' 'meson')
provides=("${pkgname}")
conflicts=("${pkgname}")
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('ec4ab70337938038a386a56872c24fc9d1ac13b0e28003b3d530854b9c77171c')

build() {
	arch-meson "${pkgname}-${pkgver}/" build
	meson compile -C build
}

package() {
	meson install -C build --destdir "${pkgdir}"
	install -Dm644 "${pkgname}-${pkgver}/COPYING" -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
