# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: Thomas Baechler <thomas@archlinux.org>

pkgname=libfprint-git
pkgver=1.94.5.r19.g7080a5ff
pkgrel=1
pkgdesc="Library for fingerprint readers"
arch=(x86_64)
url="https://www.freedesktop.org/wiki/Software/fprint/libfprint"
license=(LGPL)
depends=('libgusb' 'pixman' 'nss' 'systemd' 'libgudev')
makedepends=('git' 'meson' 'gtk-doc' 'gobject-introspection' 'cmake')
checkdepends=(cairo)
provides=('libfprint-2.so' 'libfprint')
conflicts=('libfprint')
source=("libfprint-git::git+https://gitlab.freedesktop.org/libfprint/libfprint.git")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --long | sed -r 's/^v//g;s/([^-]*-g)/r\1/;s/-/./g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}

# vim: set ts=2 sw=2 et:
