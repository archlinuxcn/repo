# Maintainer: Allen Wild <allenwild93 at gmail dot com>
# Contributor: mrxx <mrxx at cyberhome dot at>

_name=dislocker
pkgname=${_name}-noruby
pkgver=0.7.1
pkgrel=5
pkgdesc="Read BitLocker encrypted volumes under Linux (no ruby bindings/dependency)"
arch=('i686' 'x86_64')
url="https://github.com/Aorimn/dislocker"
license=('GPL2')
depends=("fuse" "libmbedcrypto.so" "mbedtls")
makedepends=('cmake')
provides=("dislocker" "libdislocker.so")
conflicts=('dislocker' 'dislocker-git')
optdepends=('ntfs-3g: NTFS file system support')
source=(https://github.com/Aorimn/dislocker/archive/v$pkgver.tar.gz
        # Fix issues with Win10 1903 https://github.com/Aorimn/dislocker/pull/187
        https://github.com/Aorimn/dislocker/commit/f778706e72d9f7bac84a5a4cd37f0b56cebdef49.patch
        no-ruby.patch)
sha256sums=('742fb5c1b3ff540368ced54c29eae8b488ae5a5fcaca092947e17c2d358a6762'
            'ad177e697e84f88ee91f7cb6a9ab3a47100665155107abc8c9e965529bc81cff'
            '0b8d211cab004d17ec7a3841ca4597a909a5b61fe6197125d26a09f4ef669a85')

prepare() {
  cd "$srcdir/$_name-$pkgver"

  patch -p1 -i "$srcdir/f778706e72d9f7bac84a5a4cd37f0b56cebdef49.patch"
  patch -p1 -i "$srcdir/no-ruby.patch"
}

build() {
  cd "$srcdir/$_name-$pkgver"

  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -Dlibdir=/usr/lib \
        -D WARN_FLAGS:STRING="-Wall -Wextra" \
        -DWITH_RUBY=OFF \
        .
  make
}

package() {
  cd "$srcdir/$_name-$pkgver"
  make DESTDIR="$pkgdir/" install

  install -Dm644 README.md ${pkgdir}/usr/share/doc/${_name}/README
  install -Dm644 CHANGELOG.md ${pkgdir}/usr/share/doc/${_name}/CHANGELOG
  install -Dm644 LICENSE.txt ${pkgdir}/usr/share/licenses/${_name}/LICENSE
}

# vim: ft=sh et sw=2 sts=2
