# Maintainer: Coelacanthus <uwu@coelacanthus.name>
# Contributor: Peter Cai <peter@typeblog.net>

pkgname=lpac
pkgver=2.2.1
pkgrel=1
pkgdesc="Local Profile Agent (LPA) for eSIM cards via PC/SC readers, or AT / APDU commands over stdio."
arch=(x86_64 aarch64 armv7h)
url="https://github.com/estkme-group/lpac"
license=('AGPL-3.0-only AND LGPL-2.0-only AND MIT')
depends=(curl pcsclite glibc)
makedepends=(cmake ninja)
source=($pkgname-$pkgver.tar.gz::https://github.com/estkme-group/lpac/archive/refs/tags/v$pkgver.tar.gz)
b2sums=('ab9bcad38d92088df79040372cca1dbc1d71dd28b05dc847c35a1250ee3e66ff9320ef49635ef3463dec71b38ee4499901117c0c50b588456ff5370fd2f0aace')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  cmake -S . -B build \
    -G Ninja \
    -DCMAKE_INSTALL_PREFIX="/usr" \
    -DLPAC_DYNAMIC_LIBEUICC=ON \
    -DLPAC_DYNAMIC_DRIVERS=ON
  ninja -C build
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 -t "$pkgdir"/usr/share/licenses/"$pkgname" README.md
}

