# Maintainer: Mattia Borda <mattiagiovanni.borda@icloud.com>
# Contributor: Enes Hecan <nshecan@gmail.com>

pkgname=whatsapp-for-linux
pkgver=1.6.4
pkgrel=2
pkgdesc="An unofficial WhatsApp desktop application for linux"
url="https://github.com/eneshecan/${pkgname}"
arch=(i686 x86_64 armv7h aarch64)
license=('GPL-3.0-or-later')
depends=(gtkmm3 webkit2gtk libayatana-appindicator libcanberra gst-plugins-base gst-plugins-good gst-plugins-bad gst-libav)
makedepends=(cmake)
optdepends=('hunspell-en_us: spell-checking')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('8d09d8d95593e7642cbed4ea99c99eebe178a86a073aa6d6192da036f0ccf568')

build() {
	cd "${pkgname}-${pkgver}/"
	cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr .
	make
}

package() {
	cd "${pkgname}-${pkgver}/"
	make DESTDIR="${pkgdir}/" install
}
