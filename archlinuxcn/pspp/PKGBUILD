# Maintainer: Hu Butui <hot123tea123@gmail.com>
# Contributor: Miguel Revilla <yo@miguelrevilla.com>
# Contributor: joyfulgirl <joyfulgirl (at) archlinux.us>

pkgname=pspp
pkgver=2.0.0
pkgrel=2
pkgdesc='Statistical analysis program. Free replacement for SPSS'
arch=('x86_64')
url='http://www.gnu.org/software/pspp'
license=('GPL-3.0-or-later')
depends=(
  gsl
  gtksourceview3
  postgresql-libs
  spread-sheet-widget
)
makedepends=(
  python
)
optdepends=(
  'zlib: GNUmeric support'
  'libxml2: GNUMERIC support'
)
options=('!libtool' '!emptydirs')
source=("${pkgname}-${pkgver}.tar.gz::https://ftp.gnu.org/gnu/pspp/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}-${pkgver}.tar.gz.sig::https://ftp.gnu.org/gnu/pspp/${pkgname}-${pkgver}.tar.gz.sig"
)
sha256sums=('a8f6cb886af5b083843579bcd6fb191c054acce28cc68b58e7c5f09996a2d8df'
            'SKIP')
validpgpkeys=('C2D1AB061656AAC54B5E975485199DE8C6648E90')
build() {
  cd "${pkgname}-${pkgver}"
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
# vim:set ts=2 sw=2 et:
