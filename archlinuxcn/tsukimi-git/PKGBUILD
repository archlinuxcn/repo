_pkgname="tsukimi"
pkgname="${_pkgname}-git"
pkgver=0.21.0.r1219.g30c938
pkgrel=1
pkgdesc='A simple third-party Emby client'
arch=('x86_64')
url="https://github.com/tsukinaha/tsukimi"
license=('GPL-3.0-or-later')
provides=('tsukimi')
conflicts=('tsukimi')
depends=(
	'mpv'
	'ffmpeg'
	'libadwaita'
	'gstreamer'
	'gtk4'
)
makedepends=(
	'cargo-nightly'
	'git'
	'meson'
)
source=(
	git+https://github.com/tsukinaha/tsukimi.git
)
sha256sums=(
	'SKIP'
)
options=(!lto)

pkgver() {
	cd "${_pkgname}"
	echo $(grep '^version =' Cargo.toml | head -n1 | cut -d\" -f2).r$(git rev-list --count HEAD).g$(git describe --long --tags --abbrev=7 | tail -c 7)
}

build() {
	arch-meson "${srcdir}/${_pkgname}" build --no-rebuild
	meson compile -C build
}

package() {
	meson install -C build --destdir "$pkgdir"
}
