# Maintainer: Gavin Luo <lunt.luo@gmail.com>
# Contributor: Igor Dyatlov <dyatlov.igor@protonmail.com>

pkgname=gnome-shell-extension-pip-on-top-git
_pkgname=${pkgname%-git}
pkgver=r43.5cae4e2
pkgrel=1
pkgdesc='Makes "Picture-in-Picture" windows stay on top (even on Wayland session). Compatible with Firefox and Clapper media player.'
arch=('any')
url="https://github.com/Rafostar/gnome-shell-extension-pip-on-top"
license=('GPL-2.0-only')
depends=('gnome-shell>=45')
makedepends=('git' 'zip')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=("$_pkgname"::git+"${url}".git
       gnome-49-support.patch)
b2sums=('SKIP'
        'ea854d94f70790912c469adc0deeeea0508896d56ee6d503e33c0134f74fe0860153ab760c0ea0e969b4000abe06b8e8034119282b8621e1b31eb84012e02ff6')

pkgver() {
  cd "${_pkgname}"

  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${_pkgname}"

  git apply -3 "${srcdir}"/*.patch

  ./translate.sh
}

build() {
  cd "${_pkgname}"

  ./bundle.sh
}

package() {
  cd "${_pkgname}"

  local _uuid; _uuid=$(grep -Po '(?<="uuid": ")[^"]*' metadata.json)

  local _ext_dir="$pkgdir/usr/share/gnome-shell/extensions/${_uuid}"; install -d "$_ext_dir"
  bsdtar --uid 0 --gid 0 -xvf "${_uuid}".zip -C "$_ext_dir"

  local _schema_dir="$pkgdir/usr/share/glib-2.0"; install -d "$_schema_dir"
  rm "${_ext_dir}/schemas/gschemas.compiled" && mv "$_ext_dir/schemas" "$_schema_dir"

  mv "$_ext_dir/locale" "$pkgdir/usr/share" 
}
