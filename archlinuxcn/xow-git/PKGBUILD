pkgname=xow-git
_pkgname=xow
pkgver=r147.a396e3f
pkgrel=1
pkgdesc="Linux driver for the Xbox One wireless dongle"
arch=(x86_64)
url=https://github.com/medusalix/xow
license=('GPL2')
depends=(udev)
makedepends=('git' 'curl' 'cabextract')
install=xow.install
source=('00-make-libusb-static.patch'
        'git+https://github.com/medusalix/xow'
        'git+https://github.com/libusb/libusb')
md5sums=('9038edbe2d9710888f750d0967128c6b'
         'SKIP'
         'SKIP')

pkgver() {
  cd $srcdir/$_pkgname
  ( set -o pipefail
    git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  ) 
}

prepare(){
  cd $srcdir/$_pkgname
  patch -Np1 <../00-make-libusb-static.patch

  cd $srcdir/libusb
  autoreconf -vfi
}

build() {
  cd $srcdir/libusb
  ./configure --prefix="$PWD/target"
  make install

  cd $srcdir/$_pkgname
  make BUILD=RELEASE
}

package() {
  cd $srcdir/$_pkgname

  make PREFIX=/usr DESTDIR=$pkgdir/ \
  UDEVDIR=/usr/lib/udev/rules.d \
  MODLDIR=/usr/lib/modules-load.d \
  MODPDIR=/usr/lib/modprobe.d \
  SYSDDIR=/usr/lib/systemd/system \
  install
}

