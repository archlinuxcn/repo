# Maintainer: Shreyansh Khajanchi <shreyansh_k@live.com>

pkgname=gnirehtet
pkgver=2.5.1
pkgrel=1
pkgdesc="A tool that provides reverse tethering for Android"
arch=('x86_64')
url="https://github.com/Genymobile/gnirehtet"
license=('Apache-2.0')
depends=(
    'android-tools'
    'gcc-libs'
    'glibc'
)
makedepends=('rust')
source=("${pkgname}-${pkgver}::${url}/archive/v${pkgver}.tar.gz"
        "${url}/releases/download/v${pkgver}/gnirehtet-rust-linux64-v${pkgver}.zip")
sha256sums=('0d41361b9ac8b3b7fa4f4a0aff933472a72886556bd3fc4659be299b546274e6'
            'dee55499ca4fef00ce2559c767d2d8130163736d43fdbce753e923e75309c275')

prepare() {
    cd "$srcdir/gnirehtet-${pkgver}/relay-rust"
    sed -i 's|gnirehtet.apk|/usr/share/gnirehtet/gnirehtet.apk|' src/main.rs
}

build() {
    cd "$srcdir/gnirehtet-${pkgver}/relay-rust"
    cargo build --release
}

package() {
    cd "$srcdir/gnirehtet-${pkgver}/relay-rust"
    install -Dm0755 target/release/gnirehtet -t "$pkgdir/usr/bin"
    install -Dm0644 ../LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
    cd "$srcdir/gnirehtet-rust-linux64"
    install -Dm0644 gnirehtet.apk -t "$pkgdir/usr/share/gnirehtet"
}
