# Maintainer: Hao Long <imlonghao@archlinuxcn.org>
# Contributor: Lin Ruoshui <LinRs at users.noreply.github dot com>
# Contributor: hexchain <i at hexchain.org>

pkgname=hmcl
_pkgname=HMCL
pkgver=3.5.5
# https://github.com/huanghongxun/HMCL/commits/release-${pkgver}
_commit=470c3d6c24619ee5c3d6b8f0cec28bce7b22bf09
pkgrel=1
pkgdesc="A Minecraft Launcher which is multi-functional, cross-platform and popular"
arch=('any')
url="https://github.com/huanghongxun/HMCL"
license=('GPL3')
provides=('hmcl')
conflicts=('hmcl')
depends=('java-runtime')
makedepends=('liberica-jdk-11-full-bin')
source=("hmcl-launch-script"
        "${pkgname}.desktop"
        "${pkgname}-${pkgver}.tgz::${url}/archive/${_commit}.tar.gz"
        "patchOfAvoidingHCMLOfficalBuildCheckToLogin.patch")
b2sums=('1de8eca922b01b9c4c6d9a173143f05b900fd6b12b1b27bae175948a43125619d0e14c82d704548f70539af13eb4fd9f7288749265949089c7d6a8fe9032d284'
        '6c38da38fa13ad0af061d593f7733d6a406025c473240ccee4d07c89e71f8d5ead430374a7500f1395a7341c67e06ef3664cb1747ab93cba63459f7906b1598a'
        '0474fc9de121e3b5638a76c8d74769da00ff2af4a3f5e9718ec70745407dec5c1ef479981a5f8a6b555708d7d63ed11c5b6138c07d21d1b07d3080424f28ef06'
        'd23d7f24854419a0e441e5295cd70609dac0384425b636958cdee5131ceba8ea6d65ad3c123d9a865d1329d48bed71856a51c05c1d921008e8dafc7446f1b31c')

build() {
  cd "${_pkgname}-${_commit}"
  patch --strip=1 < $srcdir/patchOfAvoidingHCMLOfficalBuildCheckToLogin.patch
  export JAVA_HOME=/usr/lib/jvm/liberica-jdk-11-full
  ./gradlew clean build
}

package() {
  # custom launch script
  install -Dm755 "hmcl-launch-script" "${pkgdir}/usr/bin/$pkgname"
  # desktop file
  install -Dm644 "hmcl.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"

  cd "${_pkgname}-${_commit}/${_pkgname}/build"

  # install jar
  _path=$(echo libs/HMCL*.jar)
  install -Dm644 $_path "${pkgdir}/usr/share/java/${pkgname}/${pkgname}.jar"
  # install icon
  install -Dm644 "resources/main/assets/img/craft_table.png" "${pkgdir}/usr/share/icons/hicolor/48x48/apps/${pkgname}.png"
}
