# Maintainer: Christian Boxd√∂rfer <christian.boxdoerfer@posteo.de>
pkgname=fsearch-git
_gitname=fsearch
pkgver=0.2.r154.ga90ded1
pkgrel=1
pkgdesc="A fast graphical file search utility. Development version."
arch=('i686' 'x86_64')
url="https://cboxdoerfer.github.io/fsearch"
license=('GPL2')
depends=('gtk3' 'icu' 'pcre2')
makedepends=('git' 'meson' 'itstool')
conflicts=('fsearch')
source=('git+https://github.com/cboxdoerfer/fsearch.git')
md5sums=('SKIP')

pkgver() {
    cd "$srcdir/${pkgname%-git}"
    git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g' 
}

build() {
    arch-meson $_gitname build -Dchannel=AUR-devel
    meson compile -C build
}

check() {
    meson test -C build
}

package() {
    DESTDIR="$pkgdir" meson install -C build
}
