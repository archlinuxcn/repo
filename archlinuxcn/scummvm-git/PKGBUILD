# Maintainer: Thomas Krug <t.krug@elektronenpumpe.de>
# Contributor: Joe Davison <joedavison.davison@gmail.com>

pkgname=scummvm-git
_pkgname=scummvm
pkgver=r98254.580c49ba42
pkgrel=1
pkgdesc="A 'virtual machine' for several classic graphical point-and-click adventure games."
arch=('i686' 'x86_64')
url="http://www.scummvm.org"
license=('GPL')
depends=('libpng' 'libtheora' 'sdl2' 'sdl2_net' 'fluidsynth' 'flac' 'faad2' 'libvorbis' 'libmad' 'freetype2'
         'libgl' 'glu' 'libjpeg-turbo' 'libmpeg2' 'curl' 'a52dec')
makedepends=('git')
conflicts=('scummvm')
provides=('scummvm')
source=("$_pkgname"::'git+https://github.com/scummvm/scummvm.git')
md5sums=('SKIP')
 
pkgver() {
  cd "$srcdir/$_pkgname"

  echo r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}
 
build() {
  cd "$srcdir/$_pkgname"
 
  ./configure --enable-c++11 \
              --enable-release \
              --with-flac-prefix=/usr \
              --prefix=/usr \
              --enable-all-engines

  make
}
 
package() {
  cd "$srcdir/$_pkgname"
 
  make DESTDIR="$pkgdir" install

  install -D -m 644 dists/scummvm.desktop "$pkgdir/usr/share/applications/scummvm.desktop"
}

# vim:set ts=2 sw=2 et:
