# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=openbve-git
_pkgname=OpenBVE
_pkgname2=openbve
pkgver=1.7.2.0.r147.gfb8463f2
pkgrel=1
pkgdesc="Free-as-in-freedom train simulator placed in the public domain"
arch=(any)
url="https://openbve-project.net/"
license=(MIT 'custom:public domain')
depends=(hicolor-icon-theme libxi mono openal)
makedepends=(git mono-msbuild nuget)
provides=('openbve')
conflicts=('openbve')
source=(git+https://github.com/leezer3/$_pkgname.git
        $_pkgname2.sh
        $_pkgname2.appdata.xml)
sha256sums=('SKIP'
            'cf0f2a28e65248f742d6bb80f3101e36f24097e699c4e5acdf3fd4dbb965d3c3'
            '1ae1d4bf4dbe0d4e012fafef090d98b4c67ba87bee8981af0b2de11015b57df2')

pkgver() {
  cd "$_pkgname"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd $_pkgname
  nuget restore OpenBVE.sln
}

build() {
  cd $_pkgname
  msbuild /p:Configuration=Release OpenBVE.sln
}

package() {
  cd $_pkgname

  # Binaries
  install -dm755 "$pkgdir/usr/lib/$_pkgname2/"
  cp -r bin_release/* "$pkgdir/usr/lib/$_pkgname2/"

  # Data
  install -dm755 "$pkgdir/usr/share/games/$_pkgname2/"
  mv "$pkgdir/usr/lib/$_pkgname2/Data" "$pkgdir/usr/share/games/$_pkgname2/"
  ln -s ../../share/games/$_pkgname2/Data "$pkgdir/usr/lib/$_pkgname2/Data"

  # Executable
  install -Dm755 ../$_pkgname2.sh "$pkgdir/usr/bin/$_pkgname2"

  # Desktop file and icon
  install -Dm644 debian/$_pkgname2.png "$pkgdir/usr/share/icons/hicolor/128x128/apps/$_pkgname2.png"
  install -Dm644 debian/$_pkgname2.desktop "$pkgdir/usr/share/applications/$_pkgname2.desktop"
  install -Dm644 ../$_pkgname2.appdata.xml "$pkgdir/usr/share/metainfo/$_pkgname2.appdata.xml"

  # License
  install -dm755 "$pkgdir/usr/share/licenses/$_pkgname2/"
  install -m644 licenses/* Readme.md \
    "$pkgdir/usr/share/licenses/$_pkgname2/"
}
