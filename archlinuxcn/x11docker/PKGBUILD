# Maintainer: Hu Butui <hot123tea123@gmail.com>
# Contributor: Andre Schr√∂der <andre.schroedr at gmail dot com>

pkgname=x11docker
pkgver=7.6.0
pkgrel=1
pkgdesc='Run GUI applications and desktops in Docker. Focus on security.'
arch=('any')
url='https://github.com/mviereck/x11docker'
license=('MIT')
depends=(
  bash
  docker
  xclip
  xorg-server-xephyr
  xorg-xauth
  xorg-xdpyinfo
  xorg-xhost
  xorg-xinit
  xorg-xrandr
  xpra
)

optdepends=('cups: --printer support'
            'kwin: --kwin, --kwin-xwayland support'
            'nxagent: --nxagent support'
            'pulseaudio: --pulseaudio support'
            'weston: --weston, --xpra-xwayland, --weston-xwayland, --xdummy-xwayland support'
            'xdotool: --xpra-xwayland, --xdummy-xwayland support'
            'xorg-server-xvfb: --xvfb support'
            'xorg-server-xwayland: --xpra-xwayland, --weston-xwayland, --kwin-xwayland, --xwayland, --xdummy-xwayland support'
            'xorg-server: --xorg, --xdummy support')

source=("${pkgname}-${pkgver}.tar.gz::$url/archive/v${pkgver}.tar.gz")
sha256sums=('7a1c6d67a9ac39cbc800f3b4494c4f74c00c12e27f332b5fd73247f17982f06c')

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 x11docker -t "${pkgdir}/usr/bin"
  install -Dm644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
