pkgname=julia-git-llvmextra_jll-src
pkgver=0.0.27+1
_commit=722731315d89df2a0ad0a7df23270fb910fdd255
pkgrel=1
pkgdesc="LLVMExtra_jll.jl"
url="https://github.com/JuliaBinaryWrappers/LLVMExtra_jll.jl.git"
arch=('any')
license=('MIT')
makedepends=(git julia-pkg-scripts)
depends=(julia-git libllvmextra_jll)
source=("git+https://github.com/JuliaBinaryWrappers/LLVMExtra_jll.jl.git#commit=$_commit"
        jll.toml.in)
sha256sums=('SKIP'
            '8fa0f6501498012f5f4a258bd42ccf315e32eb39894ca1218e4eb0b620db2c39')

_versioned_llvm=

build() {
  if [[ -n $_versioned_llvm ]]; then
    _llvm_ver=$(llvm-config-${_versioned_llvm} --version)
  else
    _llvm_ver=$(llvm-config --version)
  fi
  [[ $_llvm_ver =~ ^([0-9]*).* ]]
  llvm_maj_ver=${BASH_REMATCH[1]}

  sed -e "s/@LLVM_VER@/${llvm_maj_ver}/g" jll.toml.in > jll.toml

  cd LLVMExtra_jll.jl

  julia /usr/lib/julia/julia-gen-jll.jl LLVMExtra_jll ../jll.toml
}

package() {
  cd LLVMExtra_jll.jl

  JULIA_INSTALL_SRCPKG=1 . /usr/lib/julia/julia-install-pkg.sh LLVMExtra_jll "${pkgdir}" "${pkgname}" julia-git
}
