# Maintainer: hubutui <hot123tea123@gmail.com>

_pkgname=XVector
_pkgver=0.32.0
pkgname=r-${_pkgname,,}
pkgver=0.32.0
pkgrel=1
pkgdesc='Foundation of external vector representation and manipulation in Bioconductor'
arch=('x86_64')
url="https://bioconductor.org/packages/${_pkgname}"
license=('Artistic2.0')
depends=(
  r
  r-biocgenerics
  r-iranges
  r-s4vectors
  r-zlibbioc
)
optdepends=(
  r-biostrings
  r-drosophila2probe
  r-runit
)
source=("https://bioconductor.org/packages/release/bioc/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
sha256sums=('192b8175d836bf574254cf163580cc7f18fc6c3eb49f7136a057ab98da2383b3')

build() {
  R CMD INSTALL ${_pkgname}_${_pkgver}.tar.gz -l "${srcdir}"
}

package() {
  install -dm0755 "${pkgdir}/usr/lib/R/library"
  cp -a --no-preserve=ownership "${_pkgname}" "${pkgdir}/usr/lib/R/library"
}
# vim:set ts=2 sw=2 et:
