pkgbase=julia-git-plots-src
_subpkgs=(Plots
          RecipesBase
          RecipesPipeline)
pkgname=()
pkgver=1.41.5
_commit=586b8c63b1c8a0bf21d52874cd53751177f0e69d
pkgrel=1
pkgdesc="Plots.jl"
url="https://github.com/JuliaPlots/Plots.jl.git"
arch=('any')
license=('MIT')
makedepends=(git julia-pkg-scripts)
depends=(julia-git)
source=("git+https://github.com/JuliaPlots/Plots.jl.git#commit=$_commit")
sha256sums=('9a99cfb98381f465743f41859851bec3b5b0266cb3e7cdcc79dda88696c3577a')

_subpackage() {
  local _subpkg=$1
  cd Plots.jl/${_subpkg}

  JULIA_INSTALL_SRCPKG=1 . /usr/lib/julia/julia-install-pkg.sh ${_subpkg} "${pkgdir}" "${pkgname}" julia-git
}

for _subpkg in "${_subpkgs[@]}"; do
  pkgname=("${pkgname[@]}" julia-git-${_subpkg,,}-src)
  eval "package_julia-git-${_subpkg,,}-src() { _subpackage ${_subpkg}; }"
done
