pkgbase=julia-git-plots-src
_subpkgs=(Plots
          RecipesBase
          RecipesPipeline)
pkgname=()
pkgver=1.41.6
_commit=3165bad4cc30aa83ffb7b76382b3f19ce6674c4f
pkgrel=1
pkgdesc="Plots.jl"
url="https://github.com/JuliaPlots/Plots.jl.git"
arch=('any')
license=('MIT')
makedepends=(git julia-pkg-scripts)
depends=(julia-git)
source=("git+https://github.com/JuliaPlots/Plots.jl.git#commit=$_commit")
sha256sums=('23d195b4ef677549d110c04a2e7d282f3060357438fcad6cce34b73d7a0e9db2')

_subpackage() {
  local _subpkg=$1
  cd Plots.jl/${_subpkg}

  JULIA_INSTALL_SRCPKG=1 . /usr/lib/julia/julia-install-pkg.sh ${_subpkg} "${pkgdir}" "${pkgname}" julia-git
}

for _subpkg in "${_subpkgs[@]}"; do
  pkgname=("${pkgname[@]}" julia-git-${_subpkg,,}-src)
  eval "package_julia-git-${_subpkg,,}-src() { _subpackage ${_subpkg}; }"
done
