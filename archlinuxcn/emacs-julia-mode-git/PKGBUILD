# Maintainer: Yichao Yu <yyc1992@gmail.com>

pkgname=emacs-julia-mode-git
epoch=1
pkgver=0.358.g7aafa8e
pkgrel=1
pkgdesc="Emacs major mode for the Julia programming language"
arch=('any')
url="http://julialang.org"
license=('custom:MIT')
provides=('emacs-julia-mode')
conflicts=('emacs-julia-mode')
makedepends=('git')
depends=('emacs')
source=('git+https://github.com/JuliaLang/julia-emacs')
md5sums=('SKIP')

pkgver() {
  cd julia-emacs

  commit_short=$(git rev-parse --short HEAD)
  build_number=$(git rev-list --count HEAD)
  printf "0.%s.g%s" "$build_number" "$commit_short"
}

build() {
  cd julia-emacs

  emacs --batch --eval \
        '(progn
          (push "." load-path)
          (batch-byte-compile))' julia-mode.el julia-mode-latexsubs.el
}

package() {
  cd julia-emacs

  mkdir -p "$pkgdir/usr/share/emacs/site-lisp"
  install -m644 julia-mode.el "$pkgdir/usr/share/emacs/site-lisp"
  install -m644 julia-mode.elc "$pkgdir/usr/share/emacs/site-lisp"
  install -m644 julia-mode-latexsubs.el "$pkgdir/usr/share/emacs/site-lisp"
  install -m644 julia-mode-latexsubs.elc "$pkgdir/usr/share/emacs/site-lisp"

  install -dm755 "${pkgdir}/usr/share/licenses/$pkgname/"
  install -m644 LICENSE.md "${pkgdir}/usr/share/licenses/$pkgname/"
}
