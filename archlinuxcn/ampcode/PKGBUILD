# Maintainer: Yuzu <aur at vitayuzu dot day>
_npmname=@sourcegraph/amp
pkgname=ampcode
pkgver=0.0.1769515295_g65c888
pkgrel=1
pkgdesc="The frontier coding agent from Sourcegraph."
arch=('x86_64')
url="https://ampcode.com"
license=('LicenseRef-Amp-Commercial-License')
depends=('nodejs' 'ripgrep' 'gcc-libs' 'glibc')
makedepends=('npm')
provides=('amp')
conflicts=('amp')
_archive="${pkgname}-${pkgver}"
source=("${_archive}.tgz::https://registry.npmjs.org/${_npmname}/-/amp-${pkgver//_/-}.tgz")
noextract=("${_archive}.tgz")
sha256sums=('0f33e42f130e7868abb64c3d58b16591a9141d2a9339e49fceace257eb8ac75c')

package() {
    npm install -g --prefix "${pkgdir}/usr" \
        --cache "${srcdir}/npm-cache" \
        "${_archive}.tgz"

    find "${pkgdir}/usr" -type d -exec chmod 755 {} +
    chown -R root:root "${pkgdir}"

    install -Dm644 "${pkgdir}/usr/lib/node_modules/${_npmname}/LICENSE.md" \
        "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
