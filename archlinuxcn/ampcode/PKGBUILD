# Maintainer: Yuzu <aur at vitayuzu dot day>
_npmname=@sourcegraph/amp
pkgname=ampcode
pkgver=0.0.1771013177_g23242a
pkgrel=1
pkgdesc="The frontier coding agent from Sourcegraph."
arch=('x86_64')
url="https://ampcode.com"
license=('LicenseRef-Amp-Commercial-License')
depends=('nodejs' 'ripgrep' 'gcc-libs' 'glibc')
makedepends=('npm')
provides=('amp')
conflicts=('amp')
_archive="${pkgname}-${pkgver}"
source=("${_archive}.tgz::https://registry.npmjs.org/${_npmname}/-/amp-${pkgver//_/-}.tgz")
noextract=("${_archive}.tgz")
sha256sums=('826a8bbd50bca90409abdf818b6bc5fe2e01f95aa2eaaccf33cc4587622b3559')

package() {
    npm install -g --prefix "${pkgdir}/usr" \
        --cache "${srcdir}/npm-cache" \
        "${_archive}.tgz"

    find "${pkgdir}/usr" -type d -exec chmod 755 {} +
    chown -R root:root "${pkgdir}"

    install -Dm644 "${pkgdir}/usr/lib/node_modules/${_npmname}/LICENSE.md" \
        "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
