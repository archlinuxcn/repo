# Maintainer: Roald Clark <roaldclark@gmail.com>
# Contributor: robertfoster (https://aur.archlinux.org/account/robertfoster)
# Contributor: Eschwartz <eschwartz93@gmail.com>

_pkgname=winetricks
pkgname="${_pkgname}-git"
pkgver=20250709.203317
pkgrel=1
pkgdesc="An easy way to work around problems in Wine"
arch=('any')
url="https://github.com/Winetricks/${_pkgname}"
license=('LGPL-2.1-or-later')
depends=(
    'bash'
    'cabextract'
    'unzip'
)
makedepends=(
    'git'
)
optdepends=(
    'kdialog: For the KDE GUI'
    'zenity: For the GNOME GUI'
)
provides=(
    "${_pkgname}=${pkgver}"
)
conflicts=(
    "${_pkgname}"
    "bin32-${_pkgname}"
)
source=("${_pkgname}::git+${url}.git")
sha256sums=('SKIP')

pkgver() {
    cd "${srcdir}/${_pkgname}"
    printf "%s" "$(TZ=UTC git log -1 --pretty='%cd' --date=format-local:%Y%m%d.%H%M%S)"
}

package() {
    depends+=('wine')

    cd "${srcdir}/${_pkgname}"
    DESTDIR="${pkgdir}" make install
}
