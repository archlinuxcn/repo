pkgbase=julia-git-mpi-src
_subpkgs=(MPIPreferences)
pkgname=(julia-git-mpi-src)
pkgver=0.20.21
_commit=780aaa0fdb768713a329659338a9c9cde23c41a8
pkgrel=1
pkgdesc="MPI.jl"
url="https://github.com/JuliaParallel/MPI.jl.git"
arch=('any')
license=('MIT')
makedepends=(git julia-pkg-scripts)
depends=(julia-git)
source=("git+https://github.com/JuliaParallel/MPI.jl.git#commit=$_commit")
sha256sums=('eb35f9d02032bae8e551774250e558081329baa2215011086d0ea96b86a1ae74')

package_julia-git-mpi-src() {
  cd MPI.jl

  JULIA_INSTALL_SRCPKG=1 . /usr/lib/julia/julia-install-pkg.sh MPI "${pkgdir}" "${pkgname}" julia-git

  rm -rf "${dest_dir}"/lib
}

_subpackage() {
  local _subpkg=$1
  cd MPI.jl/lib/${_subpkg}

  JULIA_INSTALL_SRCPKG=1 . /usr/lib/julia/julia-install-pkg.sh ${_subpkg} "${pkgdir}" "${pkgname}" julia-git
}

for _subpkg in "${_subpkgs[@]}"; do
  pkgname=("${pkgname[@]}" julia-git-${_subpkg,,}-src)
  eval "package_julia-git-${_subpkg,,}-src() { _subpackage ${_subpkg}; }"
done
