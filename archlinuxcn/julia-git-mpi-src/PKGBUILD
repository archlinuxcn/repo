pkgbase=julia-git-mpi-src
_subpkgs=(MPIPreferences)
pkgname=(julia-git-mpi-src)
pkgver=0.20.22
_commit=230c9a239c8d2eaf7047f87fd090d751e47cfd75
pkgrel=1
pkgdesc="MPI.jl"
url="https://github.com/JuliaParallel/MPI.jl.git"
arch=('any')
license=('MIT')
makedepends=(git julia-pkg-scripts)
depends=(julia-git)
source=("git+https://github.com/JuliaParallel/MPI.jl.git#commit=$_commit")
sha256sums=('abad344e7b5900127f789816ab4655215a493cab9c7407b0f4fe27bbe7e84961')

package_julia-git-mpi-src() {
  cd MPI.jl

  JULIA_INSTALL_SRCPKG=1 . /usr/lib/julia/julia-install-pkg.sh MPI "${pkgdir}" "${pkgname}" julia-git

  rm -rf "${dest_dir}"/lib
}

_subpackage() {
  local _subpkg=$1
  cd MPI.jl/lib/${_subpkg}

  JULIA_INSTALL_SRCPKG=1 . /usr/lib/julia/julia-install-pkg.sh ${_subpkg} "${pkgdir}" "${pkgname}" julia-git
}

for _subpkg in "${_subpkgs[@]}"; do
  pkgname=("${pkgname[@]}" julia-git-${_subpkg,,}-src)
  eval "package_julia-git-${_subpkg,,}-src() { _subpackage ${_subpkg}; }"
done
