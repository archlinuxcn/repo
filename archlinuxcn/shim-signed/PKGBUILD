# Maintainer: nl6720 <nl6720@archlinux.org>

pkgname='shim-signed'
pkgver='15.8+fedora+3'
pkgrel='1'
pkgdesc='Initial UEFI bootloader that handles chaining to a trusted full bootloader under secure boot environments (prebuilt X64 and AA64 binaries from Fedora)'
url='https://koji.fedoraproject.org/koji/packageinfo?packageID=14502'
arch=('any')
license=('BSD-3-Clause')
options=('!debug' '!strip')
install="${pkgname}.install"
source=("https://kojipkgs.fedoraproject.org/packages/shim/${pkgver//+fedora+/\/}/x86_64/shim-x64-${pkgver//+fedora+/-}.x86_64.rpm"
        "https://kojipkgs.fedoraproject.org/packages/shim/${pkgver//+fedora+/\/}/aarch64/shim-aa64-${pkgver//+fedora+/-}.aarch64.rpm")
sha512sums=('4c3635d82980ccb604b568f072f30ac0c0055c6ed2985dade6a28c9070852a240bb33f15f9ef42c83fa46d212c18ae6f17abca9f3adda0a332b908ceda537e87'
            'd9b879706f7f6c7f7bfea92a443191811d39835074b3b6db3979c707b406b00884baff66ac79bf9bd66869240f668c6e6f987f8954a570d648933c85ea0da1c2')
package() {
    install -D -m0644 -t "${pkgdir}/usr/share/${pkgname}/" "${srcdir}/boot/efi/EFI/BOOT/fb"{aa64,x64}".efi"
    install -D -m0644 -t "${pkgdir}/usr/share/${pkgname}/" "${srcdir}/boot/efi/EFI/fedora/"{mm,shim}{aa64,x64}".efi"
}
