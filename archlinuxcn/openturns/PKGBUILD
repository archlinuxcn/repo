# Maintainer: Butui Hu <hot123tea123@gmail.com>

pkgname=openturns
pkgver=1.20
pkgrel=1
pkgdesc='Uncertainty treatment library'
license=('LGPL')
arch=('x86_64')
url='http://www.openturns.org'
depends=(
  bison
  blas
  boost
  eigen
  flex
  hdf5
  intel-tbb
  lapack
  libmpc
  libxml2
  muparser
  nlopt
  python-matplotlib
  python-pandas
  python-psutil
  python-scipy
  spectra
)
makedepends=(
  cmake
  swig
)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/openturns/openturns/archive/v${pkgver}.tar.gz")
sha256sums=('2be5247f0266d153619b35dfb1eeeb46736c502dad993b40aff8857d6314f293')

build() {
  cmake -B 'build' -S "${srcdir}/${pkgname}-${pkgver}" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DOPENTURNS_SYSCONFIG_PATH=/etc \
    -DCMAKE_SKIP_INSTALL_RPATH=ON
  make -C 'build'
}

package() {
  make DESTDIR="${pkgdir}" -C 'build' install
  python -O -m compileall "${pkgdir}/usr/lib/"
}
# vim:set ts=2 sw=2 et:

