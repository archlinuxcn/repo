# Maintainer: Chih-Hsuan Yen <yan12125@gmail.com>

_pkgname=lxqt-archiver
pkgname=$_pkgname-git
pkgver=0.7.0.r5.g0b49388
pkgrel=3
pkgdesc=" A simple & lightweight desktop-agnostic Qt file archiver"
arch=("i686" "x86_64")
url="https://lxqt.org"
license=("GPL2")
depends=('libfm-qt-git' 'qt5-x11extras' 'glib2' 'json-glib')
makedepends=('git' 'cmake' 'qt5-tools' 'lxqt-build-tools-git')
provides=("$_pkgname=$pkgver")
conflicts=("$_pkgname")
source=("git+https://github.com/lxqt/$_pkgname.git")
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  ( set -o pipefail
    git describe --long --tag 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/^v//'
  )
}

prepare() {
  mkdir -p build
}

build() {
  cd build

  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    "$srcdir/$_pkgname"

  make
}

package() {
  cd build

  make DESTDIR="$pkgdir" install
}
