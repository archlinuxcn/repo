# Maintainer: Gavin Lloyd <gavinhungry@gmail.com>

pkgname=acme.sh-git
pkgver=0.5417.6e14a073f
pkgrel=1
pkgdesc='An ACME Shell script, an acme client alternative to certbot'
arch=('any')
url='https://github.com/Neilpang/acme.sh'
license=('GPL3')
makedepends=('git')
source=("${pkgname}::git+${url}.git#branch=master")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}"
  echo 0.$(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "${srcdir}/${pkgname}"

  install -dm755 "${pkgdir}/usr/share/acme.sh/deploy"
  install -dm755 "${pkgdir}/usr/share/acme.sh/dnsapi"
  install -dm755 "${pkgdir}/usr/bin"

  install -m755 acme.sh "${pkgdir}/usr/share/acme.sh"
  install -m755 deploy/*.sh "${pkgdir}/usr/share/acme.sh/deploy"
  install -m755 dnsapi/*.sh "${pkgdir}/usr/share/acme.sh/dnsapi"

  ln -s /usr/share/acme.sh/acme.sh "${pkgdir}/usr/bin/acme.sh"
}
