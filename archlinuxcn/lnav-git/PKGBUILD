# Maintainer: gt <AT> notfoss.com
# Contributor: Andy Weidenbaum <archbaum@gmail.com>

pkgname="lnav-git"
pkgver=0.11.1.v13.g6bde93e8
pkgrel=1
pkgdesc="A featureful ncurses based log file viewer"
arch=("i686" "x86_64")
url="http://lnav.org/"
license=("BSD")
depends=("bzip2" "ncurses" "pcre" "readline" "sqlite" "zlib")
makedepends=("git" "re2c")
conflicts=("lnav")
provides=("lnav")
source=("$pkgname"::"git+https://github.com/tstack/lnav.git")
md5sums=('SKIP')

pkgver() {
    cd "$pkgname"
    # tag + number of commits since that + SHA-1 (first 7 characters) of the last commit
    git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g;s/\.rc/rc/;s/\.r/.v/'
}

build() {
    cd "$pkgname"
    ./autogen.sh
    ./configure \
        --prefix=/usr \
        --sbindir=/usr/bin \
        --libexecdir=/usr/lib/lnav \
        --sysconfdir=/etc \
        --sharedstatedir=/usr/share/lnav \
        --localstatedir=/var/lib/lnav \
        --disable-static \
        --with-ncurses \
        --with-pcre \
        --with-readline \
        --with-sqlite3
    make
}

package() {
    cd "$pkgname"
    make PREFIX=/usr DESTDIR="$pkgdir" install

    # BSD license
    install -D -m644 LICENSE "$pkgdir/usr/share/licenses/lnav/LICENSE"
}
