# Maintainer: Gesh <gesh@gesh.uni.cx>
# Contributor: JP-Ellis <josh@jpellis.me>

pkgname='python-habanero'
pkgver=2.3.0
pkgrel=1
_name=${pkgname#python-}
_name="${_name//-/_}"
_src_folder="${_name}-${pkgver}"
pkgdesc="A low level client for Crossref's Search API"
url="https://github.com/sckott/habanero"
depends=(
  'python'
  'python-httpx'
  'python-packaging'
  'python-pyyaml'
  'python-tqdm'
  'python-urllib3'
)
makedepends=(
  'python-build'
  'python-installer'
  'python-hatchling'
)
checkdepends=(
  # Only needed to test integration of the optdep, uncomment this if using
  'python-pytest'
  'python-pytest-cov'
  'python-pytest-recording'
)
optdepends=(
  'python-bibtexparser: attempt to fix misformatted bibtex'
)
license=('MIT')
arch=('any')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha256sums=('871e5d088ef641b05514d44b004af512852b309bcd0d81f10545e58d54a654ab')

build() {
    cd "${srcdir}/${_src_folder}"
    python -m build --wheel --no-isolation
}

check() {
    cd "${srcdir}/${_src_folder}"

    export PYTEST_DISABLE_PLUGIN_AUTOLOAD=1
    python -m pytest -p pytest_cov -p pytest_recording
}

package() {
    cd "${srcdir}/${_src_folder}"
    python -m installer --destdir="${pkgdir}" dist/*.whl
    install -Dm644 LICENSE.md -t "${pkgdir}"/usr/share/licenses/"${pkgname}"/
}

# vim:set ts=2 sw=2 et:
