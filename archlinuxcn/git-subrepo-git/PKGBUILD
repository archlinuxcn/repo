# Maintainer: James An <james@jamesan.ca>

pkgname=git-subrepo-git
_pkgname=${pkgname%-git}
epoch=1
pkgver=0.4.6.r0.g110b9eb
pkgrel=1
pkgdesc="Git command is an improvement from git-submodule and git-subtree."
arch=('any')
url="https://github.com/ingydotnet/$_pkgname"
license=('GPL')
depends=('git')
provides=("$_pkgname=$pkgver")
conflicts=("$_pkgname")
source=("$_pkgname"::"git+$url.git")
md5sums=('SKIP')

pkgver() {
  cd "$_pkgname"
  (
    set -o pipefail
    git describe --long --tag | sed -r 's/([^-]*-g)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

package() {
  cd "$_pkgname"

  make DESTDIR="$pkgdir" PREFIX=/usr install
  install -Dm644 share/zsh-completion/_git-subrepo "$pkgdir/usr/share/zsh/site-functions/_git-subrepo"
}
