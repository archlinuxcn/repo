# Maintainer: lilydjwg <lilydjwg@gmail.com>
# Contribuitor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contribuitor: Alfredo Palhares <masterkorp@masterkorp.net>
# Contribuitor: Christian Babeux <christian.babeux@0x80.ca>

pkgname=sysdig-lily
pkgver=0.29.3
pkgrel=5

# from src/sysdig-*/cmake/modules/falcosecurity-libs.cmake
_falcover=e5c53d648f3c4694385bbe488e7d47eaa36c229a

pkgdesc="Open source system-level exploration and troubleshooting tool"
arch=('x86_64')
url="https://www.sysdig.com/"
license=('GPL2' 'Apache' 'MIT')
depends=('jsoncpp' 'libjsoncpp.so' 'luajit' 'curl' 'libcurl.so' 'jq' 'libb64' 'intel-tbb' 'grpc' "linux-lily=5.15.77-1" 'libgrpc++_unsecure.so' 'yaml-cpp')
makedepends=('cmake' 'llvm' 'clang' 'gtest' 'linux-lily-headers')
source=("https://github.com/draios/sysdig/archive/$pkgver/sysdig-$pkgver.tar.gz"
        "falcosecurity-libs-$_falcover.tar.gz::https://github.com/falcosecurity/libs/archive/$_falcover.tar.gz"
        "bashcomp-location.patch"
        "falcosecurity-libs-nodownload.patch"
        "sme.patch")
sha256sums=('6b96797859002ab69a2bed4fdba1c7fe8064ecf8661621ae7d8fbf8599ffa636'
            '80903bc57b7f9c5f24298ecf1531cf66ef571681b4bd1e05f6e4db704ffb380b'
            'aaee8a0ff414a24c5d5a479229324be1667bc5eb70702838f5d617fd986f947b'
            'b8cedef6766c691a8a92810eb459593d80070f0cf5dfc32cae2a80ec00639429'
            'eac65420a1f564b2336336b22a2717e5fb5c15928f859995152d00e2e4ed90d5')
conflicts=(sysdig)
provides=(sysdig=$pkgver)

prepare() {
  cd "$srcdir/sysdig-$pkgver"
  patch -p1 -i "$srcdir"/bashcomp-location.patch
  patch -p1 -i "$srcdir"/falcosecurity-libs-nodownload.patch

  sed s/USE_BUNDLED_DEPS/USE_BUNDLED_NJSON/ -i cmake/modules/nlohmann-json.cmake

  cd "$srcdir/libs-$_falcover"
  patch -p1 -i "$srcdir"/sme.patch
}

build() {
  cd "$srcdir"/sysdig-$pkgver
  rm -rf build
  mkdir build
  cd build

  export KERNELDIR=/usr/src/linux-lily
  cmake .. \
        -DCMAKE_VERBOSE_MAKEFILE:BOOL=ON \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_CXX_FLAGS="${CXXFLAGS} ${CPPFLAGS} -Wno-deprecated-declarations" \
        -DCMAKE_C_FLAGS="${CFLAGS} ${CPPFLAGS} -Wno-deprecated-declarations" \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DSYSDIG_VERSION=$pkgver \
        -DUSE_BUNDLED_DEPS=OFF \
        -DBUILD_DRIVER=ON \
        -DBUILD_BPF=ON \
        -DENABLE_DKMS=OFF \
        -DBUILD_LIBSCAP_EXAMPLES=OFF \
        -DUSE_BUNDLED_NJSON=ON
  make
}

package() {
  cd "$srcdir"/sysdig-$pkgver

  install -dm755 "$pkgdir"/usr/share/licenses/$pkgname
  install -m644 NOTICES COPYING "$pkgdir"/usr/share/licenses/$pkgname

  cd build
  make install DESTDIR="$pkgdir"

  _extradir="/usr/lib/modules/$(</usr/src/linux-lily/version)/extramodules"
  install -Dt "${pkgdir}${_extradir}" -m644 driver/src/*.ko
  find "${pkgdir}" -name '*.ko' -exec xz {} +

  rm -r "$pkgdir"/usr/share/zsh/vendor-completions/

  rm -r "$pkgdir"/usr/src/
  mkdir -p "$pkgdir"/usr/src/sysdig-${pkgver}/bpf/
  install -Dm644 "$srcdir"/sysdig-$pkgver/build/driver/bpf/probe.o "$pkgdir"/usr/src/sysdig-${pkgver}/bpf/
  sed -i '/probe\.o/s/\<mv\>/cp --reflink=auto/' "$pkgdir"/usr/bin/scap-driver-loader
}
