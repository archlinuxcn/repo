# Maintainer: lilydjwg <lilydjwg@gmail.com>
# Contribuitor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contribuitor: Alfredo Palhares <masterkorp@masterkorp.net>
# Contribuitor: Christian Babeux <christian.babeux@0x80.ca>

pkgname=sysdig-lily
pkgver=0.26.7
pkgrel=6
pkgdesc="Open source system-level exploration and troubleshooting tool"
arch=('x86_64')
url="https://www.sysdig.com/"
license=('GPL2' 'Apache' 'MIT')
depends=('jsoncpp' 'libjsoncpp.so' 'luajit' 'curl' 'libcurl.so' 'jq' 'libb64' 'intel-tbb' 'grpc' "linux-lily=5.4.42-1")
makedepends=('cmake' 'pandoc' 'gtest' 'llvm' 'clang' 'linux-lily-headers')
source=("https://github.com/draios/sysdig/archive/$pkgver/sysdig-$pkgver.tar.gz"
        "bashcomp-location.patch"
        "glibc.patch")
sha256sums=('c82aa4201e8ad37e22c780c27c28ac28359a8e677b4dc0ea295eb1452115d6c0'
            'aaee8a0ff414a24c5d5a479229324be1667bc5eb70702838f5d617fd986f947b'
            '9d9ee715500cb5c3709cf6a77e95421edaaae5f15dbb1c7c8ad2147cc9637939')
conflicts=(sysdig)
provides=(sysdig=$pkgver)

prepare() {
  cd "$srcdir"/sysdig-$pkgver
  patch -p1 -i "$srcdir"/bashcomp-location.patch
  patch -p1 -i "$srcdir"/glibc.patch
}

build() {
  cd "$srcdir"/sysdig-$pkgver
  rm -rf build
  mkdir build
  cd build
  export KERNELDIR=/usr/src/linux-lily
  cmake .. \
        -DCMAKE_VERBOSE_MAKEFILE:BOOL=ON \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_CXX_FLAGS="-Wno-deprecated-declarations" \
        -DCMAKE_C_FLAGS="${CFLAGS} ${CPPFLAGS} -Wno-deprecated-declarations" \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DSYSDIG_VERSION=$pkgver \
        -DUSE_BUNDLED_DEPS=OFF \
        -DBUILD_DRIVER=ON \
        -DENABLE_DKMS=OFF \
        -DBUILD_LIBSCAP_EXAMPLES=OFF
  make

  ln -srf driver/src/driver_config.h ../driver
  cd ../driver/bpf
  make
}

package() {
  cd "$srcdir"/sysdig-$pkgver

  install -dm755 "$pkgdir"/usr/share/licenses/$pkgname
  install -m644 NOTICES COPYING "$pkgdir"/usr/share/licenses/$pkgname

  cd build
  make install DESTDIR="$pkgdir"

  _extradir="/usr/lib/modules/$(</usr/src/linux-lily/version)/extramodules"
  install -Dt "${pkgdir}${_extradir}" -m644 driver/src/*.ko
  find "${pkgdir}" -name '*.ko' -exec xz {} +

  rm -r "$pkgdir"/usr/share/zsh/vendor-completions/

  rm -r "$pkgdir"/usr/src/
  mkdir -p "$pkgdir"/usr/src/sysdig-${pkgver}/bpf/
  install -Dm644 "$srcdir"/sysdig-$pkgver/driver/bpf/probe.o "$pkgdir"/usr/src/sysdig-${pkgver}/bpf/
  sed -i '/probe\.o/s/\<mv\>/cp --reflink=auto/' "$pkgdir"/usr/bin/sysdig-probe-loader
}
