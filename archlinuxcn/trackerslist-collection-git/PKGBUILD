# Maintainer: Roald Clark <roaldclark@gmail.com>

_name=TrackersListCollection
_pkgname=trackerslist-collection
pkgname="${_pkgname}-git"
pkgver=20250518.081013
pkgrel=1
pkgdesc="A regularly updated collection of popular BitTorrent trackers"
arch=('any')
url="https://github.com/XIU2/${_name}"
license=('GPL-3.0-or-later')
makedepends=('git')
provides=("${_pkgname}=${pkgver}")
conflicts=(${_pkgname})
source=("${_pkgname}::git+${url}.git")
sha256sums=('SKIP')

pkgver() {
    cd "${srcdir}/${_pkgname}"
    printf "%s" "$(TZ=UTC git log -1 --pretty='%cd' --date=format-local:%Y%m%d.%H%M%S)"
}

package() {
    cd "${srcdir}/${_pkgname}"
    install -Dm0644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
    install -Dm0644 blacklist.txt -t "${pkgdir}/usr/share/${_pkgname}/"
    install -Dm0644 all.txt best.txt http.txt nohttp.txt -t "${pkgdir}/usr/share/${_pkgname}/trackers/"
    install -Dm0644 all_aria2.txt best_aria2.txt http_aria2.txt nohttp_aria2.txt -t "${pkgdir}/usr/share/${_pkgname}/trackers/aria2/"
}
