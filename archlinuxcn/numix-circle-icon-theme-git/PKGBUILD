# Maintainer : Erik Dubois <erik.dubois@gmail.com>
# Previous Maintainer: Maxime Gauduin <alucryd@archlinux.org>
pkgname=numix-circle-icon-theme-git
pkgver=23.01.12.r0.g4ae406e
pkgrel=1
pkgdesc='Circle icon theme from the Numix project'
arch=('any')
url='https://github.com/numixproject/numix-icon-theme-circle/'
license=('GPL3')
depends=()
makedepends=('git')
provides=('numix-circle-icon-theme' 'numix-circle-light-icon-theme')
conflicts=('numix-circle-icon-theme' 'numix-circle-light-icon-theme')
options=('!strip')
source=('numix-circle-icon-theme::git+https://github.com/numixproject/numix-icon-theme-circle.git')
sha256sums=('SKIP')

pkgver() {
  git -C numix-circle-icon-theme describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

package() {
  cd numix-circle-icon-theme
  install -dm 755 "${pkgdir}"/usr/share/icons
  cp -dr --no-preserve='ownership' Numix-Circle{,-Light} "${pkgdir}"/usr/share/icons/
}
