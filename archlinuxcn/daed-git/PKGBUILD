# Maintainer: Integral <integral@member.fsf.org>
# Maintainer: cubercsl <2014cais01 at gmail dot com>

pkgname=daed-git
_pkgname=${pkgname%-git}
pkgver=0.9.0.r2.g44f1bbe
pkgrel=1
pkgdesc="A modern dashboard for dae, bundled with dae-wing (backend API server) and dae (core)."
arch=('x86_64' 'aarch64')
url="https://github.com/daeuniverse/${_pkgname}"
license=('AGPL-3.0-or-later AND MIT')
depends=('v2ray-geoip' 'v2ray-domain-list-community')
makedepends=('git' 'pnpm' 'clang' 'go')
provides=("${_pkgname}" "dae")
conflicts=("${_pkgname}" "dae")
source=(
	"git+${url}.git"
	"git+https://github.com/daeuniverse/dae-wing.git"
	"git+https://github.com/daeuniverse/dae.git"
	"git+https://github.com/daeuniverse/dae_bpf_headers.git"
)
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP')
install="${_pkgname}.install"
options=(!debug)

pkgver() {
	cd "${_pkgname}/"
	git describe --tags --long --abbrev=7 | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
	cd "${_pkgname}/"
	git submodule init
	git config submodule.wing.url "${srcdir}/dae-wing"
	git -c protocol.file.allow=always submodule update

	cd wing
	git submodule init
	git config submodule.dae-core.url "${srcdir}/dae"
	git -c protocol.file.allow=always submodule update

	cd dae-core
	git config submodule.control/kern/headers.url "${srcdir}/dae_bpf_headers"
	git config submodule.trace/kern/headers.url "${srcdir}/dae_bpf_headers"
	git -c protocol.file.allow=always submodule update
}

build() {
	export CFLAGS="-fno-stack-protector"
	export CGO_ENABLED=1
	export CGO_CPPFLAGS="${CPPFLAGS}"
	export CGO_CFLAGS="${CFLAGS}"
	export CGO_CXXFLAGS="${CXXFLAGS}"
	export CGO_LDFLAGS="${LDFLAGS}"
	export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
	make -C "${_pkgname}" VERSION="unstable-${pkgver}"
}

package() {
	cd "${_pkgname}/"

	install -vDm755 "${_pkgname}" -t "${pkgdir}/usr/bin/"
	install -vDm644 "install/${_pkgname}.service" -t "${pkgdir}/usr/lib/systemd/system/"
	install -vDm644 "LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}/"
	install -d ${pkgdir}/usr/share/${_pkgname}/
	ln -vs "/usr/share/v2ray/geoip.dat" "${pkgdir}/usr/share/daed/geoip.dat"
	ln -vs "/usr/share/v2ray/geosite.dat" "${pkgdir}/usr/share/daed/geosite.dat"
}
