# Maintainer: Eric Engestrom <aur [at] engestrom [dot] ch>

pkgname=libdisplay-info-git
pkgver=0.1.1+28.g1388f16e71
pkgrel=1
pkgdesc='EDID and DisplayID library'
url='https://gitlab.freedesktop.org/emersion/libdisplay-info'
arch=('x86_64')
license=('MIT')
source=("git+$url")
sha1sums=('SKIP')
makedepends=(git meson ninja python)
depends=(hwdata)
conflicts=(libdisplay-info)
provides=(libdisplay-info)
provides+=(libdisplay-info.so)

pkgver() {
  git -C libdisplay-info describe --tags --abbrev=10 | sed 's/^v//; s/-/+/; s/-/./'
}

build() {
  arch-meson libdisplay-info build
  ninja -C build
}

check() {
  ninja -C build test
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}
