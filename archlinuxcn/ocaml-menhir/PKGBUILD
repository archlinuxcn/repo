# Maintainer: Mort Yao <soi@mort.ninja>
# Co-Maintainer: Felix Golatofski <contact@xdfr.de>
# Co-Maintainer: Xunarui Qi <me@xuanruiqi.com>
# Contributor: Serge Zirukin <ftrvxmtrx@gmail.com>
# Contributor: Sergei Lebedev <superbobry@gmail.com>
# Contributor: Guillem Rieu <guillemr@gmx.net>
# Contributor: Sergey Plaksin <serp256@gmail.com>
# Contributor: Nicolas Pouillard <nicolas.pouillard@gmail.com>

pkgname=ocaml-menhir
pkgver=20240715
pkgrel=1
pkgdesc="Menhir is a LR(1) parser generator for the OCaml."
arch=(armv7h aarch64 x86_64)
url="http://cristal.inria.fr/~fpottier/menhir/"
license=('GPL-2.0-only AND LGPL-2.0-only WITH OCaml-LGPL-linking-exception')
depends=('ocaml>=4.08.0')
makedepends=('dune>=2.8.0' 'ocaml-findlib')
options=(!strip !makeflags)
source=("https://gitlab.inria.fr/fpottier/menhir/-/archive/${pkgver}/menhir-${pkgver}.tar.gz")
sha256sums=('dca7b86e81b5dbe24f52c451be00bdbdfede3d9e81317ebe5220aaa3ee33ee42')

build() {
  cd "$srcdir/${pkgname/ocaml-/}-$pkgver"
  dune build -p menhirLib,menhirSdk,menhirCST,menhir @install
}

package() {
  cd "$srcdir/${pkgname/ocaml-/}-$pkgver"
  dune install -p menhirLib,menhirSdk,menhirCST,menhir --destdir="${pkgdir}" --prefix="/usr" \
       --libdir="$(ocamlfind printconf destdir)"

  install -dm755 "${pkgdir}/usr/share/"
  mv "${pkgdir}/usr/doc" "${pkgdir}/usr/share/"
}
