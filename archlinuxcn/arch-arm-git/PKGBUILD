#Maintainer: Yichao Yu <yyc1992@gmail.com>
#Contributor: Yichao Yu <yyc1992@gmail.com>

pkgname=arch-arm-git
pkgver=57.7c64e61
pkgrel=2
pkgdesc="Archlinux ARM cross compiling tools"
arch=('i686' 'x86_64')
provides=('arch-arm')
conflicts=('arch-arm')
url="https://github.com/yuyichao/arch-arm"
license=('GPLv3')
depends=('qemu-arch-extra')
makedepends=('git' 'cmake')
options=('debug' 'strip')
source=('git://github.com/yuyichao/arch-arm')
md5sums=('SKIP')
backup=('etc/pacman.d/mirrorlist-arm-cross')

pkgver() {
  cd "$srcdir/arch-arm"

  echo "$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir/arch-arm"

  mkdir -p build
  cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=lib \
     -DCMAKE_INSTALL_SYSCONFDIR=/etc
  make
}

package() {
  cd "${srcdir}/arch-arm/build"

  make install DESTDIR="${pkgdir}"

  # These symlinks are within the global sysroot which helps the cross compiler to
  # find the right path in the sysroot.
  install -dm755 "${pkgdir}/usr/lib/arch-arm-cross/pkg_root/armv7/usr/arm-linux-gnueabihf/"
  ln -sf ../../lib "${pkgdir}/usr/lib/arch-arm-cross/pkg_root/armv7/usr/arm-linux-gnueabihf/lib"
  install -dm755 "${pkgdir}/usr/lib/arch-arm-cross/pkg_root/armv8/usr/aarch64-linux-gnu/"
  ln -sf ../../lib "${pkgdir}/usr/lib/arch-arm-cross/pkg_root/armv8/usr/aarch64-linux-gnu/lib"
}
