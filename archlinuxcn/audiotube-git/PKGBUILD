# Maintainer: Giuseppe Cal√† <gcala at mailbox dot org>

pkgname=audiotube-git
_pkgname="${pkgname%-git}"
pkgver=24.01.90.r270.g11734f5
pkgrel=1
pkgdesc="Client for YouTube Music"
arch=('x86_64' 'aarch64')
url="https://invent.kde.org/plasma-mobile/audiotube"
license=('GPL-2.0-or-later')
depends=('futuresql-qt6'
	'kcoreaddons'
	'kcrash'
	'ki18n'
	'kirigami-addons'
	'kwindowsystem'
	'purpose'
	'python'
	'python-ytmusicapi'
	'qt6-5compat'
	'qt6-base'
	'qt6-declarative'
	'qt6-imageformats'
	'qt6-multimedia'
	'qt6-svg'
	'qt6-multimedia-gstreamer'
	'yt-dlp')
makedepends=('git'
	'extra-cmake-modules'
	'pybind11'
	'qcoro-qt6')
provides=('audiotube')
conflicts=('audiotube')
source=("git+${url}.git")
sha256sums=('SKIP')

pkgver() {
	cd "${_pkgname}/"
	git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/v//'
}

build() {
	cmake -S "${_pkgname}" \
		-B build \
		-D CMAKE_INSTALL_PREFIX=/usr \
		-D CMAKE_BUILD_TYPE=None

	cmake --build build
}

package() {
	DESTDIR="${pkgdir}" cmake --install build
}
