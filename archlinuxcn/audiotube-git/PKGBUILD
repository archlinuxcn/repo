# Maintainer: Giuseppe Cal√† <gcala at mailbox dot org>

pkgname=audiotube-git
_pkgname="${pkgname%-git}"
pkgver=23.01.0.r309.g7fc52c7
pkgrel=1
pkgdesc="Client for YouTube Music"
arch=('x86_64' 'aarch64')
url="https://invent.kde.org/plasma-mobile/audiotube"
license=('GPL-2.0-or-later')
depends=("futuresql-git" "gst-plugins-good" "kcrash5" "ki18n5" "kirigami2" "purpose5" "python-ytmusicapi" "qt5-imageformats" "yt-dlp")
makedepends=('git' 'extra-cmake-modules' 'pybind11' 'qt5-svg' 'python-ytmusicapi' 'kirigami-addons5')
provides=('audiotube')
conflicts=('audiotube')
source=("git+${url}.git")
sha256sums=('SKIP')

pkgver() {
	cd "${_pkgname}/"
	git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/v//'
}

build() {
	cd "${_pkgname}/"
	cmake -DCMAKE_INSTALL_PREFIX=/usr -B build
	make -C build
}

package() {
	cd "${_pkgname}/"
	make -C build DESTDIR="${pkgdir}" PREFIX=/usr install
}
