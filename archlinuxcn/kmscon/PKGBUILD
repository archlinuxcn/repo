# Maintainer: Coelacanthus <uwu@coelacanthus.name>
# Contributor: Kyle Keen <keenerd@gmail.com>
# Contributor: Chris Tam <lchris314 at gmail dot com>
# Contributor: Daniel Micay <danielmicay at gmail dotcom>
# Contributor: David Herrmann <dh.herrmann@googlemail dot com>

pkgname=kmscon
pkgver=9.2.1
pkgrel=1
pkgdesc='Terminal emulator based on Kernel Mode Setting (KMS)'
arch=('x86_64')
url='https://github.com/kmscon/kmscon/'
license=('MIT')
depends=(
  'glibc'
  'glib2'
  'libdrm'
  'libgl'
  'libtsm'
  'libxkbcommon'
  'mesa'
  'pango'
  'pixman'
  'sh'
  'systemd-libs'
)
makedepends=(
  'check'
  'docbook-xsl'
  'libxslt'
  'linux-api-headers'
  'meson'
)
source=(
  "$pkgname-$pkgver.tar.gz::https://github.com/kmscon/kmscon/archive/refs/tags/v$pkgver.tar.gz"
)
b2sums=('6fb3852e4941b9274af5debc17f668b8ca2c784f2a1c9cb56079d509ae3cb896bea58190d71f6913edc4282b1f9d1132688b2bde221bb72c2725519d9291d23d')

prepare() {
  cd $pkgname-$pkgver
}

build() {
  arch-meson build $pkgname-$pkgver \
    -D werror=false \
    -D backspace_sends_delete=true # https://github.com/Aetf/kmscon/issues/58
  meson compile -C build
}

# FIXME: Disable tests for wrong unit tests
# https://github.com/Aetf/kmscon/issues/50
#check() {
#  meson test -C build
#}

package() {
  depends+=(
    'libgobject-2.0.so'
    'libtsm.so'
    'libxkbcommon'
    'libpango-1.0.so'
    'libpangoft2-1.0.so'
    'libpixman-1.so'
  )
  meson install -C build --destdir="$pkgdir"
  install -Dm644 $pkgname-$pkgver/COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
