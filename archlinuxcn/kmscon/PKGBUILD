# Maintainer: Coelacanthus <CoelacanthusHex@gmail.com>
# Contributor: Kyle Keen <keenerd@gmail.com>
# Contributor: Chris Tam <lchris314 at gmail dot com>
# Contributor: Daniel Micay <danielmicay at gmail dotcom>
# Contributor: David Herrmann <dh.herrmann@googlemail dot com>

pkgname=kmscon
pkgver=9.0.0
pkgrel=3
pkgdesc='Terminal emulator based on Kernel Mode Setting (KMS)'
arch=('x86_64')
url='https://github.com/Aetf/kmscon/'
license=('MIT')
depends=('libdrm'
         'glibc'
         'glib2' 'libgobject-2.0.so'
         'pixman' 'libpixman-1.so'
         'sh'
         'systemd-libs'
         'mesa'
         'libgl'
         'pango' 'libpango-1.0.so' 'libpangoft2-1.0.so'
         'libxkbcommon' 'libxkbcommon'
         'libtsm' 'libtsm.so'
         )
makedepends=('libxslt' 'docbook-xsl' 'linux-api-headers' 'meson')
source=("https://github.com/Aetf/kmscon/releases/download/v$pkgver/$pkgname-$pkgver.tar.xz"
        "fix-backspace.patch::https://github.com/Aetf/kmscon/commit/0310e62e0473d94d649a59e7a9d7622dfa6302e8.patch")
b2sums=('a44a4b3739b1ed4a597713ca2ef7adb33f1ab7130322a419ee88517addfbf0edf01ef59f06c5626e95a7bc1708cf4cebe49d5853558aa3fedb3aa9c7c28a7dd7'
        '35e7d5ffb7779b5bb1d82b484c634797b2240057753b1cd5439ac802602d082c3ec4acd0aae011e06f5deca5682a34e06d9179d91e96d879045418cbd03ee5e2')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../fix-backspace.patch
  sed 's/>=4.1.0/>=4.0.0/' -i meson.build
}

build() {
  arch-meson build $pkgname-$pkgver \
    -D werror=false \
    -D backspace_sends_delete=true # https://github.com/Aetf/kmscon/issues/58
  meson compile -C build
}

check() {
  # FIXME: Disable tests for wrong unit tests
  # https://github.com/Aetf/kmscon/issues/50
  #meson test -C build
  :
}

package() {
  meson install -C build --destdir="$pkgdir"
  install -Dm644 $pkgname-$pkgver/COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
#  install -d "$pkgdir/usr/lib/systemd/system"
#  cp docs/kmscon{,vt@}.service "$pkgdir/usr/lib/systemd/system/"
}
