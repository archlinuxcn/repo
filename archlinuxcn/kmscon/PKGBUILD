# Maintainer: Coelacanthus <uwu@coelacanthus.name>
# Contributor: Kyle Keen <keenerd@gmail.com>
# Contributor: Chris Tam <lchris314 at gmail dot com>
# Contributor: Daniel Micay <danielmicay at gmail dotcom>
# Contributor: David Herrmann <dh.herrmann@googlemail dot com>

pkgname=kmscon
pkgver=9.0.0
pkgrel=3
pkgdesc='Terminal emulator based on Kernel Mode Setting (KMS)'
arch=('x86_64')
url='https://github.com/Aetf/kmscon/'
license=('MIT')
depends=(
  'glibc'
  'glib2'
  'libdrm'
  'libgl'
  'libtsm'
  'libxkbcommon'
  'mesa'
  'pango'
  'pixman'
  'sh'
  'systemd-libs'
)
makedepends=(
  'docbook-xsl'
  'libxslt'
  'linux-api-headers'
  'meson'
)
source=(
  "$pkgname-$pkgver.tar.gz::https://github.com/Aetf/kmscon/archive/refs/tags/v$pkgver.tar.gz"
  "$pkgname-fix-backspace.patch::https://github.com/Aetf/kmscon/commit/0310e62e0473d94d649a59e7a9d7622dfa6302e8.patch"
)
b2sums=('7316f1fcff4aa9d5693d7ab7f2da12fe50132c575667bc349b12a3e604b2cd92ffc0a55c9648d03e73d1695764067adc0807a4cd220ec190df637ad3d6929dea'
        '35e7d5ffb7779b5bb1d82b484c634797b2240057753b1cd5439ac802602d082c3ec4acd0aae011e06f5deca5682a34e06d9179d91e96d879045418cbd03ee5e2')

prepare() {
  cd $pkgname-$pkgver
  # FIXME: https://github.com/Aetf/kmscon/issues/58
  patch -Np1 -i ../"$pkgname-fix-backspace.patch"
  sed 's/>=4.1.0/>=4.0.0/' -i meson.build
}

build() {
  arch-meson build $pkgname-$pkgver \
    -D werror=false \
    -D backspace_sends_delete=true # https://github.com/Aetf/kmscon/issues/58
  meson compile -C build
}

# FIXME: Disable tests for wrong unit tests
# https://github.com/Aetf/kmscon/issues/50
#check() {
#  meson test -C build
#}

package() {
  depends+=(
    'libgobject-2.0.so'
    'libtsm.so'
    'libxkbcommon'
    'libpango-1.0.so'
    'libpangoft2-1.0.so'
    'libpixman-1.so'
  )
  meson install -C build --destdir="$pkgdir"
  install -Dm644 $pkgname-$pkgver/COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
