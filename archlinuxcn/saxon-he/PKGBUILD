# Maintainer: Anton Kudelin <kudelin at protonmail dot com>
# Contributor: eolianoe <eolianoe [at] gmail [DoT] com>
# Contributor: Andya <hugo981@gmx.com>
# Contributor: Lazaros Koromilas <koromilaz@gmail.com>

pkgname=saxon-he
_major=9.9
_minor=1.6
pkgver=${_major}.${_minor}
_pkgver=${pkgver//./-}
pkgrel=2
pkgdesc="XSLT 2.0 / XPath 2.0 and 3.0 / XQuery 1.0 and 3.0 processor for Java - Home Edition"
url="http://saxon.sourceforge.net"
license=('MPL')
arch=('any')
depends=('java-runtime-headless')
provides=('java-saxon')
conflicts=('java-saxon')
source=("https://downloads.sourceforge.net/saxon/SaxonHE${_pkgver}J.zip"
        "saxon-xslt.sh"
        "saxon-xquery.sh")
sha256sums=('e94d8bbbb9a3a3dc9518c1923c436b2eb5ddc0e74bfd0bec1a07ac63e0e31b92'
            '355cf52e3e3b35e4db5dd098fbd57335d20ed10ae070c57a6de214859d04719f'
            'dd42ecae1ef7ff371642148b4ed49fadee0d5b2333c14e138edaae7269ab2199')

package() {
    cd "${srcdir}"
    install -Dm644 saxon9he.jar "${pkgdir}/usr/share/java/saxon/saxon9he.jar"
    install -Dm755 saxon-xslt.sh "${pkgdir}/usr/bin/saxon-xslt"
    install -Dm755 saxon-xquery.sh "${pkgdir}/usr/bin/saxon-xquery"
    # link with simpler name for compat with others
    ln -s saxon9he.jar "${pkgdir}/usr/share/java/saxon/saxon.jar"
    ln -s saxon-xslt "${pkgdir}/usr/bin/saxon"
}
