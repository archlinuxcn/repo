# Maintainer: zjuyk <ownbyzjuyk@gmail.com>

pkgname=mpv-osc-uosc
_gitname=uosc
pkgver=5.11.0
pkgrel=1
pkgdesc="Feature-rich minimalist proximity-based UI for MPV player."
arch=('x86_64')
url="https://github.com/tomasklaen/${_gitname}"
license=('LGPL-2.1-only')
depends=('mpv')
conflicts=(mpv-osc-uosc-git)
backup=("etc/mpv/script-opts/uosc.conf")
install=uosc.install
source=(
  "uosc.conf::${url}/releases/download/${pkgver}/uosc.conf"
  "uosc.zip::${url}/releases/download/${pkgver}/uosc.zip"
)
sha256sums=('2d7b3319e83f4ba037d8299d28eae42efee75800ac4a8a13e03ebf53719a9013'
            '229bb17435bc39b5c57a856450f024413992008632f9c6562b72def2eb44b5ed')

package() {

  # remove unwanted binary files
  rm -rf "${srcdir}/scripts/uosc/bin/ziggy-windows.exe"
  rm -rf "${srcdir}/scripts/uosc/bin/ziggy-darwin"

  # install uosc
  mkdir -p "${pkgdir}/etc/mpv/scripts/"
  cp -r "${srcdir}/scripts/uosc/" "${pkgdir}/etc/mpv/scripts/"

  # install fonts
  cp -r "${srcdir}/fonts/" "${pkgdir}/etc/mpv/"

  # install config files
  mkdir -p "${pkgdir}/etc/mpv/script-opts"
  install -D -m644 "${srcdir}/uosc.conf" -t "${pkgdir}/etc/mpv/script-opts/"
}
