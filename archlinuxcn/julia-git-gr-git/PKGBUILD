pkgname=julia-git-gr-git
pkgver=0.64.3.0.5.gead82a0
pkgrel=1
pkgdesc="GR.jl"
url="https://github.com/jheinen/GR.jl"
arch=('any')
license=('MIT')
makedepends=(git julia-pkg-scripts)
depends=(julia-git)
provides=(julia-git-gr)
source=(git+https://github.com/jheinen/GR.jl)
md5sums=('SKIP')

pkgver() {
  cd GR.jl

  git describe --tags | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

package() {
  cd GR.jl

  echo "import GR_jll" > deps/deps.jl

  . /usr/lib/julia/julia-install-pkg.sh GR "${pkgdir}" "${pkgname}" julia-git

  # GR.jl have check to rebuild at runtime which unconditionally references the build scripts.
  # We don't care so just provide an empty one.
  touch "${dest_dir}"/deps/build.jl
}
