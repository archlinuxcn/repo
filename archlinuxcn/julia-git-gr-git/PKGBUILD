pkgname=julia-git-gr-git
pkgver=0.71.3
pkgrel=1
pkgdesc="GR.jl"
url="https://github.com/jheinen/GR.jl"
arch=('any')
license=('MIT')
makedepends=(git julia-pkg-scripts)
depends=(julia-git)
provides=(julia-git-gr)
source=(git+https://github.com/jheinen/GR.jl
        0001-Get-rid-of-problematic-path-dance.patch)
md5sums=('SKIP'
         '5baf2e950a79f4707f618db43ed83a31')

pkgver() {
  cd GR.jl

  git describe --tags | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

prepare() {
  cd GR.jl

  patch -Np1 < ../0001-Get-rid-of-problematic-path-dance.patch
}

package() {
  cd GR.jl

  . /usr/lib/julia/julia-install-pkg.sh GR "${pkgdir}" "${pkgname}" julia-git
}
