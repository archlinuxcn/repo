# Maintainer: lilydjwg <lilydjwg@gmail.com>
# Contributor: Ahmad Hasan Mubashshir <ahmubashshir@gmail.com>
# from: github
# what: openresty/lua-resty-core
# match! rc[0-9]*$

pkgname=lua-resty-core
pkgver=0.1.32.r1
_upstream_ver=${pkgver/.r/R}
pkgrel=3
pkgdesc='Lua script engine for nginx lua module'
arch=('any')
depends=('luajit' 'lua-resty-lrucache')
url="https://github.com/openresty/lua-resty-core"
license=('BSD-2-Clause')

source=("$pkgname-$pkgver.tar.gz::https://github.com/openresty/$pkgname/archive/v$_upstream_ver.tar.gz")

sha256sums=('7d36188261f44d489acc1671ced73207e2f7adba70663d3f32804509873f3370')

build() {
	cd "$pkgname-$_upstream_ver"
	make
}

package() {
	cd "$pkgname-$_upstream_ver"
	make DESTDIR="$pkgdir" PREFIX=/usr LUA_LIB_DIR='$(PREFIX)/share/lua/$(LUA_VERSION)' LUA_VERSION=5.1 install
}
