# Maintainer: Butui Hu <hot123tea123@gmail.com>

_pkgname=pydub
pkgname=python-pydub
pkgver=0.25.1
pkgrel=3
pkgdesc='Manipulate audio with a simple and easy high level interface'
arch=('any')
url='https://pydub.com'
license=('MIT')
depends=(
  ffmpeg
  python
)
makedepends=(
  python-setuptools
)
optdepends=(
  "python-audio: for playback"
  "python-simpleaudio: for playback, recommended by upstream, though it's archieved."
)
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/jiaaro/pydub/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('8c3fb3714c4b0aed37ba7ab6727776bf4cd7568c1f5060cf43c30ede8da2ce4b498fb83326daa19ef44635250d552295407289c3945681e028eedde1b2b418e0')

build() {
  cd "${_pkgname}-${pkgver}"
  python setup.py build
}

package() {
  cd "${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
