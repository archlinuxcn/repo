# Maintainer: Integral <integral@member.fsf.org>

pkgname=goldendict-ng
pkgver=24.09.0
pkgrel=1
pkgdesc="The next generation GoldenDict (Supports Qt WebEngine & Qt6)."
arch=('i686' 'x86_64' 'aarch64')
url="https://github.com/xiaoyifang/${pkgname}"
license=('GPL-3.0-or-later')
depends=(
	'hunspell'
	'libvorbis'
	'libxtst'
	'lzo'
	'zlib'
	'xz'
	'libeb'
	'tomlplusplus'
	'fmt'
	'opencc'
	'xapian-core'
	'libzim'
	'qt6-base'
	'qt6-svg'
	'qt6-multimedia'
	'qt6-webengine'
	'qt6-speech'
	'qt6-5compat'
)
makedepends=(
	'cmake'
	'ninja'
	'qt6-tools'
)
conflicts=('goldendict' 'goldendict-git' 'goldendict-svn' 'goldendict-git-opt')
provides=('goldendict')
replaces=('goldendict-svn' 'goldendict-git-opt' 'goldendict-webengine-git')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}-Release.316ec900.tar.gz")
sha256sums=('528af2f7079a1cffad8b9451c722b3de80ecff6e33972f896c015058c88cd8be')

build() {
	cmake -B build -S "${pkgname}-${pkgver}-Release.316ec900" -G Ninja \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DUSE_SYSTEM_FMT=ON \
		-DUSE_SYSTEM_TOML=ON \
		-DWITH_FFMPEG_PLAYER=OFF # Temporarily turn off ffmpeg

	cmake --build build
}

package() {
	DESTDIR="${pkgdir}" cmake --install build
}
