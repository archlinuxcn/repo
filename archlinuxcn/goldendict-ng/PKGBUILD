# Maintainer: Integral <integral@member.fsf.org>

pkgname=goldendict-ng
pkgver=24.09.1
pkgrel=1
pkgdesc="The next generation GoldenDict (Supports Qt WebEngine & Qt6)."
arch=('i686' 'x86_64' 'aarch64')
url="https://github.com/xiaoyifang/${pkgname}"
license=('GPL-3.0-or-later')
depends=(
	'hunspell'
	'libvorbis'
	'libxtst'
	'lzo'
	'zlib'
	'xz'
	'libeb'
	'tomlplusplus'
	'fmt'
	'opencc'
	'xapian-core'
	'libzim'
	'qt6-base'
	'qt6-svg'
	'qt6-multimedia'
	'qt6-webengine'
	'qt6-speech'
	'qt6-5compat'
)
makedepends=(
	'cmake'
	'ninja'
	'qt6-tools'
)
conflicts=('goldendict' 'goldendict-git' 'goldendict-svn' 'goldendict-git-opt')
provides=('goldendict')
replaces=('goldendict-svn' 'goldendict-git-opt' 'goldendict-webengine-git')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}-Release.ca9dd133.tar.gz")
sha256sums=('f5927e50d3b0674587feb6faaadd98777cb3309e133564a80bf72fa9bd2da372')

build() {
	cmake -B build -S "${pkgname}-${pkgver}-Release.ca9dd133" -G Ninja \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DUSE_SYSTEM_FMT=ON \
		-DUSE_SYSTEM_TOML=ON \
		-DWITH_FFMPEG_PLAYER=OFF # Temporarily turn off ffmpeg

	cmake --build build
}

package() {
	DESTDIR="${pkgdir}" cmake --install build
}
