# Maintainer: Integral <integral@member.fsf.org>

pkgname=goldendict-ng
pkgver=26.1.1
pkgrel=1
pkgdesc="The next generation GoldenDict (Supports Qt WebEngine & Qt6)."
arch=('i686' 'x86_64' 'aarch64')
url="https://github.com/xiaoyifang/${pkgname}"
license=('GPL-3.0-or-later')
depends=(
	'hunspell'
	'libvorbis'
	'libxtst'
	'lzo'
	'zlib'
	'xz'
	'libeb'
	'tomlplusplus'
	'fmt'
	'opencc'
	'xapian-core'
	'libzim'
	'qt6-base'
	'qt6-svg'
	'qt6-multimedia'
	'qt6-webengine'
	'qt6-speech'
	'qt6-5compat'
)
optdepends=('hunspell-dictionary: Dictionary files for Hunspell support')
makedepends=('cmake' 'qt6-tools')
conflicts=('goldendict')
provides=('goldendict')
replaces=('goldendict-svn' 'goldendict-git-opt' 'goldendict-webengine-git')
_commit_id=5491ffca
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}-Release.${_commit_id}.tar.gz")
sha256sums=('e59b343b4ecd2b34c110797119e700a25628d0ef6c7f589511e411bcf85adf7d')

build() {
	cmake -B build \
		-S "${pkgname}-${pkgver}-Release.${_commit_id}" \
		-DCMAKE_BUILD_TYPE=None \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DUSE_SYSTEM_FMT=ON \
		-DUSE_SYSTEM_TOML=ON

	cmake --build build
}

package() {
	DESTDIR="${pkgdir}" cmake --install build
}
