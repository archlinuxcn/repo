# Maintainer: Severin Kaderli <severin@kaderli.dev>
# Contributor: aimileus < $(echo YWltaWxpdXNAcHJvdG9ubWFpbC5jb20K | base64 -d)
_pkgname=vita3k
_discord_game_sdk_version=2.5.6
pkgname="${_pkgname}-git"
pkgver=r1781.a989885d
pkgrel=3
pkgdesc="Experimental PlayStation Vita emulator"
arch=('x86_64')
url="https://vita3k.org/"
license=('GPL2')
makedepends=(
	'boost'
	'cmake'
	'unzip'
	'git'
	'python2'
	'vulkan-headers'
)
depends=(
	'boost-libs'
	'gtk3'
	'sdl2'
	'vulkan-icd-loader'
)
provides=('vita3k')
conflicts=('vita3k')
source=(
	"git+https://github.com/vita3k/vita3k.git"
        "https://dl-game-sdk.discordapp.net/$_discord_game_sdk_version/discord_game_sdk.zip"
)
md5sums=('SKIP'
         'f86f15957cc9fbf04e3db10462027d58')

pkgver() {
	cd "${_pkgname}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
	cd "${_pkgname}"
	git submodule update --recursive --init
	mkdir -p build/external/discord_game_sdk
	cd build/external
	ln -sf "$srcdir"/discord_game_sdk.zip .
	unzip -d discord_game_sdk discord_game_sdk.zip
	mv discord_game_sdk/lib/x86_64/{,lib}discord_game_sdk.so
	sed -i 's/stdint\.h/cstdint/' discord_game_sdk/cpp/ffi.h
}

build() {
	cd "${_pkgname}"

	mkdir -p build
	cd build/
	cmake -DCMAKE_BUILD_TYPE=Release -DUSE_VULKAN=ON ..
	make UNICORN_QEMU_FLAGS="--python=/usr/bin/python2"
}

package() {
	cd "${_pkgname}"

	install -d -m 755 "${pkgdir}/usr/bin/"
	install -d -m 755 "${pkgdir}/opt/vita3k/"

	cp -r "build/bin/"* "${pkgdir}/opt/vita3k/"
	ln -s "/opt/vita3k/Vita3K" "${pkgdir}/usr/bin/vita3k"

	# These folders needs 777 permissions because vita3k creates files in them
	chmod 777 "${pkgdir}/opt/vita3k/"
	chmod 777 "${pkgdir}/opt/vita3k/data"

	install -Dm644 "README.md" "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}
