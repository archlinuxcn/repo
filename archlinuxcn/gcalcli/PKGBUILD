# Maintainer: Brett Cornwall <ainola@archlinux.org>
# Contributor: polyzen
# Contributor: Army
# Contributor: Dan Serban
# Contributor: insanum
# Contributor: Thomas Zervogiannis

pkgname=gcalcli
pkgver=4.5.1
pkgrel=1
pkgdesc='Google calendar command line interface'
arch=('any')
url=https://github.com/insanum/gcalcli
license=('MIT')
makedepends=("git"
    'python-build'
    'python-installer'
    'python-setuptools'
    'python-setuptools-scm'
    'python-wheel'
)
depends=("python"
    'python-argcomplete'
    'python-babel'
    'python-dateutil'
    'python-google-api-core'
    'python-google-api-python-client'
    'python-google-auth-oauthlib'
    'python-httplib2'
    'python-parsedatetime'
    'python-platformdirs'
    'python-pydantic'
    'python-truststore'
)
optdepends=(
    'python-vobject: for ics/vcal importing'
)
source=("git+https://github.com/insanum/$pkgname.git#tag=v$pkgver")
sha256sums=('093572d0151245d061c1504953e7ad9f8f2b883ea70fd2d2f48fa766c919e8af')

build() {
    cd "${pkgname}"
    python -m build --wheel --no-isolation
}

# Disabled because it downloads deps via pip. Report this upstream (if
# they're still alive).
#
# check() {
#     cd "gcalcli-$pkgver"
#     python setup.py test
# }

package() {
    cd "${pkgname}"
    python -m installer --destdir="${pkgdir}" dist/*.whl
    install -Dm644 docs/*.{md,png} -t "$pkgdir/usr/share/doc/$pkgname"
    install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
