# Maintainer: Alexandre Bouvier <contact@amb.tf>
_pkgname=cemu
pkgname=$_pkgname-git
pkgver=2.6.r89.gef1c836
pkgrel=3
pkgdesc="Nintendo Wii U emulator"
arch=('x86_64')
url="https://cemu.info/"
license=('MPL-2.0')
depends=(
	'gcc-libs'
	'glibc'
	'glslang>=14'
	'hicolor-icon-theme'
	'libx11'
	'pugixml'
	'sdl2'
	'wxwidgets-gtk3-git'
)
makedepends=(
	'bluez-libs'
	'boost'
	'cmake>=3.21.1'
	'cubeb'
	'curl'
    'gspell'
	'fmt>=9'
	'git'
	'glib2'
	'glm'
	'glu'
	'gtk3'
	'hidapi'
	'libgl'
	'libpng'
	'libusb'
	'libzip'
	'nasm'
	'openssl'
	'rapidjson'
	'vulkan-headers>=1:1.3.225'
	'wayland'
	'wayland-protocols'
	'zarchive>=0.1.2'
	'zlib'
	'zstd'
)
provides=("$_pkgname")
conflicts=("$_pkgname")
source=(
	"$_pkgname::git+https://github.com/cemu-project/Cemu.git"
	'imgui::git+https://github.com/ocornut/imgui.git'
)
b2sums=(
	'SKIP'
	'SKIP'
)

pkgver() {
	cd $_pkgname
	git describe --long --tags --abbrev=7 | sed 's/^v//;s/[^-]*-g/r&/;s/-/./g'
}

prepare() {
	cd $_pkgname
	git config submodule.dependencies/imgui.url ../imgui
	git -c protocol.file.allow=always submodule update
	sed -i '/CMAKE_INTERPROCEDURAL_OPTIMIZATION/d' CMakeLists.txt
	sed -i '/FMT_HEADER_ONLY/d' src/Common/precompiled.h
}

build() {
	local options=(
		-D ALLOW_PORTABLE=OFF
		-D CMAKE_BUILD_TYPE=Release
		-D CMAKE_C_FLAGS_RELEASE="-DNDEBUG"
		-D CMAKE_CXX_FLAGS_RELEASE="-DNDEBUG"
		-D ENABLE_VCPKG=OFF
		-Wno-dev
	)
	cmake "${options[@]}" -B build -S $_pkgname
	cmake --build build
}

package() {
	depends+=(
		'libbluetooth.so'
		'libboost_program_options.so'
		'libcrypto.so'
		'libcubeb.so'
		'libcurl.so'
		'libfmt.so'
		'libgdk-3.so'
		'libgobject-2.0.so'
		'libgtk-3.so'
		'libhidapi-hidraw.so'
		'libssl.so'
		'libusb-1.0.so'
		'libwayland-client.so'
		'libz.so'
		'libzarchive.so'
		'libzip.so'
		'libzstd.so'
	)
	cd $_pkgname
	# shellcheck disable=SC2154
	install -d "$pkgdir"/usr/{bin,share/Cemu}
	mv bin/Cemu_release "$pkgdir"/usr/bin/Cemu
	cp -dr --no-preserve=ownership -t "$pkgdir"/usr/share/Cemu bin/*
	install -Dm644 -t "$pkgdir"/usr/share/applications dist/linux/info.cemu.Cemu.desktop
	install -Dm644 -t "$pkgdir"/usr/share/icons/hicolor/128x128/apps dist/linux/info.cemu.Cemu.png
	install -Dm644 -t "$pkgdir"/usr/share/metainfo dist/linux/info.cemu.Cemu.metainfo.xml
}
