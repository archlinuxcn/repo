# Maintainer: Thayne McCombs <astrothayne@gmail.com>
# Contributor: Ariel AxionL <i at axionl dot me>

pkgname=dart-sass
pkgver=1.26.2
pkgrel=2
pkgdesc="Sass makes CSS fun again (canonical implementation)"
arch=('x86_64')
url="https://github.com/sass/dart-sass"
license=('MIT')
makedepends=('dart>=2.6.0')
provides=('sass')
conflicts=('ruby-sass')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/sass/${pkgname}/archive/${pkgver}.tar.gz")

sha256sums=('1e99fe7936c886a65cb893c000c14dc022b53ab63fdb8d936e6917b7e66f9027')

build() {
    cd "${pkgname}-${pkgver}"
    pub get
    dart2native "bin/sass.dart" -Dversion=${pkgver} -o "bin/sass"
}

package() {
    cd "${pkgname}-${pkgver}"
    install -Dm755 "bin/sass" "${pkgdir}/usr/bin/sass"
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=4 sw=4 et:
