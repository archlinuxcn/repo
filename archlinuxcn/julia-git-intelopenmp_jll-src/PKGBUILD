pkgname=julia-git-intelopenmp_jll-src
pkgver=2025.0.4+0
_commit=bb4574e09abe3f820d8aff9add75de4bef4432d4
pkgrel=1
pkgdesc="IntelOpenMP_jll.jl"
url="https://github.com/JuliaBinaryWrappers/IntelOpenMP_jll.jl.git"
arch=(aarch64 armv7h i686 x86_64)
license=('MIT')
makedepends=(git julia-pkg-scripts)
depends=(julia-git)
if [[ $CARCH = x86_64 ]]; then
  depends+=(intel-oneapi-openmp)
fi
source=("git+https://github.com/JuliaBinaryWrappers/IntelOpenMP_jll.jl.git#commit=$_commit"
        jll.toml
        jll_x86_64.toml.in)
sha256sums=('c38b03940a88b4890d0345176fdc8502fea40e45086bb93b63b09e3bc721e18c'
            'e1c9c4d4e3ea147638ce2c17c6ff95bbf09ee894c24b43db3ea7bfa85e92acd6'
            '93ba4574bcb9d27344e01837c1e7a8d07f7e96b3c41d01a9f7acbc94f708198a')

build() {
  cd IntelOpenMP_jll.jl

  if [[ $CARCH = x86_64 ]]; then
    found=0
    for path in $(find /opt/intel/oneapi/compiler/ -name 'libiomp5.so'); do
      echo "Found libiomp5.so at ${path}"
      sed -e "s:@LIBIOMP_PATH@:${path}:g" ../jll_x86_64.toml.in > ../jll_x86_64.toml
      found=1
      break
    done
    ((found))
    julia /usr/lib/julia/julia-gen-jll.jl IntelOpenMP_jll ../jll_x86_64.toml
  else
    julia /usr/lib/julia/julia-gen-jll.jl IntelOpenMP_jll ../jll.toml
  fi
}

package() {
  cd IntelOpenMP_jll.jl

  JULIA_INSTALL_SRCPKG=1 . /usr/lib/julia/julia-install-pkg.sh IntelOpenMP_jll "${pkgdir}" "${pkgname}" julia-git
}
