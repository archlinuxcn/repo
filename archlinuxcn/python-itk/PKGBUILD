# Maintainer: Butui Hu <hot123tea123@gmail.com>

_pkgname=ITKPythonPackage
pkgname=python-itk
pkgver=5.1.1.post1
pkgrel=1
pkgdesc='An open-source toolkit for multidimensional image analysis. This package provides only the python binding'
arch=('x86_64')
url='https://itk.org/'
license=('Apache')
depends=(
  gcc-libs
  python-numpy
)
makedepends=(
  clang
  doxygen
  git
  python-setuptools
  python-scikit-build
)

source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/InsightSoftwareConsortium/ITKPythonPackage/archive/v${pkgver}.tar.gz")
sha512sums=('4a88a4ea348f53847a52a4061e6765cfd0218665392df014d58cb0196a715ac8b5f015dbbb5255008d555ededa10668706ab04380b809712b31cd7c3cfa5075f')

get_pyver() {
  python -c 'import sys; print(str(sys.version_info[0]) + "." + str(sys.version_info[1]))'
}

build() {
  export CC=clang
  export CXX=clang++
  cd "${_pkgname}-${pkgver}"
  python setup.py build
}

package() {
  cd "${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  mv "${pkgdir}/usr/itkHelpers.py" "${pkgdir}/usr/lib/python$(get_pyver)/site-packages/itkHelpers.py"
}
# vim:set ts=2 sw=2 et:

