# Maintainer: Megumi_fox < megumifox@archlinuxcn.org >
_pkgname=google-chinese-handwriting-ime
pkgname=$_pkgname-git
pkgver=v3.0.7.r2.g36844c0
pkgrel=2
pkgdesc="Chinese handwriting IME powered by Google Translate. MacOS style touchpad writing is supported. Written in Electron for Linux."
arch=('x86_64')
license=('custom')
url="https://github.com/Saren-Arterius/google-chinese-handwriting-ime"
depends=('xdotool' 'xorg-xinput' 'python-pyperclip' 'evtest' 'gconf' 'unclutter' 'electron4')
makedepends=('yarn' 'git')
backup=('opt/google-chinese-handwriting-ime/config.js')
install="google-chinese-handwriting-ime.install"
source=(
  "git://github.com/Saren-Arterius/google-chinese-handwriting-ime.git" 
  'google-chinese-handwriting-ime.desktop'
  'google-chinese-handwriting-ime.sh'
)
sha256sums=('SKIP' 'SKIP' 'SKIP')

pkgver() {
	cd $_pkgname
	git describe --long | sed -r 's/^r//;s/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  cd ${srcdir}/${_pkgname}
  rm -rf ./.git
  rm .gitignore
  yarn
}

package() {
  mkdir -p "${pkgdir}/opt/"
  cp -r "${srcdir}/${_pkgname}" "${pkgdir}/opt/${_pkgname}"
  cd "${pkgdir}/opt/${_pkgname}/node_modules"
  rm -r ./electron

  # create run script
  install -Dm755 "$srcdir/google-chinese-handwriting-ime.sh" "$pkgdir/usr/bin/google-chinese-handwriting-ime"
  # create desktop file
  install -Dm644 "$srcdir/google-chinese-handwriting-ime.desktop" "${pkgdir}/usr/share/applications/google-chinese-handwriting-ime.desktop"
  # license
  install -Dm644 "${srcdir}/${_pkgname}/LICENSE.md" "${pkgdir}/usr/share/licenses/${_pkgname}/LICENSE.md"
}
