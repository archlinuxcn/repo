pkgbase=joplin
pkgname=('joplin' 'joplin-desktop')
pkgdesc="A note taking and to-do application with synchronization capabilities"
pkgver=3.1.22
groups=('joplin')
pkgrel=1
_electronVersion=33
depends=("electron${_electronVersion}" "nodejs<23" "nodejs>18" "libvips")
optdepends=('libappindicator-gtk3: for tray icon')
arch=('x86_64')
makedepends=('npm' 'git' 'rsync' 'python-setuptools')
url="https://joplinapp.org/"
license=('MIT')
source=(
    "joplin-desktop.sh"
    "joplin-desktop.desktop"
    "joplin-${pkgver}.tar.gz::https://github.com/laurent22/joplin/archive/v${pkgver}.tar.gz"
)
sha256sums=(
    'SKIP'
    'SKIP'
    '8f9275a6fd28d80300daaec90db427a3a82d10df61f78163567e158ef3933ce5'
)

_setup_env() {
    export YARN_CACHE_FOLDER="${srcdir}/yarn-cache"
    export ELECTRON_SKIP_BINARY_DOWNLOAD=1
    #export npm_config_build_from_source=true
    export npm_config_yes=true
    export SHARP_IGNORE_GLOBAL_LIBVIPS=1
}

prepare() {
    _setup_env

    # Create the yarn cache folder
    mkdir -p "${YARN_CACHE_FOLDER}"

    cd "${srcdir}/joplin-${pkgver}"
}

build() {
    _setup_env

    cd "${srcdir}/joplin-${pkgver}"

    # Delete unused components
    rm -r packages/{app-mobile,app-clipper,server,doc-builder}
    # Fix: Build error due to removal of app-mobile
    sed -i '/app-mobile\/commands/d' packages/tools/gulp/tasks/buildScriptIndexes.js

    corepack install
    npx yarn install

    cd "packages"
    sed -i -E 's_"@joplin/(\w+)": .*_"@joplin/\1": "file://'$PWD'/\1",_g' */package.json
}

check() {
    _setup_env

    cd "${srcdir}/joplin-${pkgver}"

    env ELECTRON_OVERRIDE_DIST_PATH=/usr/lib/electron${_electronVersion}/ \
        TZ=UTC \
        npx yarn workspaces foreach -Rptiv --from 'joplin' --from '@joplin/app-desktop' run test
}

package_joplin() {
    pkgdesc="A note taking and to-do application with synchronization capabilities - CLI App"
    depends=('nodejs')
    optdepends=( )

    _setup_env

    # Pack the app-cli package
    cd "${srcdir}/joplin-${pkgver}/packages/app-cli"
    npx gulp build

    # Install the package
    cd build
    npm pack
    npm install -g --install-links --prefix "${pkgdir}/usr" *.tgz

    # Fix permissions set by npm
    chown -R root:root "${pkgdir}"
}

package_joplin-desktop() {
    pkgdesc="A note taking and to-do application with synchronization capabilities - Desktop"
    depends=("electron${_electronVersion}" "nodejs")
    optdepends=('libappindicator-gtk3: for tray icon')

    _setup_env

    cd "${srcdir}/joplin-${pkgver}/packages/app-desktop"

    electronRoot=/usr/lib/electron${_electronVersion}/
    electronVersion="$(<${electronRoot}/version)"

    # Build the electron package
    npx electron-builder \
      --linux --x64 --dir=dist/ \
      -c.electronDist="${electronRoot}" \
      -c.electronVersion="${electronVersion}"

    mkdir -p "${pkgdir}/usr/lib"
    cp -vr dist/linux-unpacked/resources "${pkgdir}/usr/lib/${pkgname}"

    # Install icons
    while read -r size; do
        mkdir -p "${pkgdir}/usr/share/icons/hicolor/${size}/apps/"
        cp "${pkgdir}/usr/lib/${pkgname}/build/icons/${size}.png" \
           "${pkgdir}/usr/share/icons/hicolor/${size}/apps/${pkgname}.png"
    done < <(ls build/icons | grep -Po '^(\d+)x\1+(?=\.png)')

    install -vDm644 "${srcdir}/${pkgname}.desktop" -t "${pkgdir}/usr/share/applications"
    install -vDm755 "${srcdir}/${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"
    sed -i "s|@electronversion@|${_electronVersion}|" "${pkgdir}/usr/bin/${pkgname}"
}
