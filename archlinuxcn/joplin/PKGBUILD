pkgbase=joplin
pkgname=('joplin' 'joplin-desktop')
pkgver=2.10.18
groups=('joplin')
pkgrel=1
_electronVersion=19
depends=("electron${_electronVersion}" "nodejs")
optdepends=('libappindicator-gtk3: for tray icon')
arch=('x86_64')
makedepends=('npm' 'git' 'rsync' 'yarn')
url="https://joplinapp.org/"
license=('MIT')
source=(
    "joplin-desktop.sh"
    "joplin-desktop.desktop"
    "joplin-${pkgver}.tar.gz::https://github.com/laurent22/joplin/archive/v${pkgver}.tar.gz"
    "00-fix-test.patch::https://github.com/laurent22/joplin/commit/35037e2dc9e033f44163fde7e0b12356cb1d0503.patch"
)
sha256sums=(
    'SKIP'
    'SKIP'
    'd3f0d39596110926a7e557bd06783f5b4d24fff25ef2f14ee51afffaa4984a96'
    '774aac406eed0c5307572935bf14f5641fa2b5cbdbff9b69b75c85c1f2bb08fc'
)

_setup_env() {
    export YARN_CACHE_FOLDER="${srcdir}/yarn-cache"
    export ELECTRON_SKIP_BINARY_DOWNLOAD=1
    export npm_config_build_from_source=true
}

prepare() {
    _setup_env

    # Create the yarn cache folder
    mkdir "${YARN_CACHE_FOLDER}"

    cd "${srcdir}/joplin-${pkgver}"

    # Fix test cases (which was committed after the release)
    patch -Np1 < ../00-fix-test.patch

    # Delete unused components
    rm -r "packages/app-mobile" "packages/app-clipper" "packages/server"

    # Disable optional dependencies
    sed -i 's_"optionalDependencies":_"UNUSED":_' package.json packages/*/package.json
}

build() {
    _setup_env

    cd "${srcdir}/joplin-${pkgver}"

    # Fix "Error: Cannot find module '../../vendor/...'" error
    sed -i '/^\/vendor\/$/d' .gitignore

    yarn install
}

check() {
    _setup_env

    cd "${srcdir}/joplin-${pkgver}"

    env ELECTRON_OVERRIDE_DIST_PATH=/usr/lib/electron${_electronVersion}/ \
        TZ=UTC \
        yarn test
}

package_joplin() {
    pkgdesc="A note taking and to-do application with synchronization capabilities - CLI App"
    depends=('nodejs')

    _setup_env

    # Pack the app-cli package
    cd "${srcdir}/joplin-${pkgver}/packages/app-cli"
    npx gulp build

    # Install the package
    cd build
    npm pack
    npm install -g --prefix "${pkgdir}/usr" *.tgz

    # Fix permissions set by npm
    chown -R root:root "${pkgdir}"
}

package_joplin-desktop() {
    pkgdesc="A note taking and to-do application with synchronization capabilities - Desktop"
    depends=("electron${_electronVersion}" "nodejs")
    optdepends=('libappindicator-gtk3: for tray icon')

    _setup_env

    cd "${srcdir}/joplin-${pkgver}/packages/app-desktop"

    electronRoot=/usr/lib/electron${_electronVersion}/
    electronVersion="$(<${electronRoot}/version)"

    # Build the electron package
    npx electron-builder \
      --linux --x64 --dir=dist/ \
      -c.electronDist="${electronRoot}" \
      -c.electronVersion="${electronVersion}"

    mkdir -p "${pkgdir}/usr/lib"
    cp -vr dist/linux-unpacked/resources "${pkgdir}/usr/lib/${pkgname}"

    # Install icons
    while read -r size; do
        mkdir -p "${pkgdir}/usr/share/icons/hicolor/${size}/apps/"
        cp "${pkgdir}/usr/lib/${pkgname}/build/icons/${size}.png" \
           "${pkgdir}/usr/share/icons/hicolor/${size}/apps/${pkgname}.png"
    done < <(ls build/icons | grep -Po '^(\d+)x\1+(?=\.png)')

    install -vDm644 "${srcdir}/${pkgname}.desktop" -t "${pkgdir}/usr/share/applications"
    install -vDm755 "${srcdir}/${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"
    sed -i "s|@electronversion@|${_electronVersion}|" "${pkgdir}/usr/bin/${pkgname}"
}
