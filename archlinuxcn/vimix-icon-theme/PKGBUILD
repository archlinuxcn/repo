# Maintainer: Mark Wagie <mark dot wagie at tutanota dot com>
# Contributor: Chris Lane <aur at chrislane dot com>
# Contributor: American_Jesus <american.jesus.pt AT gmail DOT com>
# Contributor: Federico Dami√°n <federicodamians@gmail.com>
pkgname=vimix-icon-theme
_pkgver=2023-01-18
pkgver=${_pkgver//-/.}
pkgrel=4
pkgdesc="A Material Design icon theme based on Paper Icon Theme"
arch=('any')
url="https://github.com/vinceliuice/vimix-icon-theme"
license=('CC BY-SA 4.0')
depends=('hicolor-icon-theme' 'gtk-update-icon-cache')
options=('!strip')
source=("$pkgname-$_pkgver.tar.gz::$url/archive/$_pkgver.tar.gz")
sha256sums=('9498701d2f7e7f44901e677b28f5a89daab8a0e96d5ddf20f4557458ea6b86c8')

prepare() {
   cd "$pkgname-$_pkgver"

  # Disable running gtk-update-icon-cache
  sed -i '/gtk-update-icon-cache/d' install.sh
}

package() {
  cd "$pkgname-$_pkgver"
  install -d "$pkgdir/usr/share/icons"
  ./install.sh -a -d "$pkgdir/usr/share/icons"

  install -Dm644 COPYING -t "$pkgdir/usr/share/licenses/$pkgname"
}
