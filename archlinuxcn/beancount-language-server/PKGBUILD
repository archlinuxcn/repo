# Maintainer: Coelacanthus <uwu@coelacanthus.name>
# Contributor: Gyara <laxect39@gmail.com>

pkgname=beancount-language-server
pkgver=1.3.6
pkgrel=1
pkgdesc="A Language Server Protocol (LSP) for beancount files"
arch=('x86_64' 'aarch64')
url="https://github.com/polarmutex/${pkgname}"
license=('MIT')
depends=(
    'beancount'
    'glibc'
    'gcc-libs'
)
makedepends=('cargo')
source=($pkgname-$pkgver.tar.gz::https://github.com/polarmutex/${pkgname}/archive/v${pkgver//_/-}.tar.gz)
b2sums=('3f64c3f56ffaebf67b9dd252db0b7db127a36c044814be83217c6cd324d6024718447cd5ab549e0c5d2356271d757c58a2bfc651419b30467742d439e8fff9d0')
options=(!lto)

prepare() {
    cd "${pkgname}-${pkgver}"
    export RUSTUP_TOOLCHAIN=stable
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
    cd "${pkgname}-${pkgver}"
    export RUSTUP_TOOLCHAIN=stable
    export CARGO_TARGET_DIR=target
    cargo build --frozen --release --all-features
}

check() {
    cd "${pkgname}-${pkgver}"
    export RUSTUP_TOOLCHAIN=stable
    cargo test --frozen --all-features --workspace
}


package() {
    cd "${pkgname}-${pkgver}"
    install -Dm755 "target/release/$pkgname" "$pkgdir/usr/bin/$pkgname"
    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
