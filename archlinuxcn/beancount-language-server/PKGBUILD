# Maintainer: Coelacanthus <CoelacanthusHex@gmail.com>
# Contributor: Gyara <laxect39@gmail.com>

pkgname=beancount-language-server
pkgver=1.3.5
pkgrel=1
pkgdesc="A Language Server Protocol (LSP) for beancount files"
arch=('x86_64' 'aarch64')
url="https://github.com/polarmutex/${pkgname}"
license=('MIT')
depends=(
    'beancount'
    'glibc'
    'gcc-libs'
)
makedepends=('cargo')
source=($pkgname-$pkgver.tar.gz::https://github.com/polarmutex/${pkgname}/archive/v${pkgver//_/-}.tar.gz)
b2sums=('720d11e80b6ce354fe7fd19dfaef33568d6fcd5575cc416e0c9ee6775a9ad8a9bd0b98f52c48f642165ae4a6664c462a1d259bd595034ed7311d6f5d99d10cca')
options=(!lto)

prepare() {
    cd "${pkgname}-${pkgver}"
    export RUSTUP_TOOLCHAIN=stable
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
    cd "${pkgname}-${pkgver}"
    export RUSTUP_TOOLCHAIN=stable
    export CARGO_TARGET_DIR=target
    cargo build --frozen --release --all-features
}

check() {
    cd "${pkgname}-${pkgver}"
    export RUSTUP_TOOLCHAIN=stable
    cargo test --frozen --all-features --workspace
}


package() {
    cd "${pkgname}-${pkgver}"
    install -Dm755 "target/release/$pkgname" "$pkgdir/usr/bin/$pkgname"
    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
