# Maintainer: Heddxh <g311571057 at gmail dot com>
# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=gtksourceview5-apostrophe
pkgver=5.12.1
pkgrel=2
pkgdesc="A text widget adding syntax highlighting and more to GNOME, patched for Apostrophe"
url="https://wiki.gnome.org/Projects/GtkSourceView"
arch=(x86_64)
license=(LGPL-2.1-or-later)
provides=(gtksourceview5=$pkgver libgtksourceview-${pkgver%%.*}.so)
conflicts=(gtksourceview5)
depends=(
  cairo
  fontconfig
  fribidi
  gdk-pixbuf2
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  libxml2
  pango
  pcre2
)
makedepends=(
  gi-docgen
  git
  glib2-devel
  gobject-introspection
  meson
  vala
)
checkdepends=(xorg-server-xvfb)
source=(
  "git+https://gitlab.gnome.org/GNOME/gtksourceview.git#tag=$pkgver"
  "https://gitlab.gnome.org/World/apostrophe/-/raw/main/build-aux/flatpak/sourceview_text_commits.patch"
)
b2sums=('490bd11ed98ec9e11b6db05ca073970d104e99e16dc8f7655abdd5b1055b23ee330e67959db90c615d735b9cb506a26aefc65fd419d667b6a8e393d89a20599c'
        '1e33c2f8f6dcd404bba1022d6e294fb8dd6a8670b05c7d3f50fc6ea6b2e6eab17179dfd8191957d8f49b8f346f246f26b06d148817fc259661d454451fc034ef')

prepare() {
  patch -d gtksourceview -p1 <sourceview_text_commits.patch
}

build() {
  arch-meson gtksourceview build -D documentation=false
  meson compile -C build
}

check() {
  GTK_A11Y=none dbus-run-session xvfb-run -s '-nolisten local' \
    meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
# vim:set sw=2 sts=-1 et:
