# Maintainer: Butui Hu <hot123tea123@gmail.com>

_CUDA_ARCH_LIST="6.0;6.1;6.2;7.0;7.2;7.5;8.0;8.6;8.9;9.0;9.0+PTX"
pkgname=python-apex-git
pkgver=22.05.r1168.b496d85f
_pkgver=22.05
pkgrel=1
pkgdesc="A PyTorch Extension for easy mixed precision and distributed training"
arch=('x86_64')
url="https://github.com/NVIDIA/apex"
license=('BSD-3-Clause')
depends=(python-pytorch-cuda)
makedepends=(
  git
  numactl
  python-pip
  python-setuptools
  python-sympy
  qt5-base
)
provides=(python-apex=${pkgver})
conflicts=(python-apex)
source=("${pkgname}::git+https://github.com/NVIDIA/apex.git"
        "0001-fix-building-torch-extension-with-glog.patch"
)
sha512sums=('SKIP'
            '3d40481858d3e5b2005ab2ba4fb19197b2b14da060fd95afae4045213257c74de78bf2f74e11118093dea2de593822da1d7fdfc531754dc832e581f02d0bfd8d')

prepare() {
  cd "${srcdir}/${pkgname}"
  patch -p1 -i "${srcdir}/0001-fix-building-torch-extension-with-glog.patch"
}

pkgver() {
  cd "${srcdir}/${pkgname}"
  ver=$(printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)")
  echo ${_pkgver}.${ver}
}

build() {
  export TORCH_CUDA_ARCH_LIST=${_CUDA_ARCH_LIST}
  cd "${srcdir}/${pkgname}"
  python setup.py --cuda_ext --cpp_ext build
}

package() {
  cd "${srcdir}/${pkgname}"
  python setup.py --cuda_ext --cpp_ext install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
