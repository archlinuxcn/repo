# Maintainer: Coelacanthus <CoelacanthusHex@gmail.com>
# Contributor: Cooper Pierce <cppierce@andrew.cmu.edu>

pkgname=millet
pkgver=0.14.6
pkgrel=1
pkgdesc="Language server implementation for Standard ML"
arch=('x86_64')
license=('Apache-2.0 AND MIT')
url="https://github.com/azdavis/millet"
depends=(
  gcc-libs
  glibc
)
makedepends=('cargo')
source=("$pkgname-$pkgver.tgz::${url}/archive/refs/tags/v${pkgver}.tar.gz")
b2sums=('620c28a32eb51e2980bb6f5ebcdcccf178de4a6a39464309d4cd162bfe6f6de8197525af5e84200e12d2098ca9187ffa6d03b24dee9af2ac8d3aec3d40238d4a')

prepare() {
  cd "$pkgname-$pkgver"
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}


build() {
  cd "$pkgname-$pkgver"
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --all-features
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm755 "target/release/millet-ls" "$pkgdir/usr/bin/millet"
  install -Dm644 "LICENSE-MIT.md" "$pkgdir/usr/share/licenses/${pkgname}/LICENSE-MIT"
}

check() {
  cd "$pkgname-$pkgver"

  # Ignores tests starting with repo. These tests assume we're in a git repo
  # (we're not, here, since this is a source tarball), and test for things
  # like changelog entries existing.
  cargo test --frozen --all-features --workspace -- --skip repo
}
# vim: set ts=2 sw=2 et:
