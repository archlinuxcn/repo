# Maintainer: Xeonacid <h.dwwwwww@gmail.com>

_name=coincurve
pkgname=python-${_name}
pkgver=21.0.0
pkgrel=1
pkgdesc="Cross-platform Python bindings for libsecp256k1"
arch=(x86_64)
url="https://github.com/ofek/${_name}"
license=(Apache-2.0 MIT)
depends=(glibc python python-asn1crypto python-cffi)
makedepends=(python-build python-installer python-setuptools python-wheel python-hatchling python-scikit-build-core)
checkdepends=(python-pytest python-pytest-benchmark)
source=(${_name}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz)
sha512sums=('6ceb185eb57ae9bdd6bd989b9c99cc63e6c371c173134fc77b9a76b0955315607d635ff935dd177a958e56a572cf9f33f68126d5c2bb55e313cf4d717c8c6d6b')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check(){
  cd $_name-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest -vv tests/
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm 644 LICENSE-MIT -t "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm 644 LICENSE-APACHE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -dm 755 "$pkgdir/usr/share/doc/$pkgname"
  cp -r docs "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
}
