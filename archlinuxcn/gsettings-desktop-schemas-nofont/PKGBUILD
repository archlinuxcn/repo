# Maintainer: Rocket Aaron <i at rocka dot me>
# Contributor: Piroro-hs

_pkgname=gsettings-desktop-schemas
pkgname="${_pkgname}-nofont"
pkgver=47.1
pkgrel=1
pkgdesc='Shared GSettings schemas for the desktop (without font dependencies)'
arch=('any')
url="https://gitlab.gnome.org/GNOME/${_pkgname}"
license=('LGPL-2.1-or-later')
depends=('dconf' 'glib2')
makedepends=('glib2-devel' 'gobject-introspection' 'meson')
provides=('gsettings-desktop-schemas' 'gsettings-system-schemas')
conflicts=('gsettings-desktop-schemas' 'gsettings-system-schemas')
source=("${url}/-/archive/${pkgver}/${_pkgname}-${pkgver}.tar.gz"
        '0001-Revert-default-font.patch')
sha256sums=('77cbca64af31e243c1bb387f939175f47b6776001fb2f6232f4d2bb1f71bbb71'
            '5683807b3f17a5577fdbb9f77be78681c9b935fd81584b38c8f1f0e1a50f5fb0')

prepare() {
  cd "${_pkgname}-${pkgver}"
  patch -p1 -i "${srcdir}/0001-Revert-default-font.patch"
}

build() {
  arch-meson "${_pkgname}-${pkgver}" build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "${pkgdir}"
}
