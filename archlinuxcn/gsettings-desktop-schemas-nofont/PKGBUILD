# Maintainer: Rocket Aaron <i at rocka dot me>
# Contributor: Piroro-hs

_pkgname=gsettings-desktop-schemas
pkgname="${_pkgname}-nofont"
pkgver=48.0
pkgrel=1
pkgdesc='Shared GSettings schemas for the desktop (without font dependencies)'
arch=('any')
url="https://gitlab.gnome.org/GNOME/${_pkgname}"
license=('LGPL-2.1-or-later')
depends=('dconf' 'glib2')
makedepends=('glib2-devel' 'gobject-introspection' 'meson')
provides=('gsettings-desktop-schemas' 'gsettings-system-schemas')
conflicts=('gsettings-desktop-schemas' 'gsettings-system-schemas')
source=("${url}/-/archive/${pkgver}/${_pkgname}-${pkgver}.tar.gz"
        '0001-Revert-default-font.patch')
sha256sums=('0819caff9e179d3e144acc38fa694e468fec1742be8be28c88126aa3a69aef2d'
            '3bf0b91d51fd3fb8ec6f2e04d1f35831c73f009471cd87b483738ae6f0ccc52a')

prepare() {
  cd "${_pkgname}-${pkgver}"
  patch -p1 -i "${srcdir}/0001-Revert-default-font.patch"
}

build() {
  arch-meson "${_pkgname}-${pkgver}" build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "${pkgdir}"
}
