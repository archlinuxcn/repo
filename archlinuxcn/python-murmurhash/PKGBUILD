# Maintainer: Luis Martinez <luis dot martinez at disroot dot org>
# Contributor: Chris Brendel <cdbrendel@gmail.com>
# Python package author: Matthew Honnibal <matt@explosion.ai>

pkgname=python-murmurhash
_pkgname="${pkgname#python-}"
pkgver=1.0.11
pkgrel=1
pkgdesc='Cython bindings for MurmurHash2'
arch=('x86_64')
url='https://github.com/explosion/murmurhash'
license=('MIT')
depends=(
    'glibc'
    'python'
)
makedepends=(
    'cython'
    'python-build'
    'python-installer'
    'python-setuptools'
    'python-wheel'
)
checkdepends=('python-pytest')
source=("$pkgname-$pkgver.tar.gz::https://files.pythonhosted.org/packages/source/m/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha256sums=('87ff68a255e54e7648d0729ff4130f43f7f38f03288a376e567934e16db93767')

prepare() {
    cd "$srcdir/$_pkgname-$pkgver"

    sed -i '/PACKAGES =/c\PACKAGES = ["murmurhash"]' setup.py
}

build() {
    cd "$srcdir/$_pkgname-$pkgver"

    python -m build --wheel --no-isolation
}

check() {
    cd "$srcdir/$_pkgname-$pkgver"

    mv murmurhash/tests .
    local _python_version="$(python -c 'import sys; print("".join(map(str, sys.version_info[:2])))')"
    PYTHONPATH="$PWD/build/lib.linux-$CARCH-cpython-${_python_version}" pytest -v --import-mode=importlib
}

package() {
    cd "$srcdir/$_pkgname-$pkgver"

    python -m installer --destdir="$pkgdir" dist/*.whl

    local _site="$(python -c 'import site; print(site.getsitepackages()[0])')"
    install -dv "$pkgdir/usr/share/licenses/$pkgname/"
    ln -sv "$_site/$_pkgname-$pkgver.dist-info/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/"
}
