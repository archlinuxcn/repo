# Maintainer: Evert Vorster <evorster@gmail.com>
# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>
# Contributor: IncredibleLaser
# Contributor: nous <nous at archlinux.us>
# Contributor: pressh

pkgname=kdenlive-git
_pkgname=${pkgname%-git}
pkgver=24.04.70.r19555
pkgrel=1
pkgdesc="A non-linear video editor for Linux using the MLT video framework. KF5 Frameworks (Latest Applications GIT Version)"
arch=('i686' 'x86_64')
url="http://www.kdenlive.org/"
license=('GPL-3.0-only')
depends=('kfilemetadata5' 'knewstuff5' 'knotifyconfig5'
	'mlt' 'hicolor-icon-theme' 'purpose5' 'qt5-networkauth' 'python'
)
makedepends=('extra-cmake-modules' 'kdoctools5' 'git' 'v4l-utils')
optdepends=('ffmpeg: for FFmpeg plugin'
	'cdrkit: for creation of DVD ISO images'
	'dvdauthor: for creation of DVD'
	'dvgrab: for firewire capture'
	'libdv: for webcam capture (if FFmpeg is not installed)'
	'recordmydesktop: for screen capture'
	'xine-ui: for DVD preview'
	'perl-image-exif-tool: for exif information'
	'mediainfo: for exif information'
	'oxygen-icons: optional for xfce'
	'breeze-icons: otional for default theme'
	'opentimelineio: interchange format for editorial timeline information'
	'python-setuptools: for python modules'
	'python-vosk-bin: open source speech recognition')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
#source=(${pkgname}::git://anongit.kde.org/kdenlive)
#source=(git://anongit.kde.org/kdenlive)
#The git repo has changed recently. To update your git repo to the new one
#and save yourself from downloading the entire repo again, do:
#git remote set-url https://invent.kde.org/multimedia/kdenlive
#in the kdenlive git directory
source=("git+https://invent.kde.org/multimedia/${_pkgname}.git")
sha1sums=('SKIP')
#install=$pkgname.install
#options=(debug !strip)

pkgver() {
	cd "${_pkgname}/"
	_ver="$(cat CMakeLists.txt | grep RELEASE_SERVICE_VERSION | cut -d '"' -f2 | tr '\n' '.' | cut -d "." -f 1-3)"
	echo "$(echo ${_ver}).r$(git rev-list --count HEAD)"
}

prepare() {
	mkdir -p build
}

#To get debug info, change -DCMAKE_BUILD_TYPE=Release to either "Debug" or "RelWithDebInfo"

build() {
	cd build
	cmake "../${_pkgname}/" \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DKDE_INSTALL_LIBDIR=lib \
		-DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
		-DBUILD_TESTING=OFF

	make
}

package() {
	DESTDIR="${pkgdir}" cmake --install build
}
