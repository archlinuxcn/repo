# Maintainer: Konrad Beckmann <konrad.beckmann@gmail.com>

pkgname=airspyhf-git
pkgver=r147.87cf12a
pkgrel=1
pkgdesc="Host code for AirspyHF+ SDR."
arch=('i686' 'x86_64' 'aarch64')
url="http://www.airspy.com"
license=('BSD')
depends=('libusb>=1.0')
provides=('airspyhf')
conflicts=('airspyhf')
makedepends=('git' 'cmake')
source=("git+https://github.com/airspy/airspyhf" "52-airspyhf.rules")
sha512sums=('SKIP' '9b10bd19efe9862be88e0040f40c72ecd3582e90eebbc189cae230de591e9dc45f5c4d7ab6609ef5d419be6045323c8b5fb7bfabe13916568be2d777939ea381')

_gitname="airspyhf"

pkgver() {
  cd "$srcdir/$_gitname"
  printf 'r%s.%s' "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir/$_gitname"
  mkdir -p build
  cd build
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr -DCMAKE_POLICY_VERSION_MINIMUM=3.5 -Wno-dev ../
  make
}

package() {
  install -Dm644 "52-airspyhf.rules" "$pkgdir/usr/lib/udev/rules.d/52-airspyhf.rules"
  cd "$srcdir/$_gitname/build"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
