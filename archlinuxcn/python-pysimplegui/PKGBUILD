# Maintainer: Hu Butui <hot123tea123@gmail.com>
# Contributor: hawkeye116477 <hawkeye116477 at gmail dot com>

_pkgname=PySimpleGUI
pkgname=('python-pysimplegui')
pkgver=4.60.4
pkgrel=2
pkgdesc='Python GUIs for Humans'
arch=('any')
license=('LGPL3')
url='https://github.com/PySimpleGUI/PySimpleGUI'
depends=(
  python
  tk
)
makedepends=(
  python-setuptools
)
source=(${pkgname}-${pkgver}.tar.gz::https://files.pythonhosted.org/packages/source/${_pkgname::1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz)
sha256sums=('f88c82c301a51aea35be605dc060bcceb0dcb6682e16280544884701ab4b23ba')

build() {
  cd "${_pkgname}-${pkgver}"
  python setup.py build
}

package() {
  cd "${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
# vim:set ts=2 sw=2 et:
