# Maintainer: Butui Hu <hot123tea123@gmail.com>
# Contributor: Maciej Sieczka <msieczka at sieczka dot org>

_pkgname=singularity
pkgname=singularity-container
pkgver=3.7.3
pkgrel=1
pkgdesc='Application containers for Linux'
arch=('x86_64')
url='https://www.sylabs.io/singularity/'
license=('BSD')
makedepends=(
  go
)
depends=(
  cryptsetup
  libseccomp
  squashfs-tools
)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/hpcng/singularity/releases/download/v${pkgver}/${_pkgname}-${pkgver}.tar.gz")
sha256sums=('6667eb8875d2b66d73504f40c956b42b1351744f488d164204376215d885da5c')

build() {
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
  cd "${_pkgname}"
  ./mconfig \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --prefix=/usr \
    --sysconfdir=/etc \
    -P release-stripped \
    -V ${pkgver} \
    -v
  make -C builddir
}

package() {
  make -C "${_pkgname}/builddir" DESTDIR="${pkgdir}" install man
  install -Dm644 "${srcdir}/${_pkgname}/LICENSE.md" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:

