# Maintainer: Butui Hu <hot123tea123@gmail.com>

_pkgname=scenedetect
pkgname=python-scenedetect
pkgver=0.6.7
pkgrel=1
pkgdesc='Python and OpenCV-based scene cut/transition detection program & library'
arch=('any')
url='http://scenedetect.com'
license=('BSD-3-Clause')
depends=(
  python-click
  python-numpy
  python-opencv
  python-platformdirs
  python-tqdm
)
makedepends=(
  python-build
  python-installer
  python-wheel
  python-setuptools
)
optdepends=(
  "ffmpeg: required to split video files"
  "mkvtoolnix-cli: required to split video files in stream copy mode"
)
source=("${_pkgname}-${pkgver}.tar.gz::https://files.pythonhosted.org/packages/source/${_pkgname::1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
sha512sums=('c53d2c18fb882fa3088848175158d9624282eaca53a8a86d517564e9b3c19ab5a5357ee5f70f9981213e39cfb9ce2706ae9330e58278c244a69fa04f90aea307')

build() {
  cd "${_pkgname}-${pkgver}"
  python -m build --wheel --no-isolation
}

package() {
  cd "${_pkgname}-${pkgver}"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
