# Maintainer: HurricanePootis <hurricanepootis@protonmail.com>

pkgname=treefetch
pkgver=2.0.0
pkgrel=3
pkgdesc="A plant-based system fetch tool made with Rust"
arch=("x86_64" "aarch64" "riscv64")
url="https://github.com/angelofallars/${pkgname}"
license=('GPL-3.0-or-later')
makedepends=("cargo")
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('b0ba38c039e510482d11b24e1bdc87e5bedd13d306a0d82e771dc4c298dd585b')

prepare() {
	cd "${pkgname}-${pkgver}/"
	cargo fetch --target "$CARCH-unknown-linux-gnu"
}

build() {
	cd "${pkgname}-${pkgver}/"
	export CARGO_TARGET_DIR=target
	cargo build --frozen --release --all-features
}

check() {
	cd "${pkgname}-${pkgver}/"
	cargo test --frozen --all-features
}

package() {
	cd "${pkgname}-${pkgver}/"
	install -Dm755 "target/release/${pkgname}" -t "${pkgdir}/usr/bin/"
	install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
