# Maintainer: Butui Hu <hot123tea123@gmail.com>
# Contributor: Thomas Jost <schnouki@schnouki.net>
# Contributor: Andrey Mikhaylenko <neithere at gmail dot com>

pkgname=python-slugify
pkgver=3.0.2
pkgrel=2
pkgdesc="A Python slugify application that handles unicode"
arch=(any)
url="https://github.com/un33k/python-slugify"
license=('MIT')
depends=("python-text-unidecode")
optdepends=("python-unidecode: Unidecode support")
makedepends=("python-setuptools")
checkdepends=("flake8" "python-pytest" "python-pytest-cov")
source=(${pkgname}-${pkgver}::https://github.com/un33k/python-slugify/archive/${pkgver}.tar.gz)
sha512sums=('c8a7e321c1706fb7296badf3167aa3bdedd1b9ba4741bee987381a92cbfa1741d7c7128823a46d724929132cd1080686ae86045b08a377ea37efd01df8b021a9')

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${pkgname}-${pkgver}"
  pytest -v test.py
}

package() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:

