# Maintainer: schuay <jakob.gruber@gmail.com>
# Contributor: Ray Powell <ray_al@xphoniexx.net>

pkgname=mcomix
pkgver=3.1.0
pkgrel=9
pkgdesc="GTK comic book viewer"
arch=('any')
url="https://sourceforge.net/p/mcomix/wiki/Home/"
license=('GPL-2.0-or-later')
depends=('gtk3' 'python-cairo' 'python-gobject' 'python-pillow')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
optdepends=(
  'lhasa: for lha compressed comics'
  'libunrar: for rar compressed comics'
  'p7zip: for 7z compressed comics'
  'python-chardet: for guessing filename encodings in archives'
  'python-pymupdf: for PDF comics'
  'unrar: for rar compressed comics'
  'unzip: for zip compressed comics'
)
source=(
  "https://downloads.sourceforge.net/project/${pkgname}/MComix-${pkgver}/${pkgname}-${pkgver}.tar.gz"
  0001-Move-multiprocessing-initialization-out-of-functions.patch
)
sha256sums=('f9286eba7ffbc3ce9528135a9939823c425f3bbe9f74a639f8706fcc28b4c427'
            '28981cb58be8de3d81bc6178a7f4c830bcf670d15a3e837e61fed32fddfbc6ed')

prepare() {
  cd "${pkgname}-${pkgver}"
  patch -Np1 -i ../0001-Move-multiprocessing-initialization-out-of-functions.patch
}

build() {
  cd "${pkgname}-${pkgver}"
  python -m build --wheel --no-isolation
}

package() {
  cd "${pkgname}-${pkgver}"
  python -m installer --destdir="$pkgdir" dist/*.whl

  # application meta files are no longer copied automatically by the installation process
  cp -a share "$pkgdir/usr"
}
