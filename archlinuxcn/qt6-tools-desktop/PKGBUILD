# Maintainer: nomisge <nomisge @ live . de>
pkgname=qt6-tools-desktop
pkgver=6.8.0
pkgrel=5
pkgdesc="Adds desktop integration files for qt6-tools
 (assistent, designer, linguist and qdbusviewer)"
arch=('any')
url="https://www.qt.io"
license=('GPL3')
depends=("qt6-tools>=${pkgver}")
makedepends=(git)
makedepends=(git)
options=()
#install=
_pkgfn=qttools
source=("git+https://code.qt.io/qt/$_pkgfn#tag=v$pkgver"
		"assistant6.desktop"
		"designer6.desktop"
		"linguist6.desktop"
		"qdbusviewer6.desktop")
sha256sums=('74659933b95b94a78b9ec24e6f24633473c4dfa180b34531abf847768ccd8822'
            '17cf9fe9c3612f7993fb2dba37b984ee49e25edaed5e9a134c431eb7e033e428'
            '13ec7f608da2c19ce8c69eae761ea836229bedc5b929bb963518e41dc9bf4c0f'
            '6c43926c4c379f0918ffbfd952a54cea887e7394eda90ec44ad8e8a193c3c361'
            'de91af3a52541e6d0aa8ea5c6fd958a734ea4b507ff04a3149aac2a7969a8f0d')

package() {
	# install icons
	cd $srcdir/$_pkgfn
	for icon in src/linguist/linguist/images/icons/linguist-*-32.png ; do
		size=$(echo $(basename ${icon}) | cut -d- -f2)
		install -p -D -m644 ${icon} \
		"${pkgdir}/usr/share/icons/hicolor/${size}x${size}/apps/linguist6.png"
	done

	install -D -m644 src/assistant/assistant/images/assistant.png \
		"${pkgdir}/usr/share/icons/hicolor/32x32/apps/assistant6.png"
	install -D -m644 src/assistant/assistant/images/assistant-128.png \
		"${pkgdir}/usr/share/icons/hicolor/128x128/apps/assistant6.png"
	install -D -m644 src/designer/src/designer/images/designer.png \
		"${pkgdir}/usr/share/icons/hicolor/128x128/apps/QtProject-designer6.png"
	install -D -m644 src/qdbus/qdbusviewer/images/qdbusviewer.png \
		"${pkgdir}/usr/share/icons/hicolor/32x32/apps/qdbusviewer6.png"
	install -D -m644 src/qdbus/qdbusviewer/images/qdbusviewer-128.png \
		"${pkgdir}/usr/share/icons/hicolor/128x128/apps/qdbusviewer6.png"

	# install .desktop files
	cd $srcdir
  	install -d "${pkgdir}/usr/share/applications"
	install -m644 "${srcdir}"/{linguist,designer,assistant,qdbusviewer}6.desktop \
		"${pkgdir}/usr/share/applications/"
}
