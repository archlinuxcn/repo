# Maintainer: Gavin Luo <lunt.luo@gmail.com>
# Contributor: Guillaume Hayot <ghayot@postblue.info>

pkgname=gnome-shell-extension-gsconnect
pkgver=71
pkgrel=1
pkgdesc="KDE Connect implementation with GNOME Shell integration"
arch=('any')
url="https://github.com/GSConnect/gnome-shell-extension-gsconnect"
license=('GPL-2.0-or-later OR MPL-2.0')
makedepends=('meson')
depends=('gnome-shell>=46')
optdepends=('evolution-data-server: Contacts integration (Evolution)'
            'libgdata: Contacts integration (GNOME Online Accounts)'
            'gsound: Themed sound effects'
            'python-nautilus: Nautilus integration')
source=(https://github.com/GSConnect/"${pkgname}"/archive/v"${pkgver}".tar.gz)
b2sums=('37bfac3495e02e147df497ba52d5c20bf0fad1265951d01d75902dc0d053c42f57bec7b5d0a64f63de58184a1c5e311e77b5f48738a095b8696c76de59150663')

build() {
  arch-meson -Dinstalled_tests=false -Dfirewalld=true ${pkgname}-${pkgver} build
  meson compile -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
