# Maintainer: Blair Bonnett <blair dot bonnett at gmail dot com>

pkgname=python-aiohttp-session
pkgdesc='Web sessions for aiohttp.web'
pkgver=2.12.0
pkgrel=2
url='https://aiohttp-session.readthedocs.io/'
license=('Apache')
arch=('any')
depends=('python-aiohttp')
optdepends=(
  'python-aiomcache: Memcached session storage'
  'python-cryptography: store sessions in encrypted cookies'
  'python-pynacl: NaCL encrypted JSON session storage'
  'python-redis: Redis session storage'
)
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')

_pypi=aiohttp_session
source=(
  "https://files.pythonhosted.org/packages/source/${_pypi::1}/$_pypi/$_pypi-$pkgver.tar.gz"
)
sha256sums=('0ccd11a7c77cb9e5a61f4daacdc9170d561112f9cfaf9e9a2d9867c0587d1950')

build() {
  cd "aiohttp-session-$pkgver"
  python -m build --wheel --no-isolation
}

package() {
  cd "aiohttp-session-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl
}
