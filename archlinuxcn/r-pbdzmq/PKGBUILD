# Maintainer: Hu Butui <hot123tea123@gmail.com>

_pkgname=pbdZMQ
_pkgver=0.3-5
pkgname=r-${_pkgname,,}
pkgver=0.3.5
pkgrel=1
pkgdesc="Programming with Big Data -- Interface to 'ZeroMQ'"
arch=('x86_64')
url="https://cran.r-project.org/package=${_pkgname}"
license=('GPL')
depends=(
  r
  zeromq
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
sha256sums=('4d8088b3365d612a661f0078bcba50357cb3a7ec04a699348b4312fd6fd811ed')

build() {
  R CMD INSTALL ${_pkgname}_${_pkgver}.tar.gz -l "${srcdir}"
}

package() {
  install -dm0755 "${pkgdir}/usr/lib/R/library"
  cp -a --no-preserve=ownership "${_pkgname}" "${pkgdir}/usr/lib/R/library"
}
# vim:set ts=2 sw=2 et:
