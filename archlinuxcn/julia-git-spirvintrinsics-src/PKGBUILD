pkgname=julia-git-spirvintrinsics-src
pkgver=0.2.0
_commit=2c81f84ba90e6f3443b728b93df39f4db6bb5174
pkgrel=3
pkgdesc="SPIRVIntrinsics.jl"
url="https://github.com/JuliaGPU/OpenCL.jl.git"
arch=('any')
license=('MIT')
makedepends=(git julia-pkg-scripts)
depends=(julia-git)
source=("git+https://github.com/JuliaGPU/OpenCL.jl.git#commit=$_commit"
        0001-Don-t-use-platform-dependent-types.-272.patch
        0002-Don-t-use-platform-dependent-types-for-upsample-273.patch)
sha256sums=('4907b86888e2fabc7552b223646680fe9f7bd14a9c0f7a1d510c25bda7990f23'
            'ecdb655322774fcc424290025ce628bb80798d8427643fbb10184ee846c45c16'
            '46837ba20aa78c38f05a39167ba133ad16fda0bcbeedb2f693157be5bd1d1e57')

prepare() {
  cd OpenCL.jl

  patch -Np1 --no-backup-if-mismatch < ../0001-Don-t-use-platform-dependent-types.-272.patch
  patch -Np1 --no-backup-if-mismatch < ../0002-Don-t-use-platform-dependent-types-for-upsample-273.patch
}

package() {
  cd OpenCL.jl/lib/intrinsics

  JULIA_INSTALL_SRCPKG=1 . /usr/lib/julia/julia-install-pkg.sh SPIRVIntrinsics "${pkgdir}" "${pkgname}" julia-git
}
