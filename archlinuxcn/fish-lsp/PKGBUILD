# Maintainer: Heddxh <g311571057 at gmail dot com>
# Contributor: tippfehlr <tippfehlr@tippfehlr.eu>
# Contributor: Chewing_Bever
pkgname=fish-lsp-bin
_pkgname=fish-lsp
pkgver=1.1.0
pkgrel=1
pkgdesc="LSP implementation for the fish shell language ðŸ "
arch=('x86_64') # tree-sitter contains compiled files
url="https://github.com/ndonfris/fish-lsp"
license=('MIT')
depends=('nodejs')
optdepends=('fish: fish shell')
provides=(${_pkgname})
conflicts=(${_pkgname}-git ${_pkgname})
source=("${pkgname}-${pkgver}::${url}/releases/download/v${pkgver}/fish-lsp.standalone"
        "${pkgname}-${pkgver}-LICENSE.md"::"https://raw.githubusercontent.com/ndonfris/fish-lsp/refs/tags/v${pkgver}/LICENSE.md")
sha256sums=('67d37033789ef15dae88e5fed5ca883ec2b0007a6e0b899234055a938c454768'
            '42d622608175e998ffcdbb53217a6356a578282782a553ef9a86f5192ffcd7c2')

prepare() {
    chmod +x ${pkgname}-${pkgver}
    ./${pkgname}-${pkgver} info --man-file --show >fish-lsp.1 2> /dev/null
}


package() {
    install -Dm755 ${pkgname}-${pkgver} "$pkgdir/usr/bin/fish-lsp"
    install -Dm644 ${pkgname}-${pkgver}-LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
    install -Dm644 fish-lsp.1 "$pkgdir/usr/share/man/man1/fish-lsp.1"
}
