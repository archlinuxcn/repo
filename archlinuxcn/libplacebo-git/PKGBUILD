# Maintainer: Coelacanthus <CoelacanthusHex@gmail.com>
# Contributor: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=libplacebo-git
_pkgname=libplacebo
epoch=1
pkgver=4.208.0.r42.gddc4181
pkgrel=2
pkgdesc='Reusable library for GPU-accelerated video/image rendering primitives'
url='https://github.com/haasn/libplacebo'
arch=('x86_64')
license=('LGPL2.1')
depends=('vulkan-icd-loader' 'libvulkan.so'
         'glslang'
         'lcms2' 'liblcms2.so'
         'shaderc' 'libshaderc_shared.so'
         'libepoxy' 'libepoxy.so'
         'libunwind'
         # for demos
         #'ffmpeg' 'libavcodec.so' 'libavutil.so' 'libavformat.so'
         #'glfw'
         #'sdl2' 'sdl2_image'
         )
makedepends=('git' 'meson' 'ninja' 'vulkan-headers' 'python' 'python-mako')
checkdepends=(
              'ffmpeg'
              'dav1d'
              )
provides=('libplacebo' 'libplacebo.so')
conflicts=('libplacebo')
source=(
        'git+https://code.videolan.org/videolan/libplacebo.git'
        'git+https://github.com/Immediate-Mode-UI/Nuklear.git'
        )
b2sums=('SKIP'
        'SKIP')
options=('debug' 'strip')

pkgver() {
    cd ${srcdir}/libplacebo
    git describe --long | sed 's/^v//;s/-rc/rc/;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    cd ${srcdir}/libplacebo
    git config submodule.demos/3rdparty/nuklear.url "${srcdir}/Nuklear"
    git submodule update --init \
        demos/3rdparty/nuklear
}

build() {
    cd ${srcdir}/libplacebo

    # Disable demos for https://github.com/haasn/libplacebo/issues/120
    arch-meson build \
        --buildtype=debugoptimized \
        -D default_library=shared \
        -D tests=true \
        -D vulkan=enabled \
        -D glslang=enabled \
        -D shaderc=enabled \
        -D lcms=enabled \
        -D demos=false  \
        -D d3d11=disabled
    ninja -C build
}

check() {
    cd ${srcdir}/libplacebo
    ninja -C build test
}

package() {
    cd ${srcdir}/libplacebo
    DESTDIR="${pkgdir}" ninja -C build install
    install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=4 sw=4 et:
