# Maintainer: Fredrick Brennan <copypaste@kittens.ph>
# Maintainer: Pig Monkey <pm@pig-monkey.com>
# Contributor: mutantmonkey <aur@mutantmonkey.in>
# Contributor: Stephan Eisvogel <eisvogel at embinet dot de>
# Contributor: Daniel Reuter <daniel.robin.reuter@googlemail.com>
# Contributor: Integral <integral@member.fsf.org>

pkgname=ocrmypdf
pkgver=16.11.0
pkgrel=1
pkgdesc="A tool to add an OCR text layer to scanned PDF files, allowing them to be searched"
url="https://github.com/${pkgname}/OCRmyPDF"
arch=('any')
license=('MPL-2.0')
depends=('python>=3.9' 'img2pdf' 'python-pillow' 'tesseract' 'ghostscript' 'unpaper' 'pngquant' 'python-pikepdf' 'python-reportlab' 'python-pdfminer' 'python-tqdm' 'python-pluggy' 'python-rich' 'python-importlib_resources' 'python-packaging' 'python-deprecation')
makedepends=('python-setuptools-scm>=7.0.5' 'python-build' 'python-installer' 'python-wheel' 'python-hatchling' 'python-hatch-vcs')
optdepends=('jbig2enc: Better compression algorithm; results in smaller PDF files')
source=("${url}/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('d89077e503238dac35c6e565925edc8d98b71e5289853c02cacbc1d0901f1be7')
install="${pkgname}.install"

build() {
	cd "${pkgname}-${pkgver}/"
	python -m build --wheel --no-isolation
}

package() {
	cd "${pkgname}-${pkgver}/"
	python -m installer --destdir="${pkgdir}" dist/*.whl
	install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
