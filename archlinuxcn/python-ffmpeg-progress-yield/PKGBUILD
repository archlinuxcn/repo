# Maintainer: Jason Nader <jason.nader [] protonmail.com>
# Maintainer: Sian1468 <sian1468-aur@.39011468.xyz>

pkgname=python-ffmpeg-progress-yield
_name=${pkgname#python-}
pkgver=1.1.2
pkgrel=1
pkgdesc="Run an ffmpeg command with its progress yielded."
arch=('any')
url="https://github.com/slhck/ffmpeg-progress-yield"
license=('MIT')
depends=('ffmpeg' 'python-tqdm' python-uv-build)
makedepends=('python-setuptools' 'python-build' 'python-installer' 'python-wheel' 'git' 'python-uv-build')
checkdepends=('ffmpeg' 'python-pytest' 'python-pytest-asyncio')
source=("git+https://github.com/slhck/ffmpeg-progress-yield.git#tag=v${pkgver}")
sha512sums=('f11a156df017d45e580d6d62f9fbb0634489789614e50f35fcd0c0a6eaefee857837cd65dc95f77df387eda0baabe60a9955c66768ad9a996c035859113aae4d')

function prepare() {
	cd ffmpeg-progress-yield
	sed -i 's|"uv_build>=0.8.14,<0.9.0"|"uv_build"|g' pyproject.toml
}

function build() {
	cd ffmpeg-progress-yield
	python -m build --wheel --no-isolation
}

function package() {
	cd ${_name}
	python -m installer --destdir="$pkgdir" dist/*.whl
	install -Dm644 LICENSE.md "${pkgdir}/usr/share/licenses/$pkgname/LICENSE"
}
