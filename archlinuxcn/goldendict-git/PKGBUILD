# Maintainer:  Marcell Meszaros < marcell.meszaros AT runbox.eu >
# Contributor: aksr <aksr at t-com dot me>
# Contributor: VirtualTam <virtualtam@flibidi.net>
# Contributor: Eugene Yudin aka Infy <Eugene dot Yudin at gmail dot com>

pkgname=goldendict-git
pkgver=1.5.0rc2.r582.g9a5e1e89
pkgrel=3
epoch=1
pkgdesc="A feature-rich dictionary lookup program, supporting multiple dictionary formats."
arch=('i686' 'x86_64')
url="https://github.com/goldendict/goldendict"
license=('GPL3')
depends=('ffmpeg' 'hunspell' 'libao' 'libvorbis' 'libxtst' 'lzo' 'zlib' 'xz' 'libeb' 'libiconv' 'opencc'
	'qt5-webkit' 'qt5-svg' 'qt5-tools' 'qt5-x11extras' 'qt5-multimedia')
makedepends=('git')
conflicts=('goldendict')
provides=('goldendict')
source=("$pkgname::git+${url}.git")
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/$pkgname"

	# Generate git tag based version. Count only proper (v)#.#* [#=number] tags.
	local _gitversion=$(git describe --long --tags --match '[v0-9][0-9.][0-9.]*' | sed -e 's|^v||' | tr '[:upper:]' '[:lower:]') 

	# Format git-based version for pkgver
	echo "${_gitversion}" | sed \
		-e 's|^\([0-9][0-9.]*\)-\([a-zA-Z]\+\)|\1\2|' \
		-e 's|\([0-9]\+-g\)|r\1|' \
		-e 's|-|.|g'
}

prepare() {
	cd "$srcdir/$pkgname"
	msg "Fixing flags"
	echo "QMAKE_CXXFLAGS_RELEASE = $CFLAGS" >> goldendict.pro
	echo "QMAKE_CFLAGS_RELEASE = $CXXFLAGS" >> goldendict.pro
}

build(){
	cd "$srcdir/$pkgname"
	qmake-qt5 "CONFIG+=zim_support" "CONFIG+=chinese_conversion_support" PREFIX="/usr" goldendict.pro
	make
}

package() {
	cd "$srcdir/$pkgname"
	make INSTALL_ROOT="$pkgdir" install
}
