# Maintainer: Integral <integral@member.fsf.org>

pkgname=caligula-git
_pkgname=${pkgname%-git}
pkgver=0.4.7.r43.ge92a4ec
pkgrel=1
pkgdesc="A user-friendly, lightweight TUI for disk imaging"
arch=('x86_64' 'aarch64' 'riscv64')
url="https://github.com/ifd3f/${_pkgname}"
license=('GPL-3.0-or-later')
depends=('gcc-libs')
makedepends=('git' 'cargo')
conflicts=("${_pkgname}")
provides=("${_pkgname}")
source=("git+${url}.git")
sha256sums=('SKIP')
options=('!lto')

pkgver() {
	cd "${_pkgname}/"
	git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/v//'
}

prepare() {
	cd "${_pkgname}/"
	export RUSTUP_TOOLCHAIN=stable
	cargo fetch --target "$(rustc -vV | sed -n 's/host: //p')" # --locked
}

build() {
	cd "${_pkgname}/"
	export RUSTUP_TOOLCHAIN=stable
	export CARGO_TARGET_DIR=target
	RUSTFLAGS="--cfg tracing_unstable" cargo build --release --all-features # --frozen
}

check() {
	cd "${_pkgname}/"
	export RUSTUP_TOOLCHAIN=stable
	RUSTFLAGS="--cfg tracing_unstable" cargo test --all-features # --frozen
}

package() {
	cd "${_pkgname}/"
	install -Dm755 "target/release/${_pkgname}" -t "$pkgdir/usr/bin/"
	install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${_pkgname}/"
	install -Dm644 README.md -t "${pkgdir}/usr/share/doc/${_pkgname}/"
}
