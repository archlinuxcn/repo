# Maintainer: lsf
# Contributor: Daniel Ha√ü <aur@hass.onl>
pkgname=standardnotes-desktop
_pkgname=desktop
pkgver=3.5.18
pkgrel=2
pkgdesc="A standard notes app with an un-standard focus on longevity, portability, and privacy."
arch=('x86_64' 'aarch64')
url="https://standardnotes.org/"
license=('GPL3')
conflicts=('sn-bin')
depends=('electron')
makedepends=('npm' 'node-gyp' 'git' 'jq' 'python2' 'yarn')
makedepends+=("git" "python2" "node-gyp")
source=("git://github.com/standardnotes/desktop.git"
        "git://github.com/standardnotes/web.git#commit=687b53263f379466294a320e62204bfcada997cb"
        "git://github.com/sn-extensions/extensions-manager.git#commit=206c1da0a4bcd2389a052863de03251b103b7335"
        "git://github.com/sn-extensions/batch-manager.git#commit=3897593b98fe46505a8bf8a8e22fd64319dc5900"
        'webpack.patch'
        'standardnotes-desktop.desktop'
        'standardnotes-desktop.js')
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            '1d0d499efda1a39f55ef5fff6f8cdbca96337ad98f4ef9c65e2078c934ea9f3b'
            '8045c3baa6a3f5e0a20387913599eafb2d8c6e843745f38f34daea1ab44e73e7'
            '2d90137b689cc38d6c68b17fad2336503846152a0061a91ac2073ea0873a6fc5')

prepare() {
  cd $_pkgname
  git checkout v$pkgver
  git submodule init
  git config submodule.web.url $srcdir/web
  git config submodule.extensions-manager.url $srcdir/extensions-manager
  git config submodule.batch-manager.url $srcdir/batch-manager
  git submodule update

  cp .env.js.sample .env.js

  # Set system Electron version for ABI compatibility
  sed -r 's#("electron": ").*"#\1'$(cat /usr/lib/electron/version)'"#' -i package.json

  # workaround for TS compilation failing due to a "might be null" error.
  # this might be an ugly thing to just ignore, but, well, uh...
  patch -Np1 -i ${srcdir}/webpack.patch
}

build() {
  cd $srcdir/$_pkgname/
  # use temporary npm cache - https://wiki.archlinux.org/index.php/Node.js_package_guidelines
  npm install --cache "${srcdir}/npm-cache"
  npm install --prefix ./web --cache "${srcdir}/npm-cache"
  npm run bundle
  npm install
  npm --prefix ./app install ./app
  npx rimraf app/dist/
  npx webpack --config webpack.prod.js

  if [[ ${CARCH} == "aarch64" ]]; then
    npx electron-builder build --arm64 --linux --dir
  elif [[ ${CARCH} == "x86_64" ]]; then
    npx electron-builder build --x64 --linux --dir
  fi
}


package() {

  function remove_srcdir_ref {
    local tmppackage="$(mktemp)"
    jq '.|=with_entries(select(.key|test("_.+")|not))' "$1" > "$tmppackage"
    mv "$tmppackage" "$1"
    chmod 644 "$1"
  }

  mkdir -p $pkgdir/opt/$pkgname
  cp -r $srcdir/$_pkgname/app $pkgdir/opt/$pkgname/

  # Remove $srcdir references - https://wiki.archlinux.org/index.php/Node.js_package_guidelines
  for i in $(find "$pkgdir" -name package.json); do
    remove_srcdir_ref $i
  done

  install -D -m644 $pkgname.desktop "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -D -m644 $srcdir/$_pkgname/build/icon/Icon-512x512.png "${pkgdir}/usr/share/icons/standard-notes.png"
  install -D -m655 $pkgname.js "${pkgdir}/usr/bin/${pkgname}"
}
