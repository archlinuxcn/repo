# Maintainer: Butui Hu <hot123tea123@gmail.com>
# Contributor: Chris <christopher.r.mullins g-mail>
# Contributor: Andrzej Giniewicz <gginiu@gmail.com>

pkgname=gdcm
_pkgname=GDCM
pkgver=3.0.24
pkgrel=4
pkgdesc='A C++ library for DICOM medical files'
arch=('x86_64')
url='http://gdcm.sourceforge.net'
license=('BSD-3-Clause')
depends=(
  charls
  expat
  gcc-libs
  glibc
  libxml2
  openjpeg2
  openssl
  poppler
  python
  util-linux-libs
#  vtk
  zlib
)
makedepends=(
  adios2
  alembic
  anari-sdk
  boost
  boost-libs
  cgns
  cli11
  cmake
  docbook-xsl
  doxygen
  eigen
  fast_float
  ffmpeg
  fmt
  gdal
  git
  gl2ps
  glew
  gnuplot
  graphviz
  hdf5
  jsoncpp
  libarchive
  libharu
  liblas
  libxslt
  lz4
  mariadb-libs
  netcdf
  nlohmann-json
  opencascade
  openimagedenoise
  openmpi
  openvdb
  openvr
  openxr
  ospray
  pdal
  postgresql-libs
  proj
  python-matplotlib
  python-mpi4py
  qt5-base
  qt5-declarative
  qt5-multimedia
  sqlite
  swig
  texlive-latexextra
  tk
  unixodbc
  utf8cpp
  verdict
)
source=(
  "${pkgname}-${pkgver}.tar.gz::https://github.com/malaterre/GDCM/archive/v${pkgver}.tar.gz"
  "0001-fix-building-with-VTK.patch"
  "0002-fix-building-cmakelists.patch"
)
sha512sums=('2fe28444cee171a536d63f26c1ad7308a03b946e79dc8b7d648b5c7e6f4a8f52c0c32ec9cf463d95b876db31becc81541638b97fc7f15b79ae04de5988d6941e'
            '73a10de2ac729efe93a662841aa7e8e201d64716ff5a88806611afcbeb36ee81260ec439aab70d601a360dde1c34fa885a71d8178315d5add8c33dea5a1e001c'
            'd564903676dbf998ee56503e4510e7c6943a7c447e44b38c9ff799ca1e137e817a57f8f6702da7b580d758089ceb91c8967aac48e38ec084236d280efc0ef844')

prepare() {
  cd ${_pkgname}-${pkgver}
#  patch -p1 -i ${srcdir}/0001-fix-building-with-VTK.patch
  patch -p1 -i ${srcdir}/0002-fix-building-cmakelists.patch
}

build() {
  local python_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')
  # fix https://sourceforge.net/p/gdcm/bugs/561/ by setting -DCMAKE_CXX_STANDARD=20
  cmake \
    -B build \
    -S "${_pkgname}-${pkgver}" \
    -DCMAKE_CXX_STANDARD=20 \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_INSTALL_RPATH:BOOL=ON \
    -DEXPAT_LIBRARY=/usr/lib/libexpat.so \
    -DGDCMV2_0_COMPATIBILITY:BOOL=OFF \
    -DGDCM_BUILD_APPLICATIONS:BOOL=ON \
    -DGDCM_BUILD_DOCBOOK_MANPAGES:BOOL=ON \
    -DGDCM_BUILD_EXAMPLES:BOOL=OFF \
    -DGDCM_BUILD_SHARED_LIBS:BOOL=ON \
    -DGDCM_BUILD_TESTING:BOOL=OFF \
    -DGDCM_DOCUMENTATION:BOOL=OFF \
    -DGDCM_INSTALL_PYTHONMODULE_DIR=lib/${python_version}/site-packages \
    -DGDCM_LEGACY_REMOVE:BOOL=ON \
    -DGDCM_MANPAGES_USE_NONET=1 \
    -DGDCM_USE_SYSTEM_CHARLS:BOOL=ON \
    -DGDCM_USE_SYSTEM_EXPAT:BOOL=ON \
    -DGDCM_USE_SYSTEM_LIBXML2:BOOL=ON \
    -DGDCM_USE_SYSTEM_OPENJPEG:BOOL=ON \
    -DGDCM_USE_SYSTEM_OPENSSL:BOOL=ON \
    -DGDCM_USE_SYSTEM_POPPLER:BOOL=ON \
    -DGDCM_USE_SYSTEM_UUID:BOOL=ON \
    -DGDCM_USE_SYSTEM_ZLIB:BOOL=ON \
    -DGDCM_USE_VTK:BOOL=OFF \
    -DGDCM_VTK_INSTALL_PYTHONMODULE_DIR=lib/${python_version}/site-packages \
    -DGDCM_WRAP_PYTHON:BOOL=ON \
    -DVTKGDCM_WRAP_PYTHON:BOOL=ON
  make -C "build"
}

package() {
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  cd "${srcdir}/${_pkgname}-${pkgver}"
  install -Dm644 "${srcdir}/${_pkgname}-${pkgver}/Copyright.txt" "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"

  make -C "${srcdir}/build" DESTDIR="${pkgdir}" install
  python -O -m compileall "${pkgdir}/usr/lib"
}
# vim:set ts=2 sw=2 et:
