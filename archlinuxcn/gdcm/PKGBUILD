# Maintainer: Butui Hu <hot123tea123@gmail.com>
# Contributor: Chris <christopher.r.mullins g-mail>
# Contributor: Andrzej Giniewicz <gginiu@gmail.com>

pkgname=gdcm
_pkgname=GDCM
pkgver=3.0.9
pkgrel=3
pkgdesc='A C++ library for DICOM medical files'
arch=('x86_64')
url='http://gdcm.sourceforge.net'
license=('BSD')
depends=(
  'charls'
  'libxml2'
  'poppler'
  'python'
  'vtk'
)
makedepends=(
  'cmake'
  'doxygen'
  'docbook-xsl'
  'git'
  'glew'
  'graphviz'
  'hdf5'
  'libxslt'
  'netcdf'
  'openmpi'
  'proj'
  'qt5-base'
  'qt5-multimedia'
  'swig'
  'texlive-latexextra'
  'utf8cpp'
)
source=(
  "${pkgname}-${pkgver}.tar.gz::https://github.com/malaterre/GDCM/archive/v${pkgver}.tar.gz"
  "0001-fix-building-with-gcc11.patch::https://github.com/malaterre/GDCM/commit/4404b770be337bd0d5d3c2289abfd34426433db2.patch"
)
sha512sums=('5220ea12ac94d83ee7046a3a070cec7ef83752d2f3d838e7db8d0379b5606d19a94d5bfdc04e8ec4c72e0c3b4397751f54fad49a04a283ee9b760b59183e8f7a'
            '67ddd28837253bae450b587d36e8f3c08cfa2779593718430e8bda3fb53f336a6e131c96be2808202b84957b445f7cd5998c2cda73a112d86e7c29900acef220')

get_pyver() {
  python -c 'import sys; print(str(sys.version_info[0]) + "." + str(sys.version_info[1]))'
}

prepare() {
  cd "${_pkgname}-${pkgver}"
  patch -p1 -i "${srcdir}/0001-fix-building-with-gcc11.patch"
}

build() {
  export CXXFLAGS+=" ${CPPFLAGS} -fpermissive"
  cmake \
    -B build \
    -S "${_pkgname}-${pkgver}" \
    -DCHARLS_LIBRARY=/usr/lib/libcharls.so \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_INSTALL_RPATH:BOOL=ON \
    -DGDCMV2_0_COMPATIBILITY:BOOL=OFF \
    -DGDCM_BUILD_APPLICATIONS:BOOL=ON \
    -DGDCM_BUILD_DOCBOOK_MANPAGES:BOOL=ON \
    -DGDCM_BUILD_EXAMPLES:BOOL=OFF \
    -DGDCM_BUILD_SHARED_LIBS:BOOL=ON \
    -DGDCM_BUILD_TESTING:BOOL=OFF \
    -DGDCM_DOCUMENTATION:BOOL=OFF \
    -DGDCM_LEGACY_REMOVE:BOOL=ON \
    -DGDCM_USE_SYSTEM_CHARLS:BOOL=ON \
    -DGDCM_USE_SYSTEM_EXPAT:BOOL=ON \
    -DGDCM_USE_SYSTEM_LIBXML2:BOOL=ON \
    -DGDCM_USE_SYSTEM_OPENJPEG:BOOL=ON \
    -DGDCM_USE_SYSTEM_OPENSSL:BOOL=ON \
    -DGDCM_USE_SYSTEM_POPPLER:BOOL=ON \
    -DGDCM_USE_SYSTEM_UUID:BOOL=ON \
    -DGDCM_USE_SYSTEM_ZLIB:BOOL=ON \
    -DGDCM_USE_VTK:BOOL=ON \
    -DGDCM_WRAP_PYTHON:BOOL=ON
  make -C "build"
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  install -Dm644 "${srcdir}/${_pkgname}-${pkgver}/Copyright.txt" "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"

  make -C "${srcdir}/build" DESTDIR="${pkgdir}" install

  # fix python binding installation
  install -d "${pkgdir}/usr/lib/python$(get_pyver)/site-packages"
  ln -s "/usr/lib/_gdcmswig.so" "${pkgdir}/usr/lib/python$(get_pyver)/site-packages/_gdcmswig.so"
  ln -s "/usr/lib/vtkgdcmPython.so" "${pkgdir}/usr/lib/python$(get_pyver)/site-packages/vtkgdcmPython.so"
  mv -v "${pkgdir}/usr/lib/gdcm.py" "${pkgdir}/usr/lib/python$(get_pyver)/site-packages"
  mv -v "${pkgdir}/usr/lib/vtkgdcm.py" "${pkgdir}/usr/lib/python$(get_pyver)/site-packages"
  mv -v "${pkgdir}/usr/lib/gdcmswig.py" "${pkgdir}/usr/lib/python$(get_pyver)/site-packages"
}
# vim:set ts=2 sw=2 et:

