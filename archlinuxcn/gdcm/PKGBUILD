# Maintainer: Butui Hu <hot123tea123@gmail.com>
# Contributor: Chris <christopher.r.mullins g-mail>
# Contributor: Andrzej Giniewicz <gginiu@gmail.com>

pkgname=gdcm
_pkgname=GDCM
pkgver=3.0.0
pkgrel=12
pkgdesc='A C++ library for DICOM medical files'
arch=('x86_64')
url='http://gdcm.sourceforge.net'
license=('BSD')
depends=(
  'charls'
  'libxml2'
  'poppler'
  'python'
  'vtk'
)
makedepends=(
  'cmake'
  'doxygen'
  'git'
  'glew'
  'graphviz'
  'hdf5'
  'libxslt'
  'netcdf'
  'openmpi'
  'proj'
  'qt5-base'
  'qt5-multimedia'
  'swig'
)

source=(
  "https://github.com/malaterre/GDCM/archive/v${pkgver}.tar.gz"
  'fix-charls.patch'
  '0001-VTK-8.2-requires-explicit-vtkVersion.h-include.patch'
  '0002-VTK-8.2-requires-hierarchy-info-for-wrapping.patch'
  '0003-Remove-dead-vtkImageViewer-code-from-gdcmviewer.patch'
)
sha512sums=('2ac076dd49011234f4431ffe67fcba84a1ca9042ec5fc4dfc8aed2ed16bec5f499fa7aa666e5630796afc266ce76741d931cca333534b55fdc477e25a9189d33'
            'd777800718b8e556cbc70f8103d2ca676691a6a7e52d726405ce0644b1f691d01896f77731c6f97f6f4f3de0defbf1dc1abbc03aef41a10c5c111dab1331c07b'
            '7eb880dae563b9b4214a9dd679fec0bac1d7c8b492f343fe5c48d2e9d6fcaa692774324609c88fab22656af2b11324f2418177bbf7b2159824438bee03d73f4a'
            '57d75cb283c606d3816346d461e880af8e89207a013c5b7794f465172e1557351fa12b475af34a39677b6909bde2b5ddfcbc0c902ea3d1ade68460c020cce775'
            'ab41eb9c19796d6bbfba2e76cd64058d3bd28847fee224b334f28ed6ac6d07db581062a8b3a7ff3a34e78df9a5aa7832538237ecd9c43e0039ac3ce6ffbfda0a')

prepare() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  patch -p1 -i "${srcdir}/fix-charls.patch"
  patch -p1 -i "${srcdir}/0001-VTK-8.2-requires-explicit-vtkVersion.h-include.patch"
  patch -p1 -i "${srcdir}/0002-VTK-8.2-requires-hierarchy-info-for-wrapping.patch"
  patch -p1 -i "${srcdir}/0003-Remove-dead-vtkImageViewer-code-from-gdcmviewer.patch"
}

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  mkdir -p build
  cd build
  cmake \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_INSTALL_RPATH:BOOL=ON \
    -DGDCM_BUILD_APPLICATIONS:BOOL=ON \
    -DGDCM_BUILD_DOCBOOK_MANPAGES:BOOL=ON \
    -DGDCM_BUILD_EXAMPLES:BOOL=OFF \
    -DGDCM_BUILD_SHARED_LIBS:BOOL=ON \
    -DGDCM_BUILD_TESTING:BOOL=OFF \
    -DGDCM_DOCUMENTATION:BOOL=ON \
    -DGDCM_LEGACY_REMOVE:BOOL=ON \
    -DGDCM_USE_SYSTEM_CHARLS:BOOL=ON \
    -DGDCM_USE_SYSTEM_EXPAT:BOOL=ON \
    -DGDCM_USE_SYSTEM_LIBXML2:BOOL=ON \
    -DGDCM_USE_SYSTEM_OPENJPEG:BOOL=ON \
    -DGDCM_USE_SYSTEM_OPENSSL:BOOL=ON \
    -DGDCM_USE_SYSTEM_POPPLER:BOOL=ON \
    -DGDCM_USE_SYSTEM_UUID:BOOL=ON \
    -DGDCM_USE_SYSTEM_ZLIB:BOOL=ON \
    -DGDCM_USE_VTK:BOOL=ON \
    -DGDCMV2_0_COMPATIBILITY:BOOL=OFF \
    -DGDCM_WRAP_PYTHON:BOOL=ON \
    ..
  make -j1
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  install -Dm644 Copyright.txt "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"

  cd "${srcdir}/${_pkgname}-${pkgver}/build"
  make DESTDIR="${pkgdir}" install

  # fix python binding installation
  install -d "${pkgdir}/usr/lib/python3.7/site-packages"
  mv "${pkgdir}/usr/lib/_gdcmswig.so" "${pkgdir}/usr/lib/python3.7/site-packages"
  mv "${pkgdir}/usr/lib/gdcmswig.py" "${pkgdir}/usr/lib/python3.7/site-packages"
  mv "${pkgdir}/usr/lib/gdcm.py" "${pkgdir}/usr/lib/python3.7/site-packages"
}
# vim:set ts=2 sw=2 et:

