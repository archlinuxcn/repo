# Maintainer: Butui Hu <hot123tea123@gmail.com>
# Contributor: Chris <christopher.r.mullins g-mail>
# Contributor: Andrzej Giniewicz <gginiu@gmail.com>

pkgname=gdcm
_pkgname=GDCM
pkgver=3.0.12
pkgrel=2
pkgdesc='A C++ library for DICOM medical files'
arch=('x86_64')
url='http://gdcm.sourceforge.net'
license=('BSD')
depends=(
  charls
  libxml2
  openjpeg2
  poppler
  python
# vtk
)
makedepends=(
  cmake
  doxygen
  docbook-xsl
  git
  glew
  graphviz
  hdf5
  libxslt
  netcdf
  openmpi
  proj
  qt5-base
  qt5-multimedia
  swig
  texlive-latexextra
  utf8cpp
)
source=(
  "${pkgname}-${pkgver}.tar.gz::https://github.com/malaterre/GDCM/archive/v${pkgver}.tar.gz"
)
sha512sums=('aa40774030de0483e9b56d12fefd1493574855104b5e8371c8c5e9c903aaa764b38ea75ec5afbd27bc50e5d53332e9e415b20cb2e5a65c1c4eb5d05bb692f9e8')

get_pyver() {
  python -c 'import sys; print(str(sys.version_info[0]) + "." + str(sys.version_info[1]))'
}

# disable vtk, see also https://sourceforge.net/p/gdcm/bugs/509/
build() {
  export CXXFLAGS+=" ${CPPFLAGS} -fpermissive"
  cmake \
    -B build \
    -S "${_pkgname}-${pkgver}" \
    -DCHARLS_LIBRARY=/usr/lib/libcharls.so \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_INSTALL_RPATH:BOOL=ON \
    -DGDCMV2_0_COMPATIBILITY:BOOL=OFF \
    -DGDCM_BUILD_APPLICATIONS:BOOL=ON \
    -DGDCM_BUILD_DOCBOOK_MANPAGES:BOOL=ON \
    -DGDCM_BUILD_EXAMPLES:BOOL=OFF \
    -DGDCM_BUILD_SHARED_LIBS:BOOL=ON \
    -DGDCM_BUILD_TESTING:BOOL=OFF \
    -DGDCM_DOCUMENTATION:BOOL=OFF \
    -DGDCM_LEGACY_REMOVE:BOOL=ON \
    -DGDCM_USE_SYSTEM_CHARLS:BOOL=ON \
    -DGDCM_USE_SYSTEM_EXPAT:BOOL=ON \
    -DGDCM_USE_SYSTEM_LIBXML2:BOOL=ON \
    -DGDCM_USE_SYSTEM_OPENJPEG:BOOL=ON \
    -DGDCM_USE_SYSTEM_OPENSSL:BOOL=ON \
    -DGDCM_USE_SYSTEM_POPPLER:BOOL=ON \
    -DGDCM_USE_SYSTEM_UUID:BOOL=ON \
    -DGDCM_USE_SYSTEM_ZLIB:BOOL=ON \
    -DGDCM_USE_VTK:BOOL=OFF \
    -DGDCM_WRAP_PYTHON:BOOL=ON
  make -C "build"
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  install -Dm644 "${srcdir}/${_pkgname}-${pkgver}/Copyright.txt" "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"

  make -C "${srcdir}/build" DESTDIR="${pkgdir}" install

  # fix python binding installation
  install -d "${pkgdir}/usr/lib/python$(get_pyver)/site-packages"
  ln -s "/usr/lib/_gdcmswig.so" "${pkgdir}/usr/lib/python$(get_pyver)/site-packages/_gdcmswig.so"
  #ln -s "/usr/lib/vtkgdcmPython.so" "${pkgdir}/usr/lib/python$(get_pyver)/site-packages/vtkgdcmPython.so"
  find "${pkgdir}/usr/lib" -type f -name "*.py" -exec mv -vf {} "${pkgdir}/usr/lib/python$(get_pyver)/site-packages" \;
  python -O -m compileall "${pkgdir}/usr/lib"
}
# vim:set ts=2 sw=2 et:

