# Maintainer: Butui Hu <hot123tea123@gmail.com>
# Contributor: Chris <christopher.r.mullins g-mail>
# Contributor: Andrzej Giniewicz <gginiu@gmail.com>

pkgname=gdcm
_pkgname=GDCM
pkgver=3.0.19
pkgrel=2
pkgdesc='A C++ library for DICOM medical files'
arch=('x86_64')
url='http://gdcm.sourceforge.net'
license=('BSD')
depends=(
  charls
  libxml2
  openjpeg2
  poppler
  python
# vtk
)
makedepends=(
  cmake
  doxygen
  docbook-xsl
  git
  glew
  graphviz
  hdf5
  libxslt
  netcdf
  openmpi
  proj
  qt5-base
  qt5-multimedia
  swig
  texlive-latexextra
  utf8cpp
)
source=(
  "${pkgname}-${pkgver}.tar.gz::https://github.com/malaterre/GDCM/archive/v${pkgver}.tar.gz"
)
sha512sums=('7e4ec5a5d84c4e43b4948c3d2eff3e08a8d7c21ed865aab23b86a9994e07b1484d1ce8a9cb46e6367a99c5cf3c5d81ed18f9016b5240dd8e9edd4bf6398dbc19')

# disable vtk, see also https://sourceforge.net/p/gdcm/bugs/509/
build() {
  export CXXFLAGS+=" ${CPPFLAGS} -fpermissive"
  cmake \
    -B build \
    -S "${_pkgname}-${pkgver}" \
    -DCHARLS_LIBRARY=/usr/lib/libcharls.so \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_INSTALL_RPATH:BOOL=ON \
    -DGDCMV2_0_COMPATIBILITY:BOOL=OFF \
    -DGDCM_BUILD_APPLICATIONS:BOOL=ON \
    -DGDCM_BUILD_DOCBOOK_MANPAGES:BOOL=ON \
    -DGDCM_BUILD_EXAMPLES:BOOL=OFF \
    -DGDCM_BUILD_SHARED_LIBS:BOOL=ON \
    -DGDCM_BUILD_TESTING:BOOL=OFF \
    -DGDCM_DOCUMENTATION:BOOL=OFF \
    -DGDCM_LEGACY_REMOVE:BOOL=ON \
    -DGDCM_USE_SYSTEM_CHARLS:BOOL=ON \
    -DGDCM_USE_SYSTEM_EXPAT:BOOL=ON \
    -DGDCM_USE_SYSTEM_LIBXML2:BOOL=ON \
    -DGDCM_USE_SYSTEM_OPENJPEG:BOOL=ON \
    -DGDCM_USE_SYSTEM_OPENSSL:BOOL=ON \
    -DGDCM_USE_SYSTEM_POPPLER:BOOL=ON \
    -DGDCM_USE_SYSTEM_UUID:BOOL=ON \
    -DGDCM_USE_SYSTEM_ZLIB:BOOL=ON \
    -DGDCM_USE_VTK:BOOL=OFF \
    -DGDCM_WRAP_PYTHON:BOOL=ON
  make -C "build"
}

package() {
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  cd "${srcdir}/${_pkgname}-${pkgver}"
  install -Dm644 "${srcdir}/${_pkgname}-${pkgver}/Copyright.txt" "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"

  make -C "${srcdir}/build" DESTDIR="${pkgdir}" install

  # fix python binding installation
  install -d "${pkgdir}${site_packages}"
  ln -s "/usr/lib/_gdcmswig.so" "${pkgdir}${site_packages}/_gdcmswig.so"
  #ln -s "/usr/lib/vtkgdcmPython.so" "${pkgdir}/usr/lib/python$(get_pyver)/site-packages/vtkgdcmPython.so"
  find "${pkgdir}/usr/lib" -type f -name "*.py" -exec mv -vf {} "${pkgdir}${site_packages}" \;
  python -O -m compileall "${pkgdir}/usr/lib"
}
# vim:set ts=2 sw=2 et:

