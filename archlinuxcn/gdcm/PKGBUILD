# Maintainer: Butui Hu <hot123tea123@gmail.com>
# Contributor: Chris <christopher.r.mullins g-mail>
# Contributor: Andrzej Giniewicz <gginiu@gmail.com>

pkgname=gdcm
_pkgname=GDCM
pkgver=3.0.23
pkgrel=2
pkgdesc='A C++ library for DICOM medical files'
arch=('x86_64')
url='http://gdcm.sourceforge.net'
license=('BSD-3-Clause')
depends=(
  charls
  expat
  gcc-libs
  glibc
  libxml2
  openjpeg2
  openssl
  poppler
  python
  util-linux-libs
  vtk
  zlib
)
makedepends=(
  adios2
  boost
  boost-libs
  cgns
  cli11
  cmake
  docbook-xsl
  doxygen
  eigen
  fast_float
  ffmpeg
  fmt
  gdal
  git
  gl2ps
  glew
  glew
  gnuplot
  graphviz
  graphviz
  hdf5
  hdf5
  jsoncpp
  libarchive
  libharu
  liblas
  libxslt
  lz4
  mariadb-libs
  netcdf
  netcdf
  opencascade
  openimagedenoise
  openmpi
  openmpi
  openvdb
  openvr
  openxr
  ospray
  pdal
  postgresql-libs
  proj
  proj
  python-mpi4py
  qt5-base
  qt5-declarative
  qt5-multimedia
  swig
  texlive-latexextra
  utf8cpp
  verdict
)
source=(
  "${pkgname}-${pkgver}.tar.gz::https://github.com/malaterre/GDCM/archive/v${pkgver}.tar.gz"
  "0001-fix-building-with-VTK.patch"
)
sha512sums=('a1c45434e925ddac293993014686865aecf1b6745013ed6a141b6ee1bdbe7ddef6abcfc2491583e63f33f4292617ee65412da4c41f38731409df9149903a8772'
            '73a10de2ac729efe93a662841aa7e8e201d64716ff5a88806611afcbeb36ee81260ec439aab70d601a360dde1c34fa885a71d8178315d5add8c33dea5a1e001c')

prepare() {
  cd ${_pkgname}-${pkgver}
  patch -p1 -i ${srcdir}/0001-fix-building-with-VTK.patch
}

build() {
  local python_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')
  cmake \
    -B build \
    -S "${_pkgname}-${pkgver}" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_INSTALL_RPATH:BOOL=ON \
    -DEXPAT_LIBRARY=/usr/lib/libexpat.so \
    -DGDCMV2_0_COMPATIBILITY:BOOL=OFF \
    -DGDCM_BUILD_APPLICATIONS:BOOL=ON \
    -DGDCM_BUILD_DOCBOOK_MANPAGES:BOOL=ON \
    -DGDCM_BUILD_EXAMPLES:BOOL=OFF \
    -DGDCM_BUILD_SHARED_LIBS:BOOL=ON \
    -DGDCM_BUILD_TESTING:BOOL=OFF \
    -DGDCM_DOCUMENTATION:BOOL=OFF \
    -DGDCM_INSTALL_PYTHONMODULE_DIR=lib/${python_version}/site-packages \
    -DGDCM_LEGACY_REMOVE:BOOL=ON \
    -DGDCM_MANPAGES_USE_NONET=1 \
    -DGDCM_USE_SYSTEM_CHARLS:BOOL=ON \
    -DGDCM_USE_SYSTEM_EXPAT:BOOL=ON \
    -DGDCM_USE_SYSTEM_LIBXML2:BOOL=ON \
    -DGDCM_USE_SYSTEM_OPENJPEG:BOOL=ON \
    -DGDCM_USE_SYSTEM_OPENSSL:BOOL=ON \
    -DGDCM_USE_SYSTEM_POPPLER:BOOL=ON \
    -DGDCM_USE_SYSTEM_UUID:BOOL=ON \
    -DGDCM_USE_SYSTEM_ZLIB:BOOL=ON \
    -DGDCM_USE_VTK:BOOL=ON \
    -DGDCM_VTK_INSTALL_PYTHONMODULE_DIR=lib/${python_version}/site-packages \
    -DGDCM_WRAP_PYTHON:BOOL=ON \
    -DVTKGDCM_WRAP_PYTHON:BOOL=ON
  make -C "build"
}

package() {
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  cd "${srcdir}/${_pkgname}-${pkgver}"
  install -Dm644 "${srcdir}/${_pkgname}-${pkgver}/Copyright.txt" "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"

  make -C "${srcdir}/build" DESTDIR="${pkgdir}" install
  python -O -m compileall "${pkgdir}/usr/lib"
}
# vim:set ts=2 sw=2 et:

