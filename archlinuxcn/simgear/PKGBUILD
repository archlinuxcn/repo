# Maintainer: Jake <aur@ja-ke.tech>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Hans Janssen <hans@janserv.xs4all.nl>

pkgname=simgear
pkgver=2024.1.4
pkgrel=1
pkgdesc="A set of open-source libraries designed to be used as building blocks for quickly assembling 3d simulations, games, and visualization applications."
arch=(armv7h aarch64 x86_64)
depends=(glu glut freealut openscenegraph libexpat.so udns
         libavcodec.so libavformat.so libavutil.so libswscale.so)
makedepends=(boost cmake mesa plib)
provides=(libSimGearCore.so libSimGearScene.so)
license=("GPL")
url="http://www.flightgear.org/"
options=('makeflags' 'staticlibs')
source=(
  https://gitlab.com/flightgear/simgear/-/archive/${pkgver}/simgear-${pkgver}.tar.gz)
sha256sums=('512238508b9f4101ac1515acad31b3a1efbdd26cc24665e55523010f5753b613')

prepare() {
  cd simgear-$pkgver

  rm -rf build
  mkdir -p build/

  sed -i -e'1 i#include <boost/mpl/if.hpp>' simgear/nasal/cppbind/NasalHash.hxx
}

build() {
  cd simgear-$pkgver/build
  CFLAGS+=" -ffile-prefix-map=${srcdir}=${DBGSRCDIR:-/usr/src/debug}"
  CXXFLAGS+=" -ffile-prefix-map=${srcdir}=${DBGSRCDIR:-/usr/src/debug}"
  cmake .. \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DCMAKE_BUILD_TYPE=Release \
        -DENABLE_TESTS=Off \
        -DSIMGEAR_SHARED=On \
        -DSYSTEM_EXPAT=On \
        -DSYSTEM_UDNS=On \
        -DENABLE_SIMD=On
  make
}

package() {
  cd simgear-$pkgver/build
  make DESTDIR="$pkgdir" install
}
