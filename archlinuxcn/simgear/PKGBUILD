# Maintainer: Jake <aur@ja-ke.tech>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Hans Janssen <hans@janserv.xs4all.nl>

pkgname=simgear
pkgver=2020.3.17
_pkgver=${pkgver%.*}
pkgrel=2
pkgdesc="A set of open-source libraries designed to be used as building blocks for quickly assembling 3d simulations, games, and visualization applications."
arch=(armv7h aarch64 x86_64)
depends=(glu glut freealut openscenegraph libexpat.so udns)
makedepends=(boost cmake mesa plib)
provides=(libSimGearCore.so libSimGearScene.so)
license=("GPL")
url="http://www.flightgear.org/"
options=('makeflags' 'staticlibs')
source=("https://downloads.sourceforge.net/project/flightgear/release-${_pkgver}/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('4c5e9cd0dcff1bd311c2460aa601230bd80abdbaec751b559c1d7147749f377c')

prepare() {
  cd "$srcdir"/simgear-$pkgver
  # sed -i -e "s|#include <simgear/structure/map.hxx>|#include <simgear/structure/map.hxx>\n#include <boost/utility/enable_if.hpp>|g" simgear/nasal/cppbind/NasalHash.hxx

  rm -rf build
  mkdir -p build/
}

build() {
  cd "$srcdir"/simgear-$pkgver/build
  CFLAGS+=" -ffile-prefix-map=${srcdir}=${DBGSRCDIR:-/usr/src/debug}"
  CXXFLAGS+=" -ffile-prefix-map=${srcdir}=${DBGSRCDIR:-/usr/src/debug}"
  cmake .. \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DCMAKE_BUILD_TYPE=Release \
        -DENABLE_TESTS=Off \
        -DSIMGEAR_SHARED=On \
        -DSYSTEM_EXPAT=On \
        -DSYSTEM_UDNS=On \
        -DENABLE_SIMD=On \
        -DENABLE_SIMD_CODE=On \
        -DENABLE_OPENMP=On
  make
}

package() {
  cd "$srcdir"/simgear-$pkgver/build
  make DESTDIR="$pkgdir" install
}
