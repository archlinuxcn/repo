# Maintainer:  edward-p <edward AT edward-p DOT xyz>

pkgname=lunarvim-git
pkgver=0.5.1.r350.g3c5a6d06
pkgrel=1
pkgdesc="An IDE layer for Neovim with sane defaults. Completely free and community driven."
arch=('any')
url='https://github.com/LunarVim/LunarVim'
license=('GPL3')
depends=(
  'neovim'
  'lua'
  'git'
  'fzf'
  'neovim-remote'
  'tree-sitter'
  'python-pynvim'
  'nodejs'
  'yarn')
makedepends=('git')
optdepends=(
  'ripgrep: optional dependencies for telescope.nvim'
)
install=${pkgname}.install
source=("${pkgname}::git+https://github.com/LunarVim/LunarVim.git#branch=rolling"
	"git+https://github.com/nvim-treesitter/nvim-treesitter.git#branch=0.5-compat"
        "init-lvim.sh"
        "lvim")
sha256sums=('SKIP'
            'SKIP'
            '0bb4e331e3e47dab1610b04b1a2bcdf96a56f8880ef5cbf00ee0f3b1bd8c4873'
            'bf8a977a1ceff5240e5709de79f0eb2543816f2d627d6a750e766a78bc28d503')

pkgver() {
  cd "${pkgname}"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build(){
  cd "${srcdir}/nvim-treesitter"
  runtime="${srcdir}/nvim-treesitter"
  count_maintained=$(nvim --cmd "set runtimepath+=${runtime}" --headless \
    +"lua print(#require('nvim-treesitter.parsers').maintained_parsers())" +qall 2>&1)

  while [ "$(ls ${runtime}/parser/ | wc -l)" -lt "${count_maintained}" ]; do
    nvim --cmd "set runtimepath+=${runtime}" --headless \
      +'TSUpdateSync maintained' +qall
  done
}

package() {
  cd "${srcdir}/${pkgname}"

  mkdir -p "${pkgdir}/usr/share/lunarvim"
  cp -r {colors,ftdetect,lua,init.lua} "${pkgdir}/usr/share/lunarvim"
  
  mkdir -p "${pkgdir}/usr/share/lunarvim/prebuild/nvim-treesitter/parser"{,-info}

  for parser in "${srcdir}/nvim-treesitter/parser/"*.so; do
    install -Dm 755 "${parser}" "${pkgdir}/usr/share/lunarvim/prebuild/nvim-treesitter/parser/${parser##/*/}"
  done
  
  for info in "${srcdir}/nvim-treesitter/parser/"*; do
    install -Dm 755 "${info}" "${pkgdir}/usr/share/lunarvim/prebuild/nvim-treesitter/parser/${info##/*/}"
  done

  install -Dm 644 "README.md" "${pkgdir}/usr/share/doc/lunarvim/README.md"
  install -Dm 644 "CONTRIBUTING.md" "${pkgdir}/usr/share/doc/lunarvim/CONTRIBUTING.md"
  install -Dm 644 "utils/installer/config.example.lua" "${pkgdir}/usr/share/doc/lunarvim/config.example.lua"
  install -Dm 644 "utils/installer/config.example-no-ts.lua" "${pkgdir}/usr/share/doc/lunarvim/config.example-no-ts.lua"
  install -Dm 755 "${srcdir}/lvim" "${pkgdir}/usr/bin/lvim"
  install -Dm 755 "${srcdir}/init-lvim.sh" "${pkgdir}/usr/share/lunarvim/init-lvim.sh"
}
