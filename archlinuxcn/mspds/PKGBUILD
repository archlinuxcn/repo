# Maintainer: Christopher Bero <bigbero@gmail.com>
# Taken from https://aur.archlinux.org/packages/mspds
# PKGBUILD copied from https://github.com/greigdp/msp430-mspds
# Contributor: Alexei Colin <ac@alexeicolin.com>
pkgname=mspds
pkgver=3.15.1.001
_shortver=${pkgver/00/}
pkgrel=11
pkgdesc="MSP430 Debug Stack. Contains a dynamic link library as well as embedded firmware that runs on the MSP-FET430UIF or the eZ430 emulators."
arch=('i686' 'x86_64')
url="https://www.ti.com/tool/mspds"
# Licenses were found in "MSPDebugStackOpenSourcePackage_manifest.html" from the mspds source archive.
license=('custom:TI BSD' 'custom:IAR BSD' 'custom:TI TSPA')
groups=('msp430')
depends=('boost-libs' 'hidapi' 'libusb')
makedepends=('dos2unix' 'boost')
optdepends=('mspdebug')
source=("https://software-dl.ti.com/msp430/msp430_public_sw/mcu/msp430/MSPDS/${pkgver//./_}/export/MSPDebugStack_OS_Package_${_shortver//./_}.zip"
        'hidapi.patch')
sha256sums=('e3a59a98c43de7a92e5814d8c3304026165e6d2551e60acaca1f08c6b1a4bac8'
            'aa2bdb86118a84423f3df752f48d90d2ebcb1e1bbc5293bdfd7fb1c62f765a34')


prepare() {
    find "${srcdir}" -type f -exec dos2unix -q '{}' \;
    # This hidapi patch allows us to build mspds from the hidapi Archlinux package rather than the v0.7 source.
    patch -p1 < hidapi.patch
    sed -i 's/^\/\/\(#define FPGA_UPDATE\)/\1/' $srcdir/DLL430_v3/src/TI/DLL430/UpdateManagerFet.cpp
}

build() {
    cd "$srcdir"
    make
}

package() {
    install -Dm644 "$srcdir/libmsp430.so" "$pkgdir/usr/lib/libmsp430.so"
    # Install headers mentioned in https://www.ti.com/lit/ug/slau656b/slau656b.pdf
    install -Dm644 -t "$pkgdir/usr/include" "$srcdir/DLL430_v3/include/"*.h
    install -Dm644 "$srcdir/MSPDebugStackOpenSourcePackage_manifest.html" -t "$pkgdir/usr/share/licenses/$pkgname"
}
