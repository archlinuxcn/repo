# Maintainer: lilydjwg <lilydjwg@gmail.com>

pkgname=archrepo2-git
epoch=2
pkgver=0.5.r22.gfa4f743
pkgrel=2
pkgdesc="Arch Linux repository manager"
arch=('any')
url="https://github.com/lilydjwg/archrepo2"
license=('MIT')
depends=('python' 'python-setuptools' 'python-tornado' 'python-pyinotify' 'pacman' 'pyalpm')
makedepends=('git')
source=('git+https://github.com/lilydjwg/archrepo2.git')
md5sums=('SKIP')
backup=(etc/archrepo2.ini)

_gitroot=archrepo2

pkgver() {
  cd "$srcdir/$_gitroot"
  git describe | sed -E 's/([^-]*-g)/r\1/;s/-/./g;s/v//'
}

build() {
  cd $srcdir/$_gitroot
  python3 setup.py build
}

package() {
  cd $srcdir/$_gitroot
  python3 setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 misc/archrepo2.service $pkgdir/usr/lib/systemd/system/archrepo2.service
  install -Dm644 misc/archrepo2.ini.example $pkgdir/etc/archrepo2.ini
}

# vim:set ts=2 sw=2 et:
