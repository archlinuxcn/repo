# Maintainer: Rihards Skuja <rhssk at posteo dot eu>
# Contributor: Vojtech Kral <vojtech_kral^hk>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: Kuba Serafinowski <zizzfizzix(at)gmail(dot)com>

_remotename=kdeconnect-kde
_pkgname=kdeconnect
pkgname=$_pkgname-git
pkgver=20.03.80.r71.2728f8d8
pkgrel=1
pkgdesc='Adds communication between KDE and your smartphone'
arch=(x86_64)
url='https://community.kde.org/KDEConnect'
license=(GPL2)
depends=(hicolor-icon-theme kcmutils kwayland libfakekey qca kpeople
         kpeople-vcard-git kirigami2 pulseaudio-qt)
makedepends=(git extra-cmake-modules kdoctools qt5-declarative)
optdepends=('sshfs: remote filesystem browser'
            'kde-cli-tools: configuration UI'
            'python-nautilus: Nautilus integration')
provides=($_pkgname)
conflicts=($_pkgname)
source=("git+https://invent.kde.org/kde/$_remotename.git")
sha256sums=('SKIP')

pkgver() {
  cd $_remotename
  printf "%s" "$(git describe --long | sed 's/^v//;s/\([^-]*-\)g/r\1/;s/-/./g')"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$_remotename \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
