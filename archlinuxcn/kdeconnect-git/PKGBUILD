# Maintainer: Rihards Skuja <rhssk at posteo dot eu>
# Contributor: Vojtech Kral <vojtech_kral^hk>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: Kuba Serafinowski <zizzfizzix(at)gmail(dot)com>

_remotename=kdeconnect-kde
_pkgname=kdeconnect
pkgname=$_pkgname-git
pkgver=21.07.80.r360.62660bab
pkgrel=1
pkgdesc='Adds communication between KDE and your smartphone'
arch=(x86_64)
url='https://community.kde.org/KDEConnect'
license=(GPL2)
depends=("hicolor-icon-theme" "kcmutils" "kpeople" "kwayland" "libfakekey" "modemmanager-qt" "qca"
         kpeoplevcard kirigami2 pulseaudio-qt)
makedepends=("extra-cmake-modules" "git" "kdoctools" "qqc2-desktop-style" "qt5-declarative" "wayland-protocols"
             plasma-wayland-protocols)
optdepends=('sshfs: remote filesystem browser'
            'kde-cli-tools: configuration UI'
            'python-nautilus: Nautilus integration')
conflicts=($_pkgname)
provides=($_pkgname)
source=("git+https://invent.kde.org/network/$_remotename.git")
sha256sums=('SKIP')

pkgver() {
	cd $_remotename
	printf "%s" "$(git describe --long | sed 's/^v//;s/\([^-]*-\)g/r\1/;s/-/./g')"
}

build() {
	cmake -B build -S $_remotename \
		-DCMAKE_INSTALL_LIBEXECDIR=lib \
		-DBUILD_TESTING=OFF \
		-Wno-dev
	cmake --build build
}

package() {
	DESTDIR="$pkgdir" cmake --install build
}
