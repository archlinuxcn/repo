# Maintainer: Alexis Rouillard <contact@arouillard.fr>

pkgname=waybar-git
_pkgname=Waybar
pkgver=0.9.24.r155.g3cd3118
pkgrel=1
pkgdesc='Highly customizable Wayland bar for Sway and Wlroots based compositors (GIT)'
arch=('x86_64')
url="https://github.com/Alexays/${_pkgname}"
license=('MIT')
provides=('waybar')
conflicts=('waybar')
depends=(
	'gtkmm3'
	'libjsoncpp.so'
	'libsigc++'
	'fmt'
	'jack' 'libjack.so'
	'wayland'
	'libdate-tz.so'
	'spdlog-git'
	'gtk-layer-shell'
	'libupower-glib.so'
	'upower'
	'libevdev'
	'libinput'
	'libpulse'
	'libnl'
	'libappindicator-gtk3'
	'libdbusmenu-gtk3'
	'libmpdclient'
	'libsndio.so'
	'libxkbcommon'
	'libwireplumber'
	'playerctl'
)
makedepends=(
	'git'
	'cmake'
	'catch2'
	'meson'
	'scdoc' # For generating manpages
	'wayland-protocols'
)
backup=(
	etc/xdg/waybar/config
	etc/xdg/waybar/style.css
)
optdepends=(
	'otf-font-awesome: Icons in the default configuration'
)

replaces=("waybar-hyprland-git")
source=("git+${url}.git")
sha256sums=('SKIP')

pkgver() {
	cd "${_pkgname}/"
	git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
	cd "${_pkgname}/"

	rm -rf "${srcdir}/build"
	meson --prefix=/usr \
		--buildtype=plain \
		--auto-features=enabled \
		-Dexperimental=true \
		-Dcava=disabled \
		-Dtests=disabled \
		"${srcdir}/build"
	ninja -C "${srcdir}/build"
}

package() {
	DESTDIR="${pkgdir}" ninja -C "${srcdir}/build" install
	install -Dm644 "${_pkgname}/LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
