# Maintainer: Nocifer <apmichalopoulos at gmail dot com>
# Contributor: GordonGR <ntheo1979@gmail.com>
# Contributor: josephgbr <rafael.f.f1@gmail.com>

_pkgname=libdvdcss
pkgname=lib32-${_pkgname}
pkgver=1.4.3
pkgrel=1
pkgdesc="Portable abstraction library for DVD decryption (32 bit)"
url="https://www.videolan.org/developers/libdvdcss.html"
arch=('x86_64')
license=('GPL')
depends=('lib32-glibc' "${_pkgname}")
makedepends=('gcc-multilib')
options=('!docs' '!emptydirs')
source=("http://download.videolan.org/pub/${_pkgname}/${pkgver}/${_pkgname}-${pkgver}.tar.bz2")
sha256sums=('233cc92f5dc01c5d3a96f5b3582be7d5cee5a35a52d3a08158745d3d86070079')

build() {
    cd "${_pkgname}-${pkgver}"

    export CC='gcc -m32'
    export CXX='g++ -m32'
    export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

    ./configure --prefix=/usr --libdir=/usr/lib32
    make
}

package() {
    cd "${_pkgname}-${pkgver}"

    make DESTDIR="${pkgdir}" install
    rm -rf "${pkgdir}/usr/include"
}
provides=("libdvdcss.so")
