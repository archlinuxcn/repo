# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: Ghost of Kendo <ghostofkendo at gmail dot com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Pierre Chapuis <catwell@archlinux.us>
# Contributor: Earnest <zibeon@gmail.com>

pkgname=spin
pkgver=6.5.2
pkgrel=4
pkgdesc='Tool for the formal verification of distributed software systems'
arch=('i686' 'x86_64')
url='https://spinroot.com/'
license=('custom:BSD3')
depends=('glibc')
optdepends=('tcl: ispin graphical interface'
            'swarm: improved performance on large verification problems'
            'ispin: GUI for Spin')
source=("https://github.com/nimble-code/Spin/archive/version-${pkgver}.tar.gz")
b2sums=('a075043c55609de5860c3a428d356a9618a4062667e06d92797e1fb87cb4c805ac90ac580c7d9dfac1d5be1dda4c7f9c9f72927826b921c23710a858adb3eb4d')

build() {
  cd Spin-version-${pkgver}/Src
  make
}

package() {
  # install binary and license file
  cd Spin-version-${pkgver}/Src
  install -Dm755 spin "${pkgdir}"/usr/bin/spin
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE.txt

  # install manpage and docs
  cd "${srcdir}"/Spin-version-${pkgver}
  install -Dm644 -t "${pkgdir}"/usr/share/man/man1/ Man/spin.1

  install -dm755 "${pkgdir}"/usr/share/doc/${pkgname}/examples
  cp -a Examples/* "${pkgdir}"/usr/share/doc/${pkgname}/examples/
}
