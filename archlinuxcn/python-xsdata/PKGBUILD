# Maintainer: Xeonacid <h.dwwwwww@gmail.com>

_name=xsdata
pkgname=python-${_name}
pkgver=25.7
pkgrel=2
pkgdesc="Naive XML & JSON Bindings for python"
arch=(any)
url="https://github.com/tefra/${_name}"
license=(MIT)
depends=(python python-typing_extensions python-click python-click-default-group python-docformatter python-jinja python-toposort ruff python-requests python-lxml)
makedepends=(git python-build python-installer python-setuptools python-wheel)
checkdepends=(python-pytest python-pytest-benchmark)
source=(git+${url}.git#tag=v${pkgver})
sha512sums=('6685cb40e8aa63372fad9c1a64521fc6e9a854b021ac57f9e598ae2a884b1f0106b46584b54d62fb5f9651e335586e504fc00d32a7f23ae039400f5f200aaa2c')

prepare() {
  cd $_name
  git cherry-pick -n 28f63e8ffe912341e5622dfb7c0973d920c1fd05
}

build() {
  cd $_name
  python -m build --wheel --no-isolation
}

check(){
  cd $_name
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest -vv tests
}

package() {
  cd $_name
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  cp -r docs "$pkgdir/usr/share/doc/$pkgname"
}
