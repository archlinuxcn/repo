# Maintainer: Xeonacid <h.dwwwwww@gmail.com>

_name=xsdata
pkgname=python-${_name}
pkgver=26.2
pkgrel=1
pkgdesc="Naive XML & JSON Bindings for python"
arch=(any)
url="https://github.com/tefra/${_name}"
license=(MIT)
depends=(python python-typing_extensions python-click python-click-default-group python-docformatter python-jinja python-toposort ruff python-requests python-lxml)
makedepends=(git python-build python-installer python-setuptools python-wheel)
checkdepends=(python-pytest python-pytest-benchmark)
source=(git+${url}.git#tag=v${pkgver})
sha512sums=('3bb5c55b865697c38db431173cd083fc9e5fa2537a37cad67d274312d438424bd58301c18a08728f7079bfa7cc391ba6085b56ae49f08d3079e8f44633171adc')

build() {
  cd $_name
  python -m build --wheel --no-isolation
}

check(){
  cd $_name
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest -vv tests
}

package() {
  cd $_name
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  cp -r docs "$pkgdir/usr/share/doc/$pkgname"
}
