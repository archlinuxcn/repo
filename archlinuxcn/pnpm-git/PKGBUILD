# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Severen Redwood <me@severen.dev>
# Contributor: Tomasz Jakub Rup <tomasz.rup@gmail.com>

pkgname=pnpm-git
_pkgname=${pkgname%-git}
pkgver=8.13.1.r4.g693944b
pkgrel=1
pkgdesc='Fast, disk space efficient package manager'
arch=('any')
url=https://pnpm.io
license=('MIT')
depends=('node-gyp')
makedepends=('git' 'pnpm')
conflicts=("${_pkgname}")
provides=("${_pkgname}")
source=("git+https://github.com/${_pkgname}/${_pkgname}.git")
sha256sums=('SKIP')

pkgver() {
	cd "${_pkgname}/"
	git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/v//'
}

build() {
	cd "${_pkgname}/"
	pnpm install --frozen-lockfile
	pnpm run compile
}

package() {
	local _npmdir=/usr/lib/node_modules/${_pkgname}
	install -d ${pkgdir}/{usr/bin,${_npmdir}/dist}
	ln -s "${_npmdir}/bin/${_pkgname}.cjs" "${pkgdir}/usr/bin/${_pkgname}"
	ln -s "${_npmdir}/bin/pnpx.cjs" "${pkgdir}/usr/bin/pnpx"

	cd "${_pkgname}/${_pkgname}/"
	cp -r bin package.json "${pkgdir}/${_npmdir}/"
	install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${_pkgname}/"
	cd dist
	cp -r "${_pkgname}.cjs" pnpmrc scripts worker.js "${pkgdir}/${_npmdir}/dist"
}
