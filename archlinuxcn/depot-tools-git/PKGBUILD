# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: relrel <relrelbachar@gmail.com>
# Contributor: Luis Aranguren <pizzaman@hotmail.com>
# Contributor: Adrian Perez <aperez@igalia.com>
# Contributor: Chih-Hsuan Yen <yan12125@gmail.com>
# Contributor: rway <rway07@gmail.com>
# Contributor: wabi <aschrafl@jetnet.ch>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Andreas Schrafl <aschrafl@gmail.com>
# Contributor: piojo <aur@zwell.net>
# Contributor: hack.augusto <hack.augusto@gmail.com>
# Contributor: Rustmilian <Rustmilian@proton.me>

pkgname=depot-tools-git
pkgver=r11893.778af1e3b8
pkgrel=1
pkgdesc='Tools for working with Chromium development'
arch=(any)
url='https://chromium.googlesource.com/chromium/tools/depot_tools'
license=(BSD-3-Clause)
depends=(bash git python)
source=("git+$url.git")
sha512sums=('SKIP')
provides=(depot-tools)
conflicts=(depot-tools)

pkgver() {
	cd "$srcdir/depot_tools/"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
	install -d "$pkgdir/opt/"
	cp -r "$srcdir/depot_tools/" "$pkgdir/opt/depot_tools/"
	chmod 775 "$pkgdir/opt/depot_tools/"
	# Remove Git
	rm -rf "${pkgdir}/opt/depot_tools/"{.git,.gitignore,.gitattributes}
	# Remove Windows
	rm -r "${pkgdir}/opt/depot_tools/"{*.bat,win32imports.py,win_toolchain}
	# Remove Misc
	rm -r "${pkgdir}/opt/depot_tools/"{infra,testing_support,tests,OWNERS,WATCHLISTS}

	# Install license
	install -d "$pkgdir/usr/share/licenses/$pkgname/"
	mv "$pkgdir/opt/depot_tools/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/"

	# Install man pages
	install -d "$pkgdir/usr/share/man/"
	mv "$pkgdir/opt/depot_tools/man/"man[0-8]/ "$pkgdir/usr/share/man/"

	# Install docs
	install -d "$pkgdir/usr/share/doc/$pkgname/"
	mv "$pkgdir/opt/depot_tools/man/html/" "$pkgdir/usr/share/doc/$pkgname/"
	mv "$pkgdir/opt/depot_tools/"README* "$pkgdir/usr/share/doc/$pkgname/"

	# Remove man/ leftovers
	rm -r "$pkgdir/opt/depot_tools/man/"

	# Install bash completions
	install -Dm0644 "$pkgdir/opt/depot_tools/gclient_completion.sh" "$pkgdir/usr/share/bash-completion/completions/gclient"
	install -Dm0644 "$pkgdir/opt/depot_tools/git_cl_completion.sh" "$pkgdir/usr/share/bash-completion/completions/git-cl"

	# Install zsh completions
	install -Dm0644 -t "$pkgdir/usr/share/zsh/site-functions/" "$pkgdir/opt/depot_tools/zsh-goodies/_gclient"
}
