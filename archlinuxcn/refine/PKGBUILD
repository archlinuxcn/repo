# Maintainer: "Amhairghin" Oscar Garcia Amor (https://ogarcia.me)

pkgname=refine
pkgver=0.6.3
pkgrel=1
pkgdesc='Tweak various aspects of GNOME'
arch=('any')
url='https://tesk.page/refine/'
license=('GPL-3.0-or-later')
depends=('libadwaita' 'python' 'python-gobject')
makedepends=('blueprint-compiler' 'meson')
source=("https://gitlab.gnome.org/TheEvilSkeleton/Refine/-/archive/${pkgver}/Refine-${pkgver}.tar.gz")
b2sums=('f06bdc6cd5de708a369b2afd85dbca12800516e97c0814abc381fd927c4e86044421eb0170314b7307d6fb8eb995eda3f2785772345b0fbfda74443b5fb88c48')

build() {
  arch-meson "Refine-${pkgver}" build -Dexec_name_as_base_id=true
  meson compile -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"
}
