# Maintainer: "Amhairghin" Oscar Garcia Amor (https://ogarcia.me)

pkgname=refine
pkgver=0.7.0
pkgrel=1
pkgdesc='Tweak various aspects of GNOME'
arch=('any')
url='https://tesk.page/refine/'
license=('GPL-3.0-or-later')
depends=('libadwaita' 'python' 'python-gobject')
makedepends=('blueprint-compiler' 'meson')
source=("https://gitlab.gnome.org/TheEvilSkeleton/Refine/-/archive/${pkgver}/Refine-${pkgver}.tar.gz")
b2sums=('77087961eaebc777de9ea4b525f682de19ee173d6954d6f6af28913bb86f4df65b85103595b191ef2bc568bbbe56979a2fc39b6523dafdd900275028ce1bec07')

build() {
  arch-meson "Refine-${pkgver}" build -Dexec_name_as_base_id=true
  meson compile -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"
}
