# Maintainer: "Amhairghin" Oscar Garcia Amor (https://ogarcia.me)

pkgname=refine
pkgver=0.6.2
pkgrel=1
pkgdesc='Tweak various aspects of GNOME'
arch=('any')
url='https://tesk.page/refine/'
license=('GPL-3.0-or-later')
depends=('libadwaita' 'python' 'python-gobject')
makedepends=('blueprint-compiler' 'meson')
source=("https://gitlab.gnome.org/TheEvilSkeleton/Refine/-/archive/${pkgver}/Refine-${pkgver}.tar.gz")
b2sums=('b7929b3aef7ce34a5f9ddc0bdd291d35a4b8d59153aa52ce7b1ee4b153f45335ea073652c80db722e263987e0b5af692dad2207ce3bd54b5a8591e7eadfbb96f')

build() {
  arch-meson "Refine-${pkgver}" build -Dexec_name_as_base_id=true
  meson compile -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"
}
