_name=django-formtools
pkgname=python-django-formtools
pkgver=2.2
pkgrel=5
pkgdesc="A set of high-level abstractions for Django forms"
arch=(any)
url="https://django-formtools.readthedocs.io/en/latest/"
license=('BSD')
depends=('python')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha256sums=('c5272c03c1cd51b2375abf7397a199a3148a9fbbf2f100e186467a84025d13b2')

build() {
  cd "$srcdir/$_name-$pkgver"
  python3 setup.py build
}

package() {
  cd "$srcdir/$_name-$pkgver"
  python3 setup.py install --root=$pkgdir --optimize=1 --skip-build

  # make sure we don't install annoying files
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  rm -rf "$pkgdir/$_site_packages/tests/"
}

