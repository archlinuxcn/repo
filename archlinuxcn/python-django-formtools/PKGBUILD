_name=django-formtools
pkgname=python-django-formtools
pkgver=2.5.1
pkgrel=2
pkgdesc="A set of high-level abstractions for Django forms"
arch=(any)
url="https://django-formtools.readthedocs.io/en/latest/"
license=('BSD')
depends=('python-django')
makedepends=('python-build'
             'python-installer'
             'python-wheel'
             'python-setuptools'
             'python-setuptools-scm')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha256sums=('47cb34552c6efca088863d693284d04fc36eaaf350eb21e1a1d935e0df523c93')

build() {
  cd ${_name}-${pkgver}

  SETUPTOOLS_SCM_PRETEND_VERSION=${pkgver} \
                                python -m build --wheel --no-isolation
}

package() {
  cd ${_name}-${pkgver}

  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm 644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
