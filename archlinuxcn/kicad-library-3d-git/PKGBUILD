# Mainainer: edward-p <edward at edward-p dot xyz>

_kicad_ver=9.0
pkgname=kicad-library-3d-git
pkgver=9.0.20250813.101058.g8178218a2
pkgrel=1
pkgdesc="Kicad 3D render model libraries"
arch=('any')
url="https://kicad.github.io/"
license=('CC-BY-SA-4.0')
makedepends=('cmake' 'git')
provides=('kicad-library-3d')
conflicts=('kicad-library-3d')
options=('!strip')
source=("git+https://gitlab.com/kicad/libraries/kicad-packages3D.git")
sha256sums=('SKIP')

pkgver() {
  cd kicad-packages3D
  printf "${_kicad_ver}.%s.g%s" "$(TZ=UTC git log -1 --pretty='%cd' --date=format-local:%Y%m%d.%H%M%S)" "$(git log -1 --format='%h')"
}
        
build() {
  cd "$srcdir/kicad-packages3D/"
  cmake ./ -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$srcdir/kicad-packages3D/"
  make DESTDIR="$pkgdir" install
}

