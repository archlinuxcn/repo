# Mainainer: edward-p <edward at edward-p dot xyz>

pkgname=kicad-library-3d-git
pkgver=8.0.0.r15.g1cb78a78e
pkgrel=1
pkgdesc="Kicad 3D render model libraries"
arch=('any')
url="https://kicad.github.io/"
license=('CC-BY-SA-4.0')
makedepends=('cmake' 'git')
provides=('kicad-library-3d')
conflicts=('kicad-library-3d')
options=('!strip')
source=("git+https://gitlab.com/kicad/libraries/kicad-packages3D.git")
sha256sums=('SKIP')

pkgver() {
  cd kicad-packages3D
  git describe --long | sed 's/-rc/rc/;s/\([^-]*-g\)/r\1/;s/-/./g'
}
        
build() {
  cd "$srcdir/kicad-packages3D/"
  cmake ./ -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$srcdir/kicad-packages3D/"
  make DESTDIR="$pkgdir" install
}

