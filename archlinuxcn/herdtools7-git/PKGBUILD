# Maintainer: Nathaniel R. Lewis <linux.robotdude@gmail.com>

_pkgbase='herdtools7'
pkgname=('herdtools7-git')
provides=('herdtools7')
conflicts=('herdtools7')
pkgver=7.56+03.r598.g2de090efd
pkgrel=1
pkgdesc="The Herd toolsuite to deal with .cat memory models"
arch=(armv7h aarch64 i686 x86_64)
url='https://github.com/herd/herdtools7'
license=('CeCILL-B')
depends=(ocaml)
makedepends=(git ocamlbuild ocaml-menhir dune ocaml-zarith)
source=('git+https://github.com/herd/herdtools7')
sha256sums=('SKIP')

build() {
  cd $_pkgbase
  make PREFIX="/usr" all
}

pkgver() {
  cd $_pkgbase

  local _basever=$(cat VERSION.txt)
  local _basecommit=$(git log -n 1 --pretty=format:%H -- VERSION.txt)
  local _count=$(git rev-list --count ${_basecommit}..HEAD)
  local _commit=$(git rev-parse --short HEAD)

  printf "${_basever}.r${_count}.g${_commit}"
}

package() {
  make -C $_pkgbase PREFIX="$pkgdir/usr" install
}
