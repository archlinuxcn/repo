# Maintainer: Pylogmon <pylogmon@outlook.com>
# Maintainer: Integral <integral@member.fsf.org>

pkgname=pot-translation-git
_pkgname=pot-desktop
pkgver=3.0.6.r2.gb213fd3
pkgrel=1
pkgdesc="一个跨平台的划词翻译软件 | A cross-platform software for text translation."
arch=('x86_64' 'aarch64' 'riscv64')
url="https://github.com/pot-app/${_pkgname}"
license=('GPL-3.0-or-later')
depends=('webkit2gtk' 'gtk3' 'libayatana-appindicator' 'xdotool' 'libxcb' 'libxrandr' 'tesseract' 'tessdata')
makedepends=('git' 'nodejs' 'pnpm' 'cargo' 'dbus')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("git+${url}.git")
sha512sums=('SKIP')
options=('!lto')

pkgver() {
	cd "${_pkgname}/"
	git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
	cd "${_pkgname}/"

	# Modify versions in configuration files
	_pkgver=$(sed 's/.\(r[0-9]\)/\-\1/' <<<${pkgver})
	sed -i "s/\"version\":.*/\"version\": \"${_pkgver}\",/g" package.json
	sed -i "s/\"version\":.*/\"version\": \"${_pkgver}\"/g" src-tauri/tauri.conf.json
	sed -i "s/\"version = \"0.0.0\"/version = \"${_pkgver}\"/g" src-tauri/Cargo.toml

	pnpm i
}

build() {
	cd "${_pkgname}/"
	pnpm tauri build -b deb
}

package() {
	cd "${_pkgname}/"
	_pkgver=$(sed 's/.\(r[0-9]\)/\-\1/' <<<${pkgver})
	tar -xpf "src-tauri/target/release/bundle/deb/pot_${_pkgver}_amd64/data.tar.gz" -C "${pkgdir}"
	chown -R root:root "${pkgdir}"
}
