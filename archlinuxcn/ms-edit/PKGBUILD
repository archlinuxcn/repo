# Maintainer: SelfRef <arch@selfref.dev>

_pkgbase=ms-edit
pkgname="$_pkgbase"
pkgver=1.0.0
pkgrel=1
pkgdesc="A simple editor for simple needs (Microsoft Edit)"
arch=('x86_64' 'aarch64')
url="https://github.com/microsoft/edit"
license=('MIT')
makedepends=('git' 'cargo-nightly')
provides=("$_pkgbase")
conflicts=("$_pkgbase")
source=("$_pkgbase::git+https://github.com/microsoft/edit.git#tag=v$pkgver")
sha256sums=('8e2b415c6c680e1298c935ee0bc536357fd0b83ab0d58270e9c8810f1d88fb55')

prepare() {
	cd "$_pkgbase"
	export RUSTUP_TOOLCHAIN=nightly
	cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
	cd "$_pkgbase"
	export RUSTUP_TOOLCHAIN=nightly
	export CARGO_TARGET_DIR=target
	cargo build --frozen --release
}

check() {
	cd "$_pkgbase"
	export RUSTUP_TOOLCHAIN=nightly
	cargo test --frozen
}

package() {
	cd "$_pkgbase"
	install -Dm755 "target/release/edit" "$pkgdir/usr/bin/ms-edit"
	install -Dm644 "LICENSE" "$pkgdir/usr/share/licenses/$_pkgbase/LICENSE"
}
depends=("gcc-libs" "glibc")
