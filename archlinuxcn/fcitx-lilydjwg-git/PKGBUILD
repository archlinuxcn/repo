# Maintainer: Yichao Yu <yyc1992 AT gmail.com>
# Contributor: Weng XueTian <wengxt AT gmail com>
# Contributor: lh <jarryson AT gmail com>

_pkgname=fcitx
pkgname=$_pkgname-lilydjwg-git
pkgver=4.2.9.1.20180920
_realver=4.2.9.1
pkgrel=1
epoch=2
pkgdesc="A Flexible Input Method Framework. With Lua support"
arch=("i686" "x86_64")
license=('GPL')
url="http://fcitx-im.org/wiki/Fcitx"
depends=('pango' 'libxinerama' 'shared-mime-info' 'hicolor-icon-theme'
  'desktop-file-utils' 'icu' 'libxkbfile' 'iso-codes' 'libxml2'
  "gtk2>=2.24.20" 'gtk3' 'dbus')
makedepends=('git' 'gettext' 'cmake' 'gobject-introspection' 'coreutils'
  'sh' 'qt4' 'lua' 'python2' 'extra-cmake-modules') # python2 is for gobject-introspection
conflicts=('fcitx' 'fcitx-qt' 'fcitx-qt4' 'fcitx-gtk2' 'fcitx-gtk3'
  'fcitx-keyboard')
provides=("fcitx=${_realver}" "fcitx-qt=${_realver}" "fcitx-qt4=${_realver}"
  "fcitx-gtk2=${_realver}" "fcitx-gtk3=${_realver}" 'fcitx-keyboard=0.1.3'
  "fcitx-git=${pkgver}"
  )
optdepends=(
  'enchant: for word predication using enchant'
  'presage: for word predication using presage'
  'opencc: for simplified and traditional chinese translation'
  'qt4: for qt4 input method module'
  'lua: for lua plugin support')
options=('debug')
install="$_pkgname.install"
_gitname="fcitx"
source=("${_gitname}::git://localhost/fcitx-repo")
md5sums=(SKIP)

pkgver() {
  cd "$srcdir/$_gitname"
  echo $_realver.$(git log -1 --format='%ad' --date=short|tr -d '-')
}

build() {

  msg "Creating make environment..."
  mkdir -p "$srcdir/$_gitname-build"

  msg "Starting make..."
  cd "$srcdir/$_gitname-build"
  # export CC=clang
  # export CXX=clang++
  # rm CMakeFiles/2.*/CMakeC{,XX}Compiler.cmake &> /dev/null || true
  [[ -f CMakeCache.txt ]] && rm -v CMakeCache.txt
  cmake ../$_gitname -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DQT_QMAKE_EXECUTABLE=qmake-qt4 \
    -DENABLE_QT_IM_MODULE=On \
    -DENABLE_GTK2_IM_MODULE=On \
    -DENABLE_GTK3_IM_MODULE=On \
    -DENABLE_LUA=On \
    -DFORCE_OPENCC=On \
    -DFORCE_ENCHANT=On \
    -DFORCE_PRESAGE=On \
    -DSYSCONFDIR=/etc
  make
}

package() {
  cd "$srcdir/$_gitname-build"

  make DESTDIR="$pkgdir" install
}
