# Maintainer: Alexandre Bouvier <contact@amb.tf>
pkgname=cemu
pkgver=2.6
pkgrel=2
pkgdesc="Nintendo Wii U emulator"
arch=('x86_64')
url="https://cemu.info/"
license=('MPL2')
depends=(
	'discord-rpc'
	'glslang>=14'
	'hicolor-icon-theme'
	'libx11'
	'pugixml'
	'sdl2'
	'wxwidgets-gtk3>=3.2'
    'llvm-libs'
)
makedepends=(
    'bluez-libs'
	'boost'
	'cmake>=3.21.1'
	'cubeb'
	'curl'
	'fmt'
	'git'
	'glib2'
	'glm'
	'glu'
	'gtk3'
	'hidapi'
	'libgl'
	'libpng'
	'libusb'
	'libzip'
	'nasm'
	'openssl'
	'rapidjson'
	'vulkan-headers>=1:1.3.225'
	'wayland'
	'wayland-protocols'
	'zarchive>=0.1.2'
	'zlib'
	'zstd'
    'clang'
)
source=(
	"$pkgname::git+https://github.com/cemu-project/Cemu#tag=v${pkgver}"
	'imgui::git+https://github.com/ocornut/imgui.git'
)
b2sums=('6b0e53984a1df880d3e81d5b10f84425b4325c8cb4d68a79b812bfec5605be6554d6f0f7f812aa667350f2cfff55648229f41a9ffcb2c3ca50f711067399a477'
        'SKIP')

prepare() {
	cd $pkgname

	git config submodule.dependencies/imgui.url ../imgui
	git -c protocol.file.allow=always submodule update
	sed -i '/CMAKE_INTERPROCEDURAL_OPTIMIZATION/d' CMakeLists.txt
	sed -i '/discord-rpc/d' CMakeLists.txt
	sed -i '/FMT_HEADER_ONLY/d' src/Common/precompiled.h
    # https://github.com/cemu-project/Cemu/pull/1436
	sed -i '/set/s/"glslang"/"glslang::glslang"/' src/Cafe/CMakeLists.txt
	# https://github.com/cemu-project/Cemu/pull/1474
	sed -i 's/fmt::format("{}", v)/fmt::format("{}", fmt::underlying(v))/' src/Cafe/GameProfile/GameProfile.cpp
}

build() {
	cmake -S $pkgname -B build \
        -DALLOW_PORTABLE=OFF \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_C_FLAGS_RELEASE="-DNDEBUG" \
		-DCMAKE_CXX_FLAGS_RELEASE="-DNDEBUG" \
		-DENABLE_VCPKG=OFF \
		-DCMAKE_C_COMPILER=clang \
		-DCMAKE_CXX_COMPILER=clang++ \
		-DCMAKE_CXX_FLAGS="$CXXFLAGS -w" \
		-Wno-dev
	cmake --build build
}

package() {
		depends+=(
        'libbluetooth.so'
		'libboost_program_options.so'
		'libcrypto.so'
		'libcubeb.so'
		'libcurl.so'
		'libfmt.so'
		'libgdk-3.so'
		'libgobject-2.0.so'
		'libgtk-3.so'
		'libhidapi-hidraw.so'
		'libssl.so'
		'libusb-1.0.so'
		'libwayland-client.so'
		'libz.so'
		'libzarchive.so'
		'libzip.so'
		'libzstd.so'
	)
    cd $pkgname
	# shellcheck disable=SC2154
	install -d "$pkgdir"/usr/{bin,share/Cemu}
	mv bin/Cemu_release "$pkgdir"/usr/bin/Cemu
	cp -dr --no-preserve=ownership -t "$pkgdir"/usr/share/Cemu bin/*
	install -Dm644 -t "$pkgdir"/usr/share/applications dist/linux/info.cemu.Cemu.desktop
	install -Dm644 -t "$pkgdir"/usr/share/metainfo dist/linux/info.cemu.Cemu.metainfo.xml
	install -Dm644 src/resource/logo_icon.png "$pkgdir"/usr/share/icons/hicolor/128x128/apps/info.cemu.Cemu.png
}
