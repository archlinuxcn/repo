# Maintainer: Seppia <$(base64 -d <<< 'YXJjaGxpbnV4QHNlcHBpYS5uZXQK')>

_reponame="lk-jwt-service"
pkgname="element-${_reponame}"
pkgver=0.4.0
pkgrel=1
pkgdesc='LiveKit Management Service for Element Call'
arch=(x86_64)
url='https://github.com/vector-im/lk-jwt-service/'
license=("AGPL-3")
makedepends=(go git)
source=(
	"git+https://github.com/element-hq/lk-jwt-service.git#tag=v${pkgver}"
	"${_reponame}.service"
)
sha512sums=('52152d51d481659af8c49add4727e607da1255844a1c2a6f57c76a1d614e2c310269790a4fb4aed297ae114311d8281bc9295440480ac7c03c8342c4ec702705'
            '7f78c64b5a6de2505ea0c57b95071493bf9d57cc80e091f7bd70e8288690f4a2e0cb8a3dda267e62645d40e93769845e2a1e04844490fd04a320e763b2a69db4')

prepare() {
  cd ${_reponame}
}

build() {
  cd ${_reponame}
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
  go build -o lk-jwt-service
}

package() {
  install -vDm755 ${_reponame}/lk-jwt-service -t "$pkgdir/usr/bin"
  install -vDm644 ${_reponame}.service -t "$pkgdir/usr/lib/systemd/system/"
}
