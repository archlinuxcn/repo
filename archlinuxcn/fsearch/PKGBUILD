# Maintainer: Christian Boxd√∂rfer <christian.boxdoerfer@posteo.de>

pkgname=fsearch
pkgver=0.2.3
pkgrel=2
pkgdesc="A fast graphical file search utility."
arch=('i686' 'x86_64')
url="https://cboxdoerfer.github.io/${_pkgname}"
license=('GPL-2.0-or-later')
depends=('gtk3' 'icu' 'pcre2')
makedepends=('git' 'meson')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/cboxdoerfer/${pkgname}/archive/${pkgver}.tar.gz")
sha256sums=('b3c576bf1230da7c374d00bb32d72686b940b4dee80d941495acfdd5437bf117')

build() {
	arch-meson "${pkgname}-${pkgver}/" build --buildtype release
	meson compile -C build
}

check() {
	meson test -C build
}

package() {
	DESTDIR="${pkgdir}" meson install -C build
	install -Dm644 "${pkgname}-${pkgver}/License" -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
