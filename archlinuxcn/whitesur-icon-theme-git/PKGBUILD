# Maintainer: Roald Clark <roaldclark at gmail dot com>
# Contributor: Nico <d3sox at protonmail dot com>

_name=WhiteSur-icon-theme
_pkgbase="${_name,,}"
pkgbase="${_pkgbase}-git"
pkgname=(
    "${_pkgbase}-git"
    "${_pkgbase}-alt-git"
    "${_pkgbase}-bold-git"
    "${_pkgbase}-alt-bold-git"
)
pkgver=20250712.233627
pkgrel=1
pkgdesc="MacOS Big Sur style icon theme for linux desktops (git version)"
arch=('any')
url="https://github.com/vinceliuice/${_name}"
license=('GPL-3.0-or-later')
depends=('gtk-update-icon-cache')
makedepends=('git')
optdepends=(
    'whitesur-cursor-theme-git: Recommended cursor theme'
    'whitesur-gtk-theme-git: Recommended GTK theme'
    'whitesur-kde-theme-git: Recommended KDE theme'
)
provides=("${_pkgbase}=${pkgver}")
conflicts=("${_pkgbase}")
options=('!debug' '!strip')
source=("${_pkgbase}::git+${url}.git")
sha256sums=('SKIP')

pkgver() {
    cd "${srcdir}/${_pkgbase}"
    printf "%s" "$(TZ=UTC git log -1 --pretty='%cd' --date=format-local:%Y%m%d.%H%M%S)"
}

prepare() {
    cd "${srcdir}/${_pkgbase}"
    sed -i '/gtk-update-icon-cache/s/^/#/' install.sh
}

package_whitesur-icon-theme-git() {
    cd "${srcdir}/${_pkgbase}"
    install -dm0755 "${pkgdir}/usr/share/icons"
    ./install.sh --dest "${pkgdir}/usr/share/icons" --theme all
}

package_whitesur-icon-theme-alt-git() {
    cd "${srcdir}/${_pkgbase}"
    install -dm0755 "${pkgdir}/usr/share/icons"
    ./install.sh --dest "${pkgdir}/usr/share/icons" --theme all --alternative
}

package_whitesur-icon-theme-bold-git() {
    cd "${srcdir}/${_pkgbase}"
    install -dm0755 "${pkgdir}/usr/share/icons"
    ./install.sh --dest "${pkgdir}/usr/share/icons" --theme all --bold
}

package_whitesur-icon-theme-alt-bold-git() {
    cd "${srcdir}/${_pkgbase}"
    install -dm0755 "${pkgdir}/usr/share/icons"
    ./install.sh --dest "${pkgdir}/usr/share/icons" --theme all --alternative --bold
}
