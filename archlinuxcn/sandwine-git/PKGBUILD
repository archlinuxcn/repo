# Maintainer: Roald Clark <roaldclark@gmail.com>
# Contributor: dreieck (https://aur.archlinux.org/account/dreieck)

_pkgname=sandwine
pkgname="${_pkgname}-git"
pkgver=4.3.0.20250711.170040
pkgrel=1
pkgdesc="A command-line tool to run Windows applications on GNU/Linux that offers more isolation than raw Wine and more convenience than raw bubblewrap (uses bubblewrap)"
arch=('any')
url="https://github.com/hartwork/${_pkgname}"
license=('GPL-3.0-or-later')
depends=(
    'bubblewrap>=0.8.0'
    'python-coloredlogs>=15.0.1'
    'python>=3.9'
)
makedepends=(
    'git'
    'python-build'
    'python-installer'
    'python-setuptools>=61.0.0'
    'python-wheel'
)
provides=(
    "${_pkgname}=${pkgver}"
    "python-${_pkgname}-git=${pkgver}"
    "python-${_pkgname}=${pkgver}"
)
conflicts=(
    "${_pkgname}"
    "python-${_pkgname}"
)
source=("${_pkgname}::git+${url}.git")
sha256sums=('SKIP')

pkgver() {
    cd "${srcdir}/${_pkgname}"
    version="$(git describe --tags --abbrev=0)"
    [[ "${version}" =~ ^[0-9]+\.[0-9]+\.[0-9]+$ ]] || exit 1
    printf "%s.%s" "${version}" "$(TZ=UTC git log -1 --pretty='%cd' --date=format-local:%Y%m%d.%H%M%S)"
}

build() {
    cd "${srcdir}/${_pkgname}"
    python -m build --wheel --no-isolation
}

package() {
    depends+=('wine')

    cd "${srcdir}/${_pkgname}"
    python -m installer --destdir="${pkgdir}" dist/*.whl
}
