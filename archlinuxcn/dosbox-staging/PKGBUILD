# Maintainer: Coelacanthus <CoelacanthusHex@gmail.com>
# Contributor: silverhikari <kerrickethan@gmail.com>
# Contributor: Wouter Wijsman <wwijsman@live.nl>

pkgname=dosbox-staging
pkgver=0.80.0
pkgrel=1
pkgdesc="DOS/x86 emulator focusing on ease of use. Based on DOSBox"
arch=('x86_64')
url="https://github.com/dosbox-staging/dosbox-staging"
license=('GPL2')
depends=('sdl2' 'sdl2_net' 'sdl2_image' 'opusfile'  'alsa-lib' 'fluidsynth' 'libpng' 'munt' 'libslirp' 'glib2' 'libxi')
optdepends=(
            'ncurses'
            )
makedepends=('meson' 'ninja' 'gcc' 'gzip' 'gtest')
provides=("dosbox")
conflicts=("dosbox")
options=(debug)
source=(
  "${pkgname}-${pkgver}.gz::https://github.com/dosbox-staging/${pkgname}/archive/v${pkgver}.tar.gz"
)
b2sums=('59cb6515f17b9a661f5e9d7787b01ecef4fe047dfd0ca4573cc12c73af1f9fe433f4c20a1c42e1fcd259c31af3922738c057aa64fc777a513a7881a86436a1fa')

prepare() {
  cd "$srcdir/${pkgname}-${pkgver}"
}

build() {
  cd "$srcdir/${pkgname}-${pkgver}"
  arch-meson . build \
    --wrap-mode default \
    --buildtype=debugoptimized \
    -Ddefault_library=shared \
    -Dtry_static_libs=iir \
    -Dsystem_libraries=fluidsynth,glib,mt32emu,opusfile,png,sdl2,sdl2_net,sdl2_image,slirp,speexdsp,zlib
  meson compile -C build
}

package() {
  cd "$srcdir/${pkgname}-${pkgver}"
  
  meson install -C build --no-rebuild --skip-subprojects --destdir="$pkgdir"

  # dosbox-staging documents
  install -Dm 644 "docs/README.video" "$pkgdir/usr/share/doc/${pkgname}/video.txt"
  install -Dm 644 "README" "$pkgdir/usr/share/doc/${pkgname}/manual.txt"

  # bash completions
  install -Dm 644 "contrib/linux/bash-completion/dosbox" "$pkgdir/usr/share/bash-completion/completions/dosbox"
}
# vim: set sts=2 sw=2:
