# Maintainer: Hu Butui <hot123tea123@gmail.com>

_pkgname=svSocket
_pkgver=1.0.2
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//[:-]/.}
pkgrel=1
pkgdesc="'SciViews' - Socket Server"
arch=('any')
url="https://cran.r-project.org/package=${_pkgname}"
license=('GPL')
depends=(
  r
  r-svmisc
)
optdepends=(
  r-covr
  r-knitr
  r-rmarkdown
  r-spelling
  r-svhttp
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
sha256sums=('129ffc1b05614187a7af8e1a31a7570b5617e82c4313dbaacc953c468c0af0bf')

build() {
  R CMD INSTALL ${_pkgname}_${_pkgver}.tar.gz -l "${srcdir}"
}

package() {
  install -dm0755 "${pkgdir}/usr/lib/R/library"
  cp -a --no-preserve=ownership "${_pkgname}" "${pkgdir}/usr/lib/R/library"
}
# vim:set ts=2 sw=2 et:
