# Maintainer: Jerome Leclanche <jerome@leclan.ch>

_pkgname=lxqt-policykit
pkgname=$_pkgname-git
pkgver=0.14.1.33.ga092874
pkgrel=1
pkgdesc="The LXQt policykit authentication agent"
arch=("i686" "x86_64")
url="https://lxqt.org"
license=("GPL2")
depends=("liblxqt-git" "polkit-qt5")
makedepends=("git" "cmake" "qt5-tools" "lxqt-build-tools-git")
provides=("$_pkgname=$pkgver")
conflicts=("$_pkgname")
source=("git+https://github.com/lxqt/$_pkgname.git")
sha256sums=('SKIP')


pkgver() {
  cd "$srcdir/$_pkgname"
  git describe --always | sed "s/-/./g"
}

build() {
  mkdir -p build
  cd build
  cmake "$srcdir/$_pkgname" \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
