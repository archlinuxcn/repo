# Maintainer: Andrew Shark <ashark at linuxcomp at ru>
pkgname=evsieve-git
_pkgname=${pkgname%-git}
pkgver=v1.4.0.r107.g24207fd
pkgrel=1
pkgdesc="A utility for mapping events from Linux event devices."
arch=("x86_64")
url="https://github.com/KarsMulder/${_pkgname}"
license=("GPL2" "MIT")
depends=("libevdev" "gcc-libs")
makedepends=("git" "rust")
provides=(${_pkgname})
conflicts=(${_pkgname})
source=("git+${url}")
sha256sums=('SKIP')

pkgver() {
  cd "${_pkgname}"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    cd "${_pkgname}"
    cargo fetch --locked --target x86_64-unknown-linux-gnu
}

build() {
    cd "${_pkgname}"
    cargo build --release --frozen --target-dir=target
}

package() {
    cd "${_pkgname}"

    install -Dm 755 "target/release/${_pkgname}" -t "${pkgdir}/usr/bin"
    _pkg_license_dir="${pkgdir}/usr/share/licenses/${_pkgname}"
    install -Dm 644 "COPYING" -t "$_pkg_license_dir"
    install -Dm 644 "LICENSE" -t "$_pkg_license_dir"
    cp -R licenses "$_pkg_license_dir"
    find "$_pkg_license_dir" -type f -exec chmod 644 -- {} +
    find "$_pkg_license_dir" -type d -exec chmod 755 -- {} +
}
