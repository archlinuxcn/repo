# Maintainer: eolianoe <eolianoe [at] gmail [DoT] com>
# Contributor: Aaron Lindsay <aaron@aclindsay.com>
# Contributor: Edvinas Valatka <edacval@gmail.com>
# Contributor: Adrian HÃ¼hn <adrian.huehn@web.de>

pkgname=libsearpc
epoch=2
pkgver=3.2.0
pkgrel=5.2
pkgdesc="A simple C language RPC framework (including both server side & client side)"
arch=('i686' 'x86_64' 'armv7h' 'armv6h' 'aarch64')
url="https://github.com/haiwen/libsearpc"
license=('Apache')
depends=("glib2>=2.26.0" "jansson>=2.2.1")
makedepends=(python)
# upstream seems to change tag so let's rename the download...
source=("libsearpc-${pkgver}-5.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha256sums=('cd00197fcc40b45b1d5e892b2d08dfa5947f737e0d80f3ef26419334e75b0bff')

prepare () {
  cd ${pkgname}-${pkgver}
  sed -i 's|(DESTDIR)@prefix@|@prefix@|' './libsearpc.pc.in'
}

build () {
  echo "Building python3 version"
  cd ${pkgname}-${pkgver}
  ./autogen.sh
  ./configure --prefix=/usr PYTHON=/usr/bin/python
  make
}

check () {
  cd ${pkgname}-${pkgver}
  make check
}

package () {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
