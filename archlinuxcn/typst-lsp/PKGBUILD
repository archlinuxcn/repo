# Maintainer: Chocobo1 <chocobo1 AT archlinux DOT net>

pkgname=typst-lsp
pkgver=0.12.1
pkgrel=1
pkgdesc="Language server for Typst"
arch=('i686' 'x86_64')
url="https://github.com/nvarner/typst-lsp"
license=('Apache' 'MIT')
depends=('openssl')
makedepends=('git' 'rust')
source=("git+${url}.git#tag=v${pkgver}")
sha256sums=('SKIP')
options=('!lto')

prepare() {
	cd "${pkgname}/"

	if [ ! -f "Cargo.lock" ]; then
		cargo update
	fi

	cargo fetch
}

package() {
	cd "${pkgname}/"

	cargo install \
		--locked \
		--no-track \
		--root "${pkgdir}/usr" \
		--path .

	install -Dm644 "LICENSE-MIT.txt" -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
