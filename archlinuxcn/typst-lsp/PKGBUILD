# Maintainer: Chocobo1 <chocobo1 AT archlinux DOT net>

pkgname=typst-lsp
pkgver=0.9.3
pkgrel=1
pkgdesc="Language server for Typst"
arch=('i686' 'x86_64')
url="https://github.com/nvarner/typst-lsp"
license=('Apache' 'MIT')
depends=('openssl')
makedepends=('rust')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/nvarner/typst-lsp/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('0f922751789bdc0ac99809cff3a2fbfbb33b2d1384cdbd51a037aaeee042345d')
options=('!lto')

prepare() {
  cd "typst-lsp-${pkgver}"

  if [ ! -f "Cargo.lock" ]; then
    cargo update
  fi

  cargo fetch
}

check() {
  cd "typst-lsp-${pkgver}"

  #cargo test \
  #  --frozen
}

package() {
  cd "typst-lsp-${pkgver}"

  cargo install \
    --locked \
    --no-track \
    --root "$pkgdir/usr" \
    --path .

  install -Dm644 "LICENSE-MIT.txt" -t "$pkgdir/usr/share/licenses/typst-lsp"
}
