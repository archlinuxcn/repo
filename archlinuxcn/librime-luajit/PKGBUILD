# Maintainer: Gavin Luo <lunt.luo#gmail.com>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: GONG Chen <chen dot sst at gmail dot com>
# Contributor: 網軍總司令

pkgname=librime-luajit
pkgver=1.14.0
pkgrel=1
epoch=1
pkgdesc="Rime input method engine"
arch=('x86_64')
url="https://github.com/rime/librime"
license=('BSD-3-Clause')
depends=('boost-libs' 'capnproto' 'gcc-libs' 'glibc' 'opencc' 'yaml-cpp' 'leveldb' 'librime-data' 'luajit' 'google-glog' 'marisa')
makedepends=('git' 'cmake' 'boost' 'gtest' 'ninja')
source=("git+https://github.com/rime/librime.git#tag=$pkgver"
        "git+https://github.com/lotem/librime-octagram.git"
        "git+https://github.com/hchunhui/librime-lua.git"
        "git+https://github.com/rime/librime-charcode.git"
        "git+https://github.com/lotem/librime-proto.git"
        "git+https://github.com/rime/librime-predict.git")
sha512sums=('b738d8aa552795b927e612d9f37b4785f2f0abeab5deb88313f9f56dea20677d1a7faf9dce807300b948bfb86cca2f590c1497f6142d33b406202eb1103d8a08'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP')
provides=("librime=${pkgver}")
conflicts=(librime)

prepare() {
  cd librime/plugins
  ln -sf "$srcdir"/librime-octagram .
  ln -sf "$srcdir"/librime-lua .
  ln -sf "$srcdir"/librime-charcode .
  ln -sf "$srcdir"/librime-proto .
  ln -sf "$srcdir"/librime-predict .
}

build() {
  cd librime
  export CXXFLAGS="$CXXFLAGS -DNDEBUG -DGLOG_USE_GLOG_EXPORT -DGLOG_USE_GFLAGS"
  cmake . -GNinja -Bbuild -DCMAKE_INSTALL_PREFIX=/usr -DBUILD_MERGED_PLUGINS=Off -DENABLE_EXTERNAL_PLUGINS=On -Wno-dev -DLUA_VERSION=luajit
  cmake --build build
}

check() {
  cd librime/build
  ninja test
}

package() {
  cd librime/build
  DESTDIR="$pkgdir" ninja install
  install -vDm 644 ../LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
