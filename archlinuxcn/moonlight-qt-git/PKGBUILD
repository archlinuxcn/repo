# Maintainer: XenHat <me at xenh.at>
# Contributors: SoftwareRat <jaguar5018@gmail.com>, Michael Herzberg

pkgname=moonlight-qt-git
pkgver=6.1.0.r28.g15e337f
pkgrel=1
pkgdesc='GameStream client for PCs (Windows, Mac, and Linux) (master branch)'
arch=('aarch64' 'x86_64')
license=('GPL-3.0-or-later')
url='https://github.com/moonlight-stream/moonlight-qt'
depends=('qt6-base' 'qt6-declarative' 'qt6-svg' 'ffmpeg' 'sdl2_ttf')
makedepends=('git')
optdepends=(
	'libva-intel-driver: hardware-acelerated decoding for Intel'
	'libva-mesa-driver: hardware-acelerated decoding for AMD/others'
	'libva-nvidia-driver: hardware-acelerated decoding for NVIDIA with NVDEC via VA-API'
)
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=($pkgname::"git+https://github.com/moonlight-stream/${pkgname%-git}.git")

md5sums=('SKIP')

pkgver() {
	cd "$pkgname"
	git describe --long --tags --abbrev=7 --exclude='*[a-zA-Z][a-zA-Z]*' |
		sed -E 's/^[^0-9]*//;s/([^-]*-g)/r\1/;s/-/./g'
}

prepare() {
	cd "$pkgname"
	git submodule update --init --recursive
	if [[ -f *.patch ]]; then
		for p in ../*.patch; do
			patch -Np1 -i "$p"
		done
	fi
	qmake6 PREFIX="$pkgdir/usr" moonlight-qt.pro
}

build() {
	cd "$pkgname"
	make
}

package() {
	cd "$pkgname"
	make install
}
