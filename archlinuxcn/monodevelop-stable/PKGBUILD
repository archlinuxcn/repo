# $Id$
# Maintainer: Doctorzeus <simmlog@gmail.com>
# Co-Maintainer: Felix Golatofski <contact@xdfr.de>
# Contributor: Malah <malah@neuf.fr>
# Contributor: Brenton Horne <brentonhorne77@gmail.com>
# Contributor: Daniel Isenmann <daniel@archlinux.org>
# Contributor: Timm Preetz <timm@preetz.us>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=monodevelop-stable
_pkgname=monodevelop
pkgver=8.4.3.12
pkgrel=2
pkgdesc="An IDE primarily designed for C# and other .NET languages"
arch=('any')
url="https://www.monodevelop.com"
license=('MIT')
depends=('mono5' 'mono-addins' 'gnome-sharp' 'hicolor-icon-theme' 'mono-tools' 'fsharp' 'cmake' 'libssh2' 'http-parser' 'curl' 'dotnet-host' 'mono-msbuild')
makedepends=('rsync' 'git' 'nuget' 'referenceassemblies-pcl')
replaces=('monodevelop-debugger-gdb')
provides=('monodevelop' 'monodevelop-debugger-gdb')
conflicts=('monodevelop')
options=(!makeflags)
optdepends=('xsp: To run ASP.NET pages directly from monodevelop')

source=(git+https://github.com/mono/monodevelop.git#tag=monodevelop-$pkgver)
sha256sums=('SKIP')

prepare() {
  cd ${srcdir}/$_pkgname
  sed -i -e "s/MonoDevelop.FSharp.Shared.ToolTip /MonoDevelop.FSharp.Shared.ToolTips.ToolTip /" main/external/fsharpbinding/MonoDevelop.FSharpBinding FSharpTextEditorCompletion.fs
  sed -i -e "s/MonoDevelop.FSharp.Shared.EmptyTip /MonoDevelop.FSharp.Shared.ToolTips.EmptyTip /" main/external/fsharpbinding/MonoDevelop.FSharpBinding   FSharpTextEditorCompletion.fs
}

build() {
  export MONO_SHARED_DIR=$srcdir/src/.wabi
  mkdir -p $MONO_SHARED_DIR

  cd ${srcdir}/$_pkgname
  git submodule update --init --recursive || return 1
  git checkout tags/$_pkgname-$pkgver
  git clean -dfx

  ./configure --prefix=/usr --profile=gnome
  XDG_CONFIG_HOME="$srcdir"/config LD_PRELOAD="" make
}

package() {
  cd ${srcdir}/$_pkgname
  
  XDG_CONFIG_HOME="$srcdir"/config LD_PRELOAD="" make DESTDIR="$pkgdir" install
  # delete conflicting files
  rm -r $(find $pkgdir/usr/share/mime/ -type f | grep -v "packages")
  rm -r $MONO_SHARED_DIR

  # NuGet.exe is missing somehow, fixed FS#43423
  install -Dm755 "${srcdir}"/monodevelop/main/external/nuget-binary/nuget.exe "${pkgdir}"/usr/lib/monodevelop/AddIns/MonoDevelop.PackageManagement/NuGet.exe
}
