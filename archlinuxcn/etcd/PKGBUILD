# Maintainer: Y7n05h <Y7n05h(aT)protonmail--d0t--com>
# Contributor: Andrew Crerar <crerar@archlinux.org>
# Contributor: guillaume alaux <guillaume at alaux dot net>
# Contributor: korjjj <korjjj+aur[at]gmail[dot]com>
# Contributor: xeross <contact at xeross dot me>
# Contributor: codekoala <codekoala at gmail dot com>

pkgbase=etcd
pkgname=(etcd etcdctl etcdutl)
pkgver=3.5.7
pkgrel=1
arch=('x86_64' 'armv6h' 'armv7h')
url='https://github.com/etcd-io/etcd'
license=('Apache')
makedepends=('go' 'git')
options=(!lto)
source=(
	"${pkgname}"-"${pkgver}".tar.gz::https://github.com/etcd-io/"${pkgname}"/archive/v"${pkgver}".tar.gz
	10-EnvironmentFile.conf
	etcd.env)
sha512sums=('d865f23bd7dc664a73e3fbb78f812d73aba2437aa1430a1ebbfe665d8e2bdee0e4784c4e36f12c58e3407273e0e6c0ac12fa65b1f268beda86e54a359fd0d372'
            'fa85d772929ea7e0a18bddd4b9c41d043a7f75e560eacfab67b979985e510dea694c332b5130570e47101c1ec5c25925872c6a581568390e7b141d9c6c26aae2'
            'a4843be558e401fa6c612c88059fbe83025eb86077bec70331bc43b7dd48cc09fd186f0ea9d4b45c802a617d5f771752bb2ed8113ce02a6b6eaaabd926e227e9')

build() {
	export CGO_CPPFLAGS="${CPPFLAGS}"
	export CGO_CFLAGS="${CFLAGS}"
	export CGO_CXXFLAGS="${CXXFLAGS}"
	export CGO_LDFLAGS="${LDFLAGS}"
	export GO_BUILD_FLAGS="-trimpath -buildmode=pie -mod=readonly -modcacherw"
	export GO_LDFLAGS="-linkmode=external -extldflags=${LDFLAGS}"

	cd "${srcdir}"/"${pkgname}"-"${pkgver}"

	./build.sh
}

package_etcd() {
	pkgdesc='A highly-available key value store for shared configuration and service discovery'
	install="${pkgname}".install
	backup=('etc/conf.d/etcd')
	depends=('glibc')
	optdepends=('etcd-client')
	# binaries
	install -Dm755 "${srcdir}"/"${pkgbase}"-"${pkgver}"/bin/"${pkgname}" "${pkgdir}"/usr/bin/"${pkgname}"

	# Upstream systemd unit
	install -Dm644 "${srcdir}"/"${pkgname}"-"${pkgver}"/contrib/systemd/etcd.service "${pkgdir}"/usr/lib/systemd/system/"${pkgname}".service

	# Adding 'EnvironmentFile=-/etc/conf.d/etcd' option to the unit
	install -Dm644 "${srcdir}"/10-EnvironmentFile.conf "${pkgdir}"/usr/lib/systemd/system/"${pkgname}".service.d/10-EnvironmentFile.conf

	# env file itself
	install -Dm644 "${srcdir}"/etcd.env "${pkgdir}"/etc/conf.d/"${pkgname}"

	# License
	install -Dm644 "${srcdir}"/"${pkgname}"-"${pkgver}"/LICENSE "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE

	# Docs
	install -dm755 "${pkgdir}"/usr/share/doc/"${pkgname}"
	cp -r "${srcdir}"/"${pkgname}"-"${pkgver}"/Documentation/* "${pkgdir}"/usr/share/doc/"${pkgname}"/
	install -m644 "${srcdir}"/"${pkgname}"-"${pkgver}"/"${pkgname}".conf.yml.sample "${pkgdir}"/usr/share/doc/"${pkgname}"
}
package_etcdctl() {
	pkgdesc='a command line client for etcd'
	depends=('glibc')
	optdepends=('etcd')
	cd "${srcdir}"/"${pkgbase}"-"${pkgver}"

	install -Dm755 bin/"${pkgname}" "${pkgdir}"/usr/bin/"${pkgname}"
	install -Dm644 "${pkgname}"/LICENSE "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE

	install -Dm644 "${pkgname}"/README.md "${pkgdir}"/usr/share/doc/"${pkgname}"/README-"${pkgname}".md
	install -Dm644 "${pkgname}"/READMEv2.md "${pkgdir}"/usr/share/doc/"${pkgname}"/READMEv2-"${pkgname}".md
}
package_etcdutl() {
	pkgdesc='a command line administration utility for etcd'
	depends=('glibc')
	optdepends=('etcd')

	cd "${srcdir}"/"${pkgbase}"-"${pkgver}"

	install -Dm755 bin/"${pkgname}" "${pkgdir}"/usr/bin/"${pkgname}"
	install -Dm644 "${pkgname}"/LICENSE "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE

	install -Dm644 "${pkgname}"/README.md "${pkgdir}"/usr/share/doc/"${pkgname}"/README-"${pkgname}".md
}
