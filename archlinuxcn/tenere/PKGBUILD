# Maintainer: Integral <integral@member.fsf.org>

pkgname=tenere
pkgver=0.11.2
pkgrel=2
pkgdesc="TUI interface for LLMs written in Rust"
arch=('x86_64' 'aarch64' 'riscv64')
url="https://github.com/pythops/${pkgname}"
license=('GPL-3.0-or-later')
depends=('gcc-libs')
makedepends=('cargo')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('865c9b041faf935545dbb9753b33a8ff09bf4bfd8917d25ca93f5dc0c0cac114')
options=('!lto')

prepare() {
	cd "${pkgname}-${pkgver}/"
	cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
	cd "${pkgname}-${pkgver}/"
	cargo build --frozen --release --all-features
}

check() {
	cd "${pkgname}-${pkgver}/"
	cargo test --frozen --all-features
}

package() {
	cd "${pkgname}-${pkgver}/"
	install -Dm755 "target/release/${pkgname}" -t "${pkgdir}/usr/bin/"
	install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
