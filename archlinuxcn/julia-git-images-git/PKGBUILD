pkgname=julia-git-images-git
pkgver=0.24.1.0.10.g9796acc
pkgrel=1
pkgdesc="Images.jl"
url="https://github.com/JuliaImages/Images.jl"
arch=('any')
license=('MIT')
makedepends=(git julia-pkg-scripts)
depends=(julia-git)
provides=(julia-git-images)
source=(git://github.com/JuliaImages/Images.jl
        0001-Remove-restrict-signatures-that-are-moved-to-ImageAx.patch)
md5sums=('SKIP'
         '1d4ff8d18d374ef796dd126dd410d70a')

pkgver() {
  cd Images.jl

  git describe --tags | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

prepare() {
  cd Images.jl

  patch -Np1 < ../0001-Remove-restrict-signatures-that-are-moved-to-ImageAx.patch
}

package() {
  cd Images.jl

  . /usr/lib/julia/julia-install-pkg.sh Images "${pkgdir}" "${pkgname}" julia-git
}
