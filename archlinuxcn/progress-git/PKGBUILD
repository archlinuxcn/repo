# Maintainer: oldherl <oldherl@gmail.com>
# Contributer: Chris Severance aur.severach AatT spamgourmet.com
# Contributor: Andy Weidenbaum <archbaum@gmail.com>
# Contributor: Aurélien Chabot <contact@aurelienchabot.fr>
# Contributor: Étienne Deparis <etienne@depar.is>

_pkgname='progress'
pkgname=progress-git
pkgver=0.15.r0.gb56ae2b
pkgrel=1
_srcdir="${_pkgname}"
pkgdesc='Shows running coreutils basic commands and displays stats'
arch=('x86_64')
url="https://github.com/Xfennec/progress"
license=('GPL3')
depends=('ncurses')
makedepends=('gcc' 'make')
makedepends+=('git')
provides=("progress" 'cv')
conflicts=("progress" 'cv')
replaces=('cv')
source=("${_srcdir}::${url//https:/git:}")
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/${_pkgname}"
  git describe --long --tags | sed -e 's:^v::g' -e 's/\([^-]*-g\)/r\1/' -e 's/-/./g'
}

build() {
  cd "${_srcdir}"

  make -s -j # -j "$(nproc)"
}

package() {
  cd "${_srcdir}"

  install -Dpm644 'LICENSE' -t "${pkgdir}/usr/share/licenses/${_pkgname}/"

  local _doc
  for _doc in 'capture.png' 'README.md'; do
    install -Dpm644 "${_doc}" -t "${pkgdir}/usr/share/doc/${_pkgname}/"
  done

  make DESTDIR="${pkgdir}" PREFIX='/usr' install
}
