# Maintainer: Maikel Wever <maikelwever@gmail.com>
# Maintainer: Matthew Gamble <git@matthewgamble.net>

pkgname=python-coloredlogs
pkgver=15.0.1
pkgrel=5
pkgdesc="Colored terminal output for Python's logging module"
arch=('any')
url="https://github.com/xolox/python-coloredlogs"
license=('MIT')
depends=('python' 'python-humanfriendly')
makedepends=('python-setuptools' 'python-sphinx')
source=("https://github.com/xolox/python-coloredlogs/archive/${pkgver}.tar.gz")
sha256sums=('e8161222671bb129f7936cd220c275a3cbc0a6c22313bd4483114b9526e5695f')

build() {
  cd "python-coloredlogs-${pkgver}"

  python setup.py build

  cd docs
  sphinx-build -nb html -d build/doctrees . build/html
}

package() {
  cd "python-coloredlogs-${pkgver}"

  PYTHONHASHSEED=0 python setup.py install --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE.txt "${pkgdir}/usr/share/licenses/python-coloredlogs/LICENSE.txt"
  install -Dm644 README.rst "${pkgdir}/usr/share/doc/python-coloredlogs/README.rst"
  install -Dm644 CHANGELOG.rst "${pkgdir}/usr/share/doc/python-coloredlogs/CHANGELOG.rst"
  cp -r docs/build/html "${pkgdir}/usr/share/doc/python-coloredlogs/html"
}

# vim:set ts=2 sw=2 et:
