# Maintainer: Y7n05h <Y7n05h(aT)protonmail--d0t--com>
# Contributor: Husam Bilal <me@husam.dev>

pkgname=clipman
pkgver=1.6.2
pkgrel=1
pkgdesc="A simple clipboard manager for Wayland"
url="https://github.com/yory8/clipman"
depends=("wl-clipboard>=2.0")
makedepends=("go")
optdepends=("libnotify" "wofi" "dmenu" "rofi")
provides=()
license=("GPL3")
arch=("i686" "x86_64" "arm" "armv6h" "armv7h" "aarch64")
md5sums=('aff3a17199d06ca77e815023b66a825d')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")

build() {
  cd $pkgname-$pkgver
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
  go build .
}

package() {
  cd $pkgname-$pkgver
  install -Dm755 $pkgname $pkgdir/usr/bin/$pkgname
  install -Dm644 docs/$pkgname.1 $pkgdir/usr/share/man/man1/$pkgname.1
  gzip $pkgdir/usr/share/man/man1/$pkgname.1
}
