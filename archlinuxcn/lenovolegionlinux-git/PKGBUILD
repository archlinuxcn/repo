# Maintainer: MrDuartePT <gonegrier.duarte@gmail.com>
# Maintainer: xenhat <aur@xenh.at>
# Maintainer: johnfanv2 <https://github.com/johnfanv2>


_pkgname=lenovolegionlinux
pkgname=${_pkgname}-git
pkgver=r713.c460f14
pkgrel=1
pkgdesc="LenovoLegionLinux (LLL) brings additional drivers and tools for Lenovo Legion series laptops to Linux. PLEASE READ THE REPO BEFORE INSTALL THIS PACKAGE!!!"
arch=("x86_64")
url="https://github.com/johnfanv2/LenovoLegionLinux"
license=('GPL')

depends=(
  python-argcomplete
  python-yaml
  python-pyqt6
  polkit
  python-darkdetect
  acpid
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
optdepends=(
  "lenovolegionlinux-dkms-git: DKMS module"
)

conflicts=(
  legion-fan-utils-linux-git
)

source=("${_pkgname}::git+https://github.com/johnfanv2/LenovoLegionLinux")
sha256sums=('SKIP')
install="lenovolegionlinux.install"

pkgver() {
  cd "${_pkgname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

prepare() {
  cd "${_pkgname}"
  pkgver_commit=$(echo $pkgver | cut -c 6-)
  git checkout $pkgver_commit
  sed -i "s/version = _VERSION/version = 1.0.0/g" "${srcdir}/${_pkgname}/python/legion_linux/setup.cfg"
}

build() {
 cd "${srcdir}/${_pkgname}/python/legion_linux"
 python -m build --wheel --no-isolation
 # Make legiond daemon
 cd legion_linux/extra/service/legiond
 make
}
package() {
  mkdir -p ${pkgdir}/etc/systemd/system
  mkdir -p ${pkgdir}/etc/acpi/{events,actions}

  cd "${srcdir}/${_pkgname}/python/legion_linux"
  python -m installer --destdir="$pkgdir" dist/*.whl

# Systemd service
  cd "${srcdir}/${_pkgname}/extra"
  install -Dm664 service/*.service "${pkgdir}/etc/systemd/system"
  install -Dm664 service/*.timer "${pkgdir}/etc/systemd/system" 
  
# legiond daemon
  install -Dm775 service/legiond/legiond "${pkgdir}/usr/bin"
  install -Dm775 service/legiond/legiond-cli "${pkgdir}/usr/bin"
	
# ACPI events
  install -Dm775 acpi/events/legion_ppd "${pkgdir}/etc/acpi/events/"
  install -Dm664 acpi/events/legion_ac "${pkgdir}/etc/acpi/events/"
}
