# Maintainer: Roald Clark <roaldclark at gmail.com>
# Contributor: Slavi Pantaleev <slavi at devture.com>

_name=sftpman-rs
pkgname=sftpman
pkgver=2.1.0
pkgrel=1
pkgdesc="A CLI application for managing and mounting sshfs (SFTP) filesystems"
arch=('x86_64' 'aarch64')
url="https://github.com/spantaleev/${_name}"
license=('AGPL-3.0-or-later')
depends=(
    'fuse3'
    'gcc-libs'
    'glibc'
    'sshfs'
)
makedepends=(
    'cargo'
    'git'
)
optdepends=('sftpman-iced: GUI frontend for sftpman v2+')
conflicts=('sftpman-python')
replaces=('sftpman-python')
install="${pkgname}.install"
source=("git+${url}.git#tag=v${pkgver}")
b2sums=('a4082b2188677ea5f590a3ae8fb6dda70169be5dab59534db986c666884b5963bfb93bd23fbfb6a4a84200b7167d0f1472b8f98ca7031af9f862014eb536d0e6')

prepare() {
    cd "$srcdir/${_name}"
    export RUSTUP_TOOLCHAIN=stable
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
    cd "$srcdir/${_name}"
    export RUSTUP_TOOLCHAIN=stable
    export CARGO_TARGET_DIR=target
    cargo build --release --frozen --all-features
}

check() {
    cd "$srcdir/${_name}"
    export RUSTUP_TOOLCHAIN=stable
    cargo test --frozen --all-features
}

package() {
    cd "$srcdir/${_name}"
    install -Dm0755 "target/release/${pkgname}" -t "${pkgdir}/usr/bin/"
    install -Dm0644 etc/bash_completion.d/sftpman -t "${pkgdir}/usr/share/bash-completion/completions/"
    install -Dm0644 etc/fish-completions/sftpman.fish -t "${pkgdir}/usr/share/fish/completions/"
}
