# Maintainer: Roald Clark <roaldclark at gmail dot com>
# Contributor: Caleb Maclennan <caleb@alerque.com>
# Contributor: Carlos Aznar√°n <caznaranl@uni.pe>
# Contributor: Luis Martinez <luis dot martinez at disroot dot org>

_pyname=deepl
pkgname="python-${_pyname}"
pkgver=1.19.1
pkgrel=3
pkgdesc="Python library for the DeepL API"
arch=('any')
url="https://github.com/${_pyname}com/${_pyname}-python"
license=('MIT')
depends=(
    'python'
    'python-requests'
)
makedepends=(
    'git'
    'python-build'
    'python-installer'
    'python-poetry-core'
    'python-setuptools'
    'python-wheel'
)
optdepends=(
    'python-keyring: for support storing API Key'
)
source=("git+${url}.git#tag=v${pkgver}")
sha256sums=('1bc91a2a93079b394f1fa269bbd7d9771d6df864c57fe4d92483971352daa164')

build() {
    cd "${srcdir}/${_pyname}-python"
    python -m build --wheel --no-isolation
}

package() {
    cd "${srcdir}/${_pyname}-python"
    install -Dm0644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
    python -m installer --destdir="${pkgdir}" dist/*.whl
}
