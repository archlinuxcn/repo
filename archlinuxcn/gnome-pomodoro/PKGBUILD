# Maintainer: Gavin Luo <lunt.luo@gmail.com>
# Contributor: Liam Timms <timms5000@gmail.com>
# Contributor: gj545rndmu <6zogm6z2nh at bljvf anonbox net>
# Contributor: Pranav Jerry <libreinator@disroot.org>
# Contributor:  Marcin Wieczorek <marcin@marcin.co>
# Contributor: Andrejs Mivre≈Üiks <gim at fastmail dot fm>
# Contributor: Janne Haapsaari <haaja@iki.fi>
# Contributor: maus25 <mirko378@gmail.com>

pkgname=gnome-pomodoro
pkgver=0.27.0
pkgrel=1
pkgdesc='A time management utility for GNOME based on the pomodoro technique!'
arch=('i686' 'x86_64')
url='https://github.com/gnome-pomodoro/gnome-pomodoro'
license=('GPL-3.0')
provides=(
  'libactions.so'
  'libdark-theme.so'
  'libgnome-pomodoro.so'
  'libgnome.so'
  'libsounds.so'
)
replaces=('gnome-shell-pomodoro')
conflicts=('gnome-shell-pomodoro')
depends=(
  'gnome-shell>=40.0'
  'hicolor-icon-theme'
  'glibc'
  'libglib-2.0.so'
  'libgobject-2.0.so'
  'libgio-2.0.so'
  'gtk3'
  'libcairo.so'
  'gobject-introspection'
  'libpeas-1.0.so'
  'gom'
  'gstreamer'
  'libcanberra.so'
  'libsqlite3.so'
)
makedepends=('meson' 'vala')
source=("$pkgname-$pkgver.tar.gz::https://github.com/gnome-pomodoro/gnome-pomodoro/archive/$pkgver.tar.gz")
sha256sums=('266c773dd591ae373440805fd6885d3052155e63c4c3671322e621ad1a42f7f8')

prepare() {
  cd "$srcdir/gnome-pomodoro-$pkgver"
}

build() {
  cd "$srcdir/gnome-pomodoro-$pkgver"
  arch-meson build
  meson compile -C build
}

package() {
  cd "$srcdir/gnome-pomodoro-$pkgver"
  meson install -C build --destdir "$pkgdir"
}

