# Maintainer: Gavin Luo <lunt.luo@gmail.com>
# Contributor: Liam Timms <timms5000@gmail.com>
# Contributor: gj545rndmu <6zogm6z2nh at bljvf anonbox net>
# Contributor: Pranav Jerry <libreinator@disroot.org>
# Contributor:  Marcin Wieczorek <marcin@marcin.co>
# Contributor: Andrejs Mivre≈Üiks <gim at fastmail dot fm>
# Contributor: Janne Haapsaari <haaja@iki.fi>
# Contributor: maus25 <mirko378@gmail.com>

pkgname=gnome-pomodoro
pkgver=0.29.0
pkgrel=1
pkgdesc='A time management utility for GNOME based on the pomodoro technique!'
arch=('i686' 'x86_64')
url='https://github.com/focustimerhq/FocusTimer'
license=('GPL-3.0-only')
provides=(
  'libgnome-pomodoro.so'
)
replaces=('gnome-shell-pomodoro')
conflicts=('gnome-shell-pomodoro')
depends=(
  'hicolor-icon-theme'
  'glibc'
  'libglib-2.0.so'
  'libgobject-2.0.so'
  'libgio-2.0.so'
  'gtk3'
  'libcairo.so'
  'libpeas-2.so'
  'gom'
  'gstreamer'
  'libcanberra.so'
  'libsqlite3.so'
  'dconf'
)
makedepends=('meson' 'vala' 'gobject-introspection')
source=("$pkgname-$pkgver.tar.gz::${url}/archive/$pkgver.tar.gz")
sha256sums=('82f31c1f704e0a5d993b990a4a1160bc9281dc80b359dd9727c89d7157ad3527')

prepare() {
  cd "$srcdir/FocusTimer-$pkgver"
}

build() {
  cd "$srcdir/FocusTimer-$pkgver"
  arch-meson build
  meson compile -C build
}

package() {
  cd "$srcdir/FocusTimer-$pkgver"
  meson install -C build --destdir "$pkgdir"
}

