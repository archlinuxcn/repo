# Maintainer: Rodrigo Bezerra <rodrigobezerra21 at gmail dot com>
# Contributor: Fredy Garc√≠a <frealgagu at gmail dot com>
# Contributor: schuay <jakob.gruber@gmail.com>
# Contributor: GordonGR <ntheo1979@gmail.com>
# Contributor: Kazuo Teramoto <kaz.rag at gmail dot com>
# Contributor: damir <damir@archlinux.org>

_basename=libcdio
pkgname=lib32-libcdio
pkgver=2.1.0
pkgrel=1
pkgdesc="GNU Compact Disc Input and Control Library (32-bit)"
url="https://www.gnu.org/software/libcdio/"
arch=(x86_64)
license=(GPL3)
depends=(lib32-gcc-libs lib32-ncurses libcdio)
source=(https://ftp.gnu.org/gnu/libcdio/${_basename}-${pkgver}.tar.bz2{,.sig})
sha256sums=("8550e9589dbd594bfac93b81ecf129b1dc9d0d51e90f9696f1b2f9b2af32712b"
            "SKIP")
validpgpkeys=("DAA63BC2582034A02B923D521A8DE5008275EC21") # R. Bernstein

prepare() {
    cd "${_basename}-${pkgver}"

    autoreconf -fi
}

build() {
    export CC="gcc -m32"
    export CXX="g++ -m32"
    export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

    cd "${_basename}-${pkgver}"

    ./configure \
        --prefix=/usr \
        --libdir=/usr/lib32 \
        --enable-cpp-progs \
        --disable-vcd-info \
        --without-cd-drive \
        --without-cd-info \
        --without-cdda-player \
        --without-cd-read \
        --without-iso-info \
        --without-iso-read \
        --disable-cddb

    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make
}

package() {
    cd "${_basename}-${pkgver}"

    make -j1 DESTDIR="${pkgdir}" install

    rm -rf "${pkgdir}/usr/bin" "${pkgdir}/usr/include" "${pkgdir}/usr/share"
}
provides=("libcdio++.so" "libcdio.so" "libiso9660++.so" "libiso9660.so" "libudf.so")
