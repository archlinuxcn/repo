# Maintainer: Butui Hu <hot123tea123@gmail.com>

_pkgname=word_cloud
pkgname=python-wordcloud
pkgver=1.9.4
pkgrel=1
pkgdesc='A little word cloud generator in Python'
arch=('x86_64')
url='https://github.com/amueller/word_cloud'
license=('MIT')
depends=(
  python-matplotlib
  python-numpy
  python-pillow
)
makedepends=(
  cython
  python-build
  python-installer
  python-setuptools
  python-setuptools-scm
  python-wheel
)
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/amueller/word_cloud/archive/refs/tags/${pkgver}.tar.gz")
sha512sums=('159811c11a37fbd343e773802a57b89b06574fb8e9abbdd07ac579e908afaeab86c29562718f781b29c6ebfc66061046a28f481e734ca0d90d53bb0732123590')

prepare() {
  # quick fix for missing files
  echo "include wordcloud/DroidSansMono.ttf" >> ${srcdir}/${_pkgname}-${pkgver}/MANIFEST.in
  echo "include wordcloud/stopwords" >> ${srcdir}/${_pkgname}-${pkgver}/MANIFEST.in
}

build() {
  cd "${_pkgname}-${pkgver}"
  SETUPTOOLS_SCM_PRETEND_VERSION=${pkgver} \
  python -m build --wheel --no-isolation
}

package() {
  cd "${_pkgname}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
