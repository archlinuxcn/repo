# Maintainer: Butui Hu <hot123tea123@gmail.com>

_pkgname=word_cloud
pkgname=python-wordcloud
pkgver=1.9.3
pkgrel=1
pkgdesc='A little word cloud generator in Python'
arch=('x86_64')
url='https://github.com/amueller/word_cloud'
license=('MIT')
depends=(
  python-matplotlib
  python-numpy
  python-pillow
)
makedepends=(
  cython
  python-build
  python-installer
  python-setuptools
  python-setuptools-scm
  python-wheel
)
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/amueller/word_cloud/archive/refs/tags/${pkgver}.tar.gz")
sha512sums=('9863bec2d3cc8972e30a147cfd4ae2eadb79cbb0b472936e7ca789363cde549bb5b8bf6c5669462e9e96288cafc844510e4b33a5fb0f574c771222e85689ca87')

build() {
  cd "${_pkgname}-${pkgver}"
  SETUPTOOLS_SCM_PRETEND_VERSION=${pkgver} \
  python -m build --wheel --no-isolation
}

package() {
  cd "${_pkgname}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
