# Maintainer: Butui Hu <hot123tea123@gmail.com>
pkgname=python-torchgan-git
pkgver=r99.9650398
pkgrel=1
pkgdesc="Research Framework for easy and efficient training of GANs based on PyTorch"
arch=(any)
url="https://github.com/torchgan/torchgan"
license=('MIT')
depends=(python-numpy python-pillow python-pytorch python-torchvision)
makedepends=(git python-setuptools)
provides=(python-torchgan)
conflicts=(python-torchgan)
source=("${pkgname}::git+https://github.com/torchgan/torchgan.git")
noextract=()
md5sums=('SKIP')

pkgver() {
	cd "$srcdir/${pkgname}"

  # Git, no tags available
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

check() {
  cd "$srcdir/$pkgname"
  pytest -v
}

build() {
	cd "$srcdir/${pkgname}"
	python setup.py build
}

package() {
	cd "$srcdir/${pkgname}"
	python setup.py install --root=$pkgdir --optimize=1 --skip-build
 	install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
# vim:set ts=2 sw=2 et:
