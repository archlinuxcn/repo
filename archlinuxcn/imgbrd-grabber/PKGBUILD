# Maintainer: HurricanePootis <hurricanepootis@protonmail.com>
pkgname=imgbrd-grabber
pkgver=7.12.2
pkgrel=5
pkgdesc="Very customizable imageboard/booru downloader with powerful filenaming features."
arch=('x86_64')
url="https://github.com/Bionus/imgbrd-grabber"
license=('Apache-2.0')
depends=("gcc-libs" "glibc" "hicolor-icon-theme" "nodejs" "qt6-base" "qt6-declarative" "qt6-multimedia" "qt6-networkauth" "qt6-shadertools" "qt6-webengine"
	 'qt6-webengine')
makedepends=("clang" "cmake" "git" "jq" "ninja" "npm" "qt6-shadertools" "qt6-tools")
optdepends=('openssl: Access HTTPS sources')
conflicts=("imgbrd-grabber-git" 'imgbrd-grabber-bin' 'imgbrd-grabber-appimage')
source=('git+https://github.com/Bionus/imgbrd-grabber.git#tag=v'${pkgver}''
	'networkauth.patch'
        'git+https://github.com/LaurentGomila/qt-android-cmake.git'
        'git+https://github.com/sakra/cotire.git'
        'git+https://github.com/lexbor/lexbor.git'
        'git+https://github.com/catchorg/Catch2.git')
sha256sums=('47d682e0496439ca1c193a32288f4031142c6780cb3469e66479e46750483a36'
            'ae6bc47ed6d7e16f4400f6ed9ed0446b6c8df5e322901e9103ed8b4b83771a22'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP')

prepare() {
    cd "$srcdir/${pkgname}"
    git submodule init
    git config submodule.cmake/qt-android-cmake.url "$srcdir/qt-android-cmake"
    git config submodule.cmake/cotire.url "$srcdir/cotire"
    git config submodule.tests/src/vendor/catch.url "$srcdir/Catch2"
    git config submodule.lib/vendor/lexbor.url "$srcdir/lexbor"
    git -c protocol.file.allow=always submodule update

    patch -p1 < "$srcdir/networkauth.patch"
}

build() {
    cd "$srcdir/${pkgname}/src/sites"

    npm install --no-optional

    cd "$srcdir" 

    cmake -S $pkgname/src -B build \
    -G Ninja \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLEXBOR_BUILD_TESTS_CPP=0 \
    -Wno-dev

    cmake --build build
}

package() {
    cd "$srcdir"

    DESTDIR="$pkgdir/" cmake --install build && rm -rf "$pkgdir/usr/include" "$pkgdir/usr/lib"
    cat >> "$pkgdir/usr/share/applications/org.bionus.Grabber.desktop" <<-EOF

StartupWMClass=cr.fr.bionus.Grabber
EOF
    sed -i 's/Icon=grabber/Icon=org.bionus.Grabber.png/g' "$pkgdir/usr/share/applications/org.bionus.Grabber.desktop"
    for size in {128,96,48};
    do
      cd "$pkgdir/usr/share/icons/hicolor/${size}x${size}/apps"
      mv grabber.png org.bionus.Grabber.png
      ln -s org.bionus.Grabber.png cr.fr.bionus.Grabber.png
    done

    touch "$pkgdir/usr/share/Grabber/settings.ini"
}
