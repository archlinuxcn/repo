# Maintainer  : Thaodan          <AUR+me@thaodan.de>
# Contributor : Firef0x          <Firefgx {at) gmail [dot} com>
# Contributor : Bernhard Walle   <bwalle.de: bernhard            >
# Contributor : Jesse Jaara      <gmail.com, mail.ru: jesse.jaara>
# Contributor : Armin Wehrfritz
# Contributor : Patrick McCarty  <pnorcks at gmail dot com>

pkgname=osc
pkgver=1.6.1
pkgrel=1
pkgdesc="Command line client for the openSUSE Build Service"
arch=(any)
url="https://github.com/openSUSE/${pkgname}"
license=('GPL-2.0-or-later')
depends=('python'
	'python-m2crypto'
	'python-distro'
	'python-cryptography'
	'python-urllib3')
makedepends=('python-setuptools')
optdepends=('obs-build: required to run local builds'
	'obs-service-format_spec_file: for running the format_spec_file source service'
	'obs-service-download_files: for running the download_files source service'
	'python-keyring: keyring support'
	'python-progressbar: progressbar support for operations like uploading and services'
)
conflicts=('osc-git' 'osc-bash-completion' 'zsh-completion-osc')
replaces=('osc-bash-completion' 'zsh-completion-osc')
source=("${pkgname}-${pkgver}::https://github.com/openSUSE/${pkgname}/archive/${pkgver}.tar.gz"
	"_${pkgname}"
	"0001-Add-sb2install-support-to-osc.patch"
	"0002-Support-osc-copyprj-in-api-by-Islam-Amer-usage-osc-c.patch"
	"0003-Support-synchronous-copyproj.patch"
	"0004-Add-p-to-copyprj-to-enable-copying-of-prjconf.patch"
	"0005-Add-support-for-rebuild-and-chroot-only-in-build.-re.patch"
	"0006-Add-architecture-and-scheduler-maps.patch"
)
sha256sums=('da8d0317271335c91780ed397fd61b8d4bafff0e4e8b41f6bf88441e87c78bc8'
            '2b045e03d2fdce12683ceb9792d491a32f00b256045456412e7bc18c8726218a'
            '270e4998d3499ff42992cc9866025bb9d3976fcd922d3428d0bdaa066be52d59'
            '8176ec12b8f9e14ac655241d38aff1ed6797a3f684ba48a2f6dbea4ce988422c'
            '90072ef14c4b7d31ac79a6005f5b06d51d0a4051274ad2b70b7e7d6b6d13080f'
            '11318594468e017cee00ebbc5c0148808740557106279a4be6868851aebb2606'
            '68a9ff708c067b89430e894df941b76e6dcb499eef0b384afb762cd72bd7f721'
            'eae5f978b8385db3acae46df96fc4973f306c4a640389165a8ac54469d370e5b')

prepare() {
	# Add 'Arch_Core' and 'Arch_Extra' as osc build targets
	sed -i "s|SLE_11_SP2|SLE_11_SP2 Arch_Core Arch_Extra|" "_${pkgname}"
	cd "${pkgname}-${pkgver}/"

	for patch in ${srcdir}/*.patch; do
		patch -p1 -i $patch
	done

	# Fix version
	sed -e "s/    version = \"%(describe:tags=true)\"/    version = \"${pkgver}\"/" \
		-i "${pkgname}/util/git_version.py"
}

build() {
	cd "${pkgname}-${pkgver}/"
	python setup.py build
}

package() {
	cd "${pkgname}-${pkgver}/"
	python setup.py install --root="${pkgdir}/" --optimize=1 --prefix=/usr

	install -Dm644 "contrib/complete.sh" "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
	install -Dm755 "contrib/${pkgname}.complete" "${pkgdir}/usr/lib/${pkgname}/complete"
	install -Dm644 "../_${pkgname}" -t "${pkgdir}/usr/share/zsh/site-functions/"

	install -Dm644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}/"
	install -d "${pkgdir}/usr/share/doc/${pkgname}"
	cp -r doc/* "${pkgdir}/usr/share/doc/${pkgname}/"
}
