# Maintainer: Roald Clark <roaldclark@gmail.com>
# Contributor: Will Handley <wh260@cam.ac.uk> (aur.archlinux.org/account/wjhandley)

_name=Flake8-pyproject
pkgname=python-flake8-pyproject
pkgver=1.2.3
pkgrel=1
pkgdesc="Flake8 plug-in loading the configuration from pyproject.toml"
arch=('any')
url="https://github.com/john-hen/${_name}"
license=('MIT')
depends=(
    'flake8'
    'python'
    'python-tomli'
)
makedepends=(
    'python-build'
    'python-flit-core'
    'python-installer'
)
checkdepends=(
    'python-pytest'
    'python-pytest-cov'
)
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha256sums=('8f065721c53c1fe22d423d82db99eef6559b463a8701aa03b03668d43abfc851')

build() {
    cd "${srcdir}/${_name}-${pkgver}"
    python -m build --wheel --no-isolation
}

check() {
    cd "${srcdir}/${_name}-${pkgver}"
    python -m installer --destdir=tmp_test_install dist/*.whl
    local site_packages=$(python -c 'import site; print(site.getsitepackages()[0])')
    export PYTHONPATH="${PWD}/tmp_test_install${site_packages}:${PYTHONPATH}"
    export PATH="${PWD}/tmp_test_install/usr/bin:${PATH}"
    python -m pytest -v tests
}

package() {
    cd "${srcdir}/${_name}-${pkgver}"
    install -Dm0644 license.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    python -m installer --destdir="${pkgdir}" dist/*.whl
}
