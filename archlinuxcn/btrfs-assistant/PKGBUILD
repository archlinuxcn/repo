# Maintainer: Integral <integral@member.fsf.org>
# Contributor: dalto <dalto at fastmail.com>

pkgname=btrfs-assistant
pkgver=2.1.1
pkgrel=3
pkgdesc="An application for managing BTRFS subvolumes and Snapper snapshots"
arch=('x86_64' 'aarch64')
url="https://gitlab.com/${pkgname}/${pkgname}"
license=('GPL-3.0-or-later')
depends=('qt6-base' 'qt6-svg' 'ttf-font' 'polkit' 'btrfs-progs' 'diffutils')
optdepends=('snapper' 'btrfsmaintenance')
makedepends=('cmake' 'qt6-tools')
backup=("etc/${pkgname}.conf")
source=("${url}/-/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('cabc3824651606bd37db1809d7d2eeeaa724180d138613bff3490362751b9594')

build() {
	cmake -B build -S "${pkgname}-${pkgver}/" -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=None
	cmake --build build
}

package() {
	DESTDIR="${pkgdir}" cmake --install build

	# License
	install -Dm644 "${pkgname}-${pkgver}/LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
