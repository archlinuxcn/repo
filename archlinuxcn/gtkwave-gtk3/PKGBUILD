# Maintainer: DDoSolitary <DDoSolitary@gmail.com>

pkgname=gtkwave-gtk3
pkgver=3.3.111
pkgrel=1
pkgdesc='A wave viewer which reads LXT, LXT2, VZT, GHW and VCD/EVCD files (gtk3 version)'
arch=(x86_64)
url=https://gtkwave.sourceforge.net
license=(GPL MIT)
depends=(
	bzip2 xz gtk3 desktop-file-utils dconf judy tcl
	libbz2.so libgdk-3.so libgdk_pixbuf-2.0.so libgio-2.0.so libglib-2.0.so libgobject-2.0.so libgtk-3.so libpango-1.0.so libpangocairo-1.0.so 
)
makedepends=(gperf)
conflicts=(gtkwave)
provides=(gtkwave)
source=(http://gtkwave.sourceforge.net/$pkgname-$pkgver.tar.gz)
sha256sums=('b4e1c2f718fb3c4b4f412a220876b9da599fe11745fb6f7eb4aed107b1106233')

prepare() {
	cd $pkgname-$pkgver
	sed -i '/define GLOBALS_H/a #include <X11\/X.h>' src/globals.h
}

build() {
	cd $pkgname-$pkgver
	./configure \
            --disable-mime-update \
            --enable-struct-pack \
            --enable-fatlines \
            --enable-manymarkers \
            --disable-dependency-tracking \
            --disable-local-libz \
            --disable-local-libbz2 \
            --enable-xz \
            --enable-fasttree \
            --enable-judy \
            --enable-schemas-compile \
            --enable-largefile \
            --enable-tcl \
            --enable-tk \
            --with-tcl=/usr/lib \
            --with-tk=/usr/lib \
	make
}

package() {
	cd $pkgname-$pkgver
	make DESTDIR="$pkgdir" install
	install -Dm644 LICENSE.TXT "$pkgdir/usr/share/licenses/$pkgname/LICENSE.TXT"
}

