# Maintainer: UnicornDarkness
# Maintainer: Integral <integral@member.fsf.org>

pkgname=cpu-x
_pkgname=CPU-X
pkgver=5.1.1
pkgrel=1
pkgdesc="A Free software that gathers information on CPU, motherboard and more"
arch=('i686' 'x86_64')
url="https://thetumultuousunicornofdarkness.github.io/${_pkgname}"
license=('GPL-3.0-or-later')
depends=('gtkmm3' 'ncurses' 'libcpuid>=0.6.0' 'pciutils' 'glfw' 'vulkan-icd-loader' 'procps-ng>=4.0.0')
makedepends=('cmake' 'ninja' 'nasm' 'vulkan-headers')
optdepends=('opengl-driver: packaged openGL driver'
	'vulkan-driver: packaged Vulkan driver')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/TheTumultuousUnicornOfDarkness/${_pkgname}/archive/v${pkgver}.tar.gz"
	"fix-strequal.patch::https://github.com/TheTumultuousUnicornOfDarkness/${_pkgname}/commit/b0b57a2d9b1438512110e1752c38ea0204d2ed65.patch")
sha512sums=('8601946feaed567e144350c69761a46a7f5ff1aa47c1d550afca11d6b3eb7546b125c56ecd4f266a33d5ee8a6ef09eba01f7c6570dde25c4f1989971eb6dbb27'
            '85cca54210bd898ce5f37c48f4f54ac8d4cfbbb2cbffc14bb52f9cd5414247b248c4eb214b0bddda5a9ab365339783efaa3e8abf20127e0ad8bfbd8db17d3e44')

prepare() {
	patch -p1 -d "${_pkgname}-${pkgver}" -i ../fix-strequal.patch
}

build() {
	cmake -S "${_pkgname}-${pkgver}" \
		-B build \
		-G Ninja \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr

	cmake --build build
}

check() {
	ninja -C build test
}

package() {
	DESTDIR="${pkgdir}" cmake --install build
}
