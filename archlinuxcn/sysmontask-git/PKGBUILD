# Maintainer:
# Contributor: Mark Wagie <mark dot wagie at proton dot me>
# Contributor: Nico <d3sox at protonmail dot com>

pkgname=sysmontask-git
_pkgname=SysMonTask
pkgver=1.3.9.r17.gaee8b9c
pkgrel=1
epoch=2
pkgdesc="System monitor with the compactness and usefulness of Windows Task Manager"
arch=('any')
url="https://github.com/KrispyCamel4u/${_pkgname}"
license=('BSD-3-Clause')
depends=('gtk3' 'libwnck3' 'lshw' 'polkit' 'python-cairo' 'python-gobject' 'python-psutil')
makedepends=('git' 'python-setuptools')
optdepends=('python-matplotlib: For Log Plot utility'
	'nvidia-utils: for NVIDIA GPU monitoring')
conflicts=('sysmontask')
provides=('sysmontask')
source=("git+${url}.git"
	"sysmontask.patch"
	"sysmontask-pkexec"
	"org.freedesktop.sysmontask.policy")
sha256sums=('SKIP'
	'd2069005efc8552a96da3fb84703e8287ec5abc89f59e69fafa1d173cba0d91e'
	'5e6538589b0b6509a265cd67a28cb791e0f87a0ae88b9bb86cd97e6227a8d93b'
	'fc36c4fed51fd543697fb6c9eeb3f57151e9ba2745339119950875d8888d5046')

pkgver() {
	cd "${_pkgname}/"
	git describe --long --tags --exclude v1.x.x | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/v//'
}

prepare() {
	cd "${_pkgname}/"
	patch -Np1 -i "${srcdir}/sysmontask.patch"

	# '1.x.x' is not valid according to PEP 440
	sed -i 's/1.x.x/1.3.9/g' setup.py
	sed -i "s/Exec=sysmontask/Exec=sysmontask-pkexec/g" "${_pkgname}.desktop"
}

build() {
	cd "${_pkgname}/"
	python setup.py build
}

package() {
	pushd "${_pkgname}/"
	python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
	popd

	install -Dm755 "sysmontask-pkexec" -t "${pkgdir}/usr/bin"
	install -Dm644 "org.freedesktop.sysmontask.policy" -t \
		"${pkgdir}/usr/share/polkit-1/actions"
}
