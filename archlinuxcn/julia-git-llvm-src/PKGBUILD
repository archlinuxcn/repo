pkgname=julia-git-llvm-src
pkgver=9.4.4
_commit=90917c8d4f7ab8d6ba6acf0f0d583486b8b02610
pkgrel=3
pkgdesc="LLVM.jl"
url="https://github.com/maleadt/LLVM.jl.git"
arch=('any')
license=('MIT')
makedepends=(git julia-pkg-scripts)
depends=(julia-git)
source=("git+https://github.com/maleadt/LLVM.jl.git#commit=$_commit"
        0001-Support-LLVM-21-in-LLVMExtra.patch
        0002-Generate-wrappers-for-LLVM-21.patch)
sha256sums=('1e825342574806972fad84e5c88ad8f50d0b3dd231aba79c53eea287a12638ad'
            '784dd1e8c28f4a455cd38f1e696969d3f57ca014ba2f6e5f9efadb37d1837405'
            '1f5d4ff81200780bfc5d21e5613dcebc8d431218217d78af91c4a7293d69eaaf')

prepare() {
  cd LLVM.jl

  patch -Np1 --no-backup-if-mismatch < "../0001-Support-LLVM-21-in-LLVMExtra.patch"
  patch -Np1 --no-backup-if-mismatch < "../0002-Generate-wrappers-for-LLVM-21.patch"
}

package() {
  cd LLVM.jl

  JULIA_INSTALL_SRCPKG=1 . /usr/lib/julia/julia-install-pkg.sh LLVM "${pkgdir}" "${pkgname}" julia-git

  rm -rf "${dest_dir}"/deps
}
