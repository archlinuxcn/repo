# Maintainer: Hu Butui <hot123tea123@gmail.com>
# Contributor: mark.blakeney at bullet-systems dot net

_pkgname=PySnooper
pkgname=python-pysnooper
pkgver=1.1.0
pkgrel=1
pkgdesc='PySnooper - Never use print for debugging again'
url='https://github.com/cool-RR/PySnooper'
license=('MIT')
arch=('any')
depends=(python)
makedepends=(python-setuptools)
checkdepends=(python-pytest)
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/cool-RR/PySnooper/archive/${pkgver}.tar.gz")
sha512sums=('99d22afba25e675878b499d05583a94289bad5d2cc4211860cbbad372606182281c07c3dacabdf66c03d5d5da30eb88d5bbdd3d6191a6327dee97d16818c6fbf')

build() {
  cd "${_pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${_pkgname}-${pkgver}"
  pytest -v
}

package() {
  cd "${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
# vim:set ts=2 sw=2 et:
