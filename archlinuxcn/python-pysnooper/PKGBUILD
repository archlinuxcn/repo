# Maintainer: Hu Butui <hot123tea123@gmail.com>
# Contributor: mark.blakeney at bullet-systems dot net

_pkgname=PySnooper
pkgname=python-pysnooper
pkgver=1.2.3
pkgrel=2
pkgdesc='PySnooper - Never use print for debugging again'
url='https://github.com/cool-RR/PySnooper'
license=('MIT')
arch=('any')
depends=(python)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(python-pytest)
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/cool-RR/PySnooper/archive/${pkgver}.tar.gz")
sha512sums=('8639cd7fc6a7435272abdba1898ab7dbc16a2103ee29f07880bdfed45ec01a3f1deb917fa30ccc6f911efcf872f93ecf62b9b5d6828706be0f9490d209f4a27a')

build() {
  cd "${_pkgname}-${pkgver}"
  python -m build --wheel --no-isolation
}

check() {
  cd "${_pkgname}-${pkgver}"
  pytest -v
}

package() {
  cd "${_pkgname}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
# vim:set ts=2 sw=2 et:
