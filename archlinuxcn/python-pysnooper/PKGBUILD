# Maintainer: Hu Butui <hot123tea123@gmail.com>
# Contributor: mark.blakeney at bullet-systems dot net

_pkgname=PySnooper
pkgname=python-pysnooper
pkgver=1.2.0
pkgrel=2
pkgdesc='PySnooper - Never use print for debugging again'
url='https://github.com/cool-RR/PySnooper'
license=('MIT')
arch=('any')
depends=(python)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(python-pytest)
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/cool-RR/PySnooper/archive/${pkgver}.tar.gz")
sha512sums=('befa88eb16b3ec0dbb0f51f7d2722cf9bf06336dadb86741f53fbf6ca03a4eeaa2793fd24ad95b6b7bc9a5f9401161fcac71d6b109c57714de53b08dc11846c1')

build() {
  cd "${_pkgname}-${pkgver}"
  python -m build --wheel --no-isolation
}

check() {
  cd "${_pkgname}-${pkgver}"
  pytest -v
}

package() {
  cd "${_pkgname}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
# vim:set ts=2 sw=2 et:
