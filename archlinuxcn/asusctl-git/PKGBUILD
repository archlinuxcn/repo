# Maintainer: Static_Rocket

pkgname=asusctl-git
pkgver=4.5.0.rc3.r15.g58ff566
pkgrel=1
pkgdesc="Asus laptop control utilities"
arch=('x86_64')
url="https://gitlab.com/asus-linux/asusctl"
license=('Mozilla Public License Version 2.0')
depends=('libusb' 'udev' 'systemd' 'freetype2' 'fontconfig')
optdepends=('acpi_call: fan control' 'supergfxctl-git: switch graphics card mode')
makedepends=('git' 'rust' 'cmake')
provides=('asus-nb-ctrl')
replaces=('asus-nb-ctrl-git')
source=('git+https://gitlab.com/asus-linux/asusctl.git')
md5sums=('SKIP')
_gitdir=${pkgname%"-git"}

pkgver() {
	cd "$_gitdir"
	git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
	cd "$_gitdir"
	make build
}

package() {
	cd "$_gitdir"
	make DESTDIR="$pkgdir" install
}

