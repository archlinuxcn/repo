# Maintainer: lilydjwg@gmail.com

pkgname="dcron-git"
epoch=1
pkgver=4.5.r9.g6186237
pkgrel=7
pkgdesc="dillon's lightweight cron daemon, git version"
arch=("i686" "x86_64" "armv6h" "armv7h")
license=("GPL-2.0+")
url="http://www.jimpryor.net/linux/dcron.html"
backup=("var/spool/cron/root" "etc/conf.d/crond")
depends=("bash")
makedepends=('git')
optdepends=("msmtp-mta: sending cron job output via email")
provides=("cron")
conflicts=("cron")
source=("git+https://github.com/dubiousjim/dcron.git#commit=6186237"
        "subreap.patch"
        "systemd.patch"
        "dcron.hook"
        "service")
md5sums=('SKIP'
         '571eab9300e7bd25191950d3dfe55db5'
         '08a4d7146fb67b1398c562d5a1aa2ab2'
         '509724f00d1fa3ce7a98b92afb294b5a'
         'bb46f57f84aabf81d33eb6f756272725')

pkgver() {
  cd "$srcdir/dcron"
  git describe | sed -E 's/([^-]*-g)/r\1/;s/-/./g;s/^v//'
}

build() {
  cd "$srcdir/dcron"

  # fix paths to point to /usr/bin
  sed -i 's=/usr/sbin/sendmail=/usr/bin/sendmail=g' defs.h
  sed -i 's=/usr/sbin/run-cron=/usr/bin/run-cron=g' extra/root.crontab
  patch -Np1 < ../subreap.patch
  patch -Np1 < ../systemd.patch

  # by default, any member of group "users" can edit their own crontab
  make \
      PREFIX=/usr \
      SBINDIR=/usr/bin \
      CRONTAB_GROUP=users \
      CRONTABS=/var/spool/cron \
      CRONSTAMPS=/var/spool/cronstamps
}

package() {
  cd "$srcdir/dcron"
  make DESTDIR="$pkgdir" install

  # install standard configuration and scripts
  install -d -m755 "$pkgdir"/etc/cron.{hourly,daily,weekly,monthly}

  install -D -m755 extra/run-cron "$pkgdir"/usr/bin/run-cron
  install -D -m0600 extra/root.crontab "$pkgdir"/var/spool/cron/root
  install -D -m0644 extra/crond.conf "$pkgdir"/etc/conf.d/crond
  install -D -m644 extra/crontab.vim "$pkgdir"/usr/share/vim/vimfiles/ftplugin/crontab.vim
  sed -i -e 's=/var/spool/cron/cronstamps=/var/spool/cronstamps=' extra/prune-cronstamps
  install -D -m755 extra/prune-cronstamps "$pkgdir"/etc/cron.d/prune-cronstamps
  install -Dm644  "$srcdir"/service "$pkgdir"/usr/lib/systemd/system/dcron.service
  install -Dm644 "${srcdir}"/dcron.hook "${pkgdir}"/usr/share/libalpm/hooks/dcron.hook
}
