# Mainintainer : Lucas Rooyakkers <lucas dot rooyakkers at queensu at ca>

pkgname=python-geomag-git
pkgver=31.421564e
pkgrel=12
pkgdesc='Calculates magnetic variation/declination for any latitude/longitude/altitude, for any date.'
arch=('any')
url="https://github.com/todd-dembrey/geomag"
license=('custom:BSD')
depends=('python')
makedepends=('git' 'python-setuptools' 'python-build' 'python-installer' 'python-wheel')
source=('git+https://github.com/todd-dembrey/geomag')
md5sums=('SKIP')

pkgver() {
  cd geomag

  echo "$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

prepare() {
  cd geomag
  sed -i -e 's/distutils\.core/setuptools/g' setup.py
}

build() {
  cd geomag
  python -m build --wheel --no-isolation
}

package() {
  cd geomag

  python -m installer --destdir="${pkgdir}" dist/*.whl

  site=$(python -c 'import sysconfig; print(sysconfig.get_path("purelib"))')
  mkdir -p "${pkgdir}/${site}/geomag/"
  cp -r geomag/model_data "${pkgdir}/${site}/geomag/"

  pymin_ver=$(python -c 'import sys; print(sys.version_info.minor)')
  ((pynext_min=pymin_ver + 1))
  depends+=("python>=3.${pymin_ver}.0" "python<3.${pynext_min}.0")
}
