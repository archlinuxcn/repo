# Maintainer: Hu Butui <hot123tea123@gmail.com>
# Contributor: j.r <j.r@jugendhacker.de>
# Contributor: Jonathan Chasteen <jonathan dot chasteen at live dot com>
# Contributor: Mohammadreza Abdollahzadeh <morealaz at gmail dot com>
# Contributor: aimileus <me at aimileus dot nl>
# Contributor: spider-mario <spidermario@free.fr>

pkgname=('qgnomeplatform' 'qgnomeplatform-qt6')
pkgbase=qgnomeplatform
_pkgname=QGnomePlatform
pkgver=0.8.4
pkgrel=1
pkgdesc="QPlatformTheme for a better Qt application inclusion in GNOME"
arch=('x86_64')
url="https://github.com/FedoraQt/QGnomePlatform"
license=('LGPL2.1')
makedepends=(
  adwaita-qt
  adwaita-qt6
  cmake
  gtk3
  qt5-wayland
  qt6-wayland
)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/FedoraQt/QGnomePlatform/archive/${pkgver}.tar.gz")
sha256sums=('c648331f47b095d90594fc986a6c0bc885b846e0e9f5c10b6a1ba6a58d004686')

build() {
  cmake -B build-qt5 -S "${_pkgname}-${pkgver}" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DUSE_QT6=OFF \
    -Wno-dev
  make -C build-qt5

  cmake -B build-qt6 -S "${_pkgname}-${pkgver}" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DUSE_QT6=ON \
    -Wno-dev
  make -C build-qt6
}

package_qgnomeplatform() {
  pkgdesc="QPlatformTheme for a better Qt5 application inclusion in GNOME"
  depends=(
    adwaita-qt
    gtk3
    qt5-wayland
)

  make -C build-qt5 DESTDIR="${pkgdir}" install
}

package_qgnomeplatform-qt6() {
  pkgdesc="QPlatformTheme for a better Qt6 application inclusion in GNOME"
  depends=(
    adwaita-qt6
    gtk3
    qt6-wayland
)

  make -C build-qt6 DESTDIR="${pkgdir}" install
}
# vim:set ts=2 sw=2 et:
