# Maintainer: lilydjwg <lilydjwg@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgbase=arch-wiki-docs-zh
pkgname=(arch-wiki-docs-zh-cn arch-wiki-docs-zh-tw)
pkgver=20260201
pkgrel=2
pkgdesc='Pages from wiki.archlinuxcn.org optimized for offline browsing'
arch=('any')
url='https://github.com/archlinuxcn/arch-wiki-docs-zh'
license=('GFDL-1.3-or-later')
makedepends=(
  'git' 'python' 'python-lxml' 'python-cssselect' 'python-wiki-scripts'
)
options=('!strip' '!debug')
source=("$pkgbase::git+https://github.com/archlinuxcn/arch-wiki-docs-zh.git")
b2sums=('SKIP')

pkgver() {
  date "+%Y%m%d"
}

build() {
  cd "$pkgbase"

  LANG=en_US.UTF-8 python \
    arch-wiki-docs.py \
    --api-url https://wiki.archlinuxcn.org/wzh/api.php \
    --index-url https://wiki.archlinuxcn.org/wzh/index.php \
    --output-directory "$srcdir/build_wiki_zh-cn" \
    --variant "zh-cn" \
    --clean

  LANG=en_US.UTF-8 python \
    arch-wiki-docs.py \
    --api-url https://wiki.archlinuxcn.org/wzh/api.php \
    --index-url https://wiki.archlinuxcn.org/wzh/index.php \
    --output-directory "$srcdir/build_wiki_zh-tw" \
    --variant "zh-tw" \
    --clean
}

package_arch-wiki-docs-zh-cn() {
  _package zh-cn
}

package_arch-wiki-docs-zh-tw() {
  _package zh-tw
}

_package() {
  install -vdm755 "$pkgdir/usr/share/doc/arch-wiki-$1/html"
  cp -r build_wiki_$1/* "$pkgdir/usr/share/doc/arch-wiki-$1/html"
}
