# Maintainer: Gregory Land

pkgname=supergfxctl-git
pkgver=5.2.4.r0.g4c99316
pkgrel=1
pkgdesc="A utility for Linux graphics switching on Intel/AMD iGPU + nVidia dGPU laptops"
arch=('x86_64')
url="https://gitlab.com/asus-linux/supergfxctl"
license=('MPL2')
depends=('systemd-libs')
makedepends=('git' 'rust' 'systemd')
provides=('supergfxctl')
conflicts=('supergfxctl')
source=('git+https://gitlab.com/asus-linux/supergfxctl.git')
md5sums=('SKIP')
_gitdir=${pkgname%"-git"}

pkgver() {
	cd "$srcdir/$_gitdir"
	git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
	cd "$srcdir/$_gitdir"
	make build
}

package() {
	cd "$srcdir/$_gitdir"
	make DESTDIR="$pkgdir" install
}
