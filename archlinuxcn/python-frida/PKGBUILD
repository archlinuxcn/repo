# Maintainer: kriskras99 <admin at kriskras99 dot nl>
# Contributor: Ali Molaei <ali dot molaei at protonmail dot com>
# Contributor: algebro <algebro at tuta dot io>

_pkgname=frida
pkgname="python-${_pkgname}"
pkgver=16.3.3
pkgrel=1
pkgdesc="Inject JavaScript to explore native apps on Windows, Mac, Linux, iOS and Android. Python 3 version from PyPi"
arch=('i686' 'x86_64' 'armv7h' 'aarch64')
url="https://www.${_pkgname}.re"
license=('LicenseRef-wxWindowsLibrary')
depends=('python' 'nodejs' 'npm')
optdepends=('python-typing_extensions: for Python <3.8'
	'rustup: for injecting Rust code into targets')
makedepends=('python-build' 'python-installer' 'python-wheel' 'python-setuptools')
# Sourced from https://pypi.org/project/frida/#files
source=("https://files.pythonhosted.org/packages/source/f/${_pkgname}/${_pkgname}-${pkgver}.tar.gz"
	"COPYING")
sha256sums=('b5fe59405cbc970a72f8eae057bb9a4a4590e31a64576d9d2780587a7be25557'
	'5ea1544b51a28bc823b03159190d4108f9fb4f4ef912389f5137c6d295e175b2')
conflicts=("python2-${_pkgname}")

build() {
	cd "${_pkgname}-${pkgver}/"
	python -m build --wheel --no-isolation
}

package() {
	cd "${_pkgname}-${pkgver}/"
	python -m installer --destdir="${pkgdir}" dist/*.whl

	cd ..
	install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
