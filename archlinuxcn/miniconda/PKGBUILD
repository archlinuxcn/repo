# Maintainer: Butui Hu <hot123tea123@gmail.com>
# Contributor : Anthony Wang <ta180m@pm.me>
# Contributor : Ashwin Vishnu <ashwinvis+arch At protonmail DoT cOm>
# Contributor : Immae <ismael.bouya@normalesup.org>
# Contributor : Martin Wimpress <code@flexion.org>
# Contributor : Jingbei Li <i@jingbei.li>

_pkgname=Miniconda3
_pyver=py311
pkgname=miniconda
pkgver=23.5.2
pkgrel=1
pkgdesc='Mini version of Anaconda Python distribution'
arch=('x86_64')
url='https://conda.io/en/latest/miniconda.html'
license=("custom")
depends=(ca-certificates-utils)
source=("${pkgname}-${pkgver}.sh::https://repo.continuum.io/miniconda/${_pkgname}-${_pyver}_${pkgver}-0-Linux-x86_64.sh"
        "${pkgname}.install")
options=(!strip libtool staticlibs)
sha256sums=('634d76df5e489c44ade4085552b97bebc786d49245ed1a830022b0b406de5817'
            '02ed5095b7bd9670f749465d9cac1478deab44ebdf8616848ae4bd9371150abf')
install="${pkgname}.install"

package() {
  prefix="${pkgdir}/opt/${pkgname}"
  LD_PRELOAD="/usr/lib/libfakeroot/libfakeroot.so"
  echo "Packaging ${pkgname} for installation to /opt/${pkgname}"
  bash "${srcdir}/${pkgname}-${pkgver}.sh" -b -p ${prefix} -f
  echo "Correcting permissions"
  cd ${prefix}
  find ${pkgdir}/opt/miniconda -type f -exec chmod 644 {} \;
  find ${pkgdir}/opt/miniconda -type d -exec chmod 755 {} \;
  find ${pkgdir}/opt/miniconda/bin -type f -exec chmod 755 {} \;
  find ${pkgdir}/opt/miniconda/condabin -type f -exec chmod 755 {} \;
  find ${pkgdir}/opt/miniconda/lib -type f -exec chmod 755 {} \;
  find ${pkgdir}/opt/miniconda/lib -type f -name "*.a" -exec chmod 644 {} \;
  echo "Stripping \$pkgdir"
  sed "s|${pkgdir}||g" -i $(grep "$pkgdir" . -rIl)
  echo "Installing license"
  install -D -m644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  # use system /etc/ssl/certs/ca-certificates.crt
  find "${pkgdir}" -type f -name "cacert.pem" -exec ln -sf "/etc/ssl/certs/ca-certificates.crt" {} \;
}
# vim:set ts=2 sw=2 et:
