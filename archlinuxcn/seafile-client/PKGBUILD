# Maintainer: Joffrey <j-off@live.fr>
# Contributor: eolianoe <eolianoe [at] gmail [DoT] com>
# Contributor: localizator <localizator@ukr.net>
# Contributor: Edvinas Valatka <edacval@gmail.com>

pkgname=seafile-client
pkgver=9.0.15
pkgrel=2
pkgdesc='GUI client for synchronizing your local files with seafile server'
arch=('i686' 'x86_64' 'armv7h' 'armv6h' 'aarch64')
url="https://github.com/haiwen/$pkgname"
license=('Apache')
depends=(
  sqlite
  jansson
  libsearpc.so
  libgobject-2.0.so
  libglib-2.0.so
  libseafile.so
  zlib
  'qt6-5compat'
  'qt6-base'
  'qt6-webengine'
)
optdepends=('gtk-update-icon-cache')
makedepends=('cmake' 'qt6-tools')
source=(
  "$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz"
  'fix_build_with_QT6.diff'
)
sha256sums=('0c5602d786e53893d48bd41ca9926b9e54cacb4ef52351b8eefcc467e300805b'
            '5fc54daff54d3ea4e263aea6c23b8c812fe5287e487a56bbf05cf935dd149229')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -p1 -i "$srcdir/fix_build_with_QT6.diff"
  sed CMakeLists.txt -i -e 's/CMAKE_MINIMUM_REQUIRED\(.*\)/cmake_minimum_required(VERSION 3.10)/'
  sed -i -e'1 i#include <QJsonValue>' src/message-poller.cpp
}

build() {
  cmake -B build -S "$srcdir/$pkgname-$pkgver" \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX='/usr' \
        -DBUILD_SHIBBOLETH_SUPPORT=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
