# Maintainer: Integral <integral@member.fsf.org>
# Contributor: Adam Perkowski <adas1per@protonmail.com>

pkgname=hyprgui
pkgver=0.2.0
pkgrel=1
pkgdesc="GUI for configuring Hyprland, written in blazingly fast Rust!"
arch=('x86_64' 'aarch64' 'riscv64')
url="https://github.com/hyprutils/${pkgname}"
license=('GPL-2.0-or-later')
depends=('gtk4')
makedepends=('cargo' 'pango')
optdepends=('hyprland')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('01bf18f5efc5f9a87cfb0ba4489c7bdae6ecd173e1fdaeba0efcca167e08bd3a')

prepare() {
	cd "${pkgname}-${pkgver}/"
	cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
	cd "${pkgname}-${pkgver}/"
	cargo build --frozen --release --all-features
}

check() {
	cd "${pkgname}-${pkgver}/"
	cargo test --frozen --all-features
}

package() {
	cd "${pkgname}-${pkgver}/"
	install -Dm755 "target/release/${pkgname}" -t "${pkgdir}/usr/bin/"
	install -Dm644 "${pkgname}.svg" -t "${pkgdir}/usr/share/icons/hicolor/scalable/apps/"
	install -Dm644 "${pkgname}.desktop" -t "${pkgdir}/usr/share/applications/"
	install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
