# $Id$
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>
# Contributor: Roberto Carvajal <roberto@archlinux.org>
# Mantainer: Lorenzo Ferrillo <lorenzofer at live dot com>
_basename=mjpegtools
pkgname=lib32-${_basename}
pkgver=2.2.1
pkgrel=1
pkgdesc="Set of tools that can do recording of videos and playback, simple cut-and-paste editing and the MPEG compression of audio and video. 32bit libraries"
arch=('x86_64')
license=('GPL')
url="http://mjpeg.sourceforge.net/"
depends=("gcc-libs-multilib" "lib32-libdv" "lib32-libjpeg" "lib32-libpng" "lib32-sdl" "libjpeg.so" "mjpegtools")
makedepends=('lib32-gtk2' 'lib32-v4l-utils')
source=(http://downloads.sourceforge.net/sourceforge/mjpeg/mjpegtools/${pkgver}/${_basename}-${pkgver}.tar.gz)
sha256sums=('b180536d7d9960b05e0023a197b00dcb100929a49aab71d19d55f4a1b210f49a')
prepare() {
  cd ${_basename}-${pkgver}
  sed -i -e '/ARCHFLAGS=/s:=.*:=:' configure
}

build() {
  cd ${_basename}-${pkgver}
  export CC="gcc -m32 "
  export CXX="g++ -m32 "
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  autoreconf  -f -i
  ./configure --prefix=/usr --libdir=/usr/lib32 --enable-largefile   --without-libquicktime
  make
}

package() {
  cd ${_basename}-${pkgver}
  make DESTDIR="${pkgdir}" install
  rm ${pkgdir}/usr/include ${pkgdir}/usr/share ${pkgdir}/usr/bin -Rf
  }
 
provides=("liblavfile-${pkgver%.*}.so" "liblavjpeg-${pkgver%.*}.so" "liblavplay-${pkgver%.*}.so" "libmjpegutils-${pkgver%.*}.so" "libmpeg2encpp-${pkgver%.*}.so" "libmplex2-${pkgver%.*}.so")
