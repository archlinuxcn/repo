# Maintainer: hubutui <hot123tea123@gmail.com>

_pkgname=cli
_pkgver=3.1.0
pkgname=r-${_pkgname,,}
pkgver=3.1.0
pkgrel=1
pkgdesc='Helpers for Developing Command Line Interfaces'
arch=('any')
url="https://cran.r-project.org/package=${_pkgname}"
license=('MIT')
depends=(
  r
  r-glue
)
optdepends=(
  r-callr
  r-covr
  r-grdevices
  r-htmlwidgets
  r-knitr
  r-methods
  r-mockery
  r-prettycode
  r-ps
  r-rmarkdown
  r-rstudioapi
  r-testthat
  r-tibble
  r-withr
)
options=(!emptydirs)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
sha256sums=('c70a61830bf706a84c59eb74a809978846cee93742198ab4192742a5df1ace11')

build() {
  R CMD INSTALL ${_pkgname}_${_pkgver}.tar.gz -l "${srcdir}"
}

package() {
  install -dm0755 "${pkgdir}/usr/lib/R/library"
  cp -a --no-preserve=ownership "${_pkgname}" "${pkgdir}/usr/lib/R/library"
  install -Dm644 "${_pkgname}/LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
