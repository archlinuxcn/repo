# Maintainer: Kimiblock Zhou <pn3535 at icloud dot com>

pkgname=qcm
pkgver=1.0.2
pkgrel=6
pkgdesc="Qt client for netease cloud music"
arch=('x86_64')
url="https://github.com/hypengw/Qcm"
license=('GPL-2.0-or-later')
depends=(
	'qt6-base'
	'hicolor-icon-theme'
	'curl'
	'openssl'
	'qt6-tools'
	'qt6-declarative'
	'qt6-shadertools'
	'gcc-libs'
	'glibc'
	'dbus'
	'libnghttp2'
	'libidn2'
	'krb5'
	'zstd'
	'zlib'
	'qt6-wayland'
	'libx11'
	'libxkbcommon'
	'libpng'
	'freetype2'
	'icu'
	'pcre2'
	'gnutls'
	'graphite'
	'lz4'
	'libgpg-error'
	'ffmpeg')
makedepends=(
	'git'
	'cmake'
	'extra-cmake-modules'
	'ninja')
source=("git+${url}.git#tag=v${pkgver}")
sha256sums=('05c0d60b16a4985fbe42a57bf4f5196ef6a12f7a059bb8c1e48aa80edc17b1c1')

prepare() {
	cd Qcm
	if [ -d build ]; then
		echo "Deleting ${srcdir}/Qcm/build..."
		rm -rf build
	fi

	git submodule update --init
	mkdir build
}

build() {
	cd Qcm/build
	echo "Starting Build..."
	cmake .. -GNinja -DCMAKE_BUILD_TYPE=Release
	ninja
}

package() {
	echo "Copying files..."
	cd Qcm

	local APP_ID=io.github.hypengw.Qcm
	install -Dm644 "app/assets/Qcm.svg" "${pkgdir}/usr/share/icons/hicolor/scalable/apps/${APP_ID}.svg"
	install -Dm644 "app/assets/Qcm.desktop" "${pkgdir}/usr/share/applications/${APP_ID}.desktop"
	install -Dm755 "build/app/Qcm" -t "${pkgdir}/usr/bin/"
	install -Dm644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
