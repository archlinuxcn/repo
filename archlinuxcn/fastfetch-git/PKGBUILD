# Maintainer: Linus Dierheimer <Linus@Dierheimer.de>
pkgname=fastfetch-git
pkgver=r448.bf1fbd5
pkgrel=1
pkgdesc="Like neofetch, but much faster because written in c"
arch=('x86_64')
url="https://github.com/LinusDierheimer/fastfetch"
license=('MIT')
depends=('glibc')
makedepends=(
  'git'
  'cmake'
  'pciutils'
  'libxrandr' # Depends on libX11, which headers are also needed
  'dconf'     # Depends on glib2, which headers are also needed
  'wayland'
  'sqlite'
)
optdepends=(
  "bash-completion"
  "pciutils: GPU output"
  "libx11: Basic resolution output"
  "libxrandr: Multi monitor and refresh rate support for resolution output"
  "wayland: Native wayland support for resolution output"
  "glib2: Output for values that are only stored in GSettings"
  "dconf: Output for values that are only stored in DConf"
  "xfconf: XFWM theme + xfce-terminal font"
  "sqlite: rpm package count"
)

provides=('fastfetch')
conflicts=("fastfetch")
source=("git+https://github.com/LinusDierheimer/fastfetch.git")
sha256sums=("SKIP")

pkgver() {
  cd "fastfetch/"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build()
{
    cd "${srcdir}/fastfetch"
    mkdir -p build/
    cd build/
    cmake ..
    cmake --build .
}

package() {
    cd ${srcdir}/fastfetch

    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    install -Dm755 "build/fastfetch" "${pkgdir}/usr/bin/fastfetch"
    install -Dm755 "build/flashfetch" "${pkgdir}/usr/bin/flashfetch"
    install -Dm644 "completions/bash" "${pkgdir}/usr/share/bash-completion/completions/fastfetch"

    install -Dd "${pkgdir}/usr/share/fastfetch/presets/"
    for file in presets/*; do
      install -Dm644 "$file" "${pkgdir}/usr/share/fastfetch/presets/"
    done
}
