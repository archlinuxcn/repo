# Maintainer: Leo Papadakos <jpegxguy@outlook.com>
# Contributor: Vincent Bernardoff <vb AT luminar.eu.org>

pkgname=mmc-utils-git
pkgver=9582278
pkgrel=1
pkgdesc="Userspace tools for MMC/SD devices"
arch=(arm armv6h armv7h aarch64 x86_64 i686)
url="https://git.kernel.org/pub/scm/linux/kernel/git/cjb/mmc-utils.git"
license=('GPL')
depends=()
makedepends=('git' 'make')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
install=
source=('git://git.kernel.org/pub/scm/linux/kernel/git/cjb/mmc-utils.git')
noextract=()
sha256sums=('SKIP')

pkgver() {
    cd "$srcdir/${pkgname%-git}"
    git describe --always --dirty --tags | sed -e 's/-/./g'
}

build() {
    cd "${pkgname%-git}"
    make all
}

package() {
    cd "${pkgname%-git}"
    make DESTDIR="$pkgdir/" prefix=/usr install
    install -D man/mmc.1 "$pkgdir/usr/share/man/man1/mmc.1"
}
