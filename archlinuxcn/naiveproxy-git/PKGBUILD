# Maintainer: DuckSoft <realducksoft@gmail.com>

pkgname=naiveproxy-git
_pkgname=naiveproxy
pkgdesc="A Proxy using Chrome's network stack to camouflage traffic with strong censorship resistence and low detectablility."
pkgver=84.0.4147.89.r58.047a828dd
pkgrel=1
arch=('x86_64')
url='https://github.com/klzgrad/naiveproxy'
license=('BSD')
depends=('nss') 
source=('git+https://github.com/klzgrad/naiveproxy.git')
makedepends=('clang' 'fuse' 'git' 'gn' 'lld' 'llvm' 'ninja' 'python2')
backup=(etc/naiveproxy/config.json)
sha512sums=('SKIP')
provides=('naiveproxy')
conflicts=('naiveproxy' 'naiveproxy-bin')

pkgver(){
  cd ${srcdir}/${_pkgname}
  printf "%s.r%s.%s" "$(cat CHROMIUM_VERSION)" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build(){
  cd ${srcdir}/${_pkgname}/src
  
  flags='is_official_build=true
    exclude_unwind_tables=true
    enable_resource_whitelist_generation=false
    symbol_level=0
    
    is_clang=true
    linux_use_bundled_binutils=false

    fatal_linker_warnings=false
    treat_warnings_as_errors=false
    use_sysroot=false

    fieldtrial_testing_like_official_build=true

    enable_nacl=false
    enable_print_preview = false
    use_alsa=false
    use_cups=false
    use_dbus=false
    use_gio=false
    use_platform_icu_alternatives=true
    use_gtk=false
    use_gnome_keyring=false
    use_system_libdrm=false
    use_libpci=false
    use_pangocairo=false
    use_aura=false
    use_glib=false
    use_pulseaudio=false
    use_udev=false

    disable_file_support=true
    enable_websockets=false
    disable_ftp_support=true
    use_kerberos=false
    enable_mdns=false
    enable_reporting=false
    include_transport_security_state_preload_list=false
    rtc_use_pipewire=false
    
    use_xkbcommon=false    
    use_ozone=true
    ozone_auto_platforms=false
    ozone_platform="headless"
    ozone_platform_headless=true
    
    clang_base_path=""
    clang_use_chrome_plugins=false
    clang_use_default_sample_profile=false
  '

  out=out/Release
  rm -rf "./$out"
  mkdir -p out

  export DEPOT_TOOLS_WIN_TOOLCHAIN=0
  gn gen "$out" --args="$flags" --script-executable=/usr/bin/python2

  ninja -C "$out" naive
}

package(){
  cd ${srcdir}/${_pkgname}
  install -Dm755 src/out/Release/naive ${pkgdir}/usr/bin/naiveproxy
  install -Dm644 src/config.json ${pkgdir}/etc/naiveproxy/config.json
  install -Dm644 README.md ${pkgdir}/usr/share/doc/naiveproxy/README.md
  install -Dm644 USAGE.txt ${pkgdir}/usr/share/doc/naiveproxy/USAGE.txt
  install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/naiveproxy-git/LICENSE
}
