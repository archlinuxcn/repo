# Maintainer: lilydjwg <lilydjwg@gmail.com>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thomas Baechler <thomas@archlinux.org>

pkgname=nvidia-lily
pkgver=525.85.05
pkgrel=1
pkgdesc="NVIDIA drivers for linux-lily"
arch=('x86_64')
url="https://www.nvidia.com/"
depends=("linux-lily=6.0.18-1" "nvidia-utils=${pkgver}" 'libglvnd')
makedepends=("nvidia-utils=${pkgver}" 'libglvnd' 'linux-lily' 'linux-lily-headers')
license=('custom')
options=('!strip')
provides=("nvidia=$pkgver")
conflicts=('nvidia-dkms')
_pkg="NVIDIA-Linux-x86_64-${pkgver}"
source=("https://us.download.nvidia.com/XFree86/Linux-x86_64/${pkgver}/${_pkg}.run")
sha512sums=('db30b9186cdcf22b2dde267c08a10cadfc3d37190c57a26a71e282b0f661b187b5b28764418eacf8b2344ebfa8feb7480ae1de31b3bc3026feaf9482a3b51741')

prepare() {
    sh "${_pkg}.run" --extract-only
}

build() {
    cd "${_pkg}"/kernel
    make SYSSRC=/usr/src/linux-lily module
}

package() {

    _extradir="/usr/lib/modules/$(</usr/src/linux-lily/version)/extramodules"
    install -Dt "${pkgdir}${_extradir}" -m644 \
      "${srcdir}/${_pkg}/kernel"/nvidia{,-modeset,-drm,-uvm}.ko

    find "${pkgdir}" -name '*.ko' -exec xz {} +

    echo "blacklist nouveau" |
        install -Dm644 /dev/stdin "${pkgdir}/usr/lib/modprobe.d/${pkgname}.conf"

    install -Dt "${pkgdir}/usr/share/licenses/${pkgname}" -m644 "${srcdir}/${_pkg}/LICENSE"
}
