# Maintainer: lilydjwg <lilydjwg@gmail.com>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thomas Baechler <thomas@archlinux.org>

pkgname=nvidia-lily
pkgver=440.59
pkgrel=1
pkgdesc="NVIDIA drivers for linux-lily"
arch=('x86_64')
url="https://www.nvidia.com/"
depends=("linux-lily=4.19.84-1" "nvidia-utils=${pkgver}" 'libglvnd')
makedepends=("nvidia-utils=${pkgver}" 'libglvnd' 'linux-lily' 'linux-lily-headers')
license=('custom')
options=('!strip')
provides=("nvidia=$pkgver")
conflicts=('nvidia-dkms')
_pkg="NVIDIA-Linux-x86_64-${pkgver}"
source=("https://us.download.nvidia.com/XFree86/Linux-x86_64/${pkgver}/${_pkg}.run")
sha512sums=('2e9029bc7c93a99eac7d3a5ca1f21da799da442a70b1708d2db734956f77dd1a1eb300f947b8ab46ff9113048cd63ca57924caa912a3ea6c3d678425182b9711')

prepare() {
    sh "${_pkg}.run" --extract-only
}

build() {
    cd "${_pkg}"/kernel
    make SYSSRC=/usr/src/linux-lily module
}

package() {

    _extradir="/usr/lib/modules/$(</usr/src/linux-lily/version)/extramodules"
    install -Dt "${pkgdir}${_extradir}" -m644 \
      "${srcdir}/${_pkg}/kernel"/nvidia{,-modeset,-drm,-uvm}.ko

    find "${pkgdir}" -name '*.ko' -exec xz {} +

    echo "blacklist nouveau" |
        install -Dm644 /dev/stdin "${pkgdir}/usr/lib/modprobe.d/${pkgname}.conf"

    install -Dt "${pkgdir}/usr/share/licenses/${pkgname}" -m644 "${srcdir}/${_pkg}/LICENSE"
}
