# Maintainer: Kyle Rassweiler <dev at kylerassweiler dot ca>

pkgname=refind-theme-dracula
pkgver=1.0.0
pkgrel=1
pkgdesc="Simple theme inspired by the Dracula palette"
arch=('any')
url="https://github.com/rassweiler/refind-theme-dracula"
license=('MIT')
depends=('refind')
source=("${url}/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('f7fdc40ca24722e703164cfb6098b85a40194bb72459c284135248c142e8a471')
install=$pkgname.install

prepare() {
	cd "$pkgname-$pkgver"
	# do not reset the STDIN file descriptor
	sed -e 's|exec|# exec|g' -i 'setup.sh'
}

package() {
	cd "$pkgname-$pkgver"
	_theme_dir="usr/share/refind/themes/dracula"

	install -D -m0755 -t "${pkgdir}/${_theme_dir}/" "setup.sh"
	install -D -m0644 -t "${pkgdir}/${_theme_dir}/" {theme.conf,*.png}
	install -D -m0644 -t "${pkgdir}/${_theme_dir}/icons" "icons/"*.png

	# docs
	install -D -m0644 -t "${pkgdir}/usr/share/doc/${pkgname}" README.md
	# licenses
	install -D -m0644 -t "${pkgdir}/usr/share/licenses/${pkgname}" {LICENSE,COPYING}
}
