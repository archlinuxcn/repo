# Maintainer: acxz <akashpatel2008 at yahoo dot com>

_pkgname=opencensus-python
pkgname=python-opencensus
pkgver=0.11.1
pkgrel=1
pkgdesc='A stats collection and distributed tracing framework'
arch=('any')
url='https://github.com/census-instrumentation/opencensus-python'
license=('Apache')
depends=(
  python-google-api-core
)
makedepends=(
  python-setuptools
)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/census-instrumentation/opencensus-python/archive/v${pkgver}.tar.gz")
sha256sums=('a18253207569e308a35f31f6837872181b436807da8e717d8206669b1091b177')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}/context/opencensus-context"
  python setup.py build

  cd "${srcdir}/${_pkgname}-${pkgver}"
  python setup.py build
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}/context/opencensus-context"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build

  cd "${srcdir}/${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
