# Maintainer: Ivan Shapovalov <intelfx@intelfx.name>
# Contributor: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Gavin Yancey <gyancey@hmc.edu>
# Contributor: Timofey Titovets <nefelim4ag@gmail.com>

pkgname=bees-git
_pkgname=bees
pkgver=0.11.rc4.r7.g47243ae
pkgrel=1
pkgdesc="Best-Effort Extent-Same, a btrfs deduplicator daemon"
arch=('x86_64')
url="https://github.com/Zygo/bees"
license=('GPL-3.0-only')
depends=('util-linux-libs' 'bash')
makedepends=('git' 'btrfs-progs' 'systemd')
provides=('bees')
conflicts=('bees')
source=('git+https://github.com/Zygo/bees.git#branch=master')
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_pkgname}"
  git describe --long --tag | sed 's/^v//;s/[^-]*-g/r&/;s/-/./g'
}

prepare() {
  cd "${srcdir}/${_pkgname}"
# Honor system build flags
  sed -e "s|^CCFLAGS =.*|CCFLAGS = $CFLAGS|" -i makeflags
}

build() {
  cd "${srcdir}/${_pkgname}"
  make BEES_VERSION="${pkgver}"
}

package() {
  cd "${srcdir}/${_pkgname}"

  make install DESTDIR="${pkgdir}" BEES_VERSION="${pkgver}"

  mv "${pkgdir}/usr/sbin" "${pkgdir}/usr/bin"
}

