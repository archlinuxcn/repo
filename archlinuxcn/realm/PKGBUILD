# Maintainer: Hao Long <imlonghao@archlinuxcn.org>
# Co-Maintainer: huyz

pkgname=realm
pkgver=2.9.3
pkgrel=1
pkgdesc="A simple, high performance relay server written in rust."
url="https://github.com/zhboner/realm"
arch=("x86_64")
license=("MIT")
depends=("glibc" "gcc-libs")
makedepends=("cargo-nightly" "cmake" "clang")
options=("!lto")
source=("${pkgname}-${pkgver}.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz"
        "realm.service"
        "realm@.service")
sha512sums=('dcd3eec168a0d9690ec0ec3b5b906054ac89d08e0e9c774f7dd0a4b1ab3a75c6c974b377a740ac47a836df0e34ffdbe19aac4fe1891888aded1b3761dad391e0'
            'bc7f6ad263e9d34caf5d07a6c76b1833a18a1ab52e8b1813d6227ffb286d714efe0272931b9afc4c1db0994fbe65b445392a61657c1447b6145decef4a2ebc32'
            '61a61c7f16fe175f38d6dbef56a6e0b9ac6d66dc3d8627a8a70f921177027d9160ce45e1b6ca4034117e053ea79d13a02ceacfd8ce9ef3a15373ca989192fef8')

prepare() {
  cd $pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=nightly
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=nightly
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release
}

package() {
  install -Dm644 realm.service "$pkgdir/usr/lib/systemd/system/realm.service"
  install -Dm644 realm@.service "$pkgdir/usr/lib/systemd/system/realm@.service"
  cd $pkgname-$pkgver
  install -Dm755 "target/release/$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm644 readme.md "$pkgdir/usr/share/doc/$pkgname/readme.md"
  install -Dm644 "examples/full.json" "$pkgdir/etc/realm/config.json.example"
  install -Dm644 "LICENSE" "$pkgdir/usr/share/licenses/realm/LICENSE"
}
