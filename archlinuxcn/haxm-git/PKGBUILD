# Contributor: Pedro H Lara Campos <root@pedrohlc.com>
pkgname=haxm-git
epoch=1
pkgver=410.1aa03e2
pkgrel=3
pkgdesc="IntelÂ® Hardware Accelerated Execution Manager - a hardware-assisted virtualization engine (git version)"
arch=('x86_64' 'i686')
url="https://software.intel.com/en-us/android/articles/intel-hardware-accelerated-execution-manager"
license=('BSD')
depends=('linux>4' 'nasm>=2.11')
makedepends=(git linux-headers)
provides=(haxm)
conflicts=(haxm)
_extramodules=extramodules-ARCH
source=("$pkgname::git+https://github.com/intel/haxm.git")
md5sums=('SKIP')
install='haxm-git.install'

pkgver() {
  cd "$srcdir/$pkgname"
  printf "%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir/$pkgname/platforms/linux"
  sed -i 's|#include <linux/slab.h>|#include <linux/slab.h>\n #include <asm/io.h>|g' hax_mem_alloc.c
  _kernver="$(cat /usr/src/linux/version)"
  make KVER="${_kernver}" KDIR="/usr/lib/modules/${_kernver}/build" modules
}

package() {
  cd "$srcdir/$pkgname/platforms/linux"

  _kernver="$(</usr/src/linux/version)"

  install -Dt "${pkgdir}/usr/lib/modules/${_kernver}/extramodules" -m644 haxm.ko

  find "$pkgdir" -name '*.ko' -exec xz -T1 {} +

  mkdir -p "$pkgdir/usr/lib/modules-load.d"

  echo "haxm" >> "$pkgdir/usr/lib/modules-load.d/haxm.conf"

  mkdir -p "$pkgdir/usr/lib/udev/rules.d"

  echo "KERNEL==\"HAX\", GROUP=\"haxm\", MODE=\"0660\"" \
    > $pkgdir/usr/lib/udev/rules.d/99-haxm.rules
  # TODO: Add license
}
