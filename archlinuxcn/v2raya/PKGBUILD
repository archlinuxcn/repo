# Maintainer: Dct Mei <dctxmei@yandex.com>

pkgname=v2raya
_pkgname=v2rayA
pkgver=1.5.4
pkgrel=1
pkgdesc="A Linux web GUI client of Project V"
arch=('x86_64')
url="https://github.com/v2rayA/v2rayA"
license=('AGPL3')
depends=('glibc')
optdepends=('v2ray>=4.37.0-1' 'xray>=1.4.2-1')
makedepends=('go>=2:1.16.0-1' 'nodejs>=14' 'yarn' 'python2')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz"
        "v2raya.service"
        "v2raya@.service")
sha256sums=('5ec7f4103e07fd4f3750284dc396823fcfccab73b0d101098771d419c9d836ac'
            '3498f63fc9c4472d817d16e878ebe603889249710b890578901b884a689d3713'
            '6a7009695ab2eea3ff1970f4472f84f6c439dc766ac19485a8d72a1caac90383')

build() {
    export OUTPUT_DIR="${srcdir}/${_pkgname}-${pkgver}/service/server/router/web/"
    cd "${srcdir}"/"${_pkgname}-${pkgver}"/gui/
    yarn cache clean
    yarn install
    yarn build
    export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
    export CGO_CPPFLAGS="${CPPFLAGS}"
    export CGO_CXXFLAGS="${CXXFLAGS}"
    export CGO_CFLAGS="${CFLAGS}"
    export CGO_LDFLAGS="${LDFLAGS}"
    cd "$srcdir"/"v2rayA-${pkgver}"/service/
    go build -ldflags="-X=github.com/v2rayA/v2rayA/conf.Version=${pkgver} -linkmode=external" -o v2raya
}

package() {
    cd "${srcdir}"/"${_pkgname}-${pkgver}"/
    install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/v2raya/
    install -Dm 755 service/v2raya -t "${pkgdir}"/usr/bin/
    install -dm 750 "${pkgdir}"/etc/v2raya/
    install -Dm 644 "${srcdir}"/v2raya.service -t "${pkgdir}"/usr/lib/systemd/system/
    install -Dm 644 "${srcdir}"/v2raya@.service -t "${pkgdir}"/usr/lib/systemd/system/
    install -Dm 644 install/universal/v2raya.desktop -t "${pkgdir}"/usr/share/applications/
    install -Dm 644 gui/public/img/icons/android-chrome-512x512.png "${pkgdir}"/usr/share/icons/hicolor/512x512/apps/v2raya.png
}
