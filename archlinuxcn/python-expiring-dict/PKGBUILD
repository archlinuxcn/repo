# Maintainer: Butui Hu <hot123tea123@gmail.com>

_pkgname=expiring-dict
_name=expiring_dict
pkgname=python-expiring-dict
pkgver=1.1.1
pkgrel=2
pkgdesc='Python dict with TTL support for auto-expiring caches'
arch=('any')
url='https://pypi.org/project/expiring-dict'
license=('MIT')
depends=(
  python-sortedcontainers
)
makedepends=(
  python-build
  python-setuptools
  python-installer
  python-wheel
)
source=("${_name}-${pkgver}.tar.gz::https://files.pythonhosted.org/packages/source/${_name::1}/${_pkgname}/${_name}-${pkgver}.tar.gz"
        "https://github.com/dparker2/py-expiring-dict/raw/master/LICENSE"
)
sha512sums=('b5985e8b68845bfce9106406d6d335a5fa1d75898a7aa56d311d3cf5d1d2429818ec5a45072cd97a8efdcd8b3533eb8febe0debce2b7c1ad06de1143de6ade26'
            '10febcc48b9f8d7210144cf00edbac8b82482f7ebefb0a1af05125fb3672acff765499f1627f9b80afe24cc926751b104e205b48a414c92c28d9c2d2b7c99814')

build() {
  cd "${_name}-${pkgver}"
  python -m build --wheel --no-isolation
}

package() {
  cd "${_name}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 "${srcdir}/LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
