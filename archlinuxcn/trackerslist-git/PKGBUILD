# Maintainer: Roald Clark <roaldclark@gmail.com>

_pkgname=trackerslist
pkgname="${_pkgname}-git"
pkgver=20250717.220700
pkgrel=1
pkgdesc="An updated list of public BitTorrent trackers"
arch=('any')
url="https://github.com/ngosang/${_pkgname}"
license=('GPL-2.0-or-later')
makedepends=('git')
provides=("${_pkgname}=${pkgver}")
conflicts=(${_pkgname})
source=("${_pkgname}::git+${url}.git")
sha256sums=('SKIP')

pkgver() {
    cd "${srcdir}/${_pkgname}"
    printf "%s" "$(TZ=UTC git log -1 --pretty='%cd' --date=format-local:%Y%m%d.%H%M%S)"
}

package() {
    cd "${srcdir}/${_pkgname}"
    install -Dm0644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
    install -Dm0644 blacklist.txt -t "${pkgdir}/usr/share/trackerslist/"
    install -Dm0644 trackers_*.txt -t "${pkgdir}/usr/share/trackerslist/trackers/"
}
