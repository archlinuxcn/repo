# Maintainer: Butui Hu <hot123tea123@gmail.com>

_pkgname=SZ
pkgname=sz
pkgver=2.1.8.3
pkgrel=1
pkgdesc='Error-bounded Lossy Data Compressor (for floating-point/integer datasets)'
license=('BSD')
arch=('x86_64')
url='https://collab.cels.anl.gov/display/ESR/SZ'
depends=(
  'hdf5'
  'netcdf'
  'python-numpy'
  'zstd'
)
makedepends=(
  'cmake'
  'gcc-fortran'
  'swig'
)
checkdepends=(    
  'cunit'
)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/disheng222/SZ/archive/v${pkgver}.tar.gz")
sha256sums=('be94f3c8ab03d6849c59a98e0ebf80816a6b8d07a1d762a4b285498acb2f3871')

build() {
  mkdir build
  cd build
  cmake "${srcdir}/${_pkgname}-${pkgver}" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_INSTALL_RPATH=ON \
    -DBUILD_FORTRAN:BOOL=ON \
    -DBUILD_HDF5_FILTER:BOOL=ON \
    -DBUILD_NETCDF_READER:BOOL=ON \
    -DBUILD_PYTHON_WRAPPER:BOOL=ON \
    -DBUILD_SHARED_LIBS:BOOL=ON \
    -DBUILD_TESTS:BOOL=ON
  make
}

check() {
  cd build
  ctest -v
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
  find "${pkgdir}" -type f -name "test*" -delete
  install -Dm644 "${srcdir}/${_pkgname}-${pkgver}/copyright-and-BSD-license.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
# vim:set ts=2 sw=2 et:

