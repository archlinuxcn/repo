# Maintainer: Brendan Szymanski <bscubed@pm.me>

_pkgname=yuzu
pkgname=$_pkgname-git
pkgver=r13642.02339c2de
pkgrel=1
pkgdesc="An experimental open-source Nintendo Switch emulator/debugger (mainline GIT version)"
arch=('i686' 'x86_64')
url="https://github.com/yuzu-emu/yuzu-mainline"
license=('GPL2')
depends=('shared-mime-info' 'desktop-file-utils' 'sdl2' 'qt5-base' 'qt5-multimedia' 'qt5-tools' 'libxkbcommon-x11')
makedepends=('git' 'cmake' 'python2')
optdepends=('qt5-wayland: for Wayland support')
provides=('yuzu' 'yuzu-canary-git' 'yuzu-cmd' 'yuzu-mainline' 'yuzu-canary' 'yuzu-mainline-git')
conflicts=('yuzu-canary-git' 'yuzu-master-git')
replaces=('yuzu-canary-git' 'yuzu-mainline-git')
source=("$_pkgname::git+https://github.com/yuzu-emu/yuzu-mainline")
md5sums=('SKIP')

pkgver() {
	cd "$srcdir/$_pkgname"
	echo "r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

prepare() {
	cd "$srcdir/$_pkgname"

	git submodule init
	git submodule update --init --recursive
}

build() {
	# Trick the compiler into thinking we're building from a continuous
	# integration tool so the build number is correctly shown in the title
	cd "$srcdir/$_pkgname"
	export CI=true
	export TRAVIS=true
	export TRAVIS_REPO_SLUG=yuzu-emu/yuzu-mainline
	export TRAVIS_TAG=$(git describe --tags)
	
	# Hopefully temporary fix for a compilation error involving fmt
	CXXFLAGS+=" -DFMT_USE_USER_DEFINED_LITERALS=0"
	
	mkdir -p build && cd build
	cmake .. \
	  -DCMAKE_INSTALL_PREFIX=/usr \
	  -DCMAKE_BUILD_TYPE=Release \
	  -DYUZU_USE_BUNDLED_UNICORN=ON \
	  -DYUZU_ENABLE_COMPATIBILITY_REPORTING=ON \
	  -DENABLE_COMPATIBILITY_LIST_DOWNLOAD=ON \
	  -DUSE_DISCORD_PRESENCE=ON
	make
}

# Tests removed temporarily as they haven't been updated and throw a false error
#check() {
#	cd "$srcdir/$_pkgname/build"
#	make test
#}

package() {
	cd "$srcdir/$_pkgname/build"
	make DESTDIR="$pkgdir/" install
	
	# Temporary fix until yuzu fixes the zlib and libzip patch
	cd "$pkgdir/usr"
	rm -rf include lib lib64 share/man share/pkgconfig
}
