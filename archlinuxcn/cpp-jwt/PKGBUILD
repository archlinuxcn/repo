# Maintainer: Marco Rubin <marco.rubin@protonmail.com>

pkgname=cpp-jwt
pkgver=1.5
pkgrel=3
pkgdesc="JSON Web Token library for C++"
arch=('any')
url="https://github.com/arun11299/cpp-jwt"
license=('MIT')
depends=('nlohmann-json' 'openssl')
makedepends=('cmake')
checkdepends=('gtest' 'expat')
provides=("$pkgname=$pkgver")
conflicts=($pkgname-git)
source=("$url/archive/refs/tags/v$pkgver.tar.gz")
b2sums=('6df13d10b1f20c9422bf2da5e87f031e6c4ede655c550c5f6682f0010e3a4008777cffd29e57d76537d6bc2da88d85ae1b1bc719bd2e25ee23afbb28622b4152')

prepare() {
    # fix outdated version
    cd $pkgname-$pkgver
    sed -i s/1.2.0/1.4.0/ CMakeLists.txt
}

build() {
    cd $pkgname-$pkgver
    cmake -B build \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCPP_JWT_BUILD_EXAMPLES=OFF \
		-DCPP_JWT_BUILD_TESTS="$CHECKFUNC" \
		-DCPP_JWT_USE_VENDORED_NLOHMANN_JSON=OFF \
		-Wno-dev
    cmake --build build
}

check() {
    cd $pkgname-$pkgver
    ctest --test-dir build
}

package() {
    cd $pkgname-$pkgver
    DESTDIR="$pkgdir" cmake --install build
    install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
