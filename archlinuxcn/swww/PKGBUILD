# Maintainer: Grzegorz Koperwas <admin@grzegorzkoperwas.site>

pkgname=swww
pkgver=0.8.2
pkgrel=1
pkgdesc="Efficient animated wallpaper daemon for wayland, controlled at runtime."
arch=('x86_64' 'aarch64' 'riscv64')
url="https://github.com/Horus645/${pkgname}"
license=('GPL-3.0-or-later')
depends=('gcc-libs' 'lz4' 'libxkbcommon')
makedepends=('cargo' 'scdoc')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('6733cda771a0e635dbd00f7aef78ed60f1ccdf640647ecfe02d0cdfdef996b68')
options=(!lto)

prepare() {
	# export RUSTUP_TOOLCHAIN=nightly
	export RUSTUP_TOOLCHAIN=stable
	cd "${pkgname}-${pkgver}/"
	cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
	cd "${pkgname}-${pkgver}/"
	export RUSTUP_TOOLCHAIN=stable
	cargo build --release

	# manpages
	./doc/gen.sh
	for page in $(ls ./doc/generated/*.1); do
		gzip -f "${page}"
	done
}

package() {
	cd "${pkgname}-${pkgver}/"

	install -Dm755 target/release/{swww,swww-daemon} -t "${pkgdir}/usr/bin/"
	install -Dm644 "completions/swww.bash" "${pkgdir}/usr/share/bash-completion/completions/swww"
	install -Dm644 "completions/swww.fish" -t "${pkgdir}/usr/share/fish/vendor_completions.d/"
	install -Dm644 "completions/_swww" -t "${pkgdir}/usr/share/zsh/site-functions/"
	install -Dm644 "README.md" -t "${pkgdir}/usr/share/doc/${pkgname}/"
	install -Dm644 "LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}/"

	# manpages
	cd ./doc/generated
	for page in $(ls *.1.gz); do
		install -Dm644 "${page}" "${pkgdir}/usr/share/man/man1/${page}"
	done
}
