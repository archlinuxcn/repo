# Maintainer: Butui Hu <hot123tea123@gmail.com>

pkgname=python-pyvips
_pkgname=pyvips
pkgver=2.2.3
pkgrel=1
pkgdesc='Python binding for libvips using cffi'
arch=('x86_64')
url='https://github.com/libvips/pyvips'
license=('MIT')
depends=(
  glib2
  glibc
  libvips
  python-cffi
  python-numpy
  python-pkgconfig
)
makedepends=(
  imagemagick
  libheif
  libjxl
  openslide
  poppler-glib
  python-build
  python-installer
  python-setuptools
  python-wheel
)
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/libvips/pyvips/archive/refs/tags/v${pkgver}.tar.gz"
)
sha512sums=('c46f08f7b509305dae707c202e778372fe26430fbbfca7246b4914600554b1e85c5572ef12b68dd2184a1dc691f6f7345908a41743396121d1fb59ef1c54a0fe')

build() {
  cd "${_pkgname}-${pkgver}"
  python -m build --wheel --no-isolation
}

package() {
  cd "${_pkgname}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 "LICENSE.txt" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
