# Maintainer: Bocheng Zou <bocheng.zou@outlook.com>
# Contributor: Florian Latifi <mail@florian-latifi.at>
# Contributor: Eddy <e.pedroni91 at gmail>
# Contributor: Shanto <shanto at hotmail>
# Contributor: Jesus Jerez <jhuss@archlinux.org.ve>

pkgname=eclipse-platform
pkgver=4.36
_pkgbuild="202505281830"
pkgrel=1
pkgdesc="A minimal installation suitable for complete per-user customization with the built-in Eclipse package manager"
url="https://www.eclipse.org"
arch=("x86_64")
license=("EPL")
depends=("java-runtime>=11" "unzip" "webkit2gtk")
optdepends=()
conflicts=("eclipse-common")
provides=("eclipse=$pkgver")
options=(!strip)

source=(
  "http://www.eclipse.org/downloads/download.php?file=/eclipse/downloads/drops4/R-${pkgver}-${_pkgbuild}/eclipse-platform-${pkgver}-linux-gtk-x86_64.tar.gz&r=1"
  "eclipse.sh"
  "eclipse.desktop"
)

sha512sums=('cbd9055e959bd951d0cc52d9511db7ce6409618e4cba06a7f8f8f043510f3fd6ce2ddc846e8db5a52f79c941b41db6e78ed249bb4355999f22999f2ac9d6a67d'
            '5d0068de0b134468d6a9f0c2be1ac7a8253152cac9e9561ba32c0bc11bb503dbcc7dba64ff3989179c3276bac9153bae46205d86978442a067837eb8677c384d'
            '44b1427e884dc455726ef93276c800f7f0079c5008dd67e55084bb73f4b9e095397f126230ecdc506cb576a5747d18e216f531bf99876a0f9457d8cd5abed9d2')

package() {
  install -d ${pkgdir}/usr/bin ${pkgdir}/usr/lib ${pkgdir}/usr/share/applications
  install -m755 "${srcdir}/eclipse.sh" "${pkgdir}/usr/bin/eclipse"
  install -Dm644 "${srcdir}/eclipse.desktop" "${pkgdir}/usr/share/applications/"

  for _i in 16 32 48 256; do
    install -Dm644 "${srcdir}"/eclipse/plugins/org.eclipse.platform_*/eclipse${_i}.png "$pkgdir/usr/share/icons/hicolor/${_i}x${_i}/apps/eclipse.png"
  done

  mv "${srcdir}/eclipse" "${pkgdir}/usr/lib/"
}
