# Maintainer: Chi_Tang <me@chitang.dev>

pkgname=nekoray-git
_pkgname=${pkgname%-git}
pkgver=3.18.r0.g69c1d01
pkgrel=2
pkgdesc="Qt based cross-platform GUI proxy configuration manager (backend: v2ray / sing-box)"
arch=('x86_64')
url="https://matsuridayo.github.io"
license=('GPL')
makedepends=('cmake' 'git' 'ninja' 'go')
depends=(
	'qt6-base' 'qt6-svg' 'qt6-tools'
	'protobuf' 'yaml-cpp' 'zxing-cpp'
)

optdepends=(
	'v2ray-domain-list-community: geosite data for NekoRay'
	'v2ray-geoip: geoip data for NekoRay'
	# AUR
	'sing-geoip: geoip data for NekoBox'
	'sing-geosite: geosite data for NekoBox'
)

source=(
	"git+https://github.com/MatsuriDayo/nekoray.git"
	'nekoray.desktop'
)
sha512sums=('SKIP'
	'c8ad5a0cf5d813e1e35b827f92d526c022d529d34dd4019066b550c8f0dbdb89b0fb62e6bfc650e8c18acac826591e858eff3b2e5e6c6f5014f0f4b50f1fa88b')

pkgver() {
	cd "${_pkgname}/"
	git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/v//'
}

build() {
	cd "${_pkgname}/"

	git submodule init
	git submodule update
	bash libs/get_source.sh

	pushd .
	mkdir build && cd build
	cmake -GNinja -DQT_VERSION_MAJOR=6 -DNKR_PACKAGE=ON ..
	ninja

	popd
	GOOS=linux GOARCH=amd64 ./libs/build_go.sh
}

package() {
	install -dm755 ${pkgdir}/usr/lib/nekoray
	install -dm755 ${pkgdir}/usr/bin
	install -dm755 ${pkgdir}/usr/share/pixmaps
	install -dm755 ${pkgdir}/usr/share/applications

	# assets
	install -Dm644 ${srcdir}/nekoray.desktop ${pkgdir}/usr/share/applications/nekoray.desktop
	install -Dm644 ${srcdir}/nekoray/res/public/nekoray.png ${pkgdir}/usr/share/pixmaps/
	install -Dm644 ${srcdir}/nekoray/res/public/nekobox.png ${pkgdir}/usr/share/pixmaps/

	# core
	install -Dm755 ${srcdir}/nekoray/deployment/linux64/nekobox_core ${pkgdir}/usr/lib/nekoray/
	install -Dm755 ${srcdir}/nekoray/deployment/linux64/nekoray_core ${pkgdir}/usr/lib/nekoray/

	# app
	install -Dm755 ${srcdir}/nekoray/build/nekoray ${pkgdir}/usr/lib/nekoray/

	ln -s /usr/lib/nekoray/nekoray ${pkgdir}/usr/bin/nekoray
}
