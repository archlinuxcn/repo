# Maintainer: Hu Butui <hot123tea123@gmail.com>
# Contributor: Grey Christoforo <first name at last name dot net>

_pkgname=plotly.py
pkgname=python-plotly
pkgver=5.6.0
pkgrel=1
pkgdesc='The interactive graphing library for Python (includes Plotly Express)'
url='https://plotly.com/python'
depends=(
  python-matplotlib
  python-numpy
  python-pandas
  python-retrying
  python-six
)
optdepends=(
  'ipython: ipython support'
  'python-jupyter: jupyter support'
  'python-ipykernel: jupyter support'
  'python-pillow: image uri conversion'
)
makedepends=(
  jupyter
  jupyterlab
  npm
  python-setuptools
)
license=('MIT')
arch=('any')
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/plotly/plotly.py/archive/v${pkgver}.tar.gz")
sha512sums=('56b6797c56a1d98b5eea1e21ec8179b8ea32f8d4f19d89f5b13d015950dcd970e61eb84a69bc9304d6b64db54006140a0564b621631859593af7860fbf10056d')

build() {
  cd ${_pkgname}-${pkgver}/packages/python/plotly
  python setup.py build
}

package() {
  cd ${_pkgname}-${pkgver}/packages/python/plotly
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  mv -vf "${pkgdir}/usr/etc" "${pkgdir}"
}
# vim:set ts=2 sw=2 et:
