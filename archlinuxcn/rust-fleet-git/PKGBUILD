# Maintainer: Y7n05h <Y7n05h(aT)protonmail--d0t--com>
# Contributor: icepie <icepie.dev@gmail.com>

pkgname="rust-fleet-git"
_pkgname="fleet"
pkgver=0.0.8.152.g97c5bd2
pkgrel=1
pkgdesc="The blazing fast build tool for Rust."
arch=("x86_64")
url="https://github.com/dimensionhq/fleet"
license=("MIT")
depends=('cargo' 'lld' 'clang' 'sccache' 'openssl')
optdepends=()
makedepends=("git")
provides=("rust-fleet")
conflicts=("fleet-rust-bin" "fleet-rust-git" "rust-fleet")
replaces=("fleet-rust-git")
source=("$_pkgname::git+https://github.com/dimensionhq/fleet.git")
sha256sums=("SKIP")

pkgver() {
	cd "$_pkgname"
	echo "$(grep '^version =' Cargo.toml | head -n1 | cut -d\" -f2).$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

build() {
	cd $_pkgname
	export RUSTUP_TOOLCHAIN=stable
	cargo build --release
}

check() {
	cd $_pkgname
	cargo test --release
}

package() {
	cd "$srcdir/$_pkgname"
	install -Dm755 target/release/$_pkgname "$pkgdir/usr/bin/$_pkgname"
}
