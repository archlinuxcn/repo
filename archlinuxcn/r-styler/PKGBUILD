# Maintainer: Hu Butui <hot123tea123@gmail.com>

_pkgname=styler
_pkgver=1.4.1
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//[:-]/.}
pkgrel=1
pkgdesc='Non-Invasive Pretty Printing of R Code'
arch=('any')
url="https://cran.r-project.org/package=${_pkgname}"
license=('MIT')
depends=(
  r
  r-backports
  r-cli
  r-magrittr
  r-purrr
  r-r.cache
  r-rematch2
  r-rlang
  r-rprojroot
  r-tibble
  r-withr
  r-xfun
)
optdepends=(
  r-data.tree
  r-digest
  r-dplyr
  r-here
  r-knitr
  r-prettycode
  r-rmarkdown
  r-roxygen2
  r-rstudioapi
  r-testthat
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
sha256sums=('bdf0b5d62eac5dec9c9e5baea7fee2c7e5cabebe652fd9d2178bc5af2c23d281')

build() {
  R CMD INSTALL ${_pkgname}_${_pkgver}.tar.gz -l "${srcdir}"
}

package() {
  install -dm0755 "${pkgdir}/usr/lib/R/library"
  cp -a --no-preserve=ownership "${_pkgname}" "${pkgdir}/usr/lib/R/library"
  install -Dm644 "${_pkgname}/LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
