_pkgname=ldoce5viewer-pyqt5
pkgname=ldoce5viewer-oldherl-git
pkgver=0.1.r3.g8be1178
pkgrel=2
pkgdesc="LDOCE5 viewer ported to PyQt5. oldherl's fork."
license=(GPL3)
conflicts=(ldoce5viewer-git ldoce5viewer)
provides=(ldoce5viewer-pyqt5-git)
arch=('x86_64')
url='https://github.com/oldherl/ldoce5viewer-pyqt5'
depends=(python-pyqt5 python-lxml python-whoosh qt5-webkit qt5-multimedia gst-plugins-good gst-plugins-ugly python-gobject)
makedepends=('git')
source=("git+https://github.com/oldherl/ldoce5viewer-pyqt5.git")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  git describe --tags --always | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
	cd "${srcdir}/${_pkgname}"
	make
}

package() {
	cd "${srcdir}/${_pkgname}"

	python setup.py install --root="${pkgdir}"
	install -Dm644 "ldoce5viewer/qtgui/resources/ldoce5viewer.svg" \
	"${pkgdir}/usr/share/pixmaps/ldoce5viewer.svg"
	install -Dm644 "ldoce5viewer.desktop" \
	"${pkgdir}/usr/share/applications/ldoce5viewer.desktop"
	install -Dm644 "LICENSE.txt" \
	"${pkgdir}/usr/share/licenses/$pkgname/LICENSE.txt"
	install -Dm644 "COPYING.txt" \
	"${pkgdir}/usr/share/licenses/$pkgname/COPYING.txt"
}
