# Maintainer: Manuel Wiesinger <m {you know what belongs here} mmap {and here} at>

_ocamlname=ocaml-stdcompat
pkgname=$_ocamlname-git
pkgver=20.git20240529
pkgrel=2
pkgdesc="Compatibility module for OCaml standard library"
url="https://github.com/thierry-martinez/stdcompat"
license=('BSD-2-Clause')
source=(
    "git+https://salsa.debian.org/ocaml-team/ocaml-stdcompat"
    "ocaml53.patch::https://patch-diff.githubusercontent.com/raw/ocamllibs/stdcompat/pull/35.patch"
)
depends=('glibc' 'ocaml')
makedepends=('ocaml-findlib' 'git')
provides=('ocaml-stdcompat')
conflicts=('ocaml-stdcompat')
arch=('x86_64')
b2sums=('SKIP'
        '0fe7c9dac1287ab38cd4f57279ebaec99d59af7b30ee518d7461958eba929c9bebfa7813e5d790f22a4564ee1d5d81e443cdece95f8e6d75e1a2bbf92fb6014d')

prepare() {
    cd "${srcdir}/${_ocamlname}"
    patch -p1 < ../ocaml53.patch
}

check() {
    cd "${srcdir}/${_ocamlname}"
    make test
}

build() {
    cd "${srcdir}/${_ocamlname}"

    make -f Makefile.bootstrap
    ./configure \
        --prefix=/usr \
	--libdir "/usr/lib/ocaml" \
	--docdir "/usr/share/doc"
    make
}

package() {
    cd "${srcdir}/${_ocamlname}"

    local OCAMLFIND_DESTDIR="${pkgdir}$(ocamlfind printconf destdir)"
    env DESTDIR="${pkgdir}" \
    OCAMLFIND_DESTDIR="$OCAMLFIND_DESTDIR" \
    make install

    install -Dm644 COPYING -t $pkgdir/usr/share/licenses/$pkgname/

    # Fix permissions
    chmod 644 $pkgdir/usr/lib/ocaml/stdcompat/libstdcompat__stubs.a
    chmod 644 $pkgdir/usr/lib/ocaml/stdcompat/stdcompat.a
}
