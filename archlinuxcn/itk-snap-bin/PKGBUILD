# Maintainer: Butui Hu <hot123tea123@gmail.com>

pkgname=itk-snap-bin
_pkgname=itk-snap
_date=20250909
pkgver=4.4.0
pkgrel=4
pkgdesc="A software application used to segment structures in 3D medical images"
arch=('x86_64')
url="https://www.itksnap.org"
license=('GPL-3.0-or-later')
depends=(
  gcc-libs
  glib2
  glibc
)
makedepends=(
  gendesk
)
provides=('itk-snap')
conflicts=('itk-snap')
source=(
    "${_pkgname}-${pkgver}.tar.gz::https://sourceforge.net/projects/itk-snap/files/itk-snap/${pkgver}/itksnap-${pkgver}-${_date}-Linux-x86_64.tar.gz"
    "${_pkgname}.png::https://github.com/pyushkevich/itksnap/raw/master/GUI/Qt/Resources/logo_square.png"
)
noextract=("${_pkgname}-${pkgver}.tar.gz")
sha512sums=('6b845d22e087c7f0e6bbabe664ab0cfde19478e72dbcba5b98c73f5131d403162a75dcb34aff806b0f093458e5803a1e9b01d4452e55a2100f13cf90d2f49b03'
            '7d7866a4f28ee645cf4a454488d197a776475d2959d0f9d4d34cf534f34a73ffbb1b92430518f36948b4c25b736990693be07dd345600ed8292e526e2846fca1')

prepare() {
  echo "Creating desktop file"
  gendesk -f -n --pkgname ${_pkgname} \
    --pkgdesc "${pkgdesc}" \
    --categories "Education;Graphics;Science;DataVisualization;MedicalSoftware;Viewer" \
    --icon "${_pkgname}" \
    --exec "/opt/itk-snap/bin/itksnap"
  # manually extract tarball due to their unpredicted name
  mkdir "${srcdir}/${_pkgname}"
  tar xvf "${srcdir}/${_pkgname}-${pkgver}.tar.gz" -C "${srcdir}/${_pkgname}" --strip-components 1
}
package() {
  mkdir -p "${pkgdir}/opt"
  cp -a "${srcdir}/${_pkgname}" "${pkgdir}/opt/${_pkgname}"
  install -Dm644 "${srcdir}/${_pkgname}.desktop" "${pkgdir}/usr/share/applications/${_pkgname}.desktop"
  install -Dm644 "${srcdir}/${_pkgname}.png" "${pkgdir}/usr/share/pixmaps/${_pkgname}.png"
}
# vim:set ts=2 sw=2 et:
