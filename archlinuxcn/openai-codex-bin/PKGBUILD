# Maintainer: Jack Mahoney <jacksmahoney@gmail.com>
# Maintainer: DeepChirp <DeepChirp@outlook.com>
pkgname=openai-codex-bin
pkgver=0.42.0
pkgrel=1
pkgdesc="Lightweight coding agent that runs in your terminal (binary release)"
arch=('x86_64' 'aarch64')
url="https://github.com/openai/codex"
license=('Apache-2.0')
provides=("${pkgname%-bin}")
conflicts=("${pkgname%-bin}" "${pkgname%-bin}-git")
optdepends=(
   'git: for working with git repositories'
   'ripgrep: accelerated large-repo search'
)

source_x86_64=(
    "codex-${pkgver}-x86_64.tar.gz::${url}/releases/download/rust-v${pkgver}/codex-x86_64-unknown-linux-gnu.tar.gz"
)
source_aarch64=(
    "codex-${pkgver}-aarch64.tar.gz::${url}/releases/download/rust-v${pkgver}/codex-aarch64-unknown-linux-gnu.tar.gz"
)
sha256sums_x86_64=('0b87da1bd496bdc8638053adab5718b4edd271c8415d171b70cbf1149e47b2f8')
sha256sums_aarch64=('ab62aa22062c97d04fb255c95008970f493b57c654c9a1787db22175a8113ff0')

package() {
    cd "$srcdir"

    if [[ "$CARCH" == "x86_64" ]]; then
        install -Dm755 "codex-x86_64-unknown-linux-gnu" "$pkgdir/usr/bin/codex"
    fi

    if [[ "$CARCH" == "aarch64" ]]; then
        install -Dm755 "codex-aarch64-unknown-linux-gnu" "$pkgdir/usr/bin/codex"
    fi
}
