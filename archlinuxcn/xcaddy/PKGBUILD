# Maintainer: OpenSorcerer <alex cat fossfed dog eu>
pkgname=xcaddy
pkgver=0.4.5
pkgrel=2
pkgdesc="Build Caddy with plugins"
arch=('i686' 'x86_64' 'armv7h' 'aarch64')
url="https://github.com/caddyserver/xcaddy"
license=('Apache-2.0')
depends=('go' 'glibc')
makedepends=('git')
source=("git+$url.git#tag=v$pkgver")
sha512sums=('ebf1a80311ceaca34945875e152173e0eeaea3094ccca774f23eca76060d264d2d81172502e16d22dea595a3e98153b8e564b179719a558e4a8840c8504b5cf9')

build() {
    cd "$pkgname"
    export CGO_CPPFLAGS="${CPPFLAGS}"
    export CGO_CFLAGS="${CFLAGS}"
    export CGO_CXXFLAGS="${CXXFLAGS}"
    export CGO_LDFLAGS="${LDFLAGS}"
    export GOFLAGS="-buildmode=pie -trimpath -modcacherw"
    go build -o xcaddy "cmd/$pkgname/main.go"
}

package() {
    cd "$pkgname"
    install -Dm755 "./xcaddy" "$pkgdir/usr/bin/xcaddy"
}
