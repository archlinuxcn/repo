# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: josephgbr <rafael.f.f1@gmail.com>
# Contributor: Themaister <maister@archlinux.us>

pkgname=pcsx2-git
pkgver=1.7.0.r92.5362fc9c2
pkgrel=1
pkgdesc='A Sony PlayStation 2 emulator (Generic)'
arch=('x86_64')
url='http://www.pcsx2.net'
license=('GPL2' 'GPL3' 'LGPL2.1' 'LGPL3')
depends=('lib32-llvm-libs' 'lib32-glew' 'lib32-libaio' 'lib32-libcanberra' 'lib32-libjpeg-turbo'
         'lib32-libpulse' 'lib32-nvidia-cg-toolkit' 'lib32-portaudio'
         'lib32-sdl2' 'lib32-soundtouch' 'lib32-wxgtk2' 'lib32-gtk2' 'lib32-libpcap' 'lib32-libxml2')
makedepends=('cmake' 'git' 'png++') 
provides=('pcsx2')
conflicts=('pcsx2')
source=('git+https://github.com/PCSX2/pcsx2.git')
sha256sums=('SKIP')

pkgver() {
  cd pcsx2

  git describe --tags | sed 's/^v//; s/-dev//; s/-/.r/; s/-g/./'
}

prepare() {
  # Disable ZZOgl-PG
  rm -rf pcsx2/plugins/zzogl-pg
}

build() {
  cd pcsx2

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  CXXFLAGS+=' -I/usr/include/harfbuzz' cmake .. \
    -DCMAKE_TOOLCHAIN_FILE='cmake/linux-compiler-i386-multilib.cmake' \
    -DCMAKE_BUILD_TYPE='Release' \
    -DDISABLE_ADVANCED_SIMD='TRUE' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCMAKE_LIBRARY_PATH='/usr/lib32' \
    -DPLUGIN_DIR='/usr/lib32/pcsx2' \
    -DGAMEINDEX_DIR='/usr/share/pcsx2' \
    -DDISABLE_ADVANCE_SIMD='TRUE' \
    -DEXTRA_PLUGINS='TRUE' \
    -DREBUILD_SHADER='TRUE' \
    -DGLSL_API='TRUE' \
    -DPACKAGE_MODE='TRUE' \
    -DXDG_STD='TRUE' \
    -DwxWidgets_CONFIG_EXECUTABLE=/usr/bin/wx-config32
  make
}

package() {
  cd pcsx2/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
