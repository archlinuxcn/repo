# Maintainer: Butui Hu <hot123tea123@gmail.com>
# Contributor: Miguel Useche <migueluseche@skatox.com>
# Contributor: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=dcmtk
_pkgname=dcmtk-DCMTK
pkgver=3.6.5
pkgrel=5
pkgdesc="A collection of libraries and applications implementing large parts the DICOM standard"
arch=("x86_64")
url="http://dicom.offis.de/dcmtk"
license=("custom")
depends=(
  libsndfile
  libtiff
  libxml2
  libwrap
  openjpeg2
)
makedepends=(cmake)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/DCMTK/dcmtk/archive/DCMTK-${pkgver}.tar.gz")
sha512sums=('506c9dbb88f20e7e4c859208e944712f174ceb1053d2657dbb7ed0aceebd38026acb79838e7c9b60b140a03dc69a1bfa4aa31744dbdcbf054f84847fc06fb8cb')

build() {
  cmake \
    -B "build" \
    -S "${_pkgname}-${pkgver}" \
    -DBUILD_SHARED_LIBS:BOOL=ON \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_INSTALL_RPATH:BOOL=ON \
    -DDCMTK_ENABLE_BUILTIN_DICTIONARY:BOOL=ON \
    -DDCMTK_ENABLE_CXX11:BOOL=ON \
    -DDCMTK_ENABLE_PRIVATE_TAGS:BOOL=ON \
    -DDCMTK_ENABLE_STL:BOOL=ON \
    -DDCMTK_WITH_ICONV:BOOL=ON \
    -DDCMTK_WITH_ICU:BOOL=ON \
    -DDCMTK_WITH_OPENSSL:BOOL=ON \
    -DDCMTK_WITH_PNG:BOOL=ON \
    -DDCMTK_WITH_SNDFILE:BOOL=ON \
    -DDCMTK_WITH_TIFF:BOOL=ON \
    -DDCMTK_WITH_XML:BOOL=ON \
    -DDCMTK_WITH_ZLIB:BOOL=ON
  make -C "build"
}

check() {
  cd "build"
  ctest --verbose
}

package() {
  make -C "build" DESTDIR="${pkgdir}/" install
  install -D -m644 "${srcdir}/${_pkgname}-${pkgver}/COPYRIGHT" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # Move configuration files from /usr/etc to /etc
  mv "${pkgdir}/usr/etc/" "${pkgdir}/"

  # delete conflict file with charls
  # TODO: use system's charls
  rm -vf "${pkgdir}/usr/lib/libcharls.so"
}
# vim:set ts=2 sw=2 et:

