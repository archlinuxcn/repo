# Maintainer: Butui Hu <hot123tea123@gmail.com>
# Contributor: Miguel Useche <migueluseche@skatox.com>
# Contributor: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=dcmtk
_pkgname=dcmtk-DCMTK
pkgver=3.6.7
pkgrel=4
pkgdesc="A collection of libraries and applications implementing large parts the DICOM standard"
arch=("x86_64")
url="http://dicom.offis.de/dcmtk"
license=("custom")
depends=(
  charls
  libsndfile
  libtiff.so
  libwrap
  libxml2
  openjpeg2
)
makedepends=(cmake)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/DCMTK/dcmtk/archive/DCMTK-${pkgver}.tar.gz")
sha512sums=('c348298377c26692932f22821b13f2ca422c4a4326dc3bb9aabaf7d993c48488f328d4a641acc58a01d2c48fe160494730405e30928f41e4d04067b2ec5add6f')

build() {
  cmake \
    -B "build" \
    -DBUILD_SHARED_LIBS:BOOL=ON \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_INSTALL_RPATH:BOOL=ON \
    -DDCMTK_DEFAULT_DICT=builtin \
    -DDCMTK_ENABLE_CXX11:BOOL=ON \
    -DDCMTK_ENABLE_PRIVATE_TAGS:BOOL=ON \
    -DDCMTK_ENABLE_STL:BOOL=ON \
    -DDCMTK_WITH_ICONV:BOOL=ON \
    -DDCMTK_WITH_ICU:BOOL=ON \
    -DDCMTK_WITH_OPENSSL:BOOL=ON \
    -DDCMTK_WITH_PNG:BOOL=ON \
    -DDCMTK_WITH_SNDFILE:BOOL=ON \
    -DDCMTK_WITH_TIFF:BOOL=ON \
    -DDCMTK_WITH_XML:BOOL=ON \
    -DDCMTK_WITH_ZLIB:BOOL=ON \
    -S "${_pkgname}-${pkgver}" \
    -Wno-dev
  make -C "build"
}

check() {
  cd "build"
  ctest --verbose
}

package() {
  make -C "build" DESTDIR="${pkgdir}/" install
  install -D -m644 "${srcdir}/${_pkgname}-${pkgver}/COPYRIGHT" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # Move configuration files from /usr/etc to /etc
  mv "${pkgdir}/usr/etc/" "${pkgdir}/"
}
# vim:set ts=2 sw=2 et:

