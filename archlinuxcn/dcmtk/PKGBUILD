# Maintainer: Butui Hu <hot123tea123@gmail.com>
# Contributor: Miguel Useche <migueluseche@skatox.com>
# Contributor: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=dcmtk
_pkgname=dcmtk-DCMTK
pkgver=3.6.9
pkgrel=1
pkgdesc="A collection of libraries and applications implementing large parts the DICOM standard"
arch=("x86_64")
url="http://dicom.offis.de/dcmtk"
license=(LicenseRef-dcmtk)
depends=(
  gcc-libs
  glibc
  icu
  libjpeg-turbo
  libpng
  libsndfile
  libtiff.so
  libxml2
  openjpeg2
  openssl
  zlib
)
makedepends=(
  cmake
)
checkdepends=(
  expat
)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/DCMTK/dcmtk/archive/DCMTK-${pkgver}.tar.gz")
sha512sums=('fcb222182ea653304a1c49db31899a8b08d881916f90d3d35bfab2896aa11473232ac0c0f2195e4d478a6188d3b2c5f54d5172f29c42688c5d05f9bf738ca775')

build() {
  cmake \
    -B "build" \
    -DBUILD_SHARED_LIBS:BOOL=ON \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_INSTALL_RPATH:BOOL=ON \
    -DDCMTK_DEFAULT_DICT=builtin \
    -DDCMTK_ENABLE_CXX11:BOOL=ON \
    -DDCMTK_ENABLE_PRIVATE_TAGS:BOOL=ON \
    -DDCMTK_ENABLE_STL:BOOL=ON \
    -DDCMTK_WITH_ICONV:BOOL=ON \
    -DDCMTK_WITH_ICU:BOOL=ON \
    -DDCMTK_WITH_OPENSSL:BOOL=ON \
    -DDCMTK_WITH_PNG:BOOL=ON \
    -DDCMTK_WITH_SNDFILE:BOOL=ON \
    -DDCMTK_WITH_TIFF:BOOL=ON \
    -DDCMTK_WITH_XML:BOOL=ON \
    -DDCMTK_WITH_ZLIB:BOOL=ON \
    -S "${_pkgname}-${pkgver}" \
    -Wno-dev
  make -C "build"
}

check() {
  cd "build"
  ctest --verbose
}

package() {
  make -C "build" DESTDIR="${pkgdir}/" install
  install -D -m644 "${srcdir}/${_pkgname}-${pkgver}/COPYRIGHT" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
# vim:set ts=2 sw=2 et:

