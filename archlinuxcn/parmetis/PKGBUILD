# Maintainer: Jed Brown <jed@59A2.org>
# Contributor: George Eleftheriou <eleftg>

pkgname=parmetis
pkgver=4.0.3p4
_pkgver=4.0.3p4
_prefix=/usr
pkgrel=3
pkgdesc="A parallel graph partitioning library"
url="http://glaros.dtc.umn.edu/gkhome/metis/parmetis/overview"
arch=(aarch64 x86_64)
license=(custom)
depends=(openmpi metis)
makedepends=(cmake)
source=(https://bitbucket.org/petsc/pkg-parmetis/get/v${_pkgver}.tar.gz)
sha256sums=('071160d45fa588611e90534d28073319035d3650101905a9e508ed2157e155c8')

prepare() {
  mv petsc-pkg-parmetis-* parmetis-$_pkgver
}

build() {
  cd parmetis-$_pkgver
  make config cc=${_prefix}/bin/mpicc mpicc=${_prefix}/bin/mpicc mpicxx=${_prefix}/bin/mpicxx shared=1 prefix=${_prefix}
  make
}

package () {
  cd parmetis-$_pkgver
  make install "DESTDIR=$pkgdir"
}
