# Maintainer: Alexandre Bouvier <contact@amb.tf>
pkgname=dynarmic
pkgver=20240305.162440
pkgrel=4
pkgdesc="An ARM dynamic recompiler"
arch=('aarch64' 'x86_64')
url="https://github.com/yuzu-mirror/dynarmic"
license=('BSD')
depends=('llvm-libs' 'fmt' 'zydis' 'gcc-libs' 'glibc')
makedepends=(
    'git'
	'boost>=1.57'
	'cmake>=3.12'
	'llvm'
	'robin-map>=0.6.2'
	'xbyak>=6.68'
)
provides=('libdynarmic.so')
source=("git+$url")
b2sums=('SKIP')

build() {
	cmake -S $pkgname -B build \
		-DBUILD_SHARED_LIBS=ON \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_CXX_FLAGS_RELEASE="-DNDEBUG" \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DDYNARMIC_IGNORE_ASSERTS=ON \
		-DDYNARMIC_USE_LLVM=ON \
		-DBUILD_TESTING=OFF \
        -DCMAKE_DISABLE_FIND_PACKAGE_Catch2=ON \
        -DDYNARMIC_USE_PRECOMPILED_HEADERS=OFF \
		-Wno-dev
	cmake --build build
}

package() {
	# shellcheck disable=SC2154
	DESTDIR="$pkgdir" cmake --install build
	install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname $pkgname/LICENSE.txt
}
