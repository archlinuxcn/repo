# Maintainer: Butui Hu <hot123tea123@gmail.com>
# Contributor: Andrew Sun <adsun701@gmail.com>
# Contributor: Alexander F. RÃ¸dseth <xyproto@archlinux.org>

pkgname=python-nose-timer
_pkgname=nose-timer
pkgver=1.0.0
pkgrel=2
pkgdesc='Timer plugin for nosetests'
arch=(any)
url='https://github.com/mahmoudimus/nose-timer'
license=(MIT)
depends=(python-nose)
makedepends=(python-setuptools)
checkdepends=(
  'python-mock'
  'python-parameterized'
  'python-termcolor'
)
source=("https://github.com/mahmoudimus/nose-timer/archive/v${pkgver}.tar.gz")
sha512sums=('1afca31d422fc0306721cdcf4e6f23a88dffdbd85e3ec83d6a72c59c584f3919f17f0042129993d857bbf6f4105f58e989a7ee71835a7debf2844d674e587344')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  nosetests --verbosity 2 --with-coverage --cover-erase --cover-html --cover-package=nosetimer tests
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
# vim: ts=2 sw=2 et:
