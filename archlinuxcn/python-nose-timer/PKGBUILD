# Maintainer: Butui Hu <hot123tea123@gmail.com>
# Contributor: Andrew Sun <adsun701@gmail.com>
# Contributor: Alexander F. RÃ¸dseth <xyproto@archlinux.org>

pkgname=python-nose-timer
_pkgname=nose-timer
pkgver=1.0.1
pkgrel=1
pkgdesc='Timer plugin for nosetests'
arch=(any)
url='https://github.com/mahmoudimus/nose-timer'
license=(MIT)
depends=(python-nose)
makedepends=(python-setuptools)
checkdepends=(
  'python-mock'
  'python-parameterized'
  'python-termcolor'
)
source=("https://github.com/mahmoudimus/nose-timer/archive/v${pkgver}.tar.gz")
sha512sums=('ee6b6378998db06892959056916750b094546cc2fbef32cfbdad9dd7b941c21d20eb31eab3f4a32de29b66ba5a33312dd97423778057eee24c861488c110afc7')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  nosetests --verbosity 2 --with-coverage --cover-erase --cover-html --cover-package=nosetimer tests
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
# vim: ts=2 sw=2 et:
