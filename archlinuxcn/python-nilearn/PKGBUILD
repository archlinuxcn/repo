# Maintainer: Butui Hu <hot123tea123@gmail.com>

pkgname=python-nilearn
_pkgname=nilearn
pkgver=0.13.0
pkgrel=1
pkgdesc='Machine learning for NeuroImaging in Python'
arch=('any')
url='https://github.com/nilearn/nilearn'
license=('BSD-3-Clause')
depends=(
  'python-joblib'
  'python-matplotlib'
  'python-nibabel'
  'python-numpy'
  'python-pandas'
  'python-pillow'
  'python-pydicom'
  'python-requests'
  'python-scikit-learn'
  'python-scipy'
)
checkdepends=(
  'python-coverage'
  'python-nose'
  'python-pytest'
)
makedepends=(
  'cython'
  python-build
  python-installer
  python-wheel
  python-hatchling
  python-hatch-vcs
)
source=("${_pkgname}-${pkgver}.tar.gz::https://files.pythonhosted.org/packages/source/${_pkgname::1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
sha512sums=('7f08b5023be3e6874db0af4559c4e3e4198105671684abf1de4cde3eb6adae38e9e9fb9ac5c819d9fd103a22af91ed04a5fda6a3bd18f6e74702da57c29bf4e6')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python -m build --wheel --no-isolation
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
