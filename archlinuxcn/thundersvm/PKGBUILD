# Maintainer: Butui Hu <hot123tea123@gmail.com>
# Contributor: Adrien Wu <adrien.sf.wu@gmail.com>

_pkgname=thundersvm
pkgname=('thundersvm' 'thundersvm-cpu')
pkgver=0.3.4
pkgrel=7
pkgdesc='A Fast SVM Library on GPUs and CPUs'
url='https://github.com/Xtra-Computing/thundersvm'
license=('Apache')
makedepends=(
  chrpath
  cmake
  cuda
  eigen
  gtest
  python-setuptools
  python-wheel
)
depends=(
  python-numpy
  python-scikit-learn
  python-scipy
)
arch=('x86_64')
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/Xtra-Computing/thundersvm/archive/v${pkgver}.tar.gz"
        "0001-fix-build-with-system-gtest.patch::https://github.com/Xtra-Computing/thundersvm/pull/194.patch"
        "0002-fix-build-with-system-eigen.patch::https://github.com/Xtra-Computing/thundersvm/pull/196.patch"
        "0003-fix-build-with-cuda11.patch::https://github.com/Xtra-Computing/thundersvm/pull/233.patch"
)
sha256sums=('c8b4f7ece312a51ab72ef72e550f7ca3973f5328bc128df64158fe6e3b3b8c0e'
            'd8ca14fbfa14599aa503b98be6222165e90bd2549561f0dd6432844a55db02db'
            'ecc951b00bac2082f0410a6289670c724e8e048ca2897ba9ecac4897dfd091e4'
            '917f075816c192c48d813e4f59e2000fd698a5454aba7b2b9935e35c0e79135b')

get_pyver() {
  python -c 'import sys; print(str(sys.version_info[0]) + "." + str(sys.version_info[1]))'
}

prepare() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  patch -p1 -i "${srcdir}/0001-fix-build-with-system-gtest.patch"
  patch -p1 -i "${srcdir}/0002-fix-build-with-system-eigen.patch"
  patch -p1 -i "${srcdir}/0003-fix-build-with-cuda11.patch"
  cp -a "${srcdir}/${_pkgname}-${pkgver}" "${srcdir}/${_pkgname}"
  mkdir "${srcdir}/${_pkgname}/build"
  cp -a "${srcdir}/${_pkgname}" "${srcdir}/${_pkgname}-cpu"

  export BUILD_TAG=""
}

build() {
  cd "${srcdir}/${_pkgname}/build"
  cmake .. \
    -DBUILD_TESTS=ON \
    -DCMAKE_INSTALL_PREFIX:PATH=/usr \
    -DCMAKE_SKIP_INSTALL_RPATH:BOOL=ON \
    -DCMAKE_C_COMPILER=/opt/cuda/bin/gcc \
    -DCMAKE_CXX_COMPILER=/opt/cuda/bin/g++ \
    -DUSE_CUDA:BOOL=ON \
    -DUSE_DOUBLE:BOOL=ON \
    -DUSE_EIGEN:BOOL=OFF
  make
  cd ../python
  python setup.py build

  cd "${srcdir}/${_pkgname}-cpu/build"
  cmake .. \
    -DBUILD_TESTS=ON \
    -DCMAKE_INSTALL_PREFIX:PATH=/usr \
    -DCMAKE_SKIP_INSTALL_RPATH:BOOL=ON \
    -DUSE_CUDA:BOOL=OFF \
    -DUSE_DOUBLE:BOOL=ON \
    -DUSE_EIGEN:BOOL=ON
  make
  cd ../python
  python setup.py build
}

check() {
# no NVIDIA GPU available on the build machine, so disable the tests
# cd "${srcdir}/${_pkgname}/build"
# make runtest
  
  cd "${srcdir}/${_pkgname}-cpu/build"
  make runtest
}

_package() {
  cd "${srcdir}/${pkgname}/build"
  install -Dm755 "bin/thundersvm-predict" "${pkgdir}/usr/bin/thundersvm-predict"
  install -Dm755 "bin/thundersvm-train" "${pkgdir}/usr/bin/thundersvm-train"
  install -Dm755 "lib/libthundersvm.so" "${pkgdir}/usr/lib/libthundersvm.so"
  install -Dm644 "${srcdir}/${pkgname}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  cd "${srcdir}/${pkgname}/python"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  ln -sf "/usr/lib/libthundersvm.so" "${pkgdir}/usr/lib/python$(get_pyver)/site-packages/thundersvm/libthundersvm.so"
  find "${pkgdir}" -type f -executable -exec chrpath --delete {} \;
}

package_thundersvm() {
  provides=(thundersvm)
  depends+=(cuda)
  
  _package
}

package_thundersvm-cpu() {
  desc="${desc} (CPU only)"
  provides=(thundersvm=${pkgver})
  conflicts=(thundersvm)
  
  _package
}
# vim:set ts=2 sw=2 et:

