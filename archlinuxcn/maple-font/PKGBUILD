# Maintainer: Gavin Luo <lunt.luo@gmail.com>

pkgbase="maple-font"

pkgver=7.7
pkgrel=1
pkgdesc="Open source monospace font with round corner, ligatures and Nerd-Font icons for IDE and terminal. 带连字和控制台图标的圆角等宽字体，中英文宽度完美2:1。"
url="https://github.com/subframe7536/maple-font"
arch=("any")
license=("OFL-1.1")
makedepends=("unzip")

pkgname=()
source=()
noextract=()
sha256sums=('b39143c70724a1b7c8deb67b64c12464f86de23770cdd9c87811fd1b9383d6ee'
            'a1ca4ee74ce1c101cdf59a85ee2cc1aac82626e0a083a3f8095dca483b664cd9'
            'b8cb09527237cf1cfff6db395d8dee861e522b50932bb31f6559c54b78619202'
            'bc101015e32c8c1d3df1b1ab1021813b5909e76fd34516de96eeee02f1614b88'
            '6e237b3c671d9dce2f6e32c027c80ff7e8e6b94f19989010190f653e2d3bbe8b'
            '71deecb6c648e52bf9ba8eec1e98773e8d0d8da250899620e6c291f703285250'
            '8dcac8fe45a1255e183a420b16f8f61de9d1eea2b79acd03d25b1dba55525bed'
            '60793a95a5032b40d5fd734fdbc06a7aba1e9f80a328a1ed8a8fb2f0372650e4'
            '5a8736d2eca43d0aece94aca28aa0540d644c2d416582ba8e433b6f991cce4a4'
            'bc700c7bc97eadf8f72c43f1eda2859f393ca8e9134c28e8ef4eb5deaf59bb59'
            '6a95fc262ff583119c884ddcc825fd9f483821533c63edb78af3ccc5ca0859ea'
            'cfaba46cb2f5f92ddcccbdf22690379407ad61bac8cb9ca78584e2fb73337857'
            'b78b45379a3f9d83c53725ac1a092021a0aa78bcdcd5d892b6b6c94d3826189a'
            '27d2487e448ac2773b1c60642c4f708330bde5799bad14cef5452a4ecf404a61'
            '926b128d5d622d360c381977e5379ed23404a7fea9706cdd99964c5c4c497568'
            '819e698b86cbba3ee20a0827dcf8e02652be0477c8ebdbc339f83b9cf829c11d'
            'c674542c4a637cdd74c8960e7b7d1de6b827b6d96409dfa1e6d29d80e89d8bfd'
            '09e7d91110442a0cec7fe48e9e822c2bd3cdf28570c040721b470efc122fac81'
            'f15e58d657a490c9f56060ba1097a0b735c6d1380141112899329a132a5b3c51'
            '6fbb0afb0dc683aebd5719af4090fd0f6602371321ffeb6c4d423ea707d6eb77'
            '25f7b719d889bda77386b1a195789a9124c4bfe4d9cd4e946f618e5ebc54b1b2'
            '738f9552009b3688a577fa8a71160010e2525829dd316d509f858dc64331ed46'
            'd4aab135dbbf66b056ce842d2f0c4c73247d7b434acb82c4d9471b674953427b'
            '5e41f94b7de243b7cbc7c2966f925e0dcb2dea58ebd493cce6dcdaa9b85d8e8e'
            '3e4fc8affc55e1e17002eb41d552fb3c9a7f3fa22ac19ba701a5081352735760'
            '050336b9b6aa1f8b1b74f530f752d4a3880c1a34cf0640a447fb20c0496f9b55'
            '7a877776fdae7b63298cb83f9c4b15ab622d9a912ab38f24800acb062bc15c35'
            '8c9c4bccd517786ea505fc517dfb68b8dd0b0f36ff04fcb7bc6f7663cce01f3a'
            '7b3a191015890cb9cf3e12f560dbbc5bd894306f464aa61a253dabcdd9254891'
            '5dba70e9cee393f2dd59d982bcc8b7255b8a2f9c83d8c92993a85c5d29fd6011'
            'd52f88a04016a7d820cca3cbab031f4b72f5c55c754e25875ce4f7b51a41fe0e'
            '8371a734e02c784d6316feca52cb6dccaedb4ed3816373b03f8896f8bc5dc47d'
            '645ca6b2ebd8a9b64d3237e5987f7b2b46714ee78f42dc25fca2e781b507a5c2'
            'fc4ba09835c32f7e66aaa78d4993b4763f9dccaa30481a3e8b4f0fb863a75ce8'
            '78aa928d1d7308d040bcb351c7b7258661274ac243b27078b181cc6d7d4117c0'
            '8f22fcdd952421a57fab07bba9bcd27db682a76ef806a0ae3fb3a4bb986503ed'
            '190edd97c5f7763de8bd89b005809d2d525b1c5c8d107f081e654a42cfba6bab'
            '823c79537e69b21c713e517c4125cb6b1830ce8ac4438d04a92afd52b7a9a418'
            '7f59731bc5aa6a4f44d0e21cb07a648f3cecc73576c34899773c5266485467a7'
            '806f73249c775249a8573545702b05b440b53f0aefdc4a31f6f35d04e928a8ba')
_variant=("MapleMono-CN-unhinted"
          "MapleMono-CN"
          "MapleMono-NF-CN-unhinted"
          "MapleMono-NF-CN"
          "MapleMono-NF-unhinted"
          "MapleMono-NF"
          "MapleMono-OTF"
          "MapleMono-TTF"
          "MapleMono-Variable"
          "MapleMono-Woff2"
          "MapleMonoNL-CN-unhinted"
          "MapleMonoNL-CN"
          "MapleMonoNL-NF-CN-unhinted"
          "MapleMonoNL-NF-CN"
          "MapleMonoNL-NF-unhinted"
          "MapleMonoNL-NF"
          "MapleMonoNL-OTF"
          "MapleMonoNL-TTF"
          "MapleMonoNL-Variable"
          "MapleMonoNL-Woff2"
          "MapleMonoNormal-CN-unhinted"
          "MapleMonoNormal-CN"
          "MapleMonoNormal-NF-CN-unhinted"
          "MapleMonoNormal-NF-CN"
          "MapleMonoNormal-NF-unhinted"
          "MapleMonoNormal-NF"
          "MapleMonoNormal-OTF"
          "MapleMonoNormal-TTF"
          "MapleMonoNormal-Variable"
          "MapleMonoNormal-Woff2"
          "MapleMonoNormalNL-CN-unhinted"
          "MapleMonoNormalNL-CN"
          "MapleMonoNormalNL-NF-CN-unhinted"
          "MapleMonoNormalNL-NF-CN"
          "MapleMonoNormalNL-NF-unhinted"
          "MapleMonoNormalNL-NF"
          "MapleMonoNormalNL-OTF"
          "MapleMonoNormalNL-TTF"
          "MapleMonoNormalNL-Variable"
          "MapleMonoNormalNL-Woff2")

for _var in "${_variant[@]}"; do
  _filename="${_var}.zip"
  noextract+=("${_filename}")
  source+=("${url}/releases/download/v${pkgver}/${_filename}")

  if [[ "${_var}" == *"OTF"* ]]; then
    _pkgname="otf-${_var//-OTF/}"
  elif [[ "${_var}" == *"Woff2"* ]]; then
    _pkgname="woff2-${_var//-Woff2/}"
  else
    _pkgname="ttf-${_var//-TTF/}"
  fi
  _pkgname=${_pkgname,,}
  pkgname+=("${_pkgname}")

  if [[ "${_var}" == "MapleMono-TTF" ]]; then
    _replaces="ttf-maple"
  elif [[ "${_var}" == "MapleMonoNL-TTF" ]]; then
    _replaces="ttf-maple-autohint"
  elif [[ "${_var}" == "MapleMono-OTF" ]]; then
    _replaces="otf-maple"
  elif [[ "${_var}" == "MapleMono-Woff2" ]]; then
    _replaces="woff2-maple"
  elif [[ "${_var}" == "MapleMono-NF" ]]; then
    _replaces="ttf-maple-nerd"
  elif [[ "${_var}" == "MapleMono-NF-CN" ]]; then
    _replaces="ttf-maple-sc-nerd"
  else
    _replaces=""
  fi

  _features=()
  if [[ "${_var}" == *"Normal"* ]]; then _features+=("使用normal预设"); fi
  if [[ "${_var}" == *"NL"* ]]; then _features+=("无连字"); fi
  if [[ "${_var}" == *"NF"* ]]; then _features+=("内嵌Nerd-Font"); fi
  if [[ "${_var}" == *"CN"* ]]; then _features+=("含中文"); fi
  if [[ "${_var}" == *"unhinted"* ]]; then _features+=("无微调"); fi
  if [[ "${_var}" == *"Variable"* ]]; then _features+=("可变"); fi

  if (( ${#_features[@]} > 0 )); then
    old_ifs="$IFS"
    IFS='，'
    _pkg_desc="${pkgdesc//。/}（${_features[*]}版本）"
    IFS=${old_ifs}
  fi

  _conflicts=""
  if [[ "${_pkgname}" == *"-unhinted" ]]; then
    _conflicts="${_pkgname//-unhinted/}"
  fi

  eval "package_${_pkgname}() {
  replaces=(${_replaces})
  conflicts=(${_conflicts})
  pkgdesc='${_pkg_desc}'

  _package '${_pkgname}' '${_filename}'
}"
done

_package() {
  local _pkg_name=$1
  local _filename=$2

  unzip "${_filename}" -d "${_pkg_name}"

  install -d "${pkgdir}/usr/share/fonts/maple"
  install -Dm644 -t "${pkgdir}/usr/share/fonts/maple" "${_pkg_name}"/Maple*

  install -d "${pkgdir}/usr/share/licenses/${_pkg_name}"
  install -Dm644 -t "${pkgdir}/usr/share/licenses/${_pkg_name}" "${_pkg_name}"/LICENSE.txt
}
