# Maintainer: Chih-Hsuan Yen <base64_decode("eWFuMTIxMjUgQVQgYXJjaGxpbnV4IERPVCBvcmc=")>

_pkgname=python-keepassxc-browser
pkgname=$_pkgname-git
pkgver=r59.a06cb38
pkgrel=2
pkgdesc='Access the KeepassXC Browser API from python'
arch=(any)
url='https://github.com/hrehfeld/python-keepassxc-browser'
license=(AGPL3)
depends=(python python-pysodium)
makedepends=(python-setuptools git)
provides=("$_pkgname=$pkgver")
conflicts=("$_pkgname")
source=(git+https://github.com/hrehfeld/python-keepassxc-browser.git)
sha256sums=('SKIP')

pkgver() {
  cd $_pkgname
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd $_pkgname
  python setup.py build
}

package() {
  cd $_pkgname
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
