# Contributor: David Runge <dave@sleepmap.de>
# Contributor: Arnaud Taffanel <dev@taffanel.org>
# Contributor: Victor HÃ¤ggqvist <aur a snilius d com>

pkgname=solaar-git
pkgver=2157.2d76d77
pkgrel=1
pkgdesc="Device manager for Logitech's Unifying receiver peripherals"
url="https://pwr-solaar.github.io/Solaar/"
license=('GPL2')
arch=('any')
provides=("solaar")
conflicts=("solaar")
depends=(
  'gtk3'
  'libnotify'
  'python-dbus'
  'python-gobject'
  'python-pyudev'
  'python-yaml'
  'python-xlib'
  'python-psutil'
  'python-evdev'
)
optdepends=('libappindicator-gtk3: tray icon support')
makedepends=('git')
source=("${pkgname}::git+https://github.com/pwr-Solaar/Solaar.git")
sha512sums=('SKIP')

pkgver() {
  cd "${pkgname}"
  printf "%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${pkgname}"
  python setup.py build
}

package() {
  cd "${pkgname}"

  tools/po-compile.sh

  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}/"
  # udev
  install -vDm 644 rules.d/42-logitech-unify-permissions.rules \
    "${pkgdir}/usr/lib/udev/rules.d/42-logitech-unify-permissions.rules"
  # docs
  install -vDm 644 {ChangeLog.md,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
