# Maintainer: c0gnitivefl0w

pkgname=seadrive-gui
pkgver=3.0.16
pkgrel=1
pkgdesc="GUI part of seadrive"
arch=('i686' 'x86_64' 'armv7h' 'armv6h' 'aarch64')
url="https://github.com/haiwen/seadrive-gui"
license=('Apache')
depends=(qt6-base qt6-5compat qt6-webengine libgobject-2.0.so libglib-2.0.so
         jansson libsearpc sqlite)
makedepends=(git cmake qt6-tools)
source=("git+https://github.com/haiwen/seadrive-gui#tag=v${pkgver}")
sha256sums=('4c4c48d55de9f48db30985c1e0bb58d4934b69c6d435d24273941df7ab04f603')

prepare() {
  mkdir bin
  ln -sf /usr/bin/qmake6 bin/qmake
  sed seadrive-gui/CMakeLists.txt -i \
      -e 's/CMAKE_MINIMUM_REQUIRED\(.*\)/cmake_minimum_required(VERSION 3.10)/' \
      -e 's/TARGET_LINK_LIBRARIES(seadrive-gui/TARGET_LINK_LIBRARIES(seadrive-gui PRIVATE/'
  sed seadrive-gui/CMakeLists.txt -i \
      -e 's/TARGET_LINK_LIBRARIES(seadrive-gui PRIVATE PRIVATE/TARGET_LINK_LIBRARIES(seadrive-gui PRIVATE/'
}

build() {
  cd seadrive-gui

  mkdir -p build
  cd build

  # Build with Qt6 since the Qt5 cmake configuration is very buggy...
  PATH="${srcdir}/bin:${PATH}" cmake .. \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr

  make
}

package() {
  depends+=(seadrive-daemon)
  cd seadrive-gui/build

  make DESTDIR="${pkgdir}" install
}
