pkgname=julia-git-libalpm-git
pkgver=0.9.0
pkgrel=4
pkgdesc="LibALPM.jl"
url="https://github.com/yuyichao/LibALPM.jl"
arch=('any')
license=('MIT')
makedepends=(git julia-pkg-scripts)
depends=(julia-git pacman)
provides=(julia-git-libalpm)
source=(git://github.com/yuyichao/LibALPM.jl)
md5sums=('SKIP')

pkgver() {
  cd LibALPM.jl

  git describe --tags | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

build() {
  cd LibALPM.jl

  cat > deps/deps.jl <<EOF
const libalpm = "/usr/lib/libalpm.so"
check_deps() = nothing
EOF
}

package() {
  cd LibALPM.jl

  . /usr/lib/julia/julia-install-pkg.sh LibALPM "${pkgdir}" "${pkgname}" julia-git
  rm "${dest_dir}"/REQUIRE
  rm -rf "${dest_dir}"/travis
}
