# Maintainer: Gavin Luo <lunt.luo#gmail.com>
# Contributor: TurtleRuss <tttturtleruss@gmail.com>
# Contributor: coolingtool <coolingtool-backup@outlook.com>

pkgbase=qadwaitadecorations
_pkgname=QAdwaitaDecorations
pkgname=(qadwaitadecorations-qt5 qadwaitadecorations-qt6)
pkgver=0.1.7
pkgrel=3
pkgdesc='Qt decoration plugin implementing Adwaita-like client-side decorations'
arch=('x86_64')
url='https://github.com/FedoraQt/QAdwaitaDecorations'
license=(LGPL2.1-or-later)
makedepends=(make cmake qt5-wayland-decorations qt6-wayland qt5-svg qt6-svg)
source=("https://github.com/FedoraQt/QAdwaitaDecorations/archive/$pkgver/$_pkgname-$pkgver.tar.gz"
        "0001-fix-QT-6.10-build.patch")
b2sums=('69c687e6e909d0260b95bbf2980c8550606149d051562418d5d3489625c39911da73267f01ec263db46b0ef9717de3145942d7ac682aa93ba91853ec41ad0b6d'
        'e2a529dcbc1f026da93fd275a11215940450a20d1c7170a2ea6f7bad1d9f177e792ef315ac3ea808806e2473492cdb5dfd50c479e255b225ac93a8d6631b6f67')

prepare() {
  cd ${_pkgname}-${pkgver}
  patch -p1 < "${srcdir}"/0001-fix-QT-6.10-build.patch
}

build() {
  cmake -B build-$pkgver-qt5 -S $_pkgname-$pkgver \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DHAS_QT6_SUPPORT=true \
        -DUSE_QT6=false
  make -C build-$pkgver-qt5

  cmake -B build-$pkgver-qt6 -S $_pkgname-$pkgver \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DUSE_QT6=true
  make -C build-$pkgver-qt6
}

package_qadwaitadecorations-qt5() {
  pkgdesc='Qt5 decoration plugin implementing Adwaita-like client-side decorations'
  depends=(qt5-wayland-decorations qt5-svg)

  DESTDIR="$pkgdir" make -C build-$pkgver-qt5 install
}

package_qadwaitadecorations-qt6() {
  pkgdesc='Qt6 decoration plugin implementing Adwaita-like client-side decorations'
  depends=(qt6-wayland qt6-svg)

  DESTDIR="$pkgdir" make -C build-$pkgver-qt6 install
}
