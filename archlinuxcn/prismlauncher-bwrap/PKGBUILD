# Maintainer: Kimiblock Moe
# Contributor: Giovanni Harting <anonfunc@archlinux.org>
# Contributor: Sefa Eyeoglu <contact@scrumplex.net>
# Contributor: txtsd <aur.archlinux@ihavea.quest>
# Contributor: seth <getchoo at tuta dot io>
# Contributor: Lenny McLennington <lennymclennington@protonmail.com>
# Contributor: Elijah Gregg <lovetocode999@tilde.team>
# Contributor: Miko <mikoxyzzz@gmail.com>
# Contributor: Cheru Berhanu <aur attt cheru doot dev>
# Contributor: dada513 <dada513@protonmail.com>

pkgname=prismlauncher-bwrap
pkgver=9.4
pkgrel=2
provides=(prismlauncher prismlauncher-portable)
conflicts=(prismlauncher)
pkgdesc="Minecraft launcher with ability to manage multiple instances. Sandboxed by portable."
arch=(x86_64)
url='https://prismlauncher.org'
license=('GPL-3.0-only AND LGPL-3.0-or-later AND LGPL-2.0-or-later AND Apache-2.0 AND MIT AND LicenseRef-Batch AND OFL-1.1')
depends=(
  glibc
  gcc-libs
  java-runtime
  libgl
  qt6-base
  qt6-5compat
  qt6-svg
  qt6-imageformats
  qt6-networkauth
  quazip-qt6
  zlib
  hicolor-icon-theme
  tomlplusplus
  cmark
  glfw
  portable
  xorg-xrandr
  openal
  flite
)
makedepends=(cmake extra-cmake-modules git jdk17-openjdk scdoc ghc-filesystem gamemode desktop-file-utils)
optdepends=('visualvm: Profiling support')
source=("git+https://github.com/PrismLauncher/PrismLauncher#tag=${pkgver}"
        {lionshead,batch,mdi}.license
        portable-config
        start.sh)
b2sums=('57bb4391e8b84265e42b08545bb0cf64046915fca2a80a7f40923f6abf605d9bdd7efefae40694cb5118451346ed4cf8b9d77291b6ebc5b82ec1bb1fbafc16fc'
        'be4289832af95b1cd6e721dc16b84a034533de9718d9b43a49bd08dd6fe4e28eaa15228bfb311867b18fddbda1c9fc4c91f04c6d5c1a3bcc39aaa5161425e3ba'
        '356248a6b86f06d260e0920b49d34034f79f9bc504c7fdc1849d929d2ff9b169e693a8269a2c0b34656b3802970d9b8be41a92b35177eaa3c4ccc89a702f5c9d'
        'b35c447cd9223e096a2bb75e0741a7d0a3a1606af54c957e4f276f4e6861a9b3f06ae1d646137e8d2f24ba2238c9967c76eff8cc631a68d7e48e376056982cc6'
        'e996cbba63dec2f49b5fcf35c9b78a2ca367c08cda6240847b6b6c1ff71b8fdff5734581e887021c08ee3a44f66e9f81b4a6cade3f3ac6e8f60ab1c70b766772'
        '417edca8819febc409431dee12ccb1d9466a274ce28d8bde554e6f28bfe352372ee0d88c5b53ebf95c9d13d0f91e9ff3b363980687fb04d0adf010fa0f8a4e26')

function build() {
	cd PrismLauncher
	export PATH="/usr/lib/jvm/java-17-openjdk/bin/:$PATH"

	cmake -DCMAKE_BUILD_TYPE= \
		-DCMAKE_INSTALL_PREFIX="/usr" \
		-DLauncher_BUILD_PLATFORM="archlinux" \
		-DLauncher_QT_VERSION_MAJOR="6" \
		-DLauncher_ENABLE_JAVA_DOWNLOADER=OFF \
		-Bbuild -S.
		cmake --build build
}

check() {
	cd PrismLauncher/build
	ctest .
}

package() {
	# licenses
	install -Dm644 lionshead.license -t "$pkgdir"/usr/share/licenses/$pkgname/
	install -Dm644 batch.license -t "$pkgdir"/usr/share/licenses/$pkgname/
	install -Dm644 mdi.license -t "$pkgdir"/usr/share/licenses/$pkgname/
	cd PrismLauncher/build
	DESTDIR="$pkgdir" cmake --install .
	install -Dm755 \
		"${srcdir}/start.sh" \
		"${pkgdir}/usr/bin/prismlauncher-bwrap"
	install -Dm755 \
		"${srcdir}/portable-config" \
		"${pkgdir}/usr/lib/portable/info/org.prismlauncher.PrismLauncher/config"
	desktop-file-edit \
		--set-key=Exec \
		--set-value='env _portableConfig=org.prismlauncher.PrismLauncher portable -- %U' \
		"${pkgdir}/usr/share/applications/org.prismlauncher.PrismLauncher.desktop"
	install -d "${pkgdir}/usr/lib/prismlauncher-bwrap/prismlauncher"
	mv "${pkgdir}/usr/bin/prismlauncher" "${pkgdir}/usr/lib/prismlauncher-bwrap/prismlauncher"
	ln -srf "${pkgdir}/usr/bin/prismlauncher-bwrap" "${pkgdir}/usr/bin/prismlauncher"
}
