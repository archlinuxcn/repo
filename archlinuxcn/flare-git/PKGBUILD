# Maintainer: Integral <integral@member.fsf.org>
# Contributor: Julian <juliannfairfax@protonmail.com>

pkgname=flare-git
_pkgname=${pkgname%-git}
pkgver=0.17.0.r6.g59ac74a
pkgrel=1
pkgdesc="Chat with your friends on Signal"
url="https://gitlab.com/schmiddi-on-mobile/${_pkgname}"
arch=("i686" "x86_64" "armv6h" "armv7h" "aarch64")
license=("AGPL-3.0-or-later")
makedepends=("git" "blueprint-compiler" "cargo" "meson")
depends=("libadwaita" "libsecret" "protobuf" "gtksourceview5" "libspelling")
optdepends=("feedbackd: for vibrating notifications")
conflicts=("${_pkgname}")
provides=("${_pkgname}")
source=("git+${url}.git")
sha256sums=("SKIP")

pkgver() {
	cd "${_pkgname}/"
	git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
	arch-meson "${_pkgname}" build
	CFLAGS+=" -ffat-lto-objects" meson compile -C build
}

check() {
	meson test -C build --print-errorlogs
}

package() {
	meson install -C build --no-rebuild --destdir "${pkgdir}"
	install -Dm644 ${_pkgname}/doc/* -t "${pkgdir}/usr/share/doc/${_pkgname}/"
}
