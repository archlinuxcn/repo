# Maintainer: Julian <juliannfairfax@protonmail.com>

pkgname=flare-git
_pkgname=${pkgname%-git}
pkgver=0.13.0.r9.gd1602f1
pkgrel=1
pkgdesc="Chat with your friends on Signal"
url="https://gitlab.com/schmiddi-on-mobile/${_pkgname}"
arch=("i686" "x86_64" "armv6h" "armv7h" "aarch64")
license=("AGPL-3.0-or-later")
makedepends=("git" "blueprint-compiler" "cargo" "meson")
depends=("libadwaita" "libsecret" "protobuf" "gtksourceview5")
optdepends=(
	"feedbackd: for vibrating notifications"
	"libspelling-git: for spell-checking support"
)
conflicts=("${_pkgname}")
provides=("${_pkgname}")
source=("git+${url}.git")
sha256sums=("SKIP")
options=(!lto)

pkgver() {
	cd "${_pkgname}/"
	git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
	cd "${_pkgname}/"
	arch-meson . build/
}

package() {
	cd "${_pkgname}/"
	DESTDIR="${pkgdir}" ninja -C build/ install
	install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${_pkgname}/"
	install -Dm644 README.md -t "${pkgdir}/usr/share/doc/${_pkgname}/"
}
