# Maintainer: taotieren <admin@taotieren.com>

pkgname=python-fontmake
_name=${pkgname#python-}
pkgver=3.5.1
pkgrel=1
pkgdesc='Compile fonts from sources (UFO, Glyphs) to binary (OpenType, TrueType)'
url='https://github.com/googlefonts/fontmake'
license=('Apache-2.0')
arch=(any)
_pydeps=(attrs
         fontmath
         fonttools
         fs # for fonttools[ufo]
         glyphslib
         lxml # for fonttools[lxml] and defcon[lxml]
         ttfautohint-py
         ufo2ft
         ufolib2
         unicodedata2) # for fonttools[unicode]
depends=(python
         "${_pydeps[@]/#/python-}")
checkdepends=(python-compreffor
              python-defcon
              python-mutatormath
              python-pytest
              python-skia-pathops)
makedepends=(python-{build,installer}
             python-setuptools-scm
             python-wheel)
optdepends=(python-mutatormath
            python-skia-pathops)
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.zip)
sha256sums=('9e3240acdabb9e1768ab44a2dfe4540c4f95252c2e52f93e7bb59eb8d6a5b8ad')

build() {
    cd "$_name-$pkgver"
    python -m build -wn
}

check() {
    cd "$_name-$pkgver"
    PYTHONPATH="$PWD/build/lib" pytest -v
}

package() {
    cd "$_name-$pkgver"
    python -m installer -d "$pkgdir" dist/*.whl
    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}

