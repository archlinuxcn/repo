# Maintainer: Jerome Leclanche <jerome@leclan.ch>
# Co-Maintainer: Chih-Hsuan Yen <yan12125@gmail.com>

_pkgname=libqtxdg
pkgname=$_pkgname-git
pkgver=3.10.0.3.ga3727a1
pkgrel=1
pkgdesc="Library providing freedesktop.org specs implementations for Qt."
arch=("i686" "x86_64")
url="https://lxqt.org"
license=("LGPL2.1")
depends=("qt5-base" "qt5-svg")
makedepends=("git" "cmake" "qt5-tools" "lxqt-build-tools-git")
checkdepends=("xorg-server-xvfb")
provides=("$_pkgname=$pkgver")
conflicts=("$_pkgname-qt5-git" "$_pkgname")
replaces=("$_pkgname-qt5-git")
source=("git+https://github.com/lxqt/$_pkgname.git")
sha256sums=('SKIP')


pkgver() {
  cd "$srcdir/$_pkgname"
  git describe --always | sed "s/-/./g"
}

build() {
  mkdir -p build
  cd build
  cmake "$srcdir/$_pkgname" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTS=ON
  make
}

check() {
  cd build

  xvfb-run make test
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
