# Maintainer: Rasphino <im@rasphino.cn>
# Contributor: Shatur <genaloner@gmail.com>

pkgbase=optimus-manager-qt
pkgname=(optimus-manager-qt optimus-manager-qt-kde)
pkgver=1.5.3
pkgrel=1
pkgdesc="A Qt interface for Optimus Manager that allows to configure and switch GPUs on Optimus laptops using the tray menu"
arch=('x86_64')
url="https://github.com/Shatur95/optimus-manager-qt"
license=('GPL3')
depends=('qt5-base' 'qt5-svg' 'qt5-x11extras' 'optimus-manager>=1.3')
makedepends=('cmake' 'git' 'qt5-tools' 'extra-cmake-modules' 'knotifications' 'kiconthemes')
source=(${pkgname}-${pkgver}.tar.gz::"https://github.com/Shatur95/optimus-manager-qt/archive/${pkgver}.tar.gz"
        "git+https://github.com/itay-grudev/SingleApplication.git")
sha256sums=('7e28acb2d4668fddf660a975129750157215e31732dc38390a519ac800f21496'
            'SKIP')

# Move submodules into the project
prepare() {
  cd "${pkgname}-${pkgver}"
  git init
  rm -r "src/third-party/singleapplication"
  git submodule add "${srcdir}/SingleApplication" "src/third-party/singleapplication"
}

build() {
  mkdir -p $pkgname-$pkgver/build
  cd $pkgname-$pkgver/build
  cmake ..
  cmake --build .

  cd ..
  mkdir build-kf5
  cd build-kf5
  cmake -D PLASMA=ON ..
  cmake --build .
}

package_optimus-manager-qt() {
    cd $pkgname-$pkgver/build
    cmake --install . --prefix $pkgdir/usr 
}

package_optimus-manager-qt-kde() {
    _pkgname=optimus-manager-qt-kf5
    depends+=('knotifications' 'kiconthemes')
    replaces=($_pkgname)
    pkgdesc="optimus-manager-qt with KDE API features."
    provides=(optimus-manager-qt)
    conflicts=(optimus-manager-qt)

    cd optimus-manager-qt-$pkgver/build-kf5
    cmake --install . --prefix $pkgdir/usr 
} 
