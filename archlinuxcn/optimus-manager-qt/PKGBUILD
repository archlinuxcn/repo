# Maintainer: Shatur <genaloner@gmail.com>

pkgbase=optimus-manager-qt
pkgname=(optimus-manager-qt optimus-manager-qt-kde)
pkgver=1.2.5
pkgrel=1
pkgdesc="A Qt interface for Optimus Manager that allows to configure and switch GPUs on Optimus laptops using the tray menu"
arch=('x86_64')
url="https://github.com/Shatur95/optimus-manager-qt"
license=('GPL3')
depends=('qt5-base' 'optimus-manager>=0.8')
makedepends=('qt5-tools' 'git' 'knotifications' 'kiconthemes')
source=(${pkgname}-${pkgver}.tar.gz::"https://github.com/Shatur95/optimus-manager-qt/archive/${pkgver}.tar.gz"
        "git+https://github.com/itay-grudev/SingleApplication.git")
sha256sums=('7ed18c39030cb9098c3505b1b74bc234acc953a9ba8e27ded11f7b67b3e440ef'
            'SKIP')

# Move submodules into the project
prepare() {
  cd "${pkgname}-${pkgver}"
  git init
  rm -r "src/third-party/singleapplication"
  git submodule add "${srcdir}/SingleApplication" "src/third-party/singleapplication"
}

build() {
  mkdir build
  cd build
  qmake "../${pkgname}-${pkgver}"
  make

  cd ..
  mkdir build-kf5
  cd build-kf5
  qmake "DEFINES += PLASMA" "../${pkgname}-${pkgver}"
  make
}

package_optimus-manager-qt() {
    cd "build"
    make INSTALL_ROOT="${pkgdir}/" install
    install -Dm755 optimus-manager-qt "${pkgdir}/usr/bin/optimus-manager-qt"
}

package_optimus-manager-qt-kde() {
    _pkgname=optimus-manager-qt-kf5
    replaces=($_pkgname)
    depends+=('knotifications' 'kiconthemes')
    pkgdesc="optimus-manager-qt with KDE API features."
    provides=(optimus-manager-qt)
    conflicts=(optimus-manager-qt)
    cd "build-kf5"
    make INSTALL_ROOT="${pkgdir}/" install
    install -Dm755 optimus-manager-qt "${pkgdir}/usr/bin/optimus-manager-qt"
} 

