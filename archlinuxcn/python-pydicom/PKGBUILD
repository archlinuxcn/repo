# Maintainer: Butui Hu <hot123tea123@gmail.com>

pkgname=python-pydicom
_pkgname=pydicom
pkgver=1.2.2
pkgrel=2
pkgdesc="Read, modify and write DICOM files with python code"
arch=(any)
url="https://github.com/pydicom/pydicom"
license=(MIT)
optdepends=(
    "python-numpy: for working with pixel data"
    "python-pillow: for working with compressed image data"
)
makedepends=("python-setuptools")
checkdepends=("python-pytest" "python-pytest-cov")
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/pydicom/pydicom/archive/v${pkgver}.tar.gz")
sha512sums=('01bde031e65fa0e10e6a40b1534bb0471a26354338d8a624bad298e39b4f7322f4c0ed839566fee85b42995b577bf898625bc07666b8a6767ed905955df2d049')

build() {
  cd "${_pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${_pkgname}-${pkgver}"
  make test-code
  make test-coverage
}

package() {
  cd "${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
