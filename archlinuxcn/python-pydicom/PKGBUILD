# Maintainer: Butui Hu <hot123tea123@gmail.com>

pkgname=python-pydicom
_pkgname=pydicom
pkgver=1.3.0
pkgrel=5
pkgdesc='Read, modify and write DICOM files with python code'
arch=(any)
url='https://github.com/pydicom/pydicom'
license=(MIT)
optdepends=(
    'python-numpy: for working with pixel data'
    'python-pillow: for working with compressed image data'
)
makedepends=(
  'python-setuptools'
  'git'
)
checkdepends=(
  'python-pytest'
  'python-pytest-cov'
)
#source=("${pkgname}-${pkgver}.tar.gz::https://github.com/pydicom/pydicom/archive/v${pkgver}.tar.gz")
source=("${_pkgname}-${pkgver}::git+https://github.com/pydicom/pydicom.git#commit=e6419c1")
sha512sums=('SKIP')

build() {
  cd "${_pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${_pkgname}-${pkgver}"
  make test-code
  make test-coverage
}

package() {
  cd "${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
