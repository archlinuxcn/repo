# Maintainer: Mort Yao <soi@mort.ninja>

pkgname=ulex-git
pkgver=20171127
pkgrel=3
pkgdesc="ulex is a lexer generator for Unicode and OCaml"
url="https://github.com/whitequark/ulex"
arch=(x86_64)
license=('GPL')
depends=('ocaml' 'camlp4')
makedepends=('git' 'ocaml-findlib' 'ocamlbuild')
provides=('ulex')
conflicts=('ulex')
source=("${pkgname}::git://github.com/whitequark/ulex.git")
md5sums=('SKIP')

pkgver() {
  cd "$pkgname"
  git log -1 --pretty=format:%cd --date=short | sed 's/-//g'
}

prepare() {
  cd "$pkgname"
  # build native code as well
  sed -i '1s/.*/ALL=pa_ulex.cmxa ulexing.cmxa pa_ulex.cma ulexing.cma/' Makefile
}

build() {
  cd "$pkgname"
  make
  make doc
}

package() {
  _DOCDIR="${pkgdir}/usr/share/doc/${pkgname}"

  cd "$pkgname"

  export OCAMLFIND_DESTDIR="${pkgdir}$(ocamlfind printconf destdir)"
  mkdir -p "${OCAMLFIND_DESTDIR}"
  make install

  # Install doc
  mkdir -p $_DOCDIR
  cp *.html *.css $_DOCDIR/
}
