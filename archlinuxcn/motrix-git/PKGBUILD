# Maintainer: Butui Hu <hot123tea123@gmail.com>

pkgname=motrix-git
_pkgname=motrix
pkgver=1.5.15.r27.g20c3027
pkgrel=1
pkgdesc='A full-featured download manager'
arch=('any')
url='https://motrix.app/'
license=('MIT')
depends=(
  'aria2'
  'electron'
  'nodejs'
)
makedepends=(
  'gendesk'
  'git'
  'npm'
  'python2'
)
provides=(motrix=${pkgver})
conflicts=(motrix)
source=("${_pkgname}::git+https://github.com/agalwood/Motrix.git")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_pkgname}"
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  sed -i 's/"postinstall"/"ignore"/' "${srcdir}/${_pkgname}/package.json"
  rm -vf "${srcdir}/${_pkgname}/package-lock.json"
  echo "Creating desktop file..."
  gendesk -f -n --pkgname ${_pkgname} \
  --pkgdesc "${pkgdesc}" \
  --categories "Network;FileTransfer;" \
  --icon "${_pkgname}" \
  --exec "${_pkgname}"
}

build() {
  cd "${srcdir}/${_pkgname}"
  npm_config_cache="${srcdir}/npm_cache" npm install
  npm run pack
  npm prune --production
}

package() {
  install -d "${pkgdir}/usr/bin" "${pkgdir}/usr/lib/${_pkgname}" "${pkgdir}/usr/share/applications" "${pkgdir}/usr/share/pixmaps"
  install -Dm644 "${srcdir}/${_pkgname}/extra/linux/engine/aria2.conf" "${pkgdir}/usr/lib/${_pkgname}/engine/aria2.conf"
  install -Dm644 "${srcdir}/${_pkgname}/package.json" "${pkgdir}/usr/lib/${_pkgname}/app/package.json"
  ln -s /usr/bin/aria2c "${pkgdir}/usr/lib/${_pkgname}/engine/aria2c"
  cp -r "${srcdir}/${_pkgname}/dist" "${pkgdir}/usr/lib/${_pkgname}/app"
  cp -r "${srcdir}/${_pkgname}/node_modules" "${pkgdir}/usr/lib/${_pkgname}/app/node_modules"
  install -Dm644 "${srcdir}/${_pkgname}/static/512x512.png" "${pkgdir}/usr/share/pixmaps/${_pkgname}.png"
  # launch script
  cat << EOF > "${pkgdir}/usr/bin/${_pkgname}"
#!/bin/bash
export ELECTRON_IS_DEV=0
exec /usr/bin/electron /usr/lib/${_pkgname}/app
EOF
  chmod +x "${pkgdir}/usr/bin/${_pkgname}"
  install -Dm644 "${srcdir}/${_pkgname}.desktop" "${pkgdir}/usr/share/applications/${_pkgname}.desktop"
  install -Dm644 "${srcdir}/${_pkgname}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
# vim:set ts=2 sw=2 et:
