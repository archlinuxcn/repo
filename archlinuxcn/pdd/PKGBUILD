# Maintainer: Patrick Northon <northon_patrick3@yahoo.ca>
# Contributor: Jameson Pugh <imntreal@gmail.com>

pkgname=pdd
pkgver=1.7.r12.gaedb2f7
pkgrel=2
pkgdesc="Tiny date, time diff calculator."
arch=('any')
url="https://github.com/jarun/pdd"
license=('GPL')
depends=('python-dateutil')
makedepends=('git')
_commit='aedb2f791dcd2266f51a5ca1a1d111b2b5869483'
source=("git+https://github.com/jarun/pdd.git#commit=$_commit")
sha256sums=('bda6622f6c28b0c9a060a8ce97922483ca09f3276652d517a576d0027455b7e6')

pkgver() {
	cd "${pkgname}"
	( set -o pipefail
		git describe --tags --abbrev=7 --long 2>/dev/null | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g' ||
		printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
	)
}

build() {
	cd "${pkgname}"
	make
}

package() {
	cd "${pkgname}"
	make PREFIX=/usr DESTDIR="${pkgdir}" install
}
