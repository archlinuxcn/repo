# Maintainer: Butui Hu <hot123tea123@gmail.com>

_pkgname=python-crc32c
pkgname=python-google-crc32c
epoch=1
pkgver=1.8.0
pkgrel=1
pkgdesc='Python wrapping for google-crc32c'
arch=('x86_64')
url='https://github.com/googleapis/python-crc32c'
license=('Apache-2.0')
depends=(
  glibc
  google-crc32c
  python
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)

source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/googleapis/python-crc32c/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('bcfba53d9d859ebc32d15698caf259674a85935a44ddf1a3edcdccbee85d8fc6ad8154afe78ae8c7e41188188b98b276d2a39cfcc9c21c9ad61e9922f68ebf95')

build() {
  cd "${_pkgname}-${pkgver}"
  python -m build --wheel --no-isolation
}

package() {
  cd "${_pkgname}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
}
# vim:set ts=2 sw=2 et:
