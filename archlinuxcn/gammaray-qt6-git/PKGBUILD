# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=gammaray-qt6-git
_pkgname=gammaray
pkgver=2.11.3.r448.g24ba764ee
pkgrel=1
pkgdesc='A tool for examining the internals of a Qt application and to some extent also manipulate it, work/qt6-support branch'
arch=(x86_64)
url='https://www.kdab.com/gammaray/'
license=(GPL)
depends=(syntax-highlighting qt6-tools qt6-svg qt6-3d)
makedepends=(cmake qt6-wayland qt6-webengine qt6-scxml qt6-location qt6-connectivity doxygen glslang git)
optdepends=('qt6-wayland: Wayland compositor inspector plugin'
            'qt6-webengine: web inspector plugin'
            'qt6-scxml: state machine viewer plugin'
            'qt6-connectivity: bluetooth plugin')
#           'qt6-script: script engine debugger plugin'
#           'kcoreaddons: KJob tracker plugin')
source=($_pkgname::git+https://github.com/KDAB/GammaRay.git#branch=master)
sha256sums=('SKIP')
pkgver() {
    cd $_pkgname
    git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}
prepare() {
# Fix plugin install dir
  sed -e 's|plugins/gammaray|lib/qt6/plugins/gammaray|' -i $_pkgname/CMakeLists.txt
}

build() {
  cmake -B build -S $_pkgname \
    -DGAMMARAY_QT6_BUILD=ON \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DGAMMARAY_INSTALL_QT_LAYOUT=ON \
    -DECM_MKSPECS_INSTALL_DIR=/usr/lib/qt6/mkspecs/modules \
    -DPLUGIN_INSTALL_DIR=/usr/lib/qt6/plugins/gammaray
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
