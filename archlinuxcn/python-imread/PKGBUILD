# Maintainer: Butui Hu <hot123tea123@gmail.com>

_name=imread
pkgname=python-imread
pkgver=0.7.6
pkgrel=1
pkgdesc='Read images to numpy arrays'
arch=('x86_64')
url='https://github.com/luispedro/imread'
license=('MIT')
makedepends=(
  cython
  python-build
  python-installer
  python-setuptools
  python-wheel
)
depends=(
  libjpeg
  libpng
  libwebp
  python-numpy
)
optdepends=(
  "xcftools: for xcf support"
)

source=("${_name}-${pkgver}.tar.gz::https://github.com/luispedro/imread/archive/v${pkgver}.tar.gz")
sha256sums=('f0ab1cb5ec7eb8b61ad8f462c467e994f44790cd226c13f12439b14a46611b73')

build() {
  cd "${srcdir}/${_name}-${pkgver}"
  python -m build --wheel --no-isolation
}

package() {
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  cd ${srcdir}/${_name}-${pkgver}
  install -D -m644 COPYING.MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
  rm -rfv "${pkgdir}/${site_packages}/imread/tests/"
}
# vim:set ts=2 sw=2 et:
