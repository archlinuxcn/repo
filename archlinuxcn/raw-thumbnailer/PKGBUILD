# Maintainer: Gavin Luo <lunt.luo@gmail.com>

pkgname=raw-thumbnailer
pkgver=48.0.0
pkgrel=1
pkgdesc="A lightweight and fast raw image thumbnailer that can be used by file managers."
url="https://gitlab.gnome.org/World/gnome-raw-thumbnailer/"
license=('GPL-2.0-or-later')
arch=('x86_64')
depends=('libopenraw.so=9')
makedepends=('meson' 'python' 'ninja' 'cargo')
source=("$url/-/archive/$pkgver/gnome-$pkgname-$pkgver.tar.gz")
b2sums=('f90efd65e32c31f23e8a7a6f2e8ea451b43f83fc511a46dd12860fd3feda7fdf5b908e86e3f82361bdda1b2c0f730fa3d19a0f3f7f034ef4a50bafea9de83809')

prepare() {
    cd gnome-$pkgname-$pkgver

    export RUSTUP_TOOLCHAIN=stable
    cargo fetch --locked --target $(rustc --print host-tuple)
}

build() {
    export RUSTUP_TOOLCHAIN=stable
    arch-meson gnome-$pkgname-$pkgver build -Dprofile=release
    meson compile -C build
}

package() {
    meson install -C build --destdir "$pkgdir"
}
