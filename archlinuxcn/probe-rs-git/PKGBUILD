# Maintainer: Coelacanthus <coelacanthus@archlinuxcn.org>
# Contributor: Dominic Meiser [git at msrd0 dot de]

pkgname="probe-rs-git"
pkgver=0.29.0.r138.g65628ac
pkgrel=3
pkgdesc='A collection of on chip debugging tools to communicate with microchips.'
url='https://probe.rs'
arch=('aarch64' 'i686' 'x86_64')
license=('Apache-2.0' 'MIT')
depends=(
    'gcc-libs'
    'systemd-libs'
)
makedepends=(
    'cargo'
    'cargo-auditable'
    'cmake'
    'git'
)
provides=(
    'cargo-embed'
    'cargo-flash'
    'probe-rs'
    'rtthost'
    'smoke_tester'
    'target-gen'
    'xtask'
)
conflicts=(
    'cargo-embed'
    'cargo-flash'
    'probe-rs'
    'rtthost'
    'smoke_tester'
    'target-gen'
    'xtask'
)
source=(
    "$pkgname::git+https://github.com/probe-rs/probe-rs.git"
    "$pkgname-69-probe-rs.rules::https://github.com/probe-rs/webpage/raw/refs/heads/master/public/files/69-probe-rs.rules"
)
b2sums=('SKIP'
        '155e37902a17198be77c3aa1f1f9407ab2112083606630f5115ca27261a47345fb646770ff44d90cd258d8bc187d4b2a0f5620b3bc0eadaddd5a845d4226a45f')

pkgver() {
    cd "$pkgname"
    (
        set -o pipefail
        git describe --long --tag --abbrev=7 2>/dev/null | sed 's/^v//g;s/\([^-]*-g\)/r\1/;s/-/./g' ||
            printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
    )
}

prepare() {
	cd "$pkgname"

    git cherry-pick -n a5b2248d804e35faef0b39e3f5a9d4c0ed7c318c^..d66feaccd7fff7ab3b0f9cdc38532134343969c0
    
    # Arch doesn't have this system group, but if user create this group and it's not system group, newer systemd will
    # ignore this rule.
    sed -i \
        -e 's/, MODE="660"//g' \
        -e 's/, GROUP="plugdev"//g' \
        ../"$pkgname-69-probe-rs.rules"

	export RUSTUP_TOOLCHAIN=stable
	cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
	cd "$pkgname"
	
	export RUSTUP_TOOLCHAIN=stable
	export CARGO_TARGET_DIR=target
	CFLAGS+=" -ffat-lto-objects"

	cargo auditable build \
		--frozen \
		--release \
        --all-features

    SHELL=zsh  target/release/probe-rs complete --shell zsh install --manual > _probe-rs
    SHELL=bash target/release/probe-rs complete --shell bash install --manual > probe-rs.bash
    SHELL=fish target/release/probe-rs complete --shell fish install --manual > probe-rs.fish
}

# FIXME: disable because of util::cargo::test::get_binary_artifact_with_cargo_config{,_toml} need thumbv7m-none-eabi std.
_check() {
    export RUSTUP_TOOLCHAIN=stable
    cargo test \
        --workspace \
        --frozen \
        --all-features
}

package() {
	cd "$pkgname"
    find target/release \
        -maxdepth 1 \
        -executable \
        -type f \
        -exec install -Dm0755 -t "$pkgdir/usr/bin/" {} +

	install -Dm644 _probe-rs -t "$pkgdir/usr/share/zsh/site-functions"
	install -Dm644 probe-rs.bash -t "$pkgdir/usr/share/bash-completion/completions/"
	install -Dm644 probe-rs.fish -t "$pkgdir/usr/share/fish/vendor_completions.d"
	install -Dm644 "$srcdir/$pkgname-69-probe-rs.rules" "$pkgdir/usr/lib/udev/rules.d/69-probe-rs.rules"
}

# vim: set sts=4 ts=4 sw=4 et:
