# Maintainer: Hu Butui <hot123tea123@gmail.com>

_pkgname=reprex
_pkgver=2.0.0
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//[:-]/.}
pkgrel=1
pkgdesc='Prepare Reproducible Example Code via the Clipboard'
arch=('any')
url="https://cran.r-project.org/package=${_pkgname}"
license=('MIT')
depends=(
  r
  r-callr
  r-cli
  r-clipr
  r-fs
  r-glue
  r-knitr
  r-rlang
  r-rmarkdown
  r-rstudioapi
  r-withr
)
optdepends=(
  r-covr
  r-fortunes
  r-miniui
  r-mockr
  r-rprojroot
  r-sessioninfo
  r-shiny
  r-spelling
  r-styler
  r-testthat
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
sha256sums=('905377fcc644a485e97caffda3b077f6b3ad8fda09c0bbf378c422e13feb96d1')

build() {
  R CMD INSTALL ${_pkgname}_${_pkgver}.tar.gz -l "${srcdir}"
}

package() {
  install -dm0755 "${pkgdir}/usr/lib/R/library"
  cp -a --no-preserve=ownership "${_pkgname}" "${pkgdir}/usr/lib/R/library"
  install -Dm644 "${_pkgname}/LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
