pkgname=julia-git-makie-src
pkgver=0.24.8
_commit=f9b432dd4409e51962a4d331a74db6f3faab06b4
pkgrel=1
pkgdesc="Makie.jl"
url="https://github.com/MakieOrg/Makie.jl.git"
arch=('any')
license=('MIT')
makedepends=(git julia-pkg-scripts wget)
depends=(julia-git)
source=("git+https://github.com/MakieOrg/Makie.jl.git#commit=$_commit")
sha256sums=('a25ec0644da73187df99b9b7e06351b352a62b8ad03c1dd7990316b4f342d1b4')

build() {
  cd Makie.jl/Makie

  local url
  url=$(sed -ne '/url *= *".*"/{s/ *url *= *"\(.*\)" */\1/p;q}' Artifacts.toml)

  wget -O MakieAssets.tar.gz "$url"

  (
    rm -rf assets/
    mkdir assets/
    cd assets/
    tar xf ../MakieAssets.tar.gz
    rm ../MakieAssets.tar.gz
  )

  sed -i -e 's|artifact"MakieAssets"|joinpath(@__DIR__, "../assets")|' src/Makie.jl
}

package() {
  cd Makie.jl/Makie

  JULIA_INSTALL_SRCPKG=1 . /usr/lib/julia/julia-install-pkg.sh Makie "${pkgdir}" "${pkgname}" julia-git
}
