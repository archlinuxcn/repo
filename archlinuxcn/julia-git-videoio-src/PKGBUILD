pkgname=julia-git-videoio-src
pkgver=1.1.0
_commit=6f965b620b1c2760a6756ee44faa96a5ccbc3d43
pkgrel=2
pkgdesc="VideoIO.jl"
url="https://github.com/JuliaIO/VideoIO.jl.git"
arch=('any')
license=('MIT')
makedepends=(git julia-pkg-scripts)
depends=(julia-git)
source=("git+https://github.com/JuliaIO/VideoIO.jl.git#commit=$_commit"
        0001-Support-FFMPEG-6.patch)
sha256sums=('SKIP'
            '4eda28024aa57f4f52def0f90357a8738f1dc578f956105ec579532871c1e82c')

prepare() {
  cd VideoIO.jl

  patch --no-backup-if-mismatch -Np1 < ../0001-Support-FFMPEG-6.patch
}

package() {
  cd VideoIO.jl

  JULIA_INSTALL_SRCPKG=1 . /usr/lib/julia/julia-install-pkg.sh VideoIO "${pkgdir}" "${pkgname}" julia-git
}
