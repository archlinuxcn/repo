# Maintainer: Yuzu <g311571057 at gmail dot com>

_pkgname=karousel
pkgname=kwin-karousel-git
pkgver=0.13.r6.g9910bc7
pkgrel=1
pkgdesc='KWin tiling script with scrolling '
arch=('any')
url='https://github.com/peterfajdiga/karousel/'
license=('GPL-3.0-or-later')
depends=('qt6-declarative' 'knotifications')
makedepends=('git' 'nodejs' 'kpackage' 'typescript')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("${pkgname%-git}::git+https://github.com/peterfajdiga/karousel")
sha1sums=('SKIP')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
    git describe --tags --abbrev=7 | sed 's/^v//;s/-/.r/;s/-/./'
}

build() {
	cd "$srcdir/${pkgname%-git}"
    tsc -p ./src/main --outFile ./package/contents/code/main.js
    tsc -p ./src/generators/config --outFile ./run-ts-tmp.js
    mkdir -p ./package/contents/config/
    node ./run-ts-tmp.js > ./package/contents/config/main.xml
}

package() {
    install -D -o root -m 755 -d "$pkgdir/usr/share/kwin/scripts/karousel"
    cd $srcdir/${pkgname%-git}
    cp -r package/* "$pkgdir/usr/share/kwin/scripts/karousel/"
}
