# Maintainer: Philip Jones <philj56@gmail.com>

pkgname=tofi-git
_pkgname=${pkgname%-git}
pkgver=0.9.1.r23.g1eb6137
pkgrel=1
pkgdesc="Tiny rofi / dmenu replacement for wlroots-based Wayland compositors."
arch=("x86_64")
url="https://github.com/philj56/${_pkgname}"
license=("MIT")
depends=("freetype2" "harfbuzz" "cairo" "pango" "wayland" "libxkbcommon" "glib2")
makedepends=("git" "meson" "scdoc" "wayland-protocols")
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=("git+${url}.git")
sha512sums=("SKIP")

pkgver() {
	cd "${_pkgname}/"
	git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/v//'
}

prepare() {
	rm -rf build
	CFLAGS=$CFLAGS LDFLAGS=$LDFLAGS meson setup "${_pkgname}" build --prefix /usr -Dbuildtype=release
}

build() {
	ninja -C build
}

check() {
	ninja -C build test
}

package() {
	DESTDIR="${pkgdir}" ninja -C build install
	install -Dm644 "${_pkgname}/LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
