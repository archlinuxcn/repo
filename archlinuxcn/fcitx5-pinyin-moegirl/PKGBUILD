# Maintainer: Hao Long <imlonghao@archlinuxcn.org>

pkgbase=fcitx5-pinyin-moegirl
pkgname=('fcitx5-pinyin-moegirl' 'rime-pinyin-moegirl')
pkgver=20260109
pkgrel=1
pkgdesc="Fcitx 5 Pinyin Dictionary from zh.moegirl.org.cn"
arch=('any')
url="https://github.com/outloudvi/mw2fcitx"
license=('CC-BY-SA-3.0')
source=("moegirl-$pkgver.dict::${url}/releases/download/${pkgver}/moegirl.dict"
        "moegirl-$pkgver.dict.yaml::${url}/releases/download/${pkgver}/moegirl.dict.yaml")
sha256sums=('c4667bb3110b3e53e8dbba120a80d742b96282499cc3f7c37b1205b7f4b4aea7'
            '4bccd04c730c145d03eee5bb38600ea475d62b52a0827659e9b96fa9d70628ce')

package_fcitx5-pinyin-moegirl() {
  install -Dm644 moegirl-$pkgver.dict ${pkgdir}/usr/share/fcitx5/pinyin/dictionaries/moegirl.dict
}

package_rime-pinyin-moegirl() {
  replaces=('fcitx5-pinyin-moegirl-rime')
  install -Dm644 moegirl-$pkgver.dict.yaml ${pkgdir}/usr/share/rime-data/moegirl.dict.yaml
}
