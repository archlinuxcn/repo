# Maintainer: Scott Cheng <aur@chengscott.io>
# Contributer: Grey Christoforo <first name at last name dot net>
# Contributer: Sven-Hendrik Haase <svenstaro at gmail dot com>
# Contributer: Raphael Scholer <rascholer at gmail dot com>
pkgname=python-pep8-naming
_name=${pkgname#python-}
pkgver=0.13.3
pkgrel=2
pkgdesc="This module provides a name plugin for flake8, the Python code checker"
arch=(any)
url="https://github.com/PyCQA/pep8-naming"
license=('MIT')
depends=('python')
provides=('flake-pep8-naming')
makedepends=("python-setuptools"
  python-build
  python-installer
  python-wheel
)
source=("${pkgname}-${pkgver}.tar.gz"::"${url}/archive/${pkgver}.tar.gz")
sha256sums=('d693ebff6b6a6107722b3009265fb5b854aa4b5551df0b76fdb97a39eba98f34')

build() {
  python -m build -nw "${_name}-${pkgver}"
}

package() {
  python -m installer \
      --compile-bytecode 1 \
      --destdir "${pkgdir}" \
      "${_name}-${pkgver}/dist/pep8_naming-"*.whl
  install -Dm644 "${_name}-${pkgver}/LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
