# Maintainer: Scott Cheng <aur@chengscott.io>
# Contributer: Grey Christoforo <first name at last name dot net>
# Contributer: Sven-Hendrik Haase <svenstaro at gmail dot com>
# Contributer: Raphael Scholer <rascholer at gmail dot com>
pkgname=python-pep8-naming
_name=${pkgname#python-}
pkgver=0.14.1
pkgrel=2
pkgdesc="This module provides a name plugin for flake8, the Python code checker"
arch=(any)
url="https://github.com/PyCQA/pep8-naming"
license=('MIT')
depends=('python')
provides=('flake-pep8-naming')
makedepends=("python-build" "python-installer" "python-setuptools" "python-wheel")
source=("${pkgname}-${pkgver}.tar.gz"::"${url}/archive/${pkgver}.tar.gz")
b2sums=('1ecf577041ff53204e9cda8e09723a1ad86d8a1715153cd18a7cefe6554435921ec6738f2caa1397e56a1c57c1a14277fe478f623b4a830ad001e5d8de16112a')

build() {
  python -m build -nw "${_name}-${pkgver}"
}

package() {
  python -m installer \
      --compile-bytecode 1 \
      --destdir "${pkgdir}" \
      "${_name}-${pkgver}/dist/pep8_naming-"*.whl
  install -Dm644 "${_name}-${pkgver}/LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
