# Contributer: Sven-Hendrik Haase <svenstaro at gmail dot com>
# Contributer: Raphael Scholer <rascholer at gmail dot com>
pkgname=python-pep8-naming
_name=${pkgname#python-}
pkgver=0.15.1
pkgrel=1
pkgdesc="This module provides a name plugin for flake8, the Python code checker"
arch=(any)
url="https://github.com/PyCQA/pep8-naming"
license=('MIT')
depends=('python')
provides=('flake-pep8-naming')
makedepends=("python-build" "python-installer" "python-setuptools" "python-wheel")
source=("${pkgname}-${pkgver}.tar.gz"::"${url}/archive/${pkgver}.tar.gz")
b2sums=('fa5807fedcd9fea910e930c313b80ef70ae247d1940adec6d89ad3f77f586dc4288c8dcf341135f58713f644456d6d1867a3b403aaf73e6ede71519a991081e3')

build() {
  python -m build -nw "${_name}-${pkgver}"
}

package() {
  python -m installer \
      --compile-bytecode 1 \
      --destdir "${pkgdir}" \
      "${_name}-${pkgver}/dist/pep8_naming-"*.whl
  install -Dm644 "${_name}-${pkgver}/LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
