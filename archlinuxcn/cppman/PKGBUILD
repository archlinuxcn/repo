# Maintainer: Marco Rubin <marco.rubin@protonmail.com>
# Contributor: motte <ettom22 at hotmail dot com>
# Contributor: Wei-Ning Huang <aitjcize@gmail.com>
pkgname=cppman
pkgver=0.5.7
pkgrel=3
pkgdesc="C++ 98/11/14/17/20 manual pages for Linux, with source from cplusplus.com and cppreference.com."
arch=("any")
url="https://github.com/aitjcize/cppman"
license=("GPL-3.0-only")
conflicts=(cppman-git)
depends=(python-beautifulsoup4 python-html5lib)
makedepends=(python-build python-installer python-setuptools python-wheel)
optdepends=(
"vim: For using vim as a pager"
"perl: for bash/zsh completion"
)

source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha256sums=('df42088a9c8601289e18589aba3506a803eba8bb31446c183153de6936700526')

build() {
    cd $pkgname-$pkgver
    python -m build --wheel --no-isolation
}

package() {
    cd $pkgname-$pkgver
    python -m installer --destdir="$pkgdir" dist/*.whl
}

