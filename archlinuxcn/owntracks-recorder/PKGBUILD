pkgname=owntracks-recorder
pkgver=1.0.1
pkgrel=2
pkgdesc='Lightweight program for storing and accessing location data published via MQTT (or HTTP) by the OwnTracks apps.'
arch=('x86_64')
url='https://github.com/owntracks/recorder'
license=('GPL-2.0-or-later' 'LicenseRef-custom' 'BSD-1-Clause' 'LicenseRef-unknown' 'GPL-2.0-only' "MIT")
depends=(
	'curl'
	glibc
	util-linux-libs
	'libconfig'
	'libsodium'
	'lmdb'
	'lua'
	'mosquitto'
)
makedepends=(
	'coreutils'
	'curl'
	'libconfig'
	'libsodium'
	'lua'
	'mosquitto'
	'pkgconf'
)
backup=(
	'etc/owntracks-recorder.conf'
)
source=(
	"$pkgname-$pkgver.tar.gz::https://github.com/owntracks/recorder/archive/refs/tags/$pkgver.tar.gz"
	"systemd.service"
)
sha256sums=('047040ee49cb411079ed3f129d24b313c8c8d203fe8a99952a0096aee2986fc6'
            '79ba2e2e9446893e43c42eee10182123d6c8cfe110ea43f81bd6b41c2c084e02')

build() {
	cd "${srcdir}/recorder-${pkgver}"

	cp config.mk.in config.mk
	sed -i 's#INSTALLDIR = /usr/local#INSTALLDIR = /usr#g'                                                         config.mk
	sed -i 's#WITH_LUA ?= no#WITH_LUA ?= yes#g'                                                                    config.mk
	sed -i 's#WITH_ENCRYPT ?= no#WITH_ENCRYPT ?= yes#g'                                                            config.mk
	sed -i 's#STORAGEDEFAULT = /var/spool/owntracks/recorder/store#STORAGEDEFAULT = /var/lib/owntracks-recorder#g' config.mk
	sed -i 's#DOCROOT = /var/spool/owntracks/recorder/htdocs#DOCROOT = /usr/share/webapps/owntracks-recorder#g'    config.mk
	sed -i 's#CONFIGFILE = /etc/default/ot-recorder#CONFIGFILE = /etc/owntracks-recorder.conf#g'                   config.mk

	make
}

package() {
	cd "${srcdir}/recorder-${pkgver}"

	# Make installation
	DESTDIR="${pkgdir}" make install

	# Copy systemd service file
	install -vDm644 "${srcdir}/systemd.service" "${pkgdir}/usr/lib/systemd/system/owntracks-recorder.service"

	cd "${pkgdir}"

	# Move ot-recorder out of /usr/sbin
	mkdir -p usr/bin
	mv 'usr/sbin/ot-recorder' usr/bin/ot-recorder
	rm -rf usr/sbin

	rm -rf var

	# Tune owntracks-recorder.conf
	sed -i 's#OTR_STORAGEDIR="/var/spool/owntracks/recorder/store"#OTR_STORAGEDIR="/var/lib/owntracks-recorder"#g' etc/owntracks-recorder.conf
	sed -i 's%# OTR_PORT=1883%OTR_PORT=0%g' etc/owntracks-recorder.conf
	install -Dm644 "${srcdir}/recorder-${pkgver}/LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}"
	chmod 755 "${pkgdir}/etc/owntracks-recorder.conf"
}
