# Maintainer: FabioLolix
# Maintainer: Ã©clairevoyant
# Contributor: ThatOneCalculator <kainoa at t1c dot dev>

pkgname=hyprland-git
pkgver=0.35.0.r100.dfcfb92e
pkgrel=1
pkgdesc="A dynamic tiling Wayland compositor based on wlroots that doesn't sacrifice on its looks."
arch=(x86_64 aarch64)
url="https://github.com/hyprwm/Hyprland"
license=('BSD-3-Clause')
depends=(cairo
	glib2
	glslang
	libdisplay-info
	libdrm
	libglvnd
	libinput
	libliftoff
	libx11
	libxcb
	libxcomposite
	libxfixes
	libxkbcommon
	libxrender
	opengl-driver
	pango
	pixman
	polkit
	seatd
	systemd-libs
	vulkan-icd-loader
	vulkan-validation-layers
	wayland
	wayland-protocols
	xcb-proto
	xcb-util
	xcb-util-errors
	xcb-util-keysyms
	xcb-util-renderutil
	xcb-util-wm
	xorg-xinput
	xorg-xwayland
	libdisplay-info.so
	tomlplusplus
	hyprlang
	libhyprlang.so)
makedepends=(
	git
	cmake
	ninja
	gcc
	meson
	wayland-protocols
	xorgproto)
provides=("hyprland=${pkgver%%.r*}")
conflicts=(hyprland)
replaces=(hyprland-nvidia-git)
source=("git+https://github.com/hyprwm/Hyprland.git"
	"git+https://gitlab.freedesktop.org/wlroots/wlroots.git"
	"git+https://github.com/hyprwm/hyprland-protocols.git"
	"git+https://github.com/canihavesomecoffee/udis86.git"
	"git+https://github.com/wolfpld/tracy.git")
sha256sums=('SKIP'
	'SKIP'
	'SKIP'
	'SKIP'
	'SKIP')

prepare() {
	cd Hyprland
	rm -rf subprojects/wlroots subprojects/hyprland-protocols

	git submodule init
	git config submodule.wlroots.url "${srcdir}/wlroots"
	git config submodule.subprojects/hyprland-protocols.url "${srcdir}/hyprland-protocols"
	git config submodule.subprojects/udis86.url "${srcdir}/udis86"
	git config submodule.subprojects/tracy.url "${srcdir}/tracy"
	git -c protocol.file.allow=always submodule update
}

pkgver() {
	git -C Hyprland describe --long --tags | sed 's/^v//;s/\([^-]*-\)g/r\1/;s/-/./g'
}

build() {
	cd Hyprland
	mkdir -p build && cd build
	cmake -G Ninja -DCMAKE_BUILD_TYPE=None -DCMAKE_SKIP_RPATH=ON -DCMAKE_INSTALL_PREFIX=/usr ..
	ninja
}

package() {
	cd Hyprland

	meson install -C subprojects/wlroots/build --destdir "${pkgdir}/tmpwlr"
	install -Dm755 build/Hyprland -t "${pkgdir}/usr/bin/"

	pushd "${pkgdir}/usr/bin"
	ln -sf Hyprland hyprland
	popd

	install -Dm755 build/{hyprctl/hyprctl,hyprpm/hyprpm} -t "${pkgdir}/usr/bin/"
	install -Dm644 assets/*.png -t "${pkgdir}/usr/share/hyprland/"

	install -Dm644 example/hyprland.desktop -t "${pkgdir}/usr/share/wayland-sessions"
	install -Dm644 example/hyprland.conf -t "${pkgdir}/usr/share/hyprland"

	install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
	install -Dm644 subprojects/wlroots/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE-wlroots"
	install -Dm644 subprojects/udis86/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE-udis86"

	install -Dm644 docs/*.1 -t "${pkgdir}/usr/share/man/man1/"
	install -Dm755 ${pkgdir}/tmpwlr/usr/local/lib/libwlroots.so.* -t "${pkgdir}/usr/lib/"
	install -d ${pkgdir}/usr/include/hyprland/{protocols,wlroots}

	cp -R src "${pkgdir}/usr/include/hyprland/"
	cp -R ${pkgdir}/tmpwlr/usr/local/include/* "${pkgdir}/usr/include/hyprland/wlroots/"

	find "${pkgdir}/usr/include/hyprland/" -type f ! -name '*.h*' -delete
	rm -rf "${pkgdir}/tmpwlr/"

	cp protocols/*-protocol.h "${pkgdir}/usr/include/hyprland/protocols/"
	install -Dm644 build/hyprland.pc -t "${pkgdir}/usr/share/pkgconfig/"
}
