# Maintainer: FabioLolix
# Maintainer: Ã©clairevoyant
# Contributor: ThatOneCalculator <kainoa at t1c dot dev>

pkgname=hyprland-git
pkgver=0.33.1.r28.e53134ca
pkgrel=1
pkgdesc="A dynamic tiling Wayland compositor based on wlroots that doesn't sacrifice on its looks."
arch=(x86_64 aarch64)
url="https://github.com/hyprwm/Hyprland"
license=(BSD)
depends=(cairo
	glib2
	glslang
	libdisplay-info
	libdrm
	libglvnd
	libinput
	libliftoff
	libx11
	libxcb
	libxcomposite
	libxfixes
	libxkbcommon
	libxrender
	opengl-driver
	pango
	pixman
	polkit
	seatd
	systemd-libs
	vulkan-icd-loader
	vulkan-validation-layers
	wayland
	wayland-protocols
	xcb-proto
	xcb-util
	xcb-util-errors
	xcb-util-keysyms
	xcb-util-renderutil
	xcb-util-wm
	xorg-xinput
	xorg-xwayland
	libdisplay-info.so)
makedepends=(cmake
	jq
	git
	meson
	ninja
	pkgconf
	vulkan-headers
	xorgproto
	tomlplusplus)
provides=("hyprland=${pkgver%%.r*}")
conflicts=(hyprland)
replaces=(hyprland-nvidia-git)
source=("git+https://github.com/hyprwm/Hyprland.git"
	"git+https://gitlab.freedesktop.org/wlroots/wlroots.git"
	"git+https://github.com/hyprwm/hyprland-protocols.git"
	"git+https://github.com/canihavesomecoffee/udis86.git"
	"git+https://github.com/wolfpld/tracy.git")
sha256sums=('SKIP'
	'SKIP'
	'SKIP'
	'SKIP'
	'SKIP')

prepare() {
	cd Hyprland
	git submodule init
	git config submodule.wlroots.url "${srcdir}/wlroots"
	git config submodule.subprojects/hyprland-protocols.url "${srcdir}/hyprland-protocols"
	git config submodule.subprojects/udis86.url "${srcdir}/udis86"
	git config submodule.subprojects/tracy.url "${srcdir}/tracy"
	git -c protocol.file.allow=always submodule update

	cd subprojects/wlroots
	sed -i 's/soversion = 12/soversion = 13/g' ../packagefiles/wlroots-meson-build.patch
	patch -p1 <../packagefiles/wlroots-meson-build.patch
}

pkgver() {
	git -C Hyprland describe --long --tags | sed 's/^v//;s/\([^-]*-\)g/r\1/;s/-/./g'
}

build() {
	cd Hyprland

	meson setup build \
		--prefix /usr \
		--libexecdir lib \
		--sbindir bin \
		--buildtype plain \
		--wrap-mode nodownload \
		-D b_lto=true \
		-D b_pie=true \
		-D default_library=shared \
		-D xwayland=enabled

	ln -sf wlroots build/subprojects/wlroots/include/wlr
	meson compile -C build
}

package() {
	cd Hyprland

	meson install -C build --destdir "${pkgdir}"

	rm -rf "${pkgdir}/usr/include/hyprland/wlroots/wlr/"
	ln -sf . "${pkgdir}/usr/include/hyprland/wlroots/wlr"

	# resolve conflicts with system wlr
	rm -f "${pkgdir}/usr/lib/libwlroots.so"
	rm -f "${pkgdir}/usr/lib/pkgconfig/wlroots.pc"

	# resolve conflicts with xdg-desktop-portal-hyprland from repo
	rm -rf "${pkgdir}/usr/share/xdg-desktop-portal/"
	rm -rf "${pkgdir}/usr/share/hyprland-protocols/"
	rm -f "${pkgdir}/usr/share/pkgconfig/hyprland-protocols.pc"

	# FIXME: meson.build shall install version.h
	install -Dm644 src/version.h -t "${pkgdir}/usr/include/hyprland/src/"

	# license
	install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
