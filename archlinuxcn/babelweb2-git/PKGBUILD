# Maintainer: Coelacanthus <uwu@coelacanthus.name>

pkgname=babelweb2-git
_pkgname=babelweb2
pkgver=r226.0dc8cdb
pkgrel=2
pkgdesc='Yet Another Monitoring Tool for the Babel routing daemon.'
arch=('x86_64' 'i686')
url='https://github.com/Vivena/babelweb2'
license=('MIT')
makedepends=('git' 'go')
source=("${_pkgname}::git+https://github.com/Vivena/babelweb2.git#branch=junk"
        "babelweb2.service")
b2sums=('SKIP'
        'c2ac6603f7cb9b98e88d5e7d9dd9d8e58048e1bf0ef759a33866d519af0c0e7e1d5ac83a6b3d1ff84879516140c6b5fcddaf01ec1d1ebcae22adabc67b83b33d')

pkgver() {
  cd "${srcdir}/${_pkgname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}/${_pkgname}"
  go mod init "${url#https://}" # strip https:// from canonical URL
  go mod tidy
}


build() {
  cd "${srcdir}/${_pkgname}"
  
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"

  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external -extldflags \"${LDFLAGS}\"" \
    -o ${_pkgname} \
    .
}

package() {
  cd "$_pkgname"
  install -Dm 755 $_pkgname "${pkgdir}/usr/bin/$_pkgname"
  mkdir -p $pkgdir/usr/share/$_pkgname
  cp -r static $pkgdir/usr/share/$_pkgname
  install -Dm644 "$srcdir/babelweb2.service" "$pkgdir/usr/lib/systemd/system/babelweb2.service"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/babelweb2/LICENSE"
}
