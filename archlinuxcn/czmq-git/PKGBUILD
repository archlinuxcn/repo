# Maintainer: Morteza NourelahiAlamdari <m at 0t1.me>
# Contributor: Andy Weidenbaum <archbaum at gmail.com>
# Contributor: Brian Knox <taotetek at gmail.com>

pkgname=czmq-git
_pkgname=${pkgname%-git}
pkgver=4.2.1.r117.g899f8198
pkgrel=1
epoch=1
pkgdesc="High-level C binding for ZeroMQ"
arch=('i686' 'x86_64' 'aarch64')
depends=('zeromq' 'curl' 'lz4' 'systemd-libs' 'util-linux-libs')
makedepends=('git' 'pkg-config')
url="https://github.com/zeromq/${_pkgname}"
license=('MPL-2.0')
source=("git+${url}")
sha256sums=('SKIP')
provides=("${_pkgname}=${pkgver%.r*}" libczmq.so)
conflicts=(${_pkgname})

pkgver() {
  cd "${_pkgname}/"

  # Generate git tag based version. Count only proper (v)#.#* [#=number] tags.
  local _gitversion=$(git describe --long --match '[v0-9][0-9.][0-9.]*' | sed -e 's|^v||' | tr '[:upper:]' '[:lower:]')

  # Format git tag based version for pkgver
  # Expected format: e.g. 1.5.0rc2.r521.g99982a1c
  echo "${_gitversion}" | sed \
      -e 's|^\([0-9][0-9.]*\)-\([a-zA-Z]\+\)|\1\2|' \
      -e 's|\([0-9]\+-g\)|r\1|' \
      -e 's|-|.|g'
}

build() {
  cd "${_pkgname}/"

  msg2 'Building...'
  ./autogen.sh
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib/czmq \
    --sysconfdir=/etc \
    --sharedstatedir=/usr/share/czmq \
    --localstatedir=/var/lib/czmq \
    --disable-czmq_selftest \
    --disable-zmakecert \
    --with-gnu-ld \
    --with-nss=no
  make
}

package() {
  cd "${_pkgname}/"

  msg2 'Installing license...'
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/czmq"

  msg2 'Installing...'
  make DESTDIR="${pkgdir}" install

  msg2 'Renaming binaries...'
  for _bin in $(find "${pkgdir}/usr/bin" -type f -printf '%f\n'); do
    mv "${pkgdir}/usr/bin/${_bin}" "${pkgdir}/usr/bin/${_pkgname}_${_bin}"
  done

  msg2 'Cleaning up pkgdir...'
  find "${pkgdir}" -type d -name .git -exec rm -r '{}' +
  find "${pkgdir}" -type f -name .gitignore -exec rm -r '{}' +
}
