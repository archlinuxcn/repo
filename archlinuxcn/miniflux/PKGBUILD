# Maintainer: Thor77 <thor77 at thor77 dot org>
# Maintainer: Clar Charr <clar@charr.xyz>
# Contributor: somini <dev at somini dot xyz>

pkgname=miniflux
pkgver=2.0.27
pkgrel=1
pkgdesc='Minimalist Feed Reader'
arch=('x86_64')
_target='amd64'
url='https://miniflux.app'
license=('Apache')
optdepends=("postgresql: unless the database is on another machine, this machine needs it")
makedepends=('go' 'git')
conflicts=('miniflux-git')
backup=('etc/miniflux.conf')
source=(
  https://github.com/miniflux/v2/archive/${pkgver}.tar.gz
  miniflux.conf
  miniflux.service
  miniflux.sysusers
)
sha256sums=('ec6696b1cf41ef50b76d02d1179838e03d9c3bcb3dce48febe2b5f4ecc450473'
            '1247a3cfe0307f5f1bb070e616f57344d8f16201070ecf7dda243b3fa1fbd8b8'
            'b0b26d629ebb3a7b4c65d4a293dbc272f4e9695c0803989604519ccac3454402'
            'd1ce5acf1e633b04b92bf9330bd64d97c19d8f8d39c7bb4b64e11a96be4b73e2')

prepare() {
  mkdir -p "${srcdir}/src/github.com/miniflux"
  ln -s "${srcdir}/v2-${pkgver}" "${srcdir}/src/github.com/miniflux/${pkgname}"
}

build() {
  cd ${srcdir}/v2-${pkgver}

  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"

  GOPATH="${srcdir}" PATH="${PATH}:${GOPATH}/bin/" make VERSION="$pkgver" linux-$_target
}

package() {
  install -Dm644 "${srcdir}/miniflux.conf" "${pkgdir}/etc/miniflux.conf"
  install -Dm644 "${srcdir}/miniflux.service" "${pkgdir}/usr/lib/systemd/system/miniflux.service"
  install -Dm644 "${srcdir}/miniflux.sysusers" "${pkgdir}/usr/lib/sysusers.d/miniflux.conf"
  install -Dm644 "${srcdir}/v2-${pkgver}/miniflux.1" "${pkgdir}/usr/share/man/man1/miniflux.1"
  install -Dm755 "${srcdir}/v2-${pkgver}/miniflux-linux-${_target}" "${pkgdir}/usr/bin/miniflux"
}

