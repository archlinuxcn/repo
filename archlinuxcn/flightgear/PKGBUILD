# Maintainer: Deon Spengler <deon at spengler dot co dot za>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Hans Janssen <hans@janserv.xs4all.nl>

pkgname=flightgear
pkgver=2020.3.17
_pkgver=${pkgver%.*}
pkgrel=2
pkgdesc="An open-source, multi-platform flight simulator"
arch=(armv7h aarch64 x86_64)
depends=(libxmu libxi zlib openscenegraph libxrandr glu glew openal "simgear=$pkgver"
         systemd-libs speex speexdsp gsm sqlite libevent)
# flightgear-data used at compile time for translation
makedepends=(boost cmake mesa sharutils plib qt5-base qt5-declarative qt5-svg qt5-tools
             flightgear-data)
optdepends=('qt5-base: fgfs --launcher'
            'qt5-declarative: fgfs --launcher'
            'flightgear-data')
license=("GPL")
url="http://www.flightgear.org/"
source=("http://downloads.sourceforge.net/project/flightgear/release-${_pkgver}/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('6670dedeaca2683aca77f9f06bf9d1d8062ae32a2a5459dddc8aa0989c5f1854')

prepare() {
  cd flightgear-$pkgver
  sed -i 's|Exec=.*|Exec=fgfs --launcher --fg-root=/usr/share/flightgear/data|' package/org.flightgear.FlightGear.desktop

  rm -rf build
  mkdir build
}

build() {
  cd flightgear-$pkgver/build
  # no -DSYSTEM_FLITE since flite1 is required
  CFLAGS+=" -ffile-prefix-map=${srcdir}=${DBGSRCDIR:-/usr/src/debug}"
  CXXFLAGS+=" -ffile-prefix-map=${srcdir}=${DBGSRCDIR:-/usr/src/debug}"
  # Based on warnings from LTO
  CFLAGS+=' -fno-strict-aliasing'
  CXXFLAGS+=' -fno-strict-aliasing'
  LDFLAGS+=' -fno-strict-aliasing'
  cmake .. -Wno-dev \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DFG_DATA_DIR="/usr/share/flightgear/data" \
        -DCMAKE_BUILD_TYPE=Release \
        -DFG_BUILD_TYPE=Release \
        -DEVENT_INPUT=On \
        -DSYSTEM_SQLITE=On \
        -DUSE_DBUS=On \
        -DSYSTEM_SPEEX=On \
        -DSYSTEM_GSM=On \
        -DSYSTEM_FLITE=Off \
        -DSYSTEM_HTS_ENGINE=Off \
        -DENABLE_AUTOTESTING=Off
  make
}

package() {
  cd flightgear-$pkgver/build
  make DESTDIR="$pkgdir" install

  install -Dm0644 ../package/flightgear.ico "$pkgdir"/usr/share/icons/flightgear.ico
  install -Dm0644 ../scripts/completion/fg-completion.bash "$pkgdir"/usr/share/bash-completion/completions/fgfs
  install -Dm0644 ../scripts/completion/fg-completion.zsh "$pkgdir"/usr/share/zsh/site-functions/_fgfs
  ln -sf flightgear "$pkgdir"/usr/share/FlightGear
}
