# Maintainer: Peter Mattern <pmattern at arcor dot de>

_pkgname='pavucontrol-qt'
pkgname="$_pkgname-git"
pkgver=1.2.0.5.g8f474f8
pkgrel=1
pkgdesc='Qt port of pavucontrol'
arch=('i686' 'x86_64')
url="https://github.com/lxde/$_pkgname"
license=('GPL2')
depends=('libpulse' 'qt5-base')
makedepends=('git' 'cmake' 'qt5-tools' 'lxqt-build-tools-git')
provides=("$_pkgname")
conflicts=("$_pkgname")
source=("git+https://github.com/lxde/$_pkgname.git")
sha256sums=("SKIP")

pkgver() {
  cd $_pkgname
  git describe --always | sed "s/-/./g"
}

build() {
  rm -Rf build ; mkdir build ; cd build
  cmake $srcdir/$_pkgname \
          -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
