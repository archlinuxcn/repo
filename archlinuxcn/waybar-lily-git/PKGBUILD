# Maintainer: lilydjwg <lilydjwg@gmail.com>
# Contributor: Brett Cornwall <ainola@archlinux.org>
# Contributor: Maxim Baz <$pkgname at maximbaz dot com>

pkgname=waybar-lily-git
pkgver=0.10.4.r98.ge7b2d33d
pkgrel=2
pkgdesc='Highly customizable Wayland bar for Sway and Wlroots based compositors, with less modules and deps, for PulseAudio users, and other changes'
arch=('x86_64')
url="https://github.com/lilydjwg/Waybar"
license=('MIT')
depends=(
    'fmt'
    'gtk-layer-shell'
    'gtkmm3'
    'libdate-tz.so'
    'libdbusmenu-gtk3'
    'libjsoncpp.so'
    'libnl'
    'libpulse'
    'libsigc++'
    'libspdlog.so'
    'libxkbcommon'
    'wayland'
)
makedepends=(
    'catch2'
    'cmake'
    'meson'
    'glib2-devel'
    'scdoc' # For generating manpages
    'wayland-protocols'
    'git'
)
backup=(
    etc/xdg/waybar/config.jsonc
    etc/xdg/waybar/style.css
)
optdepends=(
    'otf-font-awesome: Icons in the default configuration'
)
source=("git+https://github.com/lilydjwg/Waybar.git")
sha256sums=('SKIP')
provides=(waybar=$pkgver waybar-lily=$pkgver)
conflicts=(waybar waybar-lily)
replaces=(waybar-lily)

pkgver() {
    cd Waybar
    git describe --tags | sed -E 's/([^-]*-g)/r\1/;s/-/./g'
}

build() {
    cd Waybar
    meson setup --prefix=/usr \
          --buildtype=plain \
          --wrap-mode=nodownload \
          -Dcpp_std=c++20 \
          -Dexperimental=true \
          -Dcava=disabled \
          -Dtests=disabled \
          build
    meson compile -C build
}

package() {
    cd Waybar
    meson install -C build --destdir "$pkgdir"
    install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
