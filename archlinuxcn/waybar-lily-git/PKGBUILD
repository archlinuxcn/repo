# Maintainer: lilydjwg <lilydjwg@gmail.com>
# Contributor: Brett Cornwall <ainola@archlinux.org>
# Contributor: Maxim Baz <$pkgname at maximbaz dot com>

pkgname=waybar-lily-git
pkgver=0.14.0
pkgrel=1
pkgdesc='Highly customizable Wayland bar for Sway and Wlroots based compositors, with less modules and deps, for PulseAudio users, and other changes'
arch=('x86_64')
url="https://github.com/lilydjwg/Waybar"
license=('MIT')
depends=(
    'cairomm'
    'fmt'
    'glibmm'
    'gtk-layer-shell'
    'gtk3'
    'gtkmm3'
    'jsoncpp'
    'libdbusmenu-gtk3'
    'libnl'
    'libpulse'
    'libsigc++'
    'libxkbcommon'
    'spdlog'
    'systemd-libs'
    'wayland'
)
makedepends=(
    'catch2'
    'cmake'
    'meson'
    'glib2-devel' # gdbus-codegen
    'scdoc' # For generating manpages
    'wayland-protocols'
    'git'
)
backup=(
    etc/xdg/waybar/config.jsonc
    etc/xdg/waybar/style.css
)
optdepends=(
    'otf-font-awesome: Icons in the default configuration'
)
source=("git+https://github.com/lilydjwg/Waybar.git")
sha256sums=('SKIP')
provides=(waybar=$pkgver waybar-lily=$pkgver)
conflicts=(waybar waybar-lily)
replaces=(waybar-lily)

pkgver() {
    cd Waybar
    git describe --tags | sed -E 's/([^-]*-g)/r\1/;s/-/./g'
}

build() {
    cd Waybar
    meson setup --prefix=/usr \
          --buildtype=plain \
          --wrap-mode=nodownload \
          -Dcpp_std=c++20 \
          -Dexperimental=true \
          -Dcava=disabled \
          -Dtests=disabled \
          build
    meson compile -C build
}

package() {
    depends+=(
      'libcairomm-1.0.so'
      'libglibmm-2.4.so'
      'libgdkmm-3.0.so' 'libgtkmm-3.0.so'
      'libjsoncpp.so'
      'libnl-3.so' 'libnl-genl-3.so'
      'libpulse.so'
      'libsigc-2.0.so'
      'libxkbregistry.so'
      'libspdlog.so'
      'libudev.so'
      'libwayland-client.so'
    )

    cd Waybar
    meson install -C build --destdir "$pkgdir"
    install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
