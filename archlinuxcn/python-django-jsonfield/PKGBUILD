# Maintainer: Joffrey <j-off@live.fr>

pkgbase='python-django-jsonfield'
pkgname=('python-django-jsonfield' 'python2-django-jsonfield')
pkgver='2.1.1'
pkgrel=3
pkgdesc='A reusable JSONField model for Django to store ad-hoc data'
arch=('any')
url='https://github.com/dmkoch/django-jsonfield'
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://github.com/dmkoch/django-jsonfield/archive/$pkgver.tar.gz")
sha256sums=('028256a8dfeda3ddd41fb47303fb998fa11a2ae758df9a2ef677b662298e6cfa')

package_python-django-jsonfield() {
    depends=('python')
    cd "$srcdir/jsonfield-$pkgver"
    install -Dm644 './LICENSE' "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
    python './setup.py' install --root="$pkgdir" --optimize=1
}

package_python2-django-jsonfield() {
    depends=('python2')
    cd "$srcdir/jsonfield-$pkgver"
    install -Dm644 './LICENSE' "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
    python2 './setup.py' install --root="$pkgdir" --optimize=1
}
