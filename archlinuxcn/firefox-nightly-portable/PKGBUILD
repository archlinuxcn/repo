# Maintainer: Kimiblock Moe

pkgname=firefox-nightly-portable
pkgver=2025_11_01
pkgrel=1
epoch=1
pkgdesc="Firefox sandboxed by portable (en_US locale)"
arch=('any')
url="https://github.com/Kraftland/portable"
license=('GPL-3.0-or-later')
groups=()
provides=(firefox-nightly)
options=(!debug !strip)
conflicts=("firefox-nightly")

optdepends=()

makedepends+=(firefox-nightly-en-us coreutils)

checkdepends=()

source=(
	portable-config
	firefox.sh
	firefox.desktop
)


md5sums=('SKIP'
         'SKIP'
         'SKIP')

function prepare() {
	pacman -Ql firefox-nightly-en-us >file.list
}

function pkgver() {
	date -I | sed 's|-|_|g'
}

function package() {
	depends=("portable")
	depends+=('dbus-glib' 'gtk3' 'libxt' 'nss' 'mime-types')
	while IFS= read -r line; do
		file="$(echo "$line" | awk '{print $2}')"
		if [[ -d ${file} ]]; then
			echo "Omitting Directory"
		else
			if [[ -L "${file}" ]]; then
				mkdir -p "$(dirname "${pkgdir}/${file}")"
				ln -vsf "$(readlink -f "${file}")" "${pkgdir}/${file}"
			else
				install -vDm755 "${file}" "${pkgdir}/${file}"
			fi
		fi
	done < file.list
	rm -f "${pkgdir}/usr/share/applications"/*
	rm -f "${pkgdir}/usr/bin"/*
	install -Dm644 portable-config \
		"${pkgdir}/usr/lib/portable/info/org.mozilla.FirefoxNightly/config"
	install -Dm755 "firefox.sh" \
		"${pkgdir}/usr/bin/firefox-nightly-portable"
	install -Dm644 "firefox.desktop" "${pkgdir}/usr/share/applications/org.mozilla.FirefoxNightly.desktop"
	echo '''[Desktop Entry]
Type=Application
Name=Firefox Nightly
GenericName=Stub for MPRIS
Icon=firefox-nightly
TryExec=portable
Exec=env _portableConfig="org.mozilla.FirefoxNightly" portable -- %u
Terminal=false
NoDisplay=true''' >"${pkgdir}/usr/share/applications/firefox-nightly.desktop"
}
