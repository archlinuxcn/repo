# Maintainer: Ren√© Wagner <rwa at clttr dot info>

pkgname=art-rawconverter
_pkgname=ART
pkgver=1.21.3
pkgrel=1
pkgdesc="raw image converter ART (forked from RawTherapee with ease of use in mind)"
arch=('i686' 'x86_64' 'aarch64')
url="https://bitbucket.org/agriggio/art/wiki/Home"
license=('GPL-3.0-or-later')
depends=('opencolorio' 'lensfun' 'libraw' 'exiv2' 'fftw' 'gtk3' 'glibmm' 'gtkmm3' 'lcms2' 'libcanberra' 'libiptcdata' 'desktop-file-utils' 'mimalloc' 'openmp')
optdepends=('perl-image-exiftool: metadata support for CR3 images'
	'art-rawconverter-imageio: add support for additional image formats'
	'lcms2-ff: lcms2 with fast-float plugin for improved export speed')
makedepends=('pkgconf' 'cmake' 'git' 'gcc' 'hicolor-icon-theme' 'fakeroot')
source=("https://bitbucket.org/agriggio/art/downloads/${_pkgname}-${pkgver}.tar.xz")
sha256sums=('347a754422031b10651ee7e494736fa1c069c9bfbf4663b4aac63cdf7408225e')

build() {
	mkdir build/
	cd build/

	cmake "../${_pkgname}-${pkgver}/" \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DPROC_TARGET_NUMBER="2" \
		-DWITH_LTO="ON" \
		-DENABLE_LIBRAW="ON" \
		-DENABLE_OCIO="ON" \
		-DBUILD_SHARED="ON"

	make
}

package() {
	cd build/
	make DESTDIR="${pkgdir}" install

	cd "../${_pkgname}-${pkgver}/"
	install -Dm644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}/"
	cp -r licenses/* "${pkgdir}/usr/share/licenses/${pkgname}/"
}
