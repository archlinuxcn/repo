# Maintainer: prg <prg _at_ xannode _dot_ com>
# Contributor: Caltlgin Stsodaat <contact@fossdaily.xyz>
pkgname='haruna'
pkgver=0.6.3
pkgrel=3
pkgdesc='Open source video player built with Qt/QML and libmpv'
arch=('x86_64')
url='https://invent.kde.org/multimedia/haruna'
license=('GPL3')
depends=('kfilemetadata' 'kio' 'mpv' 'kirigami2')
makedepends=('extra-cmake-modules')
optdepends=('nvidia-utils: hardware accelerate support'
            'breeze: recommend theme'
            'breeze-icons: recommend icons'
            'kio-extras: additional components to increase the functionality of KIO'
            'youtube-dl: play videos from Youtube.com'
            'nvidia-utils: libcuda support')
provides=('haruna')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha256sums=('c79ec1e351f47faf9a58a6ba7ec3cc05cfdc5423fde0584f2d4081f5058363e3')

build() {
  cmake -B 'build' -S "${pkgname}-${pkgver}" \
    -DCMAKE_BUILD_TYPE='Release' \
    -DCMAKE_INSTALL_PREFIX='/usr'
  make -C 'build'
}

package() {
  make DESTDIR="${pkgdir}" PREFIX='/usr' -C 'build' install
  install -Dvm644 "${pkgname}-${pkgver}/README.md" -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
