# Maintainer: prg <prg _at_ xannode _dot_ com>
# Contributor: Caltlgin Stsodaat <contact@fossdaily.xyz>
pkgname='haruna'
pkgver=0.7.3
pkgrel=1
pkgdesc='Open source video player built with Qt/QML and libmpv'
arch=('x86_64')
url='https://invent.kde.org/multimedia/haruna'
license=('GPL3')
depends=('kfilemetadata' 'kio' 'mpv' 'kirigami2')
makedepends=('extra-cmake-modules' 'kdoctools')
optdepends=('nvidia-utils: hardware accelerate support'
            'breeze: recommend theme'
            'breeze-icons: recommend icons'
            'kio-extras: additional components to increase the functionality of KIO'
            'youtube-dl: play videos from Youtube.com'
            'nvidia-utils: libcuda support')
provides=('haruna')
source=("${pkgname}-${pkgver}.tar.gz::https://invent.kde.org/multimedia/haruna/-/archive/v${pkgver}/haruna-v${pkgver}.tar.gz")
sha256sums=('8ef599a6b986fdff85067d9c9c47aa8d70f07e365446036247b8da1237d75bd4')

build() {
  mkdir -p build
  cmake -B 'build' -S "${pkgname}-v${pkgver}" \
    -DCMAKE_BUILD_TYPE='Release' \
    -DCMAKE_INSTALL_PREFIX='/usr'
  make -C 'build'
}

package() {
  make DESTDIR="${pkgdir}" PREFIX='/usr' -C 'build' install
  install -Dvm644 "${pkgname}-v${pkgver}/README.md" -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
