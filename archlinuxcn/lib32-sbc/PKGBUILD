# Maintainer: Rodrigo Bezerra <rodrigobezerra21 at gmail dot com>
# Contributor: orumin <dev@orum.in>

_basename=sbc
pkgname=lib32-sbc
pkgver=1.5
pkgrel=1
pkgdesc="Bluetooth Subband Codec (SBC) library (32-bit)"
url="http://www.bluez.org/"
arch=(x86_64)
license=(GPL LGPL)
depends=(lib32-glibc sbc)
makedepends=(git lib32-gcc-libs)
_commit=b2c20a3ecf5ed00f63c94077a9762b505935ede9  # tags/1.5^0
source=("git+https://git.kernel.org/pub/scm/bluetooth/sbc.git#commit=$_commit")
sha512sums=('SKIP')

pkgver() {
    cd $_basename

    git describe --tags | sed 's/-/+/g'
}

prepare() {
    cd $_basename

    autoreconf -fvi
}

build() {
    cd $_basename

    export CC='gcc -m32'
    export CXX='g++ -m32'
    export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

    ./configure --prefix=/usr \
        --build=i686-pc-linux-gnu \
        --libdir=/usr/lib32 \
        --disable-static \
        --disable-tester

    make
}

check() {
    cd $_basename

    make check
}

package() {
    cd $_basename

    make DESTDIR="$pkgdir" install

    cd "$pkgdir"/usr

    rm -r bin include
}
provides=("libsbc.so")
