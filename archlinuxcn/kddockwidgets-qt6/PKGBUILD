# Maintainer: marco.rubin@protonmail.com

_name=KDDockWidgets
pkgname=kddockwidgets-qt6
pkgver=2.2.1
pkgrel=4
pkgdesc="KDAB's Dock Widget Framework for Qt 6"
arch=('x86_64')
url="https://github.com/KDAB/KDDockWidgets"
license=('GPL-2.0-only OR GPL-3.0-only')
depends=(gcc-libs glibc 'fmt>=11' nlohmann-json 'qt6-base>=6.2.0' qt6-declarative)
makedepends=('cmake>=3.15' 'qt6-tools>=6.6.2')
source=("$url/archive/v$pkgver.tar.gz")
b2sums=('c22c5a5f0dafa69f66c14622ab5bb2666e1a1b91b6d60d6d6b4bbf9bc6f5e64238fa6bfef81a1a98fa24d49d4b2bd8ef5c1884d5af8ab4550ab435fb01a39369')

build() {
    cd $_name-$pkgver
    # -DKDDockWidgets_NO_SPDLOG=true is needed until KDDockWidgets can be built with fmt 11
    cmake -DCMAKE_BUILD_TYPE=Release \
          -DCMAKE_INSTALL_PREFIX=/usr \
          -DKDDockWidgets_FRONTENDS='qtwidgets;qtquick' \
          -DKDDockWidgets_NO_SPDLOG=true \
          -DKDDockWidgets_QT6=true \
          .
    cmake --build .
}

package() {
    cd $_name-$pkgver
    DESTDIR="$pkgdir" cmake --install .
}
