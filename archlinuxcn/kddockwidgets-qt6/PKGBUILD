# Maintainer: marco.rubin@protonmail.com

_name=KDDockWidgets
pkgname=kddockwidgets-qt6
pkgver=2.2.5
pkgrel=1
pkgdesc="KDAB's Dock Widget Framework for Qt 6"
arch=('x86_64')
url="https://github.com/KDAB/KDDockWidgets"
license=('GPL-2.0-only OR GPL-3.0-only')
depends=(gcc-libs glibc 'fmt>=11' nlohmann-json 'qt6-base>=6.2.0' qt6-declarative vulkan-icd-loader)
makedepends=('cmake>=3.15' 'qt6-tools>=6.6.2' 'vulkan-headers')
source=("$url/archive/v$pkgver.tar.gz")
b2sums=('b938cf5d70b9ffaa43e21f7d6748552d9d8ded3c1eb4d7b7b71884bfe6ca925e8991a5eb566f51ed12a461f937372b0be9b2cc2588063ae93658a026ac6402ab')

build() {
    cd $_name-$pkgver
    cmake -DCMAKE_BUILD_TYPE=Release \
          -DCMAKE_INSTALL_PREFIX=/usr \
          -DKDDockWidgets_FRONTENDS='qtwidgets;qtquick' \
          -DKDDockWidgets_QT6=true \
          .
    cmake --build .
}

package() {
    cd $_name-$pkgver
    DESTDIR="$pkgdir" cmake --install .
}
