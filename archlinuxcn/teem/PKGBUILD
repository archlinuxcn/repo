# Maintainer: Butui Hu <hot123tea123@gmail.com>
# Contributor: Martino Pilia <martino.pilia@gmail.com>

_revision=6479
pkgname=teem
pkgver=1.11.0
pkgrel=7
pkgdesc='A coordinated group of libraries for representing, processing, and visualizing scientific raster data'
arch=('x86_64')
url='http://teem.sourceforge.net/index.html'
license=('LGPL')
depends=(
  bzip2
  fftw
  libpng
)
makedepends=(
  cmake
  subversion
)
source=("${pkgname}-${pkgver}::svn+https://svn.code.sf.net/p/teem/code#revision=${_revision}")
sha512sums=('SKIP')

build() {
  cmake -S "${srcdir}/${pkgname}-${pkgver}/${pkgname}/trunk" -B "build" \
    -DBUILD_EXPERIMENTAL_APPS=ON \
    -DBUILD_EXPERIMENTAL_LIBS=ON \
    -DBUILD_SHARED_LIBS:BOOL=ON \
    -DCMAKE_BUILD_TYPE:STRING=Release \
    -DCMAKE_INSTALL_PREFIX:PATH="/usr" \
    -DCMAKE_SKIP_INSTALL_RPATH=ON \
    -DTeem_BZIP2=ON \
    -DTeem_FFTW3=ON \
    -DTeem_PNG=ON \
    -DTeem_USE_LIB_INSTALL_SUBDIR=OFF
  make -C "build"
}

package() {
  cd "build"
  make DESTDIR="${pkgdir}" install
  install -Dm644 "${srcdir}/${pkgname}-${pkgver}/${pkgname}/trunk/LICENSE.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  # fix cmake path
  sed -i "s,lib/TeemUse.cmake,lib/cmake/teem/TeemUse.cmake," "${pkgdir}/usr/lib/cmake/teem/TeemConfig.cmake"
}
# vim:set ts=2 sw=2 et:
