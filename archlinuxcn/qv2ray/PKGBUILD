# Maintainer: DuckSoft <realducksoft at gmail dot com>
# Contributor: ArielAxionL <i at axionl dot me>
# Contributor: Leroy.H.Y <me at lhy0403 dot top>
pkgname=qv2ray
pkgver=2.6.3
pkgrel=26
pkgdesc="Cross-platform V2ray Client written in Qt (Stable Release)"
arch=('x86_64')
url='https://github.com/Qv2ray/Qv2ray'
license=('GPL3')
depends=('hicolor-icon-theme' 'qt5-base>5.11.0' 'grpc>=1.27.0')
optdepends=('v2ray: use system v2ray core.')
makedepends=('git' 'make' 'qt5-tools' 'which' 'gcc' 'qt5-declarative'
             'grpc-cli>=1.27.0' 'cmake' 'ninja')
provides=('qv2ray')
conflicts=('qv2ray')

source=('Qv2ray::git+https://github.com/Qv2ray/Qv2ray'
        'missing-header-zxing-cpp.patch')

sha512sums=('SKIP'
            'a6e512433eedd90484868c79acc235811cc33769121f2882f58898408f253376613f3000568ea1c363eafe3b815ed2ecf94a4f445081c13e228e84008384ec78')

pkgver() {
    printf "%s" $(cat ${srcdir}/Qv2ray/makespec/VERSION)
}

prepare() {
    cd "${srcdir}/Qv2ray"
    git submodule init
    git config submodule."3rdparty/zxing-cpp".active true 
    git submodule update

    # Temporarily fix https://github.com/Qv2ray/Qv2ray/issues/1494
    patch -Np1 -i ../missing-header-zxing-cpp.patch
}

build() {
    export _QV2RAY_BUILD_INFO_="Qv2ray for Arch Linux CN"
    export _QV2RAY_BUILD_EXTRA_INFO_="(Official Build) $(uname -a | cut -d " " -f3,13)"

    cd "${srcdir}/Qv2ray"
    mkdir -p build && cd build
    cmake .. \
        -DCMAKE_INSTALL_PREFIX=${pkgdir}/usr \
        -DQV2RAY_ZXING_PROVIDER="module" \
        -DQV2RAY_TRANSLATION_PATH="/usr/share/qv2ray/lang" \
        -DQV2RAY_DEFAULT_VASSETS_PATH="/usr/share/v2ray" \
        -DQV2RAY_DEFAULT_VCORE_PATH="/usr/bin/v2ray" \
        -DQV2RAY_DISABLE_AUTO_UPDATE=on \
        -DCMAKE_BUILD_TYPE=Release \
        -GNinja
    ninja
}

package() {
    cd "${srcdir}/Qv2ray"
    ninja -C "build" install
}
