# Maintainer: Gabriel Tremblay gabriel@delvelabs.ca

_pkgname=i8kutils
pkgname=i8kutils-git
pkgver=20170307.83622d1
pkgrel=3
pkgdesc="Fan control for Dell laptops"
arch=("x86_64")
url="https://github.com/vitorafsr/$_pkgname"
license=("GPL-3.0-only")
depends=("bash" "glibc")
makedepends=("git")
optdepends=("tcl: for i8kmon daemon"
            "acpi: for i8kmon daemon"
            "tk: for i8kmon GUI mode")
conflicts=("$_pkgname")
backup=("etc/$_pkgname/i8kmon.conf")
source=("git+https://github.com/vitorafsr/$_pkgname.git"
        "i8kmon.service")
sha256sums=('SKIP'
            '71d1112f0f3b74a5b84923b9da19b168b6559c3a128406305b2550d944f32bb6')

prepare() {
    cd "$srcdir/$_pkgname"
    # conform to Arch Linux guidelines
    sed -i "s|/etc/i8kmon|/etc/$_pkgname/i8kmon.conf|g" i8kmon.1
    sed -i "s|/etc/i8kmon.conf|/etc/$_pkgname/i8kmon.conf|g" i8kmon
    make clean
}

build() {
    cd "$srcdir/$_pkgname"
    make
}

package() {
    cd "$srcdir/$_pkgname"
    install -d "$pkgdir"/usr/{bin,share/man/man1}
    install -D -m644 i8kctl.1 i8kmon.1 "$pkgdir/usr/share/man/man1"
    install -D -m755 i8kctl i8kfan i8kmon "$pkgdir/usr/bin"

    install -D -m644 i8kmon.conf "$pkgdir/etc/i8kutils/i8kmon.conf"
    install -D -m644 ../i8kmon.service "$pkgdir/usr/lib/systemd/system/i8kmon.service"
}
