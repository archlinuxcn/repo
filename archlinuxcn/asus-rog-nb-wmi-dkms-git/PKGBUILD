# Maintainer : Cole Deck <cole at deck dot sh>

pkgname=asus-rog-nb-wmi-dkms-git
_pkgname=asus-rog-nb-wmi
pkgver=0.3.0.r1.gd57f785
pkgrel=2
pkgdesc="Kernel module to patch asus-nb-wmi to support more ROG laptops"
arch=('x86_64')
url="https://gitlab.com/asus-linux/asus-rog-nb-wmi"
license=()
provides=($_pkgname)
conflicts=($_pkgname)
depends=('dkms')
makedepends=('git' 'linux' 'linux-headers')
source=("git+https://gitlab.com/asus-linux/asus-rog-nb-wmi.git"
	"dkms.conf")
sha256sums=("SKIP"
	    "b952938712ccb756b7e263cb401d9a81d6e97e5ba14a329c4b541982e8f342ad")

pkgver() {
    cd "$srcdir/${_pkgname}"
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "${srcdir}/${_pkgname}"
  _pkgver=$(git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g')
  sed -e "s/REPLACE_ME/$_pkgver/" -i "../dkms.conf"
}

package() {
  cd "${srcdir}/${_pkgname}"
  install -d -m 755 ${pkgdir}/usr/src/${_pkgname}-${pkgver}/src
  install -m 644 Makefile ${pkgdir}/usr/src/${_pkgname}-${pkgver}/Makefile
  install -m 644 src/asus-rog-nb-wmi.c ${pkgdir}/usr/src/${_pkgname}-${pkgver}/src/asus-rog-nb-wmi.c
  install -m 644 src/asus-wmi.h ${pkgdir}/usr/src/${_pkgname}-${pkgver}/src/asus-wmi.h

  cd "${srcdir}"
  install -m 644 dkms.conf ${pkgdir}/usr/src/${_pkgname}-${pkgver}/dkms.conf

  install -d -m 755 ${pkgdir}/etc/modprobe.d
  echo "blacklist asus-nb-wmi" > ${pkgdir}/etc/modprobe.d/asus-rog-nb-wmi.conf
}
