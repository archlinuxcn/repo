# Maintainer: Integral <integral@member.fsf.org>

pkgname=icalingua++
_pkgname=icalingua
pkgver=2.12.28
pkgrel=1
pkgdesc='A branch of deleted Icalingua, with limited support'
license=('AGPL-3.0-or-later')
depends=('ffmpeg' 'electron' 'libappindicator-gtk3')
optdepends=('mongodb-bin: Provides storage'
	'redis: Provides storage')
makedepends=('asar')
arch=('aarch64' 'x86_64')
url="https://github.com/Icalingua-plus-plus/Icalingua-plus-plus"
provides=("electron-qq" "${_pkgname}")
conflicts=("electron-qq")
replaces=("electron-qq" "icalingua-plus-plus")
source=("${_pkgname}.desktop" "${_pkgname}")
source_x86_64=("app-x86_64-${pkgver}.asar::${url}/releases/download/v${pkgver//_/-}/app-x86_64.asar")
source_aarch64=("app-aarch64-${pkgver}.asar::${url}/releases/download/v${pkgver//_/-}/app-arm64.asar")
sha256sums=('43da1c99b7a6aee974c20da3bcf925b660dff4ba44a447c86066c84c8bc95fbd'
            '33a02b61af9cd48abd332b6a13964c939649c7ec089bc1a1aca4014bbb8785d2')
sha256sums_aarch64=('b3454448e567f600366b4aedb6a93e1b52fd8e256f0160899b45b6e194315766')
sha256sums_x86_64=('94d97340f0d75d536e69c89cd41623580e6c18b44b95c19545aa873e6677658b')

prepare() {
	asar ef "app-${CARCH}-${pkgver}.asar" "dist/electron/static/icons/512x512.png"
}

package() {
	install -Dm644 "${_pkgname}.desktop" -t "${pkgdir}/usr/share/applications/"
	install -Dm644 "512x512.png" "${pkgdir}/usr/share/icons/hicolor/512x512/apps/${_pkgname}.png"
	install -Dm644 "app-${CARCH}-${pkgver}.asar" "${pkgdir}/usr/lib/${_pkgname}/${_pkgname}.asar"
	install -Dm755 "${_pkgname}" -t "${pkgdir}/usr/bin/"
	ln -s "/usr/bin/${_pkgname}" "${pkgdir}/usr/bin/electron-qq"
}
