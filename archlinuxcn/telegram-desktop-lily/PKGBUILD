# Maintainer: lilydjwg <lilydjwg@gmail.com>
# Contributor: Megumi_fox <megumifox@archlinuxcn.org>
# Contributor: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: hexchain <i@hexchain.org>
pkgname=telegram-desktop-lily
_pkgname=telegram-desktop
pkgver=4.15.0
pkgrel=1
pkgdesc="Official Telegram Desktop client with megumifox & lily's patch"
arch=('x86_64')
url="https://desktop.telegram.org/"
license=('GPL-3.0-only WITH x11vnc-openssl-exception')
depends=('hunspell'
         'ffmpeg' 'libavfilter.so' 'libavformat.so' 'libavcodec.so' 'libavutil.so'
         'hicolor-icon-theme' 'lz4' 'minizip' 'openal'
         'qt6-imageformats' 'qt6-svg' 'qt6-wayland' 'xxhash' 'glibmm-2.68'
         'rnnoise' 'pipewire' 'libxcomposite' 'libxtst' 'libxrandr' 'jemalloc' 'abseil-cpp' 'libdispatch'
         'openssl' 'libcrypto.so' 'libssl.so'
         'protobuf' 'libprotobuf-lite.so'
         'libvpx' 'libvpx.so')
makedepends=('cmake' 'git' 'ninja' 'python' 'range-v3' 'tl-expected' 'microsoft-gsl' 'meson'
             'extra-cmake-modules' 'wayland-protocols' 'plasma-wayland-protocols' 'libtg_owt'
             'gobject-introspection' 'glib2' 'boost' 'fmt')
optdepends=('webkit2gtk: embedded browser features'
            'xdg-desktop-portal: desktop integration')
provides=('telegram-desktop')
conflicts=('telegram-desktop')
source=("https://github.com/telegramdesktop/tdesktop/releases/download/v${pkgver}/tdesktop-${pkgver}-full.tar.gz"
        "0001-Use-font-from-environment-variables.patch"
        "0002-add-TDESKTOP_DISABLE_REGISTER_CUSTOM_SCHEME-back.patch"
        "0001-make-blockquote-markup-less-distractive.patch"
        "warn-before-delete-all-2.patch"
        0001-Drop-usage-of-Qt-6.5-colorScheme-API-on-Linux.patch)
sha512sums=('95aa5f14a9a88b9c6421049445f59f1c5c5d7ab4ca4e8b8f4ab7389bdb8f3cc6b29fea270574881633035acec769ba271261f84ec269010c63af28a03719da98'
            '13b1b0f34b0937a98aa61fed776318717c659358794c0ffa8b1af00c5c80fe195a11816db9183eee8e8e02f76c72f10c723093a24a9c0de249af18dcf2755126'
            '31d7a4c091abae49631598f2f41c8382550312e080fca784ed5a19cbaef0c6cdf2a2a4ffeffd78cd41f32dd49fb60ead2c12b3febce7cdf9a9ede6ea3c938a2b'
            '89a12954a70451b195a9b565fc3306a03d5fb5d94d3c7cb8690dec8d6710db95f4ba5ac356e5d9a48235ab077c39bd73e76a55eb708f444d795b939507af415d'
            '891a58e7d1a4d4a27a17a2b56bd614d87d22322f2f6e1247665b2b873c5c6d78336de04bc7a11feceb7cd8f1a2f79a858ad2520352644e808634e2989a17357d'
            'af73d95bbb806a5d5c791872e7083e33e83f21b18396687f6e7d8b1ca60da41faaab45b291006e4c471f847e260a28ac5e196f1422f209f9c64540b11a7374c6')

prepare() {
    patch -b -d tdesktop-$pkgver-full/Telegram/lib_ui/ -Np1 -i ${srcdir}/0001-Use-font-from-environment-variables.patch
    patch -b -l -d tdesktop-$pkgver-full/ -Np1 -i ${srcdir}/0002-add-TDESKTOP_DISABLE_REGISTER_CUSTOM_SCHEME-back.patch
    patch -b -l -d tdesktop-$pkgver-full/ -Np1 -i ${srcdir}/0001-make-blockquote-markup-less-distractive.patch
    patch -b -l -d tdesktop-$pkgver-full/ -Np1 -i ${srcdir}/warn-before-delete-all-2.patch
    patch -Np1 -d tdesktop-$pkgver-full < ${srcdir}/0001-Drop-usage-of-Qt-6.5-colorScheme-API-on-Linux.patch
}

build() {
    CXXFLAGS+=' -ffat-lto-objects'
    # Turns out we're allowed to use the official API key that telegram uses for their snap builds:
    # https://github.com/telegramdesktop/tdesktop/blob/8fab9167beb2407c1153930ed03a4badd0c2b59f/snap/snapcraft.yaml#L87-L88
    # Thanks @primeos!
    cmake -B build -S tdesktop-$pkgver-full -G Ninja \
        -DCMAKE_VERBOSE_MAKEFILE=ON \
        -DCMAKE_INSTALL_PREFIX="/usr" \
        -DCMAKE_BUILD_TYPE=Release \
        -DTDESKTOP_API_ID=611335 \
        -DTDESKTOP_API_HASH=d524b414d21f4d37f08684c1df41ac9c \
        -DTDESKTOP_DISABLE_REGISTER_CUSTOM_SCHEME=ON
    cmake --build build
}

package() {
    DESTDIR="$pkgdir" cmake --install build
}
