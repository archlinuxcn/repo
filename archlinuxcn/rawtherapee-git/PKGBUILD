# Maintainer: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix
# Contributor: Lukas Jirkovsky <l.jirkovsky AT gmail.com>

pkgname=rawtherapee-git
_pkgname=${pkgname%-git}
pkgver=5.11.r193.g5f7cea0bc
pkgrel=1
epoch=1
pkgdesc="A powerful cross-platform raw image processing program"
arch=('x86_64' 'i686' 'pentium4' 'arm' 'armv6h' 'armv7h' 'aarch64')
url="https://www.${_pkgname}.com"
license=('GPL-3.0-or-later')
depends=('fftw' 'gtk3' 'glibmm' 'gtkmm3' 'lcms2' 'lensfun' 'libcanberra'
	'libiptcdata' 'desktop-file-utils' 'hicolor-icon-theme' 'exiv2')
makedepends=('cmake' 'git')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=("${_pkgname}::git+https://github.com/Beep6581/RawTherapee.git")
sha256sums=('SKIP')
options=('!lto')

pkgver() {
	cd "${_pkgname}/"
	git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
	mkdir -p "${_pkgname}/build/"
}

build() {
	cd "${_pkgname}/build/"
	cmake .. \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release

	make
}

package() {
	make -C "${_pkgname}/build/" DESTDIR="${pkgdir}" install
}
