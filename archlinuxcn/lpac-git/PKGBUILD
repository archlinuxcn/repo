# Maintainer: Coelacanthus <uwu@coelacanthus.name>
# Contributor: Peter Cai <peter@typeblog.net>

pkgname=lpac-git
pkgver=2.2.1.r53.gc9f7256
pkgrel=1
pkgdesc="Local Profile Agent (LPA) for eSIM cards via PC/SC readers, or AT / APDU commands over stdio."
arch=(x86_64 aarch64 armv7h)
url="https://github.com/estkme-group/lpac"
license=('AGPL-3.0-only AND LGPL-2.0-only AND MIT')
depends=(curl pcsclite glibc)
makedepends=(cmake git ninja)
provides=(lpac)
conflicts=(lpac)
source=($pkgname::git+https://github.com/estkme-group/lpac)
b2sums=(SKIP)

pkgver() {
  cd "$pkgname"
  git describe --tags --long --abbrev=7 | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "$srcdir/$pkgname"
  cmake -S . -B build \
    -G Ninja \
    -DCMAKE_INSTALL_PREFIX="/usr" \
    -DLPAC_DYNAMIC_LIBEUICC=ON \
    -DLPAC_DYNAMIC_DRIVERS=ON
  ninja -C build
}

package() {
  cd "$srcdir/$pkgname"
  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 -t "$pkgdir"/usr/share/licenses/"$pkgname" README.md
}

