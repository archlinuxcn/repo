# Maintainer: dbgbgtf <dudududumaxver@outlook.com>
# Maintainer: RocketDev <ma2014119@outlook.com>
pkgname=ceccomp
pkgver=3.2
pkgrel=1
pkgdesc="A C-based seccomp analysis tool"
arch=(x86_64 aarch64)
url="https://github.com/dbgbgtf1/Ceccomp"
license=('GPL-3.0-or-later')
depends=(
    libseccomp
    glibc
)
makedepends=(
    asciidoctor # for doc generation
    python # for configure
    git
    util-linux # for flock to display a progress
    gettext # for i18n/l10n
)

source=("$pkgname"::git+https://github.com/dbgbgtf1/Ceccomp.git#tag=v${pkgver}?signed)
b2sums=('6b2296ed959e61e9cd77d95143b45af6c3762d68ff18c88d227d7685175cd6ef17ef228e0cdf40031f6ccdc0f9d0142d84d9aa7b18e5b94f3ac4c6d9626c0fae')

validpgpkeys=(
    '0816A179BB09248F30468BD6542A0969B5CEDCDB' # dbgbgtf1 <dudududumaxver@outlook.com>
    'A7ACCC386C15E3C554D34B3EAB08F98092A456BB' # RocketDev <ma2014119@outlook.com>
)

build() {
    # lilac always has clean root build
    cd "$srcdir/$pkgname"
    ./configure --prefix="$pkgdir/usr"
    # force program to load locale in system
    make LOCALE_DIR=/usr/share/locale
}

package() {
    cd "$srcdir/$pkgname"
    make install
}
