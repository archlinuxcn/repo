# Maintainer: jakob <grandchild@gmx.net>
# Maintainer: Roald Clark <roaldclark@gmail.com>

_pkgname=flask-restx
pkgname=python-${_pkgname}
pkgver=1.3.0
pkgrel=4
pkgdesc="Flask extension that adds support for quickly building REST APIs"
arch=('any')
url="https://github.com/python-restx/flask-restx"
license=('BSD-3-Clause')
depends=(
    "python"
    "python-aniso8601"
    "python-flask"
    "python-importlib_resources"
    "python-jsonschema"
    "python-pytz"
    "python-ujson"
    "python-werkzeug"
)
makedepends=(
    "python-build"
    "python-installer"
    "python-setuptools"
    "python-wheel"
)
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha256sums=('5bb8ca68ebd6f8ec9bc7889c2e6718f5c2f1fdae5622c02ee48a24d33f6b5405')

build() {
    cd "$_pkgname-$pkgver"
    python -m build --wheel --no-isolation
}

package() {
    cd "$_pkgname-$pkgver"
    python -m installer --destdir="$pkgdir" dist/*.whl
    install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
