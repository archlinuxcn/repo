# Maintainer: Coelacanthus <CoelacanthusHex@gmail.com>

pkgname=python-bitarray
pkgver=2.9.2
pkgrel=1
pkgdesc="Efficient arrays of booleans for Python"
arch=('x86_64')
url="https://github.com/ilanschnell/bitarray"
license=('PSF-2.0')
depends=('glibc' 'python')
makedepends=('python-distribute' 'python-wheel' 'python-build' 'python-installer' 'tree')
checkdepends=('python-pytest')
source=($pkgname-$pkgver.tar.gz::https://github.com/ilanschnell/bitarray/archive/refs/tags/$pkgver.tar.gz)
b2sums=('d47b30816aa9822873216a76fc6c668a05f5fb6526f05e3c3c0bd3db2c71f3107c758b275419003c5585b13d9bb6647a1f0a6497694c2ad604c1484707eece3c')

build() {
  cd "$srcdir"/bitarray-$pkgver
  python -m build --wheel --no-isolation
}

check(){
  cd "$srcdir"/bitarray-$pkgver
  local python_version=$(python -c 'import sys; print("".join(map(str, sys.version_info[:2])))')
  export PYTHONPATH="$PWD/build/lib.linux-$CARCH-cpython-$python_version"
  # avoid load wrong files from current dir
  export PYTHONSAFEPATH=1
  # follow upstream doc
  python -c 'import bitarray; assert bitarray.test().wasSuccessful()'
}

package() {
  cd "$srcdir"/bitarray-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
