# Maintainer: Dobroslaw Kijowski [dobo] <dobo90_at_gmail.com>

pkgname=lief
pkgver=0.10.1
pkgrel=4
pkgdesc='Library to instrument executable formats'
arch=(x86_64)
url='https://lief.quarkslab.com/'
license=(apache)
makedepends=(git cmake python python-setuptools)
source=("git+https://github.com/lief-project/LIEF#tag=${pkgver}")
md5sums=(SKIP)

prepare() {
  cd LIEF

  sed -i 's/class  *BuildToolVersion/class LIEF_API BuildToolVersion/g' \
      include/LIEF/MachO/BuildVersion.hpp
  # Try to delete first to make sure it exists
  rm cmake/strip.cmake
  # Don't strip
  echo > cmake/strip.cmake
}

build() {
  cd LIEF
  mkdir -p build
  CFLAGS+=" -fdebug-prefix-map=${srcdir}=${DBGSRCDIR:-/usr/src/debug}"
  CXXFLAGS+=" -fdebug-prefix-map=${srcdir}=${DBGSRCDIR:-/usr/src/debug}"
  CFLAGS+=" -ffile-prefix-map=${srcdir}=${DBGSRCDIR:-/usr/src/debug}"
  CXXFLAGS+=" -ffile-prefix-map=${srcdir}=${DBGSRCDIR:-/usr/src/debug}"
  CFLAGS+=" -fmacro-prefix-map=${srcdir}=${DBGSRCDIR:-/usr/src/debug}"
  CXXFLAGS+=" -fmacro-prefix-map=${srcdir}=${DBGSRCDIR:-/usr/src/debug}"
  export CPPFLAGS
  export CFLAGS
  export CXXFLAGS
  # LIEF doesn't use cmake install for python plugin so it'll have
  # the build time RPATH with it. Avoid it by not setting it during build time.
  cmake . -B build -DCMAKE_BUILD_TYPE=Release -DBUILD_SHARED_LIBS=1 \
        -DCMAKE_SKIP_BUILD_RPATH=On
  make -C build
  python setup.py build --build-temp=build
}

package() {
  cd LIEF

  make -C build DESTDIR="${pkgdir}" install
  python setup.py install --optimize=1 --root="${pkgdir}" --skip-build

  if [[ -f "$pkgdir/usr/include/json.hpp" ]]; then
    mv "$pkgdir/usr/include/json.hpp" "$pkgdir/usr/include/LIEF/json.hpp"
    cat >> "$pkgdir/usr/include/LIEF/json.hpp" <<EOF

#ifndef LIEF_JSON_H_
#define LIEF_JSON_H_

#include <LIEF/config.h>

using json = nlohmann::json;

#endif
EOF
  fi

  pymin_ver=$(python -c 'import sys; print(sys.version_info.minor)')
  ((pynext_min=pymin_ver + 1))
  depends+=("python>=3.${pymin_ver}.0" "python<3.${pynext_min}.0")
}
