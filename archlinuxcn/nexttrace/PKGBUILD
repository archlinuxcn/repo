# Maintainer: Coelacanthus <uwu@coelacanthus.name>

pkgname=nexttrace
pkgver=1.5.0
pkgrel=1
pkgdesc='An open source visual route tracking CLI tool'
arch=('x86_64')
url="https://github.com/nxtrace/Ntrace-core"
license=('GPL-3.0-only')
depends=('glibc')
makedepends=('go' 'git')
source=("git+https://github.com/nxtrace/Ntrace-core.git#tag=v$pkgver")
sha256sums=('13c3f54b95a5d34477cd99a9d2f777dd86c1acf122f4fe2eef846f87c4b1e187')

build() {
  cd "Ntrace-core"
  export BUILD_VERSION="$(git describe --tags --always)"
  export COMMIT_SHA1="$(git rev-parse --short HEAD)"
  export BUILD_DATE="$(date -u +'%Y-%m-%dT%H:%M:%SZ')"
  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external -extldflags \"${LDFLAGS}\" \
      -X 'github.com/nxtrace/NTrace-core/config.Version=${BUILD_VERSION}' \
      -X 'github.com/nxtrace/NTrace-core/config.BuildDate=${BUILD_DATE}' \
      -X 'github.com/nxtrace/NTrace-core/config.CommitID=${COMMIT_SHA1}' " \
    .
}

package() {
  cd "Ntrace-core"
  install -Dm755 NTrace-core "$pkgdir"/usr/bin/nexttrace
  setcap cap_net_raw,cap_net_admin+eip "$pkgdir"/usr/bin/nexttrace
}
