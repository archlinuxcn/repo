# Maintainer: Coelacanthus <uwu@coelacanthus.name>

pkgname=nexttrace
pkgver=1.3.7
pkgrel=1
pkgdesc='An open source visual route tracking CLI tool'
arch=('x86_64')
url="https://github.com/nxtrace/Ntrace-V1"
license=('GPL-3.0-only')
depends=('glibc')
makedepends=('go' 'git')
source=("git+https://github.com/nxtrace/Ntrace-V1.git#tag=v$pkgver")
sha256sums=('e5a4838da8eceb233bada3f8708e2a15778fd5456b2e1337d33c9c26be49f08d')

build() {
  cd "Ntrace-V1"
  export BUILD_VERSION="$(git describe --tags --always)"
  export COMMIT_SHA1="$(git rev-parse --short HEAD)"
  export BUILD_DATE="$(date -u +'%Y-%m-%dT%H:%M:%SZ')"
  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external -extldflags \"${LDFLAGS}\" \
      -X 'github.com/nxtrace/NTrace-core/config.Version=${BUILD_VERSION}' \
      -X 'github.com/nxtrace/NTrace-core/config.BuildDate=${BUILD_DATE}' \
      -X 'github.com/nxtrace/NTrace-core/config.CommitID=${COMMIT_SHA1}' " \
    .
}

package() {
  cd "Ntrace-V1"
  install -Dm755 NTrace-core "$pkgdir"/usr/bin/nexttrace
  setcap cap_net_raw,cap_net_admin+eip "$pkgdir"/usr/bin/nexttrace
}
