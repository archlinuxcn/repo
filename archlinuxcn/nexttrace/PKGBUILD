# Maintainer: Coelacanthus <CoelacanthusHex@gmail.com>

pkgname=nexttrace
pkgver=1.3.2
pkgrel=1
pkgdesc='An open source visual route tracking CLI tool'
arch=('x86_64')
url="https://github.com/nxtrace/Ntrace-V1"
license=('GPL-3.0-only')
depends=('glibc')
makedepends=('go' 'git')
source=("git+https://github.com/nxtrace/Ntrace-V1.git#tag=v$pkgver")
sha256sums=('b4a20b0cf2e8f5a5236602cae54b691640dc6200e3a771d93767d07886222c30')

build() {
  cd "Ntrace-V1"
  export BUILD_VERSION="$(git describe --tags --always)"
  export COMMIT_SHA1="$(git rev-parse --short HEAD)"
  export BUILD_DATE="$(date -u +'%Y-%m-%dT%H:%M:%SZ')"
  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external -extldflags \"${LDFLAGS}\" \
      -X 'github.com/nxtrace/NTrace-core/config.Version=${BUILD_VERSION}' \
      -X 'github.com/nxtrace/NTrace-core/config.BuildDate=${BUILD_DATE}' \
      -X 'github.com/nxtrace/NTrace-core/config.CommitID=${COMMIT_SHA1}' " \
    .
}

package() {
  cd "Ntrace-V1"
  install -Dm755 NTrace-core "$pkgdir"/usr/bin/nexttrace
  setcap cap_net_raw,cap_net_admin+eip "$pkgdir"/usr/bin/nexttrace
}
