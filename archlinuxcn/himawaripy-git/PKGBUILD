# Maintainer: Fabien JUIF <fabien.juif@gmail.com>
_application=himawaripy
pkgname=${_application}-git
pkgrel=1
pkgver=r155.035b5b4
license=('MIT')
pkgdesc="Put near-realtime picture of Earth as your desktop background."
url="https://github.com/boramalper/${_application}"
arch=('x86_64' 'i686')
depends=('python' 'python-pillow' 'python-tzlocal' 'python-dateutil' 'python-appdirs')
makedepends=('git' 'python-setuptools')
install=himawaripy.install
source=(
  "git+https://github.com/boramalper/${_application}"
)
sha256sums=(
  'SKIP'
)

package() {
  cd ${srcdir}/${_application}

  # Systemd
  mkdir -p "${pkgdir}/usr/lib/systemd/user/"
  cp ./systemd/* "${pkgdir}/usr/lib/systemd/user/"
  sed -e 's|<INSTALLATION_PATH>|/usr/bin/himawaripy|g' -i "${pkgdir}/usr/lib/systemd/user/himawaripy.service"

  # Package
  python setup.py install --root="${pkgdir}/" --optimize=1
}

pkgver() {
  cd ${_application}
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

# vim:set ts=2 sw=2 et:
