# Maintainer:
# Contributor: LightDot <lightdot -a-t- g m a i l>
# Contributor: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: William Rea <sillywilly@gmail.com>

## links
# https://wiki.gnome.org/Projects/GooCanvas
# https://gitlab.gnome.org/GNOME/goocanvas

_pkgname="goocanvas"
pkgname="${_pkgname}2"
pkgver=2.0.4
pkgrel=11
pkgdesc="A cairo canvas widget for GTK+, legacy version"
url="https://gitlab.gnome.org/GNOME/goocanvas"
license=('LGPL-2.1-or-later')
arch=('x86_64')

depends=(
  'gtk3'
)
makedepends=(
  'glib2-devel'
  'gobject-introspection'
  'python-gobject'
)

provides=("$_pkgname=$pkgver")

_pkgsrc="$_pkgname-$pkgver"
_pkgext="tar.xz"
source=("$_pkgsrc.$_pkgext"::"https://download.gnome.org/sources/goocanvas/${pkgver%.*}/$_pkgsrc.$_pkgext")
sha256sums=('c728e2b7d4425ae81b54e1e07a3d3c8a4bd6377a63cffa43006045bceaa92e90')

build() {
  export CFLAGS+=" -Wno-incompatible-pointer-types"

  cd "$_pkgsrc"
  ./configure --prefix=/usr --disable-python
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd "$_pkgsrc"
  make DESTDIR="$pkgdir" install
}
