# Maintainer: ZeekoZhu <vaezt@outlook.com>

pkgbase=rime-lmdg
pkgname=("${pkgbase}"
         "${pkgbase}-config")
_release_tag=LTS
pkgver=lts
pkgrel=1
epoch=1
pkgdesc='万象语言模型，基于32GB超大规模语料的RIME中文语言模型'
arch=(any)
url="https://github.com/amzxyz/RIME-LMDG"
source=("lmdg.yaml"
        "wanxiang-zh-hans.gram::${url}/releases/download/${_release_tag}/wanxiang-lts-zh-hans.gram")
license=('CC-BY-4.0')
sha256sums=('93b605fd11281ae248eed0648629ee23312fc56edd0911aa855bdee7b0c294c4'
            'b8a755d7fdc568a275d2457019cc56086d69138e9f415352976664a8bc835b55')

package_rime-lmdg() {
  provides=("${pkgbase}=${pkgver}")
  replaces=('rime-lmdg-m1')
  optdepends=('librime: Rime Input Method Engine'
              "${pkgbase}-config: default patch configurations")
  install -Dm664 "${srcdir}"/wanxiang-zh-hans.gram -t "${pkgdir}"/usr/share/rime-data/
}

package_rime-lmdg-config() {
  depends=("${pkgbase}")
  pkgdesc='RIME-LMDG万象语言模型（默认配置)'
  install -Dm664 "${srcdir}"/*.yaml -t "${pkgdir}"/usr/share/rime-data/
}
