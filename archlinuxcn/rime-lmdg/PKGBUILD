# Maintainer: Gavin Luo <lunt.luo@gmail.com>

pkgbase=rime-lmdg
pkgname=("${pkgbase}"
         "${pkgbase}-config")
pkgver=lts.1740793953
pkgrel=1
epoch=1
pkgdesc='万象语言模型，基于32GB超大规模语料的RIME中文语言模型'
arch=(any)
url="https://github.com/amzxyz/RIME-LMDG"
source=("lmdg.yaml"
        "wanxiang-zh-hans.gram::${url}/releases/download/LTS/wanxiang-lts-zh-hans.gram")
license=('CC-BY-4.0')
sha256sums=('93b605fd11281ae248eed0648629ee23312fc56edd0911aa855bdee7b0c294c4'
            '98457e6700eb7d9d1c3db1df81ca4a5a485cc7a5437dd1b10cca48b6e2abce2c')

package_rime-lmdg() {
  provides=("${pkgbase}=${pkgver}")
  replaces=('rime-lmdg-m1')
  optdepends=('librime: Rime Input Method Engine'
              "${pkgbase}-config: default patch configurations")
  install -Dm664 "${srcdir}"/wanxiang-zh-hans.gram -t "${pkgdir}"/usr/share/rime-data/
}

package_rime-lmdg-config() {
  depends=("${pkgbase}")
  pkgdesc='RIME-LMDG万象语言模型（默认配置)'
  install -Dm664 "${srcdir}"/*.yaml -t "${pkgdir}"/usr/share/rime-data/
}
