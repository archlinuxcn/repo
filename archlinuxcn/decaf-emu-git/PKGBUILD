# Maintainer: Mesmer <mesmer@fisica.if.uff.br>
# Contributor: Emmanuel Gil Peyrot <linkmauve@linkmauve.fr>
# Contributor: aldelaro5 <aldelaro5@gmail.com>

_pkgbase='decaf-emu'
pkgname="$_pkgbase-git"
pkgver=4613.5cf43d38
pkgrel=1
pkgdesc="An experimental open-source Nintendo Wii U emulator"
arch=('x86_64')
url="https://github.com/decaf-emu/decaf-emu"
license=('GPL')
depends=('curl' 'zlib' 'sdl2' 'vulkan-icd-loader' 'ffmpeg' 'qt5-base' 'qt5-svg')
makedepends=('git' 'cmake' 'vulkan-headers' 'glslang')
source=("$_pkgbase::git+https://github.com/decaf-emu/decaf-emu")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgbase"
  echo "$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

prepare() {
  cd "$srcdir/$_pkgbase"
  mkdir -p build
  git submodule update --init
}

build() {
  cd "$srcdir/$_pkgbase"
  cd build
  CXXFLAGS+=" -DFMT_USE_USER_DEFINED_LITERALS=0" cmake .. -DFMT_INSTALL=OFF -DDECAF_QT=ON -DCMAKE_INSTALL_PREFIX=/usr
  make VERBOSE=1
}

package() {
  cd "$srcdir/$_pkgbase/build"
  make DESTDIR="$pkgdir" install
}
