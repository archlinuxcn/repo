# Maintainer: Joey Dumont <joey.dumont@gmail.com>
# Contributor: LIN Ruohshoei <lin dot ruohshoei+arch at gmail dot com>
# Contributor: Michael DeGuzis <mdeguzis@gmail.com>

pkgbase=snes9x-git
_pkgbase=snes9x
pkgname=(snes9x-git snes9x-gtk-git)
pkgver=1.62.3.r40.g8d8e691c
pkgrel=1
pkgdesc="Port of the Snes9x emulator (git version)"
arch=('x86_64')
url="http://www.snes9x.com/"
license=('custom')
makedepends=(alsa-lib cairo gdk-pixbuf2 git glib2
  gtk3 intltool libepoxy libpng libpulse libx11 libxext libxml2
  libxrandr libxv meson minizip nasm portaudio sdl2 zlib gtkmm3
  cmake libyuv
)
source=(
  git+https://github.com/snes9xgit/snes9x.git
)
sha256sums=('SKIP')

pkgver() {
  cd "${_pkgbase}"
  git describe --long --tags | sed -E 's/([^-]*-g)/r\1/;s/-/./g'

}
prepare() {
  cd ${_pkgbase}
  git submodule update --init --recursive

  cd unix
  autoreconf -fiv
}

build() {
  cd "${_pkgbase}/unix"

  ./configure \
    --prefix='/usr' \
    --enable-netplay \
    --enable-libyuv \
    --enable-sse41 \
    --enable-avx2
  make

  cd "${srcdir}/${_pkgbase}/gtk"
  cmake -G Ninja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release -S . -B build
  ninja -C build
}

package_snes9x-git() {

  pkgdesc="portable Emulator for the Super Nintendo Entertainment System"
  depends=(libpng libxinerama libxv minizip libyuv alsa-lib)

  conflicts=('snes9x')
  provides=('snesx')

  cd "${_pkgbase}"
  install -D -m755 unix/snes9x -t "${pkgdir}"/usr/bin/
  install -d "${pkgdir}/usr/share/doc/${pkgname}"
  install -D -m644  {unix/snes9x.conf.default,docs/{control-inputs,controls,snapshots}.txt} \
    "${pkgdir}/usr/share/doc/${pkgname}/"
  install -vDm644  LICENSE -t \
    "${pkgdir}"/usr/share/licenses/"${pkgname}"
}

package_snes9x-gtk-git() {

  pkgdesc="Portable Emulator for the Super Nintendo Entertainment System - GTK version"
  depends=(alsa-lib cairo gdk-pixbuf2 glib2 gtk3 hicolor-icon-theme libepoxy libpng libpulse libxext libxml2 libxrandr libxv minizip portaudio sdl2 gtkmm3)

  conflicts=('snes9x-gtk')
  provides=('snes9x-gtk')

  cd ${srcdir}/${_pkgbase}/gtk
  DESTDIR="${pkgdir}" ninja -C build install

  cd "${srcdir}/${_pkgbase}"

  install -d "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm644 {unix/snes9x.conf.default,docs/{control-inputs,controls,snapshots}.txt} \
    "${pkgdir}/usr/share/doc/${pkgname}/"
  install -vDm644 LICENSE -t \
    "${pkgdir}/usr/share/licenses/${pkgname}"

   # Remove glslang files
   #rm "${pkgdir}/usr/bin/glslangValidator"
   #rm "${pkgdir}/usr/lib/libHLSL.a"
   #rm "${pkgdir}/usr/lib/libOGLCompiler.a"
   #rm "${pkgdir}/usr/lib/libOSDependent.a"
   #rm "${pkgdir}/usr/lib/libSPIRV.a"
   #rm "${pkgdir}/usr/lib/libSPVRemapper.a"
   #rm "${pkgdir}/usr/lib/libglslang.a"


}
