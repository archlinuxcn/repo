# Maintainer: Michael DeGuzis <mdeguzis@gmail.com>

pkgbase=snes9x-git
pkgname=('snes9x-git' 'snes9x-gtk-git')
pkgver=1.60.r233.g793b3f6
pkgrel=1
pkgdesc="Port of the Snes9x emulator (git version). Includes Gtk version."
arch=('i686' 'x86_64')
url="http://www.snes9x.com/"
license=('LGPL')
depends=('minizip')
makedepends=('git' 'portaudio' 'glslang' 'intltool' 'nasm' 'mesa' 'libpulse'  'libpng' 'sdl2' 'gtk3' 'libxv'
             'adwaita-icon-theme' 'autogen' 'meson' 'minizip' 'gtkmm3')
source=("$pkgname"::'git://github.com/snes9xgit/snes9x.git')
sha1sums=('SKIP')

pkgver() {

  cd "$srcdir/$pkgname"
  git describe --long --tags | sed -E 's/([^-]*-g)/r\1/;s/-/./g'

}

prepare() {
  cd "${pkgbase}"
  git submodule init
  git submodule update
}
build() {

  cd ${pkgbase}/unix
  # work around problems with automake & cvs
  touch configure
  ./configure --prefix=/usr --enable-netplay
  make

  cd ../gtk
  meson -Dbuildtype=release -Dprefix=/usr -Dlocalstatedir=/var -Dsharedstatedir=/var/lib . build
  meson configure build
  cd build
  ninja

}

package_snes9x-git() {

  pkgdesc="A portable Emulator for the Super Nintendo Entertainment System"
  depends=('libpng' 'libxinerama' 'libxext' 'libsm' 'libxv' 'minizip')
  conflicts=('snes9x')
  provides=('snesx')

  cd ${pkgbase}/unix
  install -D -m755 snes9x "${pkgdir}/usr/bin/snes9x"
  install -d "${pkgdir}/usr/share/doc/${pkgname}"
  install -D -m644 ../{unix/snes9x.conf.default,docs/{control-inputs,controls,snapshots}.txt} \
    "${pkgdir}/usr/share/doc/${pkgname}/"
  install -D -m644 ../LICENSE \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

}

package_snes9x-gtk-git() {

  pkgdesc="A portable Emulator for the Super Nintendo Entertainment System - GTK version"
  license=('custom' 'LGPL')
  depends=('sdl2' 'glslang' 'portaudio' 'libpulse' 'gtk3' 'libxv' 'adwaita-icon-theme' 'minizip' 'libgl' 'gtkmm3')
  conflicts=('snes9x-gtk')
  provides=('snes9x-gtk')

  cd ${pkgbase}/gtk
  DESTDIR="${pkgdir}" ninja -C build install
  install -d "${pkgdir}/usr/share/doc/${pkgname}"
  install -D -m644 ../{unix/snes9x.conf.default,docs/{control-inputs,controls,snapshots}.txt} \
    "${pkgdir}/usr/share/doc/${pkgname}/"
  install -D -m644 ../LICENSE \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

}
