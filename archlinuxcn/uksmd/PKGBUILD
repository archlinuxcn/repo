# Maintainer: Coelacanthus <uwu@coelacanthus.name>
# Contributor:Oleksandr Natalenko <oleksandr@natalenko.name>

pkgname=uksmd
pkgver=6.12.1
pkgrel=1
pkgdesc="Userspace KSM helper daemon"
url=https://codeberg.org/pf-kernel/uksmd
license=(GPL-3.0-only)
arch=(x86_64)
depends=(systemd-libs glibc procps-ng libcap-ng UKSMD-BUILTIN)
optdepends=('uksmdstats: for parsing /sys KSM statistics')
makedepends=(meson)
source=($pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz)
b2sums=('d3135eebd6929bc4f2ec8e5aae69c5dcec061a9d78f3d451dc934435f904a71f22fa99fb90cde8a4f1954066c7f3640bfa07f221086b8a74551d13e19323b20e')

build() {
	cd $pkgname
	arch-meson . build
	meson compile -C build
}

package() {
	cd $pkgname
	meson install -C build --destdir "$pkgdir"
}
