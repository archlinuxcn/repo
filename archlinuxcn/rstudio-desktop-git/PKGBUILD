# Maintainer: Aleksandr Beliaev <trap000d@gmail.com>

pkgname=rstudio-desktop-git
_gitname=rstudio
pkgver=42543.18f0129dd4
_srcname=rstudio-${_vermajor}.${_verminor}.${_verpatch}-${_versuffix}
_nodever=20.15.1
_pandocver="current"
_sociver="4.0.3"
_quarto="TRUE"

pkgrel=1
pkgdesc="A powerful and productive integrated development environment (IDE) for R programming language"
arch=('x86_64')
url="https://github.com/rstudio/rstudio"
license=('AGPL-3.0-only')
depends=('r>=3.3.0' 'boost-libs' 'postgresql-libs' 'sqlite3' 'clang' 'hunspell-en_US' 'mathjax2' 'pandoc' 'yaml-cpp' 'quarto-cli-bin' 'nodejs')
makedepends=('git' 'cmake>=3.29' 'boost' 'ninja' 'npm' 'gyp' 'desktop-file-utils' 'jdk11-openjdk' 'apache-ant' 'unzip' 'openssl' 'python-setuptools' 'libcups' 'pam' 'patchelf' 'wget' 'yarn')
optdepends=('git: for git support'
            'subversion: for subversion support'
            'openssh-askpass: for a git ssh access'
            'quarto: for Quarto projects support')
provides=('rstudio-desktop')
conflicts=('rstudio-desktop')

source=("git+https://github.com/rstudio/rstudio.git"
        "git+https://github.com/quarto-dev/quarto.git#branch=release/rstudio-cherry-blossom"
        "https://github.com/SOCI/soci/archive/refs/tags/v${_sociver}.tar.gz"
        "https://nodejs.org/dist/v${_nodever}/node-v${_nodever}-linux-x64.tar.gz"
        "qt.conf"
        "0001-skip_node_install.patch"
        "0002-enable_system_node.patch")

sha256sums=('SKIP'
            'SKIP'
            '4b1ff9c8545c5d802fbe06ee6cd2886630e5c03bf740e269bb625b45cf934928'
            'a9db028c0a1c63e3aa0d97de24b0966bc507d8239b3aedc4e752eea6b0580665'
            '723626bfe05dafa545e135e8e61a482df111f488583fef155301acc5ecbbf921'
            'ed60b18cde77177cd624e904adb7aafa627fc6015c5ab8ff1ef86a9fca54916b'
            'a8abe9c5222cadc7cb06b8a540fab2fc08a8b88ee48332113501f3653a894006')

options=('!emptydirs' '!debug')

pkgver() {
    cd "${srcdir}/${_gitname}"
    printf "%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    cd ${srcdir}/${_gitname}
    msg "Do not use outdated version name of pandoc"
    sed -i '/PANDOC_VERSION/s/2.18/current/' "${srcdir}/${_gitname}/cmake/globals.cmake"
    sed -i 's|/etc/os-release|/usr/lib/os-release|g' "${srcdir}/${_gitname}/cmake/modules/OsRelease.cmake"

    msg "Suppress _FORTIFY_SOURCE mismatch warnings"
    sed -i 's/D_FORTIFY_SOURCE=2/D_FORTIFY_SOURCE=3/' "${srcdir}/${_gitname}/src/cpp/CMakeLists.txt"
    
    sed -i 's/add_subdirectory(package)/# add_subdirectory(package)/' "${srcdir}/${_gitname}/CMakeLists.txt"

    # Add option to use system node
    patch -p1 < ${srcdir}/0001-skip_node_install.patch
    patch -p1 < ${srcdir}/0002-enable_system_node.patch

    cd "${srcdir}/${_gitname}/dependencies/common"
    install -d pandoc/${_pandocver}
 
    ln -sfT /usr/share/myspell/dicts dictionaries
    ln -sfT /usr/share/mathjax2 mathjax-27
    ln -sfT /usr/bin/pandoc pandoc/${_pandocver}/pandoc

    # Nodejs
    install -d node/${_nodever}
    cp -r "${srcdir}/node-v${_nodever}-linux-x64/"* node/${_nodever}

    # Fix links for src/cpp/session/CMakeLists.txt
    cd "${srcdir}/${_gitname}/dependencies"
    ln -sfT /usr/share/myspell/dicts dictionaries
    ln -sfT /usr/share/mathjax2 mathjax-27

    # Bundled SOCI libs
    ln -sfT "${srcdir}/soci-${_sociver}" "soci-${_sociver}"

    # Panmirror is picked up from Quarto repo
    ln -sfT "${srcdir}/quarto" "${srcdir}/${_gitname}/src/gwt/lib/quarto"
}

build() {
    # Quarto set up
    if (pacman -Q quarto-cli-bin >/dev/null 2>/dev/null) ; then
        _quarto="TRUE"
        msg "Quarto is installed, include it to build"
        cd "${srcdir}/${_gitname}/dependencies"
        install -d quarto/bin/tools
        ln -sfT /usr/bin/quarto quarto/bin/quarto
        ln -sfT /usr/bin/pandoc quarto/bin/tools/pandoc
    else
        _quarto="FALSE"
        msg "Quarto is not installed, use Pandoc"
        cd "${srcdir}/${_gitname}/dependencies"
        install -d pandoc/${_pandocver}/bin/tools
        ln -sfT /usr/bin/pandoc pandoc/${_pandocver}/bin/tools/pandoc
    fi

    cd "${srcdir}/${_gitname}/dependencies/soci-${_sociver}"
    msg "Building SOCI libs..."

    local _CMAKE_SOCI_OPTIONS=(
      -DCMAKE_POLICY_DEFAULT_CMP0063="NEW"
      -DCMAKE_POLICY_DEFAULT_CMP0074="NEW"
      -DCMAKE_POSITION_INDEPENDENT_CODE:BOOL=true
      -DCMAKE_CXX_VISIBILITY_PRESET="$COMPILE_VISIBILITY"
      -DSOCI_TESTS=OFF
      -DSOCI_CXX11=ON
      -DSOCI_EMPTY=OFF
      -DWITH_BOOST=ON
      -DWITH_POSTGRESQL=ON
      -DWITH_SQLITE3=ON
      -DWITH_DB2=OFF
      -DWITH_MYSQL=OFF
      -DWITH_ORACLE=OFF
      -DWITH_FIREBIRD=OFF
      -DWITH_ODBC=OFF
    )

    cmake -G Ninja -S "${srcdir}/${_gitname}/dependencies/soci-${_sociver}" -B build "${_CMAKE_SOCI_OPTIONS[@]}"
    cmake --build build --target all

    export LDFLAGS="${LDFLAGS} -L${srcdir}/${_gitname}/dependencies/soci-${_sociver}/build/lib"

    cd ${srcdir}
    msg "Downloading and installing R packages..."
    export R_LIBS_USER="${srcdir}/${_gitname}/dependencies/R"
    _JOBS="$(grep -oP -- "-j\s*\K[0-9]+" <<< "${MAKEFLAGS}")" || _JOBS="1"
    mkdir -p "${R_LIBS_USER}"
    for RPKG in rmarkdown renv testthat xml2 yaml; do
        RINSTALLCMD="if("'!'"require($RPKG, quietly = TRUE)) { options(Ncpus = ${_JOBS} ); install.packages('$RPKG', lib='$R_LIBS_USER', repos='https://cran.rstudio.com/') }"
        echo "> $RINSTALLCMD"
        Rscript -e "$RINSTALLCMD"
    done

    export PATH=/usr/lib/jvm/java-11-openjdk/jre/bin/:${PATH}
    export RSTUDIO_TOOLS_ROOT="${srcdir}/${_gitname}/dependencies"
    export RSTUDIO_NODE_PATH=/usr/
    export RSTUDIO_VERSION_MAJOR=${_vermajor}
    export RSTUDIO_VERSION_MINOR=${_verminor}
    export RSTUDIO_VERSION_PATCH=${_verpatch}
    export RSTUDIO_VERSION_SUFFIX="+${_versuffix}"
    export GIT_COMMIT=${_gitcommit}
    export PACKAGE_OS=$(uname -om)

    # node-gyp or node have a bug that prevents building with "text file busy"
    # if the kernel is too fast, so we have to disable IO_URING support. This
    # is cleary a hack and needs to be removed as soon as possible
    # nodejs/node#48444 is the necro bumped thread
    # originally from docker
    # https://github.com/nodejs/node/issues/48444
    export UV_USE_IO_URING=0

    # -DCMAKE_INSTALL_PREFIX seems ignored for sub-dependencies, 
    # which results as empty '/usr/local/bin' in package
    # Following override works for cmake >3.29
    export CMAKE_INSTALL_PREFIX=/usr/lib/rstudio
    
    local _CMAKE_RSTUDIO_OPTIONS=(
      -DRSTUDIO_TARGET=Electron
      -DCMAKE_BUILD_TYPE=Release
      -DRSTUDIO_USE_SYSTEM_BOOST=yes
      -DQT_QMAKE_EXECUTABLE=/usr/bin/qmake
      -DBoost_NO_BOOST_CMAKE=ON
      -DQUARTO_ENABLED=${_quarto}
      -DRSTUDIO_BUNDLE_QT=FALSE
    )
    cmake -G Ninja -S "${srcdir}/${_gitname}" -B build "${_CMAKE_RSTUDIO_OPTIONS[@]}"
    cmake --build build
}

package() {

    # Install the program
    DESTDIR="${pkgdir}" cmake --install build

    # Install the license
    install -Dm 644 "${srcdir}/${_gitname}/COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"

    # Symlink main binary
    install -d "${pkgdir}/usr/bin"
    ln -s "/usr/lib/rstudio/rstudio" "${pkgdir}/usr/bin/rstudio"

    # BUGFIX: qt5-webengine isn't init'ing properly. Likely an Rstudio bug.
    install -Dm 644 "${srcdir}/qt.conf" "${pkgdir}/usr/lib/qt/libexec/qt.conf"
}
