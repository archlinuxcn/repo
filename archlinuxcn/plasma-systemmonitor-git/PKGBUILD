# Maintainer: Jiachen YANG <farseerfc@gmail.com>

_pkgname='plasma-systemmonitor'
pkgname="${_pkgname,,}-git"
pkgver=v5.20.0.r61.gd4253c8
pkgrel=1
pkgdesc='New version of KSysGuard built on top of the new KStats daemon and using QML for its UI, git version'
arch=('x86_64')
url='https://github.com/KDE/plasma-systemmonitor'
license=('GPL3')
depends=('ksysguard' 'libksysguard' 'qt5-quickcontrols' 'kirigami2')
makedepends=('extra-cmake-modules' 'git' 'qt5-tools')
provides=("${_pkgname,,}")
conflicts=("${_pkgname,,}")
source=("${_pkgname}::git+${url}.git")
sha256sums=('SKIP')

pkgver() {
  cd $_pkgname
  ( set -o pipefail
    git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

build() {
  cmake -B build -S "${_pkgname}"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}

# vim: ts=2 sw=2 et:
