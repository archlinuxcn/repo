# Maintainer: kusakata <shohei atmark kusakata period com>

pkgname=openspecfun-git
pkgver=0.5.3.5.g2b049c3
pkgrel=4
pkgdesc="A collection of special mathematical functions"
arch=('i686' 'x86_64' 'armv7h' 'aarch64')
url="https://github.com/JuliaLang/openspecfun"
license=('custom')
depends=('gcc-libs' 'openlibm')
provides=(openspecfun)
conflicts=(openspecfun)
makedepends=('gcc-fortran' 'git')
options=('!emptydirs' '!strip')
source=('git://github.com/JuliaLang/openspecfun')
md5sums=('SKIP')

pkgver() {
  cd openspecfun

  git describe --tags | sed 's/^v//;s/-/./g'
}

build() {
  cd openspecfun

  make prefix=/usr bindir=/usr/bin libdir=/usr/lib includedir=/usr/include \
       USE_OPENLIBM=1 CFLAGS="-std=c99 -Wall -O3 -g -flto" \
       LDFLAGS="$LDFLAGS -g -O3 -flto  -fno-plt -Wl,-Bsymbolic-functions" \
       FFLAGS="-O3 -g -flto"
}

package() {
  cd openspecfun

  make DESTDIR="$pkgdir" prefix=/usr bindir=/usr/bin libdir=/usr/lib \
       includedir=/usr/include install CFLAGS="-std=c99 -Wall -O3 -g -flto" \
       LDFLAGS="$LDFLAGS -g -O3 -flto" FFLAGS="-O3 -g -flto"
  install -Dm644 LICENSE.md "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
