# Maintainer: kusakata <shohei atmark kusakata period com>

pkgname=openspecfun-git
pkgver=0.5.3.5.g2b049c3
pkgrel=4
pkgdesc="A collection of special mathematical functions"
arch=('i686' 'x86_64' 'armv7h' 'aarch64')
url="https://github.com/JuliaLang/openspecfun"
license=('custom')
depends=('gcc-libs' 'openlibm')
provides=(openspecfun)
conflicts=(openspecfun)
makedepends=('gcc-fortran' 'git')
options=('!emptydirs' '!strip' 'debug')
source=('git://github.com/JuliaLang/openspecfun')
md5sums=('SKIP')

_make() {
  CFLAGS+=" -fdebug-prefix-map=${srcdir}=${DBGSRCDIR:-/usr/src/debug}"
  FFLAGS+=" -fdebug-prefix-map=${srcdir}=${DBGSRCDIR:-/usr/src/debug}"
  LDFLAGS+=" -fdebug-prefix-map=${srcdir}=${DBGSRCDIR:-/usr/src/debug}"
  CFLAGS+=" -ffile-prefix-map=${srcdir}=${DBGSRCDIR:-/usr/src/debug}"
  FFLAGS+=" -ffile-prefix-map=${srcdir}=${DBGSRCDIR:-/usr/src/debug}"
  LDFLAGS+=" -ffile-prefix-map=${srcdir}=${DBGSRCDIR:-/usr/src/debug}"
  CFLAGS+=" -fmacro-prefix-map=${srcdir}=${DBGSRCDIR:-/usr/src/debug}"
  make prefix=/usr bindir=/usr/bin libdir=/usr/lib includedir=/usr/include \
       USE_OPENLIBM=1 CFLAGS="$CFLAGS -std=c99 -Wall -O3 -g -flto" \
       LDFLAGS="$LDFLAGS -g -O3 -flto  -fno-plt -Wl,-Bsymbolic-functions" \
       FFLAGS="$FFLAGS -O3 -g -flto" "$@"
}

pkgver() {
  cd openspecfun

  git describe --tags | sed 's/^v//;s/-/./g'
}

build() {
  cd openspecfun

  _make
}

package() {
  cd openspecfun

  _make DESTDIR="$pkgdir" install
  install -Dm644 LICENSE.md "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
