# Maintainer: Integral <integral@member.fsf.org>
# Contributor: Kimiblock Zhou <pn3535 at icloud dot com>

pkgname=qcm-git
_pkgname=${pkgname%-git}
pkgver=1.1.2.r8.g98d96dd
pkgrel=1
pkgdesc="Qt client for netease cloud music"
arch=('x86_64')
url="https://github.com/hypengw/Qcm"
license=('GPL-2.0-or-later')
depends=(
	'qt6-base'
	'qt6-quick3d'
	'hicolor-icon-theme'
	'curl'
	'openssl'
	'dbus'
	'ffmpeg'
	'fmt')
makedepends=(
	'git'
	'cmake'
	'ninja'
	'asio'
	'cubeb-git'
	'pegtl'
	'nlohmann-json')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=("git+${url}.git")
sha256sums=('SKIP')

pkgver() {
	git -C Qcm describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/v//'
}

prepare() {
	git -C Qcm submodule update --init --recursive
}

build() {
	cmake -B build \
		-S Qcm \
		-GNinja \
		-DCMAKE_BUILD_TYPE=None \
		-DCMAKE_INSTALL_PREFIX=/usr

	cmake --build build
}

package() {
	DESTDIR="${pkgdir}" cmake --install build
	install -Dm644 Qcm/LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
