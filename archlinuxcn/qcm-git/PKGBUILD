# Maintainer: Integral <integral@member.fsf.org>
# Contributor: Kimiblock Zhou <pn3535 at icloud dot com>

pkgname=qcm-git
_pkgname=${pkgname%-git}
pkgver=1.1.2.r95.g6085d0c
pkgrel=2
pkgdesc="Qt client for netease cloud music"
arch=('x86_64')
url="https://github.com/hypengw/Qcm"
license=('GPL-2.0-or-later')
depends=(
	'qt6-base'
	'qt6-quick3d'
	'hicolor-icon-theme'
	'curl'
	'openssl'
	'dbus'
	'ffmpeg'
	'fmt'
	'cubeb-git'
	'kdsingleapplication')
makedepends=(
	'git'
	'cmake'
	'asio'
	'pegtl'
	'nlohmann-json'
	'ctre'
	'tl-expected')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=(
	"git+${url}.git"
	"git+https://github.com/effolkronium/random.git"
	"git+https://github.com/hypengw/QmlMaterial.git"
	"git+https://github.com/hypengw/qcm-jellyfin-plugin.git"
)
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP')

pkgver() {
	git -C Qcm describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/v//'
}

prepare() {
	cd Qcm
	git rm third_party/{expected,KDSingleApplication}

	git submodule init
	git config submodule.third_party/random.url "${srcdir}/random"
	git config submodule.qml_material.url "${srcdir}/QmlMaterial"
	git config submodule.service/jellyfin.url "${srcdir}/qcm-jellyfin-plugin"
	git -c protocol.file.allow=always submodule update
}

build() {
	cmake -B build \
		-S Qcm \
		-DCMAKE_BUILD_TYPE=None \
		-DCMAKE_INSTALL_PREFIX=/usr

	cmake --build build
}

package() {
	DESTDIR="${pkgdir}" cmake --install build
	install -Dm644 Qcm/LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
