# Maintainer: Butui Hu <hot123tea123@gmail.com>

_pkgname=pdftopng
pkgname=python-pdftopng
pkgver=0.2.4
pkgrel=1
pkgdesc='A PDF to PNG conversion library (based on pdftoppm from poppler)'
arch=('x86_64')
url='https://github.com/vinayak-mehta/pdftopng'
license=('GPL-2.0-or-later')
depends=(
  gcc-libs
  glibc
  poppler
  python-click
)
makedepends=(
  pybind11
  python-build
  python-installer
  python-setuptools
  python-wheel
)
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/vinayak-mehta/pdftopng/archive/refs/tags/v${pkgver}.tar.gz"
        "https://gitlab.freedesktop.org/poppler/poppler/raw/poppler-24.03.0/utils/numberofcharacters.h"
        "setOverprintPreview.patch"
)
sha512sums=('ad15fa686d8094d4b74348973b1946f917a5cfd46daa85f87039357ed31077b066368e6f6484ecd20608fbbedc12fc9d7cc91afab1379e59a28e29e7f41f383d'
            'cb3f133df77319e1e04e97e7d391f0bdfb242bf18d0e64459eed5f96c6e835a8c0b4b28de3a354e7e910c38a5bcdf70d9dc729fa5e84b54104cdb80d51d41b63'
            '653f6fb1d001e71fc8e3285ad12778cba84493ea49c2823ea17bf40d56d88053f0ab05eaa19b922701be2505f90ddff3f0780555b71f225fe29554dc1de32705')

prepare() {
  cd "${_pkgname}-${pkgver}"
  sed -i '/#include "config.h"/d' src/pdftopng/pdftopng.cpp
  sed -i 's#^poppler_dir = .*$#poppler_dir = "/usr/include/poppler"#g' setup.py
  sed -i 's#lib/poppler/build#/usr/lib#g' setup.py
  sed -i 's#flags = \["-std=c++14", "-std=c++11"\]#flags = \["-std=c++20", "-std=c++17", "-std=c++14", "-std=c++11"\]#' setup.py
  # poppler in ArchLinux contains no such file
  # we download it from poppler upstream and copy it to here
  cp "$srcdir"/numberofcharacters.h src/pdftopng
  patch -p1 -i "${srcdir}/setOverprintPreview.patch"
}

build() {
  cd "${_pkgname}-${pkgver}"
  python -m build --wheel --no-isolation
}

package() {
  cd "${_pkgname}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
}
# vim:set ts=2 sw=2 et:
