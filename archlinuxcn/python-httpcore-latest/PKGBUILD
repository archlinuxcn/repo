_name=httpcore
pkgname=python-httpcore-latest
pkgver=0.14.4
pkgrel=1
pkgdesc="A minimal low-level HTTP client."
arch=(any)
url="https://github.com/encode/httpcore"
license=('BSD')
depends=('python-h11' 'python-sniffio' 'python-anyio')
makedepends=('python-setuptools')
optdepends=('python-h2: HTTP/2 support')
conflicts=('python-httpcore')
provides=('python-httpcore')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/httpcore-0.14.4.tar.gz")
sha256sums=('d4305811f604d3c2e22869147392f134796976ff946c96a8cfba87f4e0171d83')

build() {
  cd "$srcdir/httpcore-0.14.4"
  python3 setup.py build
}

package() {
  cd "$srcdir/httpcore-0.14.4"
  python3 setup.py install --root=$pkgdir --optimize=1 --skip-build

  # make sure we don't install any world-writable or root-readable-only files
  # we shouldn't need to fix ownership as we extract tarballs as a non-root user
  # https://github.com/pypa/setuptools/issues/1328
  # https://github.com/LonamiWebs/Telethon/issues/1605
  chmod u=rwX,go=rX -R "$pkgdir"
  # make sure we don't install annoying files
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  rm -rf "$pkgdir/$_site_packages/tests/"
}

