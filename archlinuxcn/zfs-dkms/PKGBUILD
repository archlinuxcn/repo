# Maintainer: Kevin Stolp <kevinstolp@gmail.com>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Iacopo Isimbaldi <isiachi@rhye.it>

pkgname=zfs-dkms
pkgver=2.2.5
pkgrel=1
pkgdesc="Kernel modules for the Zettabyte File System."
arch=('any')
url="https://zfsonlinux.org/"
license=('CDDL-1.0')
provides=("ZFS-MODULE=${pkgver}" "SPL-MODULE=${pkgver}")
# ambiguous, provided for backwards compat, pls don't use
provides+=('zfs')
source=("https://github.com/zfsonlinux/zfs/releases/download/zfs-${pkgver}/zfs-${pkgver}.tar.gz"{,.asc}
	"0001-only-build-the-module-in-dkms.conf.patch")
sha256sums=('2388cf6f29cd75e87d6d05e4858a09d419c4f883a658d51ef57796121cd08897'
            'SKIP'
            '8d5c31f883a906ab42776dcda79b6c89f904d8f356ade0dab5491578a6af55a5')
b2sums=('ec73c866bf931e4af63a35b3e61e1254d681d21aadba304ea95c147a3911c3d92db686591147036848ceae1683f31f6f81e04a24df6cb11a91a8abaed179567f'
        'SKIP'
        '58dc2494e71b50833d44c126b72acad52e9817626542afbc245b7ba82009e8c8252ebde6023592aac42d9942207e7655c0a421da9067fbdd619746ebc372d791')
validpgpkeys=('4F3BA9AB6D1F8D683DC2DFB56AD860EED4598027' # Tony Hutter (GPG key for signing ZFS releases) <hutter2@llnl.gov>
	'C33DF142657ED1F7C328A2960AB9E991C6AF658B')             # Brian Behlendorf <behlendorf1@llnl.gov>

prepare() {
	cd "zfs-${pkgver}/"

	patch -p1 -i ../0001-only-build-the-module-in-dkms.conf.patch

	# remove unneeded sections from module build
	sed -ri "/AC_CONFIG_FILES/,/]\)/{
/AC_CONFIG_FILES/n
/]\)/n
/^\s*(module\/.*|zfs.release|Makefile)/!d
}" configure.ac

	autoreconf -fi
}

build() {
	cd "zfs-${pkgver}/"

	./scripts/dkms.mkconf -n zfs -v ${pkgver} -f dkms.conf
	./scripts/make_gitrev.sh include/zfs_gitrev.h
}

package() {
	depends=("zfs-utils=${pkgver}" 'dkms' 'perl')
	cd "zfs-${pkgver}/"

	dkmsdir="${pkgdir}/usr/src/zfs-${pkgver}"
	install -d ${dkmsdir}/{config,scripts}
	cp -a configure dkms.conf Makefile.in META zfs_config.h.in zfs.release.in include/ module/ "${dkmsdir}/"
	install -Dm644 config/compile config/config.* config/missing config/*sh "${dkmsdir}/config/"
	install -Dm755 scripts/enum-extract.pl scripts/dkms.postbuild "${dkmsdir}/scripts/"
}
