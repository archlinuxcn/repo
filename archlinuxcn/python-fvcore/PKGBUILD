# Maintainer: Butui Hu <hot123tea123@gmail.com>
# Contributor: Jerry Lin <jerry73204 at gmail dot com>

_pkgname=fvcore
pkgname=python-fvcore
pkgver=0.1.2.post20210113
pkgrel=1
pkgdesc="Collection of common code that's shared among different research projects in FAIR computer vision team."
arch=('any')
url="https://github.com/facebookresearch/fvcore"
license=('Apache')
makedepends=(python-setuptools)
depends=(python)
source=("${_pkgname}-${pkgver}.tar.gz::https://files.pythonhosted.org/packages/source/${_pkgname::1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
sha256sums=('ef951f0bd0687dfcfdf212102e4a86ccce5ccc0d25e13ac52424472c6150fb6f')

build() {
  cd "${_pkgname}-${pkgver}"
  python setup.py build
}

package() {
  cd "${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
# vim:set ts=2 sw=2 et:
