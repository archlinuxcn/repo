# Maintainer: Butui Hu <hot123tea123@gmail.com>
# Contributor: Jerry Lin <jerry73204 at gmail dot com>

_pkgname=fvcore
pkgname=python-fvcore
pkgver=0.1.5.post20221221
pkgrel=5
pkgdesc="Collection of common code that's shared among different research projects in FAIR computer vision team."
arch=('any')
url="https://github.com/facebookresearch/fvcore"
license=('Apache-2.0')
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
depends=(
  python-iopath
  python-numpy
  python-pillow
  python-shapely
  python-tabulate
  python-termcolor
  python-tqdm
  python-yacs
  python-yaml
)
source=("${_pkgname}-${pkgver}.tar.gz::https://files.pythonhosted.org/packages/source/${_pkgname::1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
sha256sums=('f2fb0bb90572ae651c11c78e20493ed19b2240550a7e4bbb2d6de87bdd037860')

build() {
  cd "${_pkgname}-${pkgver}"
  python -m build --wheel --no-isolation
}

package() {
  cd "${_pkgname}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
}
# vim:set ts=2 sw=2 et:
