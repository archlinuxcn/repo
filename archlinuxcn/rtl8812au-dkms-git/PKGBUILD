# Maintainer: zebulon <zeb (at)zebulon(dot)org(dot)uk>
# Contributor: NovaMoon <novamoon1 (at)gmail(dot)com>

pkgname=rtl8812au-dkms-git
_pkgbase=rtl8812au
pkgver=5.6.4.2.r11.g3110ad6
pkgrel=1
pkgdesc="rtl8812AU chipset driver with firmware v5.6.4.2"
arch=('i686' 'x86_64')
url="https://github.com/gordboy/rtl8812au-5.6.4.2"
license=('GPL2')
depends=('dkms' 'bc')
makedepends=('git')
conflicts=("${_pkgbase}")

source=("git+https://github.com/gordboy/rtl8812au-5.6.4.2.git"
'dkms.conf')

sha256sums=('SKIP'
    'f0842466dad49a2d0a16fd29e3c5253128b3642d6a42a0a8e08b7310ab265204')

pkgver() {
    cd ${srcdir}/rtl8812au-5.6.4.2
    printf '%s.r%s.g%s' '5.6.4.2' "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
cd ${srcdir}/rtl8812au-5.6.4.2
mkdir -p ${pkgdir}/usr/src/${_pkgbase}-${pkgver}
cp -pr * ${pkgdir}/usr/src/${_pkgbase}-${pkgver}
cp ${srcdir}/dkms.conf ${pkgdir}/usr/src/${_pkgbase}-${pkgver}
# Set name and version
sed -e "s/@_PKGBASE@/${_pkgbase}-dkms/" \
    -e "s/@PKGVER@/${pkgver}/" \
    -i "${pkgdir}"/usr/src/${_pkgbase}-${pkgver}/dkms.conf
}