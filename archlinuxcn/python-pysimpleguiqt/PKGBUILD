# Maintainer: Hu Butui <hot123tea123@gmail.com>
# Contributor: hawkeye116477 <hawkeye116477 at gmail dot com>

_pkgname=PySimpleGUIQt
pkgname=python-pysimpleguiqt
pkgver=5.0.0
pkgrel=1
pkgdesc='Python GUIs for Humans (Qt port)'
arch=('any')
license=('LGPL-3.0-or-later')
url='https://github.com/PySimpleGUI/PySimpleGUI'
depends=(
  pyside2
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
source=(${pkgname}-${pkgver}.tar.gz::https://files.pythonhosted.org/packages/source/${_pkgname::1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz)
sha256sums=('ec7619a98327e8ea54f8332ef94d60bf70a79c8afbb01c46919fee40ec32fb19')

build() {
  cd "${_pkgname}-${pkgver}"
  python -m build --wheel --no-isolation
}

package() {
  cd "${_pkgname}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
}
# vim:set ts=2 sw=2 et:
