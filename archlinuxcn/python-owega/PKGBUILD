# Maintainer: Roald Clark <roaldclark at gmail dot com>
# Contributor: darkgeem <darkgeem at pyrokinesis dot fr>

_name=owega
pkgname="python-${_name}"
pkgver=5.23.1
pkgrel=1
pkgdesc="TUI / CLI interface for conversing with GPT models (from OpenAI and +)"
arch=('any')
url="https://pypi.org/project/owega/"
license=('LicenseRef-DGPL-1.0')
depends=(
    'python'
    'python-beautifulsoup4'
    'python-editor'
    'python-json5'
    'python-lxml'
    'python-openai'
    'python-prompt_toolkit'
    'python-requests'
)
makedepends=(
    'python-build'
    'python-installer'
    'python-setuptools'
    'python-wheel'
)
optdepends=(
    'python-markdownify: for better webpage handling (get_page function)'
    'python-pygame: for TTS audio output'
    'python-rich: fancy output print for TUI'
    'python-tiktoken: better token estimation'
)
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
b2sums=('a2f8c4f0a7d97bcf44c94208b63e9a32e78462a28c611c6a694731cde3bc2485483a15c860c723dc651818818520099bc060910b91a1330cea74c3d3484dbe43')

build() {
    cd "${srcdir}/${_name}-${pkgver}"
    python -m build --wheel --no-isolation
}

package() {
    cd "${srcdir}/${_name}-${pkgver}"
    install -Dm0644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
    python -m installer --destdir="${pkgdir}" dist/*.whl
}
