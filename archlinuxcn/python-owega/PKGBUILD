# Maintainer: Roald Clark <roaldclark at gmail dot com>
# Contributor: darkgeem <darkgeem at pyrokinesis dot fr>

_name=owega
pkgname="python-${_name}"
pkgver=5.21.0
pkgrel=1
pkgdesc="TUI / CLI interface for conversing with GPT models (from OpenAI and +)"
arch=('any')
url="https://pypi.org/project/owega/"
license=('LicenseRef-DGPL-1.0')
depends=(
    'python'
    'python-beautifulsoup4'
    'python-editor'
    'python-json5'
    'python-lxml'
    'python-openai'
    'python-prompt_toolkit'
    'python-requests'
)
makedepends=(
    'python-build'
    'python-installer'
    'python-setuptools'
    'python-wheel'
)
optdepends=(
    'python-markdownify: for better webpage handling (get_page function)'
    'python-pygame: for TTS audio output'
    'python-rich: fancy output print for TUI'
    'python-tiktoken: better token estimation'
)
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
b2sums=('0779e6a4730a08dc273fc5c2d1c3b12f92a87a81282b4188168c3e70eeac2c086497d7a5bee8080f12f4caed33d82ff807a128fa0b0f1be29b04845717fd246b')

build() {
    cd "${srcdir}/${_name}-${pkgver}"
    python -m build --wheel --no-isolation
}

package() {
    cd "${srcdir}/${_name}-${pkgver}"
    install -Dm0644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
    python -m installer --destdir="${pkgdir}" dist/*.whl
}
