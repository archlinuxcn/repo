# Maintainer: Butui Hu <hot123tea123@gmail.com>

_pkgname=EasyOCR
pkgname=python-easyocr
pkgver=1.7.2
pkgrel=1
pkgdesc='End-to-End Multi-Lingual Optical Character Recognition (OCR) Solution for 80+ supported languages'
arch=('any')
url='https://github.com/jaidedai/easyocr'
license=('Apache-2.0')
depends=(
  python-bidi
  python-numpy
  python-opencv
  python-pillow
  python-pytorch
  python-scikit-image
  python-scipy
  python-six
  python-torchvision
  python-yaml
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/JaidedAI/EasyOCR/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('83a909e068add24075057d5e043a1269440e3fd319305e7bbe02adc28add028a2f699ce7dedda88157245bb3ffaaf9bb801b31e2b0fb052244b4853c507d1967')

build() {
  cd "${_pkgname}-${pkgver}"
  python -m build --wheel --no-isolation
}

package() {
  cd "${_pkgname}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
}
# vim:set ts=2 sw=2 et:
