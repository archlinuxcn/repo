# Maintainer: lilydjwg <lilydjwg@gmail.com>
# Contributor: Bruno Pagani (a.k.a. ArchangeGabriel) <bruno.n.pagani@gmail.com>
# Contributor: Cedric MATHIEU <me.xenom @ gmail.com>

_name=firefox
_channel=nightly
pkgbase=firefox-nightly
pkgname=()
_version=148.0a1
pkgver=148.0a1.20251226.22
pkgrel=1
arch=('x86_64')
license=('MPL-2.0')
depends=('dbus-glib' 'gtk3' 'libxt' 'nss' 'mime-types')
optdepends=('pulseaudio: audio support'
            'ffmpeg: h.264 video'
            'hunspell: spell checking'
            'hyphen: hyphenation'
            'libnotify: notification integration'
            'networkmanager: location detection via available WiFi networks'
            'speech-dispatcher: text-to-speech'
            'xdg-desktop-portal: Screensharing with Wayland'
            'startup-notification: support for FreeDesktop Startup Notification')
_url="https://ftp.mozilla.org/pub/${_name}/nightly/latest-mozilla-central-l10n"
_filename_prefix="20251226.22-"
source=("${pkgbase}.desktop")
sha512sums=('b58c1b3b09772d0866d511e83e3c7f54db0fe71d2978d2a06bd19b25c020f16e201aab825f0b3d5e9f0c1a3e9db028018e5e38f7ee4868e321938f11cb5ac817'
            '127a276fe4f79a376d914ca68776aa40049e00e92c9a592e9e3f8f3f693add76ffe089420e24d966de6e5d4abe26e2e9c4bd79611f06559e5105f330aae1dd2d'
            'SKIP'
            'ffe9c5b781a95b68cf30fac96e9a06e49d840d556ae1a5df8a5bab2c355f7ec3b65f477312deb33af16010bfed40a27f9a5239bd164ec9753dd2b53db3ce32eb'
            'SKIP'
            '83af9cb52d7ae4b5b1553acb840ed042f42ee019ccb681cedb2162e7bf83ad9886e27272adf810cb9a2b480fdac0272c4eb93f2350fbb31adcadd9048baeb26f'
            'SKIP'
            'cd5c79c3cfd6f52a7dac7feaa2a5e7b36d539c66a213a41ca5ae0750a691355795a85877ff6ca88103fe301352c37f5e2df66b4f356dc1312d20f33fecdd4f63'
            'SKIP')
validpgpkeys=('14F26682D0916CDD81E37B6D61B7B526D98F0353'
              '097B313077AE62A02F84DA4DF1A6668FBB7D572E') # Mozilla’s GnuPG release key

_languages=(en-US zh-CN ja zh-TW)

for _lang in "${_languages[@]}"; do
  _locale=${_lang}
  _pkgname=firefox-nightly-${_locale,,}
  _src="${_name}-${_version}.${_lang}.linux"
  _filename="${_filename_prefix}${_src}-x86_64.tar.xz"

  pkgname+=($_pkgname)
  source+=("${_filename}"::"${_url}/${_src}-x86_64.tar.xz"
           "${_filename}.asc"::"${_url}/${_src}-x86_64.tar.xz.asc")
  eval "package_$_pkgname() {
    msg2 'Removing old firefox directory...'
    rm -rf firefox
    msg2 'Extract ${_filename}...'
    bsdtar -xf ${_filename}
    _package $_lang
  }"
done

# Don't extract anything because they'll override each other
noextract=(${source[@]%%::*})

_package() {
  OPT_PATH="opt/${pkgbase}"
  pkgdesc="Standalone Web Browser from Mozilla — Nightly build ($1)"
  url="https://www.mozilla.org/$1/${_name}/${_channel}"
  provides=(firefox-${_channel}=$pkgver)
  conflicts=(firefox-${_channel} firefox-${_channel}-i18n-${1,,})
  replaces=(firefox-${_channel}-i18n-${1,,})

  # Install the package files
  install -d "${pkgdir}"/{usr/bin,opt}
  cp -r ${_name} "${pkgdir}"/${OPT_PATH}
  ln -s "/${OPT_PATH}/${_name}" "${pkgdir}"/usr/bin/${pkgbase}

  # Install .desktop files
  install -Dm644 "${srcdir}"/${pkgbase}.desktop -t "${pkgdir}"/usr/share/applications

  # Install icons
  SRC_LOC="${srcdir}"/${_name}/browser
  DEST_LOC="${pkgdir}"/usr/share/icons/hicolor
  for i in 16 32 48 64 128; do
      install -Dm644 "${SRC_LOC}"/chrome/icons/default/default${i}.png "${DEST_LOC}"/${i}x${i}/apps/${pkgbase}.png
  done

  cat <<EOF | install -Dm644 /dev/stdin "${pkgdir}"/${OPT_PATH}/browser/defaults/preferences/vendor.js
pref("intl.locale.requested", "");
pref("spellchecker.dictionary_path", "/usr/share/hunspell");
EOF

  # Disable auto-updates
  if [[ $1 == "zh-CN" ]]; then
    # Change homepage for zh_CN to avoid ADs and tracks
    cat <<EOF | install -Dm644 /dev/stdin "${pkgdir}"/${OPT_PATH}/distribution/policies.json
{"policies": {"DisableAppUpdate": true, "Homepage": {"URL": "about:home"}}}
EOF
  else
    cat <<EOF | install -Dm644 /dev/stdin "${pkgdir}"/${OPT_PATH}/distribution/policies.json
{"policies": {"DisableAppUpdate": true}}
EOF
  fi
}
