# Maintainer: Jingbei Li <i@jingbei.li>
# Contributor: Lev Velykoivanenko <velykoivanenko dot lev at gmail dot com>
# Contributor: Fl√°vio Zavan <flavio dot zavan at gmail dot com>
# Contributor: pingplug
# Contributor: perlawk
# Contributor: xsmile

_build_cpu=1
_build_cuda=1

pkgbase=python-dlib
[[ $_build_cpu -eq 1 ]] && pkgname+=('python-dlib')
[[ $_build_cuda -eq 1 ]] && pkgname+=('python-dlib-cuda')
_pkgname=dlib
pkgver=19.24.3
pkgrel=2
pkgdesc="Dlib is a general purpose cross-platform C++ library designed using contract programming and modern C++ techniques."
arch=('x86_64')
url="http://www.dlib.net/"
license=('Boost')
depends=('cblas' 'giflib' 'lapack' 'libjpeg-turbo' 'libpng' 'libx11')
makedepends=('boost' 'cmake' 'python-setuptools' 'sqlite')
[[ $_build_cuda -eq 1 ]] && makedepends+=('ccache-ext' 'cuda' 'cudnn')
optdepends=('sqlite')
#source=("$url/files/${_pkgname}-${pkgver}.tar.bz2")
#source=("https://pypi.io/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
source=("https://github.com/davisking/dlib/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('4b1f28e76020775334e67cc348ceb26a4f5161df6659848be0d3b300406400a3')
[[ $_build_cuda -eq 1 ]] && options=(!lto)

prepare() {
	cd "$srcdir/"
	if [[ $_build_cuda -eq 1 ]]; then cp -a "${_pkgname}-${pkgver}" "${_pkgname}-${pkgver}-cuda"; fi
}

build_python-dlib(){
	cd "${srcdir}/${_pkgname}-${pkgver}"
	python setup.py build --no DLIB_USE_CUDA
}

build_python-dlib-cuda(){
	cd "${srcdir}/${_pkgname}-${pkgver}-cuda"
	python setup.py build \
		--set CUDA_HOST_COMPILER=/opt/cuda/bin/gcc \
		--set CUDA_NVCC_EXECUTABLE=/usr/lib/ccache/bin/nvcc-ccache \
		--set CUDA_HOST_COMPILER=/opt/cuda/bin/gcc \
		--set CMAKE_C_COMPILER=/opt/cuda/bin/gcc \
		--set CMAKE_CXX_COMPILER=/opt/cuda/bin/g++
}

build(){
	if [[ $_build_cpu -eq 1 ]]; then build_python-dlib; fi
	if [[ $_build_cuda -eq 1 ]]; then build_python-dlib-cuda; fi
}

package_python-dlib(){
	depends+=('python')
	cd "${srcdir}/${_pkgname}-${pkgver}"
	python setup.py install --skip-build --prefix=/usr --root="$pkgdir" --optimize=1
}

package_python-dlib-cuda(){
	depends+=('cuda' 'cudnn' 'python')
	provides=('python-dlib')
	conflicts=('python-dlib')
	cd "${srcdir}/${_pkgname}-${pkgver}-cuda"
	python setup.py install --skip-build --prefix=/usr --root="$pkgdir" --optimize=1
}
