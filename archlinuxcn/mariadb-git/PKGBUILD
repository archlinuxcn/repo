# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Maintainer: Christian Hesse <mail@eworm.de>

pkgbase=mariadb-git
pkgname=('mariadb-libs-git' 'mariadb-clients-git' 'mariadb-git' 'mytop-git')
pkgdesc='Fast SQL database server, derived from MySQL'
pkgver=11.1.4.r2.gd3e4de529bd
pkgrel=10
arch=($CARCH)
license=(GPL)
url='https://mariadb.org/'
makedepends=('boost' 'bzip2' 'cmake' 'cracklib' 'curl' 'jemalloc' 'judy' 'krb5' 'liburing'
             'libxcrypt' 'libxml2' 'lz4' 'openssl' 'systemd' 'zlib' 'zstd' 'xz' 'git')
conflicts=(${pkgbase%-git})
provides=(${pkgbase%-git})
source=("$pkgbase::git+https://github.com/MariaDB/server.git"
		0001-arch-specific.patch)
sha256sums=('SKIP'
            '3289efb3452d199aec872115f35da3f1d6fd4ce774615076690e9bc8afae1460')

pkgver() {
  cd "$pkgbase"
  git describe --long --tags | sed 's/^mariadb.//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd $pkgbase/

  git submodule update --init --recursive
  # Arch Linux specific patches:
  #  * enable PrivateTmp for a little bit more security
  #  * force preloading jemalloc for memory management
  #  * remove aliases in systemd unit files
  #  * make systemd-tmpfiles create MYSQL_DATADIR
  patch -Np1 < ../0001-arch-specific.patch

  sed -i '1 i #include<cstdint>' storage/rocksdb/rocksdb/db/compaction/compaction_iteration_stats.h
  sed -i '1 i #include<cstdint>' storage/rocksdb/rocksdb/table/block_based/data_block_hash_index.h
  sed -i '1 i #include<cstdint>' storage/rocksdb/rocksdb/util/slice.cc
  sed -i '1 i #include<cstdint>' storage/rocksdb/rocksdb/util/string_util.h
  sed -i '1 i #include<cstdint>' storage/rocksdb/rocksdb/include/rocksdb/utilities/checkpoint.h

}

build() {
  local _cmake_options=(
    # build options
    -DCOMPILATION_COMMENT="Arch Linux"
    -DCMAKE_BUILD_TYPE=RelWithDebInfo
    -Wno-dev

    # file paths
    # /etc
    -DINSTALL_SYSCONFDIR=/etc
    -DINSTALL_SYSCONF2DIR=/etc/my.cnf.d
    # /run
    -DINSTALL_UNIX_ADDRDIR=/run/mysqld/mysqld.sock
    # /usr
    -DCMAKE_INSTALL_PREFIX=/usr
    # /usr/bin /usr/include
    -DINSTALL_SCRIPTDIR=bin
    -DINSTALL_INCLUDEDIR=include/mysql
    # /usr/lib
    -DINSTALL_PLUGINDIR=lib/mysql/plugin
    -DINSTALL_SYSTEMD_UNITDIR=/usr/lib/systemd/system/
    -DINSTALL_SYSTEMD_SYSUSERSDIR=/usr/lib/sysusers.d/
    -DINSTALL_SYSTEMD_TMPFILESDIR=/usr/lib/tmpfiles.d/
    # /usr/share
    -DINSTALL_SHAREDIR=share
    -DINSTALL_SUPPORTFILESDIR=share/mysql
    -DINSTALL_MYSQLSHAREDIR=share/mysql
    -DINSTALL_DOCREADMEDIR=share/doc/mariadb
    -DINSTALL_DOCDIR=share/doc/mariadb
    -DINSTALL_MANDIR=share/man
    # /var
    -DMYSQL_DATADIR=/var/lib/mysql

    # default settings
    -DDEFAULT_CHARSET=utf8mb4
    -DDEFAULT_COLLATION=utf8mb4_unicode_ci

    # features
    -DENABLED_LOCAL_INFILE=ON
    -DPLUGIN_EXAMPLE=NO
    -DPLUGIN_FEDERATED=NO
    -DPLUGIN_FEEDBACK=NO
    -DWITH_EMBEDDED_SERVER=ON
    -DWITH_EXTRA_CHARSETS=complex
    -DWITH_JEMALLOC=ON
    -DWITH_LIBWRAP=OFF
    -DWITH_PCRE=bundled
    -DWITH_READLINE=ON
    -DWITH_SSL=system
    -DWITH_SYSTEMD=yes
    -DWITH_UNIT_TESTS=OFF
    -DWITH_ZLIB=system
  )


  mkdir build
  cd build

  cmake ../"$pkgbase" "${_cmake_options[@]}"

  make
}

package_mariadb-libs-git() {
  pkgdesc='MariaDB libraries (GIT version)'
  depends=('liburing' 'libxcrypt' 'libcrypt.so' 'openssl' 'zlib' 'zstd')
  optdepends=('krb5: for gssapi authentication')
  conflicts=(mariadb-libs libmysqlclient{,-git} libmariadbclient{,-git} mariadb-connector-c{,-git})
  provides=(mariadb-libs libmariadbclient{,-git} mariadb-connector-c{,-git} libmariadb.so libmariadbd.so)
  replaces=(libmariadbclient-git)
  
  cd build

  for dir in libmariadb libmysqld libservices include; do
    make -C "$dir" DESTDIR="$pkgdir" install
  done

    # remove static libraries
  rm "${pkgdir}"/usr/lib/*.a

  # remove man pages
  rm -r "${pkgdir}"/usr/share/man
  
  ln -s mariadb_config "$pkgdir"/usr/bin/mariadb-config
  ln -s mariadb_config "$pkgdir"/usr/bin/mysql_config
  install -D -m0644 "$srcdir"/"$pkgbase"/man/mariadb_config.1 "$pkgdir"/usr/share/man/man1/mysql_config.1
  ln -s mysql_config.1 "$pkgdir"/usr/share/man/man1/mariadb_config.1
  ln -s mysql_config.1 "$pkgdir"/usr/share/man/man1/mariadb-config.1

  install -D -m0644 support-files/mariadb.pc "$pkgdir"/usr/share/pkgconfig/mariadb.pc
  install -D -m0644 "$srcdir"/"$pkgbase"/support-files/mysql.m4 "$pkgdir"/usr/share/aclocal/mysql.m4
}

package_mariadb-clients-git() {
  pkgdesc='MariaDB client tools (GIT version)'
  depends=(mariadb-libs-git=$pkgver jemalloc ncurses)
  conflicts=(mysql-clients{,-git})
  provides=(mysql-clients=$pkgver)

  make -C build/client DESTDIR="${pkgdir}" install

  # install man pages
  make -C build/man DESTDIR="${srcdir}"/client-man install
  install -d -m0755 "${pkgdir}"/usr/share/man/man1/
  for man in $(find "${pkgdir}"/usr/bin/ ! -type d); do
    install -D -m0644 -t "${pkgdir}"/usr/share/man/man1/ "${srcdir}"/client-man/usr/share/man/man1/"$(basename "${man}")".1
  done
}

package_mariadb-git() {
  pkgdesc='Fast SQL database server, derived from MySQL (GIT version)'
  backup=('etc/my.cnf'
          'etc/my.cnf.d/client.cnf'
          'etc/my.cnf.d/enable_encryption.preset'
          'etc/my.cnf.d/mysql-clients.cnf'
          'etc/my.cnf.d/provider_bzip2.cnf'
          'etc/my.cnf.d/provider_lz4.cnf'
          'etc/my.cnf.d/provider_lzma.cnf'
          'etc/my.cnf.d/s3.cnf'
          'etc/my.cnf.d/server.cnf'
          'etc/my.cnf.d/spider.cnf'
          'etc/security/user_map.conf')
  install=mariadb.install
  depends=("mariadb-clients=${pkgver}" 'bzip2' 'libxml2' 'lz4' 'systemd-libs' 'libxml2' 'zstd')
  optdepends=('cracklib: for cracklib plugin'
              'curl: for ha_s3 plugin'
              'galera: for MariaDB cluster with Galera WSREP'
              'judy: for Open Query GRAPH (OQGraph) computation engine'
              'perl-dbd-mariadb: for mariadb-hotcopy, mariadb-convert-table-format and mariadb-setpermission'
              'python-mysqlclient: for myrocks_hotbackup'
              'xz: lzma provider')
  conflicts=(mysql)
  provides=(mysql=$pkgver)
  options=(emptydirs)

 cd build

  make DESTDIR="$pkgdir" install

  cd "$pkgdir"

  # no SysV init, please!
  rm -r etc/logrotate.d
  rm usr/bin/rcmysql
  rm usr/share/mysql/{binary-configure,mysql{,d_multi}.server}

  # move to proper licenses directories
  install -d usr/share/licenses/mariadb
  mv usr/share/doc/mariadb/COPYING* usr/share/licenses/mariadb/

  # move it where one might look for it
  mv usr/share/{groonga{,-normalizer-mysql},doc/mariadb/}

  # move to pam directories
  install -d {etc,usr/lib}/security
  mv usr/share/user_map.conf etc/security/
  mv usr/share/pam_user_map.so usr/lib/security/

  # already installed to real systemd unit directory or useless
  rm -r usr/share/mysql/systemd/
  rm -r usr/lib/systemd/system/mariadb@bootstrap.service.d

  # provided by mariadb-libs
  rm usr/bin/{mariadb{_,-},mysql_}config
  rm -r usr/include/
  rm usr/share/man/man1/{mariadb,mysql}_config.1
  rm -r usr/share/aclocal
  rm usr/lib/lib*
  rm -r usr/lib/pkgconfig
  rm usr/lib/mysql/plugin/{auth_gssapi_client,caching_sha2_password,client_ed25519,dialog,mysql_clear_password,sha256_password,zstd}.so

  # provided by mariadb-clients
  for bin in $(find "${pkgdir}"/../mariadb-clients/usr/bin/ ! -type d); do
    rm "${pkgdir}"/usr/bin/"$(basename "${bin}")" "${pkgdir}"/usr/share/man/man1/"$(basename "${bin}")".1
  done

  # provided by mytop
  rm usr/bin/mytop

  # not needed
  rm -rf usr/{mariadb-test,sql-bench}
  rm usr/share/man/man1/mysql-test-run.pl.1
}

package_mytop-git() {
  pkgdesc='Top clone for MariaDB (GIT version)'
  depends=('perl' 'perl-dbd-mysql' 'perl-term-readkey')
  provides=('mytop')
  conflicts=('mytop')

  cd build

  install -D -m0755 scripts/mytop "$pkgdir"/usr/bin/mytop
}
