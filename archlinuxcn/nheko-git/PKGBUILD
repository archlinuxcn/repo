# Maintainer: Konstantinos Sideris <siderisk at auth dot gr>
# Contributors: Ariel AxionL <axionl@aosc.io>
pkgname=nheko-git
pkgver=r914.6a90ed5
pkgrel=1
epoch=1
pkgdesc="Desktop client for the Matrix protocol"
arch=("x86_64")
url="https://github.com/mujx/nheko"
license=("GPL3")

depends=("qt5-multimedia" "lmdb" "boost-libs"
         "libsodium" "hicolor-icon-theme" "cmark")
makedepends=("git" "cmake" "gcc" "fontconfig" "boost" "qt5-svg"
             "qt5-tools" "pkgconf" "gtest" "doxygen")

provides=("nheko")
conflicts=("nheko")

source=($pkgname::git://github.com/mujx/nheko.git)
md5sums=('SKIP')

prepare() {
  cd "$pkgname"
}

pkgver() {
    cd "$pkgname"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd "$pkgname"
    # Build Deps
    cmake -Hdeps -Bbuild_deps \
        -DUSE_BUNDLED_BOOST=OFF
    cmake --build build_deps
    
    # Build nheko
    cmake -H. -Bbuild \
        -DCMAKE_BUILD_TYPE=Release \
        -DLMDBXX_INCLUDE_DIR=build_deps/usr/include \
        -DOLM_LIBRARY=build_deps/usr/lib64/libolm.a \
        -DOLM_INCLUDE_DIR=build_deps/usr/include \
        -DTWEENY_INCLUDE_DIR=build_deps/usr/include
    cmake --build build
}

package() {
    # Creating needed directories
    install -dm755 "$pkgdir/usr/bin"
    install -dm755 "$pkgdir/usr/share/pixmaps/"
    install -dm755 "$pkgdir/usr/share/applications/"

    # Program
    install -Dm755 "$pkgname/build/nheko" "$pkgdir/usr/bin/nheko"

    # Desktop launcher
    install -Dm644 "$srcdir/$pkgname/resources/nheko-256.png" "$pkgdir/usr/share/pixmaps/nheko.png"
    install -Dm644 "$srcdir/$pkgname/resources/nheko.desktop" "$pkgdir/usr/share/applications/nheko.desktop"

    # Icons
    local icon_size icon_dir
    for icon_size in 16 32 48 64 128 256 512; do
        icon_dir="$pkgdir/usr/share/icons/hicolor/${icon_size}x${icon_size}/apps"
        install -d "$icon_dir"
        install -m644 "$srcdir/$pkgname/resources/nheko-${icon_size}.png" "$icon_dir/nheko.png"
    done
}
