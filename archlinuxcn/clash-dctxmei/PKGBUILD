# Maintainer: Dct Mei <dctxmei@yandex.com>

pkgname=clash-dctxmei
_pkgname=clash
pkgver=1.15.1
pkgrel=1
pkgdesc="A rule-based tunnel in Go"
arch=('x86_64')
url="https://github.com/Dreamacro/clash"
license=('GPL3')
depends=('clash-geoip' 'glibc')
makedepends=('go')
provides=("clash=${pkgver}")
conflicts=('clash')
backup=("etc/clash/config.yaml")
options=('!lto')
source=("${_pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz"
        "clash-1.3.5.patch"
        "config.yaml"
        "clash.sysusers"
        "clash.service"
        "clash@.service")
sha256sums=('c340f30a244599692bd8c726ece292184ef9cd8910c622cdd41bbe695b283136'
            '511abd285aedc6dda651b1bf3d7fd84f51060fa313a12beb3ce68d916c2fc173'
            '62ed4460cd2ed4b400193ad04b0cccb76d7558f87c377a0033041841a73f7945'
            '149c6448a5630af1065ea230707331ac12663128568d6cf0e9d5480e94d1d104'
            '36b3585d85b7ac561dc7aff065ed59285899171f70c6e3d11aae1d99413a9bd7'
            '22e11fb72f0909cea8a414c1e6de6a3278977ff5b6b032039f4adef3485ceb92')

prepare() {
    cd "${srcdir}"/"${_pkgname}-${pkgver}"/
    patch -p1 -i ../clash-1.3.5.patch
    sed "s/unknown version/${pkgver}/" -i constant/version.go
    sed "s/unknown time/$(LANG=C date -u)/" -i constant/version.go
}

build() {
    export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
    export CGO_CPPFLAGS="${CPPFLAGS}"
    export CGO_CXXFLAGS="${CXXFLAGS}"
    export CGO_CFLAGS="${CFLAGS}"
    export CGO_LDFLAGS="${LDFLAGS}"

    cd "${srcdir}"/"${_pkgname}-${pkgver}"/
    go build -ldflags="-linkmode=external" -o clash
}

check() {
    cd "${srcdir}"/"${_pkgname}-${pkgver}"/
    go test github.com/Dreamacro/clash/...
}

package() {
    cd "${srcdir}"/"${_pkgname}-${pkgver}"/
    install -Dm 755 clash -t "${pkgdir}"/usr/bin/
    install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/clash/
    install -Dm 644 "${srcdir}"/config.yaml -t "${pkgdir}"/etc/clash/
    install -Dm 644 "${srcdir}"/clash.sysusers "${pkgdir}"/usr/lib/sysusers.d/clash.conf
    install -Dm 644 "${srcdir}"/clash.service -t "${pkgdir}"/usr/lib/systemd/system/
    install -Dm 644 "${srcdir}"/clash@.service -t "${pkgdir}"/usr/lib/systemd/system/
}
