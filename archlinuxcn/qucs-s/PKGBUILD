# Maintainer: Coelacanthus <uwu@coelacanthus.name>
# Contributor: Zes4Null <rjw1248[at]hotmail[dot]com>
# Contributor: Philip <philip1731 protonmail ch>

pkgname=qucs-s
pkgver=25.1.1
pkgrel=1
pkgdesc="A spin-off of Qucs that supports other free SPICE circuit simulators like ngspice with the same Qucs GUI"
arch=('x86_64' 'i686')
url="https://ra3xdh.github.io"
license=('GPL-2.0-or-later')
depends=(
    'glibc'
    'gcc-libs'
    'sh'
    'qt6-base'
    'qt6-svg'
    'qt6-charts'
    'ngspice'
    'python'
    'python-numpy'
    'python-matplotlib'
    'hicolor-icon-theme'
)
makedepends=(
    'cmake'
    'qt6-tools'
    'ninja'
    'gperf'
    'dos2unix'
)
optdepends=(
    'qucs: Qucsator simulation backend'
    'xyce-serial: SPICE-compatible simulation backend'
    'spiceopus: general purpose simulation backend for optimization loops'
    'openvaf: compiler for Verilog-A devices support'
    'octave: high-level language for post-simulation data processing'
)
source=(https://github.com/ra3xdh/qucs_s/releases/download/$pkgver/$pkgname-$pkgver.tar.gz)
b2sums=('dc6377ee7d8ebcf9fe56c70543a304b63a92633b4faf16d60650b4b002f66f6a80c04862128f67c7a4e18a14b95ddd8836ed025da83f79d0bd4052d3aa95a913')

build() {
    cmake -S "$pkgname-$pkgver" -B build \
        -G Ninja \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DWITH_QT6=ON
        
    cmake --build build
}

package() {
    DESTDIR="$pkgdir" cmake --install build
}

# vim: set ts=4 sw=4 et:
