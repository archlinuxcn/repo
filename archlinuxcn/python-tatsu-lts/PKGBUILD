_name=TatSu-LTS
pkgname=python-tatsu-lts
pkgver=5.13.1
pkgrel=3
pkgdesc="TatSu takes a grammar in a variation of EBNF as input, and outputs a memoizing PEG/Packrat parser in Python."
arch=(any)
url="https://github.com/dnicolodi/TatSu-LTS"
license=(BSD-4-Clause)
depends=('python' 'python-setuptools')
makedepends=('python-setuptools' 'python-build' 'python-installer' 'python-wheel')
optdepends=('python-colorama' 'python-rich')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/tatsu_lts-5.13.1.tar.gz")
sha256sums=('b9f0d38bf820d92077b5722bad26f68020c8e4ee663f7e35d4a0d95e4ebc5623')

build() {
  cd "$srcdir/tatsu_lts-5.13.1"
  python -m build --wheel --no-isolation
}

package() {
  cd "$srcdir/tatsu_lts-5.13.1"
  python -m installer --destdir="$pkgdir" dist/*.whl

  # make sure we don't install any world-writable or root-readable-only files
  # we shouldn't need to fix ownership as we extract tarballs as a non-root user
  # https://github.com/pypa/setuptools/issues/1328
  # https://github.com/LonamiWebs/Telethon/issues/1605
  chmod u=rwX,go=rX -R "$pkgdir"
  # make sure we don't install annoying files
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  rm -rf "$pkgdir/$_site_packages/tests/"
}

