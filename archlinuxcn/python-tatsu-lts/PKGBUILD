_name=TatSu-LTS
pkgname=python-tatsu-lts
pkgver=5.14.0
pkgrel=2
pkgdesc="TatSu takes a grammar in a variation of EBNF as input, and outputs a memoizing PEG/Packrat parser in Python."
arch=(any)
url="https://github.com/dnicolodi/TatSu-LTS"
license=(BSD-4-Clause)
depends=('python' 'python-setuptools')
makedepends=('python-setuptools' 'python-build' 'python-installer')
optdepends=('python-colorama' 'python-rich')
conflicts=('python-tatsu')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/tatsu_lts-5.14.0.tar.gz")
sha256sums=('1f09f328aa4a5b53242055e8e24493ef7d9e4f770b90daa4d4de7c88523c232a')

build() {
  cd "$srcdir/tatsu_lts-5.14.0"
  python -m build --wheel --no-isolation
}

package() {
  cd "$srcdir/tatsu_lts-5.14.0"
  python -m installer --destdir="$pkgdir" dist/*.whl

  # make sure we don't install any world-writable or root-readable-only files
  # we shouldn't need to fix ownership as we extract tarballs as a non-root user
  # https://github.com/pypa/setuptools/issues/1328
  # https://github.com/LonamiWebs/Telethon/issues/1605
  chmod u=rwX,go=rX -R "$pkgdir"
  # make sure we don't install annoying files
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  rm -rf "$pkgdir/$_site_packages/tests/"
}

