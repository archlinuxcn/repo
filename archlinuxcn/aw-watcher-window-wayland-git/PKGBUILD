# Maintainer: Y7n05h <Y7n05h(aT)protonmail--d0t--com>

_name='aw-watcher-window-wayland'
pkgname="$_name-git"
pkgver=r27.1cd0555
pkgrel=2
pkgdesc="window and afk watcher for wayland"
arch=('x86_64')
url="https://github.com/ActivityWatch/$_name"
license=('MPL2')
depends=(
    aw-client
    gcc-libs
    openssl
)
makedepends=(
    git
    rust
)
optdepends=()

provides=(${_name})
conflicts=(
    ${_name}
    aw-watcher-window
    aw-watcher-afk
    activitywatch-bin
)

source=("${_name}::git+$url")
sha256sums=('SKIP')

pkgver() {
    cd "${_name}"
    (
        set -o pipefail
        git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
            printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
    )
}
prepare() {
    cd "${_name}"
    cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}
build() {

    cd "${_name}"
    export RUSTUP_TOOLCHAIN=stable
    export CARGO_TARGET_DIR=target
    cargo build --frozen --release

}
package() {
    cd "${_name}"
    install -Dm0755 ./target/release/aw-watcher-window-wayland -t "${pkgdir}/usr/bin/"
}
