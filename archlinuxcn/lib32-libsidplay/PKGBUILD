# Maintainer: Rodrigo Bezerra <rodrigobezerra21 at gmail dot com>
# Contributor: GordonGR <gordongr@freemail.gr>
# Contributor: Kritoke <kritoke@gamebox.net>
# Contributor: Jan de Groot <jgc@archlinux.org>

_basename=libsidplay
pkgname=lib32-libsidplay
pkgver=1.36.59
pkgrel=8
pkgdesc="A library for playing SID music files, lib32 (32-bit)"
arch=('x86_64')
url="https://critical.ch/distfiles/"
license=('GPL')
depends=('lib32-gcc-libs' 'libsidplay')
source=(https://critical.ch/distfiles/${_basename}-${pkgver}.tgz
        libsidplay-1.36.59-gcc43.patch
        g++-6_build.patch)
sha256sums=('3da9b38d4eb5bf9e936b9604ba92da0594ef38047d50cf806a8e11c400008024'
            '68476bfee9a378b045c81eb0f6d9aee7341610fe6b22047d67ce859a7a3b5053'
            '6612b8c684bfd80d88431d3aa7f5a3fc911528f25f67ab530f78a7592cc11e23')

prepare() {
    cd ${_basename}-${pkgver}

    patch -Np1 -i ../libsidplay-1.36.59-gcc43.patch
    patch -Np1 -i ../g++-6_build.patch
}

build() {
    cd ${_basename}-${pkgver}

    export CC="gcc -m32"
    export CXX="g++ -m32 -Wno-narrowing"
    export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

    ./configure \
        --prefix=/usr \
        --libdir=/usr/lib32 \
        --libexecdir=/usr/lib32

    make
}

package() {
    cd ${_basename}-${pkgver}

    make DESTDIR=${pkgdir} install

    cd "$pkgdir/usr"
    rm -rf include/
}
provides=("libsidplay.so")
