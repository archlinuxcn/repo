# Maintainer: Sich <little_sich@tuta.io>
pkgname=celestia-qt6-git
pkgver=r7288.a8621b6b2
pkgrel=2
pkgdesc="Real-time space simulation (git version with Qt6 frontend and video capture support)"
arch=(x86_64)
license=(GPL-2.0-or-later)
url="https://celestiaproject.space"
depends=('mesa' 'libepoxy' 'fmt' 'eigen' 'freetype2' 'libjpeg-turbo' 'libpng' 'glu' 'qt6-base' 'qt6-tools' "qt6-wayland" "libavif" "icu" 'ffmpeg' 'luajit')
makedepends=('git' 'cmake' 'make' 'gperf' 'wayland-protocols' 'boost' 'imagemagick')
provides=('celestia')
conflicts=('celestia')
source=(git+https://github.com/CelestiaProject/Celestia.git
		git+https://github.com/CelestiaProject/CelestiaContent.git)
sha512sums=('SKIP'
			'SKIP')

pkgver() {
    cd "$srcdir/Celestia"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
	cd "$srcdir/Celestia"
    sed -i 's/Eigen3 3\.3/Eigen3 5/g' CMakeLists.txt
}

build() {
	cd "$srcdir/Celestia"
	cmake -B build \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DENABLE_QT5=OFF \
        -DENABLE_QT6=ON \
		-DENABLE_FFMPEG=ON \
		-DENABLE_LIBAVIF=ON \
        -DENABLE_NLS=ON \
        -DUSE_WAYLAND=ON \
        -DUSE_ICU=ON \
		-Wno-dev
	cd build && make
	cd "$srcdir/CelestiaContent"
	cmake -B build -DCMAKE_INSTALL_PREFIX=/usr
	cd build && make
}

package() {
	cd "$srcdir/Celestia/build"
	make DESTDIR="${pkgdir}" install
	cd "$srcdir/CelestiaContent/build"
	make DESTDIR="${pkgdir}" install
}
