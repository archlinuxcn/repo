# Maintainer: Chih-Hsuan Yen <base64_decode("eXUzYWN0eHQydHR0ZmlteEBjaHllbi5jYwo=")>

pkgname=python-pyasyncore
pkgver=1.0.4
pkgrel=3
pkgdesc='Make asyncore available for Python 3.12 onwards'
arch=(any)
url='https://github.com/simonrob/pyasyncore/'
# https://github.com/simonrob/pyasyncore/blob/v1.0.4/setup.py#L21
license=('PSF-2.0')
depends=(python)
makedepends=(git python-build python-installer python-setuptools python-wheel)
# Tests are extracted from CPython, and thus CPython test utilities are used
checkdepends=(python-tests)
# Not checking git signatures, as only commits are signed, not tags,
# and commit verification requires manual efforts during bumping versions
source=("git+https://github.com/simonrob/pyasyncore.git#tag=v$pkgver")
sha256sums=('7a4151e7e8b1fd39558729d290bd40bc68171a0f2fdab9a18d17b905b50cebeb')

build() {
  cd pyasyncore
  python -m build --wheel --no-isolation
}

check() {
  cd pyasyncore
  python -m unittest discover ./tests
}

package() {
  cd pyasyncore
  python -m installer --destdir="$pkgdir" dist/*.whl
}
