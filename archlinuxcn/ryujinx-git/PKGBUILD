# Maintainer: Darvin Delgado <dnmodder at gmail dot com>
pkgname=ryujinx-git
pkgver=r3188.628d092fc6
pkgrel=1
pkgdesc="Experimental Nintendo Switch Emulator written in C#"
arch=(x86_64)
url="https://github.com/Ryujinx/Ryujinx"
license=('MIT')
#depends=('dotnet-runtime>=7.0.0')
#makedepends=('git' 'dotnet-sdk>=7.0.0')
depends=('dotnet-runtime-bin')
makedepends=('git' 'dotnet-sdk-bin')
provides=(Ryujinx)
install=ryujinx.install
source=(
	"git+${url}"
	"ryujinx.install")
md5sums=('SKIP'
         '824e675295b3e9df5a7f8d9220c89c93')
options=(!strip)

pkgver() {
	cd "${srcdir}/Ryujinx"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	cd "${srcdir}/Ryujinx"

    dotnet publish -c Release -r linux-x64 -o ../publish -p:DebugType=embedded -p:ExtraDefineConstants="DISABLE_UPDATER%2CFORCE_EXTERNAL_BASE_DIR" src/Ryujinx --self-contained true
	dotnet publish -c Release -r linux-x64 -o ../publish_ava -p:DebugType=embedded -p:ExtraDefineConstants="DISABLE_UPDATER%2CFORCE_EXTERNAL_BASE_DIR" src/Ryujinx.Ava --self-contained true
}

package() {
	mkdir -p "${pkgdir}/opt/ryujinx/"
	mkdir -p -m 777 "${pkgdir}/opt/ryujinx/Logs"
	mkdir -p "${pkgdir}/usr/bin/"

	install -D "Ryujinx/distribution/linux/Ryujinx.desktop" "$pkgdir/usr/share/applications/Ryujinx.desktop"
	install -D "Ryujinx/distribution/misc/Logo.svg" "$pkgdir/usr/share/icons/hicolor/scalable/apps/Ryujinx.svg"

    cp -R "publish/"* "$pkgdir/opt/ryujinx/"
	cp -R "publish_ava/"* "$pkgdir/opt/ryujinx/"
    chmod +x "$pkgdir/opt/ryujinx/Ryujinx.sh"

	ln -s "/opt/ryujinx/Ryujinx" "${pkgdir}/usr/bin/Ryujinx"
    ln -s "/opt/ryujinx/Ryujinx.sh" "$pkgdir/usr/bin/Ryujinx.sh"
}
