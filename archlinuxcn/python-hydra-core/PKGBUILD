# Maintainer: Butui Hu <hot123tea123@gmail.com>

_pkgname=hydra
pkgname=python-hydra-core
pkgver=1.3.2
pkgrel=2
pkgdesc='A framework for elegantly configuring complex applications'
arch=('any')
url='https://hydra.cc'
license=('MIT')
depends=(
  python-omegaconf
  python-packaging
)
makedepends=(
  antlr4
  python-build
  python-installer
  python-setuptools
  python-wheel
)
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/facebookresearch/hydra/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('09e700ed9ec043d6f4fc8be4475a513ccb9a7eb1bfa5f42b31785995b83c229f14191e222c445cc2de96611d42fec18e9b5c09a8da4e5c5a29b7be2e2e1cb66e')

prepare() {
  # use system antlr4
  sed -i 's#join(root_dir, "bin/antlr-4.9.3-complete.jar")#"/usr/share/java/antlr-complete.jar"#' "${_pkgname}-${pkgver}/build_helpers/build_helpers.py"
}
build() {
  cd "${_pkgname}-${pkgver}"
  python -m build --wheel --no-isolation
}

package() {
  cd "${_pkgname}-${pkgver}"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
