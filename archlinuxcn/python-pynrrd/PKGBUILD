# Maintainer of this PKGBUILD file: Martino Pilia <martino.pilia@gmail.com>

_pkgname=pynrrd
pkgname=python-pynrrd
pkgver=0.4.2
pkgrel=3
pkgdesc='Simple pure-python module for reading and writing nrrd files'
arch=('any')
url='https://github.com/mhe/pynrrd'
license=('MIT')
depends=(
  python-numpy
)
makedepends=(
  python-setuptools
)
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/mhe/pynrrd/archive/v${pkgver}.tar.gz")
sha256sums=('0e0c60c9927adb31d97a7e3d00756dec987f115b2857dec0d6e4641490acfbd6')

build() {
  cd "${_pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${_pkgname}-${pkgver}"
  python -m unittest discover -v nrrd/tests
}

package() {
  cd "${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
# vim:set ts=2 sw=2 et:
