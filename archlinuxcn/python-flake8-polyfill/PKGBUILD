# Maintainer: Gesh <gesh@gesh.uni.cx>
# Contributor: Christopher Arndt <aur -at- chrisarndt -dot- de>

_name='flake8-polyfill'
pkgname="python-${_name}"
pkgver=1.0.2.r18.gb98e6f4
pkgrel=6
pkgdesc='Polyfill package for Flake8 plugins'
url="https://github.com/PyCQA/${_name}"
depends=('flake8')
checkdepends=('python-pytest')
makedepends=(git python-build python-installer python-setuptools)
license=('MIT')
arch=('any')
source=("$pkgname::git+$url" fix-tests.patch)
sha256sums=('SKIP'
            '33a28897eda38828c197a6f31e0415a8804209c40f698ca5b5201b7660b65985')

# Necessary since upstream, seemingly abandoned, has forgotten to tag the 1.0.2
# release it cut
_bumpTag() {
    cd "${srcdir}/$pkgname"
    local version oldVer verCmd _verCmd

    verCmd=(python -c 'import setuptools; setuptools.setup()' --version)

    git stash &> /dev/null
    version="$("${verCmd[@]}")"
    oldVer="$(git describe --abbrev=0)"
    if test "$version" != "$oldVer"; then
        printf -v _verCmd '%q ' "${verCmd[@]}"
        printf 'test "$(%s)" != %s\n' "$_verCmd" "$version" > bisector.sh
        chmod 755 bisector.sh
        git bisect start
        git bisect new
        git bisect old "$oldVer"
        git bisect run ./bisector.sh
        git tag -f "$version"
        git bisect reset
    fi &> /dev/null

    git describe --long --tags --abbrev=7
    git stash pop &> /dev/null
}

pkgver() {
    _bumpTag | sed -E 's/^[^0-9]*//;s/-([^-]*-g)/.r\1/;s/-/./g'
}

prepare() {
    cd "${srcdir}/$pkgname"
    patch -p1 -i ../fix-tests.patch
}

build() {
    cd "${srcdir}/$pkgname"
    python -m build --wheel --no-isolation

    python -m installer --destdir=tmp_install dist/*.whl
}

check() {
    cd "${srcdir}/$pkgname"

    local _site_packages
    _site_packages="$(python -c 'import site; print(site.getsitepackages()[0])')"
    export PYTHONPATH="$PWD/tmp_install/$_site_packages"

    python -m pytest tests
}

package() {
    cd "${srcdir}/$pkgname"
    python -m installer --destdir="$pkgdir" dist/*.whl

    # license
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

