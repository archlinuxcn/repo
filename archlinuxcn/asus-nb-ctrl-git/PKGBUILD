# Maintainer: Static_Rocket

pkgname=asus-nb-ctrl-git
pkgver=2.1.2.r0.g703bba9
pkgrel=1
pkgdesc="Asus laptop control utilities"
arch=('x86_64')
url="https://gitlab.com/asus-linux/asus-nb-ctrl"
license=('Mozilla Public License Version 2.0')
depends=('libusb' 'udev' 'systemd')
optdepends=('hid-asus-rog-dkms-git: fn+key combination support'
	'asus-rog-nb-wmi-dkms-git: G14/G15 support'
	'linux-g14: same functions as dkms modules'
	'acpi_call: fan control')
makedepends=('git' 'rust')
provides=('asus-nb-ctrl')
conflicts=('asus-nb-ctrl')
source=('git+https://gitlab.com/asus-linux/asus-nb-ctrl.git')
md5sums=('SKIP')
_gitdir=${pkgname%"-git"}

pkgver() {
	cd "$_gitdir"
	git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
	cd "$_gitdir"
	make build
}

package() {
	cd "$_gitdir"
	make DESTDIR="$pkgdir" install
}

