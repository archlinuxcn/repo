# Contributor: Johannes Dewender arch at JonnyJD dot net
# Contributor: Eduardo Robles Elvira <edulix AT gmail DOT com>
# Contributor: Jon Nordby <jononor@gmail.com>

pkgname=obs-build-git
_pkgname=${pkgname%-git}
pkgver=20240221.r19.gb9902ab
pkgrel=1
pkgdesc="Building part of the OpenSUSE Build Service, osc-build - git version"
url="http://build.opensuse.org"
arch=('any')
license=("GPL-2.0-or-later")
depends=('bash' 'perl' 'rpm-org' 'xz')
makedepends=('git')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=("git+https://github.com/openSUSE/${_pkgname}.git")
md5sums=('SKIP')

pkgver() {
	cd "${_pkgname}/"
	git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

package() {
	cd "${_pkgname}/"
	make install DESTDIR="${pkgdir}"
}
