# Maintainer: Integral <integral@member.fsf.org>
# Contributor: Johannes Dewender arch at JonnyJD dot net
# Contributor: Eduardo Robles Elvira <edulix AT gmail DOT com>
# Contributor: Jon Nordby <jononor@gmail.com>

pkgname=obs-build-git
_pkgname=${pkgname%-git}
pkgver=20250829.r0.gc658d80
pkgrel=1
license=('GPL-2.0-or-later')
pkgdesc="OBS build script, can be used with OBS or stand alone"
url="https://github.com/openSUSE/${_pkgname}"
arch=('any')
depends=('perl')
makedepends=('git')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=("git+${url}.git")
sha256sums=('SKIP')

pkgver() {
	cd "${_pkgname}/"
	git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

package() {
	cd "${_pkgname}/"
	make DESTDIR="${pkgdir}" install
}
