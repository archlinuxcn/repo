# Maintainer: Integral <integral at member dot fsf dot org>
#
# Contributor: satanselbow <igdfpm at gmail dot com>
# Contributor: Artem Sereda <overmind88 at gmail dot com>
# Contributor: ValHue <vhuelamo at gmail dot com>

pkgname=flacon
pkgver=12.0.0
pkgrel=1
pkgdesc="An Audio File Encoder. Extracts audio tracks from an audio CD image to separate tracks."
arch=('i686' 'x86_64' 'aarch64')
url="https://${pkgname}.github.io"
license=('LGPL-2.1-or-later')
makedepends=('cmake' 'icu' 'qt6-tools')
depends=('hicolor-icon-theme' 'qt6-base' 'uchardet' 'taglib')
optdepends=('flac: For FLAC support'
	'lame: For MP3 support'
	'mac: For APE support'
	'mp3gain: For MP3 Replay Gain support'
	'opus-tools: For OPUS support'
	'sox: For SoX support'
	'ttaenc: For TrueAudio support'
	'vorbis-tools: For OGG support'
	'vorbisgain: For OGG Replay Gain support'
	'wavpack: For WavPack support'
)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/v${pkgver}.tar.gz")
sha256sums=('78199ff925b7cd0ffeb628d47909ca4172f8ff0d8fd8192bb537e0c012e6f4c6')

prepare() {
	mkdir -p "${pkgname}-${pkgver}/build/"
}

build() {
	cd "${pkgname}-${pkgver}/build/"
	cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=None \
		-DUSE_QT6=ON \
		-DUSE_QT5=OFF

	cmake --build .
}

package() {
	cd "${pkgname}-${pkgver}/"
	DESTDIR="${pkgdir}" cmake --install build
	install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
