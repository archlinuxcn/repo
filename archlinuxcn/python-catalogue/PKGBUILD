# Maintainer: Philip Goto <philip.goto@gmail.com>
# Contributor: Caltlgin Stsodaat <contact@fossdaily.xyz>
# Contributor: Chris Brendel <cdbrendel@gmail.com>

_pkgname=catalogue
pkgname="python-${_pkgname}"
pkgver=2.0.10
pkgrel=4
pkgdesc="Super lightweight function registries for your library"
arch=('any')
url="https://github.com/explosion/${_pkgname}"
license=('MIT')
depends=(
    'python'
    'python-pytest'
    'python-typing_extensions'
    'python-zipp'
)
makedepends=(
    'python-build'
    'python-installer'
    'python-setuptools'
    'python-wheel'
)
source=("${_pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
b2sums=('961d654fff1cb56d0d1f9327aeead81f2f10fdbe95266a607ce2c9e42b747d71db14d21b3e387bb59fe50d9d05ead0ece4a2c83b8e1343417f02a11694126ac1')

build() {
    cd "${_pkgname}-${pkgver}"
    python -m build --wheel --no-isolation
}

package() {
    cd "${_pkgname}-${pkgver}"
    install -Dvm0644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}/"
    install -Dvm0644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
    python -m installer --destdir="${pkgdir}" dist/*.whl
}
