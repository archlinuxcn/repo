# Maintainer: Hu Butui <hot123tea123@gmail.com>

_pkgname=openxlsx
_pkgver=4.2.5
pkgname=r-${_pkgname,,}
pkgver=4.2.5
pkgrel=1
pkgdesc='Read, Write and Edit xlsx Files'
arch=('x86_64')
url="https://cran.r-project.org/package=${_pkgname}"
license=('MIT')
depends=(
  r
  r-rcpp
  r-stringi
  r-zip
)
optdepends=(
  r-knitr
  r-rmarkdown
  r-roxygen2
  r-testthat
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
sha256sums=('65d06d2819b656ac30fc78437ee712a83fb5a7ab750f56268e5c9e578c582519')

build() {
  R CMD INSTALL ${_pkgname}_${_pkgver}.tar.gz -l "${srcdir}"
}

package() {
  install -dm0755 "${pkgdir}/usr/lib/R/library"
  cp -a --no-preserve=ownership "${_pkgname}" "${pkgdir}/usr/lib/R/library"
  install -Dm644 "${_pkgname}/LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
