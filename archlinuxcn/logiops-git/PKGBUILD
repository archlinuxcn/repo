# Maintainer: Y7n05h <Y7n05h(aT)protonmail--d0t--com>
# Contributor: Krist√≥f Marussy <kris7topher at gmail dot com>
# Contributor: PixlOne <pixlone@pm.me>
# Contributor: Afnan Enayet <afnan@afnan.io>

pkgname=logiops-git
_pkgname="logiops"
pkgver=v0.2.3.r17.ged3b0ee
pkgrel=1
pkgdesc="An unofficial driver for Logitech HID++>2.0 mice and keyboard"
arch=('x86_64')
url="https://github.com/PixlOne/$_pkgname"
license=('GPL3')
depends=('libevdev' 'libconfig' 'systemd')
makedepends=('git' 'cmake')
conflicts=("${_pkgname}")
provides=("${_pkgname}")
source=("$pkgname::git+$url")
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname}
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    cd "$pkgname"
    mkdir -p build
    cd build
    rm -rf *
    cmake .. \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=None  \
	-Wno-dev

    make
}

package() {
    cd "$pkgname/build"
    make install DESTDIR="$pkgdir"
}
