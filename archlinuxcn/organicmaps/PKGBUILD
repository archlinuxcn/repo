# Maintainer: Aleksej Kovura <aur-b1a3 at mekboy dot ru>

pkgname=organicmaps
pkgver=2025.10.23_22
tag="${pkgver%%_*}-${pkgver##*_}-android"
pkgrel=1
pkgdesc="Organic Maps: Offline Hike, Bike, Trails and Navigation"
arch=(x86_64)
makedepends=(cmake git jq gcc libxinerama ninja mold python)
depends=(mesa libglvnd freetype2 sqlite icu qt6-svg qt6-base zlib libpng glibc
  qt6-positioning gcc-libs harfbuzz libxrandr libxi libxcursor)
optdepends=("ccache: faster re-compilation" "qt6-wayland: for Wayland users")
license=("Apache")
url="https://organicmaps.app"
source_url="https://github.com/organicmaps/organicmaps.git"
source=(organicmaps.desktop)
sha256sums=('a60443ee9a909b372f190ec8e1bde880fc2303da5d11261ef218e67c40c35c03')
conflicts=("${pkgname}-bin" "${pkgname}-git")
prepare() {
  avail=$(df -P -B 1048576 $srcdir|awk 'NR>1 {print $4}')
  if [ $avail -le 5120 ]; then
    printf "need at least 5 GiB of free space\n"
    exit 1
  fi
  src_url=$source_url
  if [ -n "$SOURCE_URL_REWRITER" ]; then
    src_url=$($SOURCE_URL_REWRITER $source_url)
    case $src_url in
      file://*)
        git -C ${src_url#file://} fetch --depth=1 origin "$tag"
        ;;
    esac
  fi
  if [ ! -d $pkgname ]; then
    git clone --depth=1 --single-branch -b "$tag" --filter=blob:limit=128k \
      $src_url $pkgname
  fi
  if [ -n "$SOURCE_URL_REWRITER" ]; then
    for submodule_path in $(git -C $pkgname submodule status|
        awk '/icu/ || /harfbuzz/ || /expat/ {print $2}');
    do
      pin_commit=$(git -C $pkgname submodule status --cached $submodule_path|
        awk '{sub("^-",""); print $1}')
      remote=$(git -C $pkgname config --file=.gitmodules submodule.${submodule_path}.url)
      local_remote=$($SOURCE_URL_REWRITER $remote)
      case "$local_remote" in
        file://*)
          git -C ${local_remote#file://} fetch --depth=1 origin "$pin_commit"
          git -C $pkgname submodule set-url $submodule_path "$local_remote"
          ;;
      esac
    done
  fi
  cd $pkgname
  git -c protocol.file.allow=always submodule update --init --recursive --depth=1
  rm -f 3party/boost/b2
  bash ./configure.sh
}
build() {
  cd $pkgname
  env CFLAGS="-fuse-ld=mold" CXXFLAGS="-fuse-ld=mold" CC=gcc CXX=g++ \
    tools/unix/build_omim.sh -n $(nproc) -c -r desktop
}
package() {
 install -dm755 "$pkgdir/usr/share/${pkgname}"
 cp -Lr "${pkgname}/data" "$pkgdir/usr/share/${pkgname}/"
 install -dm777 "$pkgdir/usr/share/${pkgname}/data/$(jq '.v' $pkgname/data/countries.txt)"
 install -Dm644 "${pkgname}/android/.idea/icon.svg" "$pkgdir/usr/share/icons/hicolor/scalable/apps/${pkgname}.svg"
 install -Dm755 "omim-build-release/OMaps" "$pkgdir/usr/bin/OMaps"
 install -Dm644 "organicmaps.desktop" -t "$pkgdir/usr/share/applications"
}
