# Maintainer: Hu Butui <hot123tea123@gmail.com>

_pkgname=languageserver
_pkgver=0.3.10
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//[:-]/.}
pkgrel=1
pkgdesc='Language Server Protocol'
arch=('x86_64')
url="https://cran.r-project.org/package=${_pkgname}"
license=('MIT')
depends=(
  r
  r-callr
  r-collections
  r-desc
  r-fs
  r-jsonlite
  r-lintr
  r-r6
  r-repr
  r-roxygen2
  r-stringi
  r-styler
  r-xml2
  r-xmlparsedata
)
optdepends=(
  r-covr
  r-magrittr
  r-mockery
  r-processx
  r-purrr
  r-rmarkdown
  r-testthat
  r-withr
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
sha256sums=('92a300234e52f1af3022ff6f3037f8342ed26ce0d9127a6790d277fb19e610d5')

build() {
  R CMD INSTALL ${_pkgname}_${_pkgver}.tar.gz -l "${srcdir}"
}

package() {
  install -dm0755 "${pkgdir}/usr/lib/R/library"
  cp -a --no-preserve=ownership "${_pkgname}" "${pkgdir}/usr/lib/R/library"
  install -Dm644 "${_pkgname}/LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
