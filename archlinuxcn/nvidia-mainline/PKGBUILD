# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thomas Baechler <thomas@archlinux.org>

pkgname=nvidia-mainline
pkgver=510.54
pkgrel=7
pkgdesc="NVIDIA drivers for linux"
arch=('x86_64')
url="https://www.nvidia.com/"
makedepends=('linux-mainline-headers' "nvidia-dkms=$pkgver")
provides=('NVIDIA-MODULE')
license=('custom')
options=('!strip')

build() {
    _kernver=$(</usr/src/linux-mainline/version)

    fakeroot dkms build --dkmstree "${srcdir}" -m nvidia/${pkgver} -k ${_kernver}
}

package() {
    depends=('linux-mainline' "nvidia-utils=${pkgver}" 'libglvnd')

    _kernver="$(</usr/src/linux-mainline/version)"

    install -Dt "${pkgdir}/usr/lib/modules/${_kernver}/extramodules" -m644 nvidia/${pkgver}/${_kernver}/${CARCH}/module/*

    # compress each module individually
    find "$pkgdir" -name '*.ko' -exec xz -T1 {} +

    install -Dm644 /usr/share/licenses/nvidia-dkms/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
