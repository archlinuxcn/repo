# Maintainer: Chris Lane <aur at chrislane dot com>
# Contributor: Stephanie Wilde-Hobbs <hi@stephanie.is>

pkgname=megacmd
pkgver=2.4.0
pkgrel=1
pkgdesc="MEGA Command Line Interactive and Scriptable Application"
url="https://github.com/meganz/MEGAcmd"
arch=('x86_64')
license=('custom')
depends=('crypto++' 'sqlite' 'c-ares' 'libsodium' 'pcre' 'libuv' 'fuse2')
makedepends=('cmake' 'git')
source=("git+https://github.com/meganz/MEGAcmd.git#tag=${pkgver}_Linux"
        "git+https://github.com/meganz/sdk.git")
sha512sums=('da7147ccf5b064702f80baee55c62cef99a1c344f026fb4196952998be92a3a2a88a4d4250c33aae13141c2597f12bbebcb0cb047f2ad81c34b8d5ae392c1d57'
            'SKIP')

prepare() {
  cd MEGAcmd
  git submodule init
  git config submodule.src/sdk.url "$srcdir/sdk"
  git -c protocol.file.allow=always submodule update

  sed -i '/^set(VCPKG_ROOT/d' CMakeLists.txt
  echo true > build/clone_vcpkg_from_baseline.sh # this script is executed by bash (not using shebang)
  sed -i '/IMPORTED_TARGET libcrypto++/s/++/pp/' sdk/cmake/modules/sdklib_libraries.cmake
}

build() {
  cd MEGAcmd

  mkdir ../builddir
  # https://github.com/meganz/MEGAcmd/issues/1041
  export LDFLAGS="-lpcrecpp -lz"
  # -DCMAKE_BUILD_TYPE=None => https://github.com/meganz/sdk/issues/2679
  cmake -B ../builddir -DCMAKE_BUILD_TYPE=RelWithDebInfo -DUSE_FREEIMAGE=no -DUSE_PDFIUM=no -DFULL_REQS=off
  cmake --build ../builddir
}

package() {
  cd MEGAcmd

  DESTDIR="${pkgdir}" cmake --install ../builddir

  rm -rf ${pkgdir}/opt/

  # change completions location
  rm -rf ${pkgdir}/etc/
  install -Dm644 src/client/megacmd_completion.sh ${pkgdir}/usr/share/bash-completion/completions/${pkgname}

  for completion_cmd in ${pkgdir}/usr/bin/mega*;
  do
    completion_cmd=$(basename "$completion_cmd")
    ln -s "${pkgname}" "${pkgdir}/usr/share/bash-completion/completions/$completion_cmd"
  done

  install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

# vim:set ts=2 sw=2 et:
