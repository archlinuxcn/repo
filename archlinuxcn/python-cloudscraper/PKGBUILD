# Maintainer: Carl Smedstad <carl.smedstad at protonmail dot com>
# Maintainer: Carlos Aznar√°n <caznaranl@uni.pe>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Mufeed Ali <fushinari@protonmail.com>
# Contributor: lastweakness <lastweakness@tuta.io>

pkgname=python-cloudscraper
_name=${pkgname#python-}
pkgver=1.2.69
pkgrel=1
pkgdesc="Python module to bypass Cloudflare's anti-bot page"
arch=(any)
url="https://github.com/VeNoMouS/cloudscraper"
license=(MIT)
depends=("python-pyparsing"
         "python-requests"
         "python-requests-toolbelt"
         "python-urllib3")
optdepends=('nodejs: alternative interpreter/solver'
            'python-js2py: alternative interpreter/solver')
makedepends=("python-build"
             "python-installer"
             "python-wheel"
             "python-setuptools")
checkdepends=("nodejs"
              "python-js2py"
              "python-pytest"
              "python-responses")

source=("${pkgname}-${pkgver}.tar.gz::$url/archive/refs/tags/${pkgver}.tar.gz")
sha256sums=('ee1df7b0c22529a190d12d34f2437997e6f4b68192ff965476d54cc8eb12fefa')

build() {
  cd "${_name}-${pkgver}"

  python -m build --wheel --no-isolation
}

check() {
  cd "${_name}-${pkgver}"

  pytest \
    --deselect tests/test_cloudscraper.py::TestCloudScraper::test_bad_interpreter_js_challenge1_16_05_2020 \
    --deselect tests/test_cloudscraper.py::TestCloudScraper::test_bad_solve_js_challenge1_16_05_2020 \
    --deselect tests/test_cloudscraper.py::TestCloudScraper::test_Captcha_challenge_12_12_2019 \
    --deselect tests/test_cloudscraper.py::TestCloudScraper::test_reCaptcha_providers
}

package() {
  cd "${_name}-${pkgver}"

  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
