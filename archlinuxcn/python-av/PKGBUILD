# Maintainer: Butui Hu <hot123tea123@gmail.com>
# Contributor: peippo <christoph.fink@gmail.com>

pkgname=python-av
_pkgname=PyAV
pkgdesc="Pythonic bindings for FFmpeg's libraries"
url='https://pyav.org'
pkgver=10.0.0
pkgrel=2
arch=('x86_64')
license=('BSD')
makedepends=(
  cython
  python-build
  python-installer
  python-setuptools
  python-wheel
)
depends=(
  ffmpeg4.4
  python-numpy
  python-pillow
)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/PyAV-Org/PyAV/archive/v${pkgver}.tar.gz")
sha512sums=('e3ecbafadbf8964e64d22202f44f30bbdc2987f70a24d78f5ab08ba9a7372630361401593e67ab664e6c6179c1b101f0020ada080443622be54e43d2b7a13b85')

build() {
  cd "${_pkgname}-${pkgver}"
  # https://github.com/PyAV-Org/PyAV/issues/817
  PKG_CONFIG_PATH=/usr/lib/ffmpeg4.4/pkgconfig \
  python -m build --wheel --no-isolation --skip-dependency-check
}

package() {
  cd "${_pkgname}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
# vim:set ts=2 sw=2 et:

