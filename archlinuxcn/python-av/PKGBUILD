# Maintainer: Butui Hu <hot123tea123@gmail.com>
# Contributor: peippo <christoph.fink@gmail.com>

pkgname=python-av
_pkgname=PyAV
pkgdesc="Pythonic bindings for FFmpeg's libraries"
url='https://pyav.org'
pkgver=14.3.0
pkgrel=1
arch=('x86_64')
license=('BSD-3-Clause')
makedepends=(
  # cython
  python-build
  python-installer
  python-pip
  python-setuptools
  python-wheel
)
depends=(
  ffmpeg
  glibc
  python-numpy
  python-pillow
)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/PyAV-Org/PyAV/archive/v${pkgver}.tar.gz")
sha512sums=('b8d5107dfd7c7d02dbbf231a0a34965ef53462c5216f2350d49811b065e302db1e71ca28243b3c77c98ff049e53620a048b884c1fbc1c74d428ec89d48fee713')

build() {
  pip install --break-system-packages -U cython==3.1.0b1
  cd "${_pkgname}-${pkgver}"
  python -m build --wheel --no-isolation --skip-dependency-check
}

package() {
  cd "${_pkgname}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
# vim:set ts=2 sw=2 et:
