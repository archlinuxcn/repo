# Maintainer: Rocket Aaron <i at rocka dot me>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

_pkgname=phonon-mpv
pkgbase="${_pkgname}-git"
pkgname=(phonon-qt5-mpv-git
         phonon-qt6-mpv-git)
pkgdesc='Phonon mpv backend'
pkgver=0.1.0.r0.g069aca4
pkgrel=2
arch=(x86_64)
url='https://github.com/OpenProgger/phonon-mpv'
license=(LGPL-2.1-or-later)
depends=(glibc gcc-libs mpv)
makedepends=(git extra-cmake-modules phonon-qt5 phonon-qt6 qt5-tools qt6-tools)
source=("git+https://github.com/OpenProgger/phonon-mpv.git")
sha256sums=('SKIP')

pkgver() {
    git -C "${_pkgname}" describe --long --tags --abbrev=7 | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cmake -B build5 -S "${_pkgname}" \
    -DPHONON_BUILD_QT5=ON \
    -DPHONON_BUILD_QT6=OFF
  cmake --build build5

  cmake -B build6 -S "${_pkgname}" \
    -DPHONON_BUILD_QT5=OFF \
    -DPHONON_BUILD_QT6=ON
  cmake --build build6
}

package_phonon-qt5-mpv-git() {
  pkgdesc+=' for Qt5'
  depends+=(qt5-base phonon-qt5)
  provides=(phonon-qt5-backend)
  DESTDIR="$pkgdir" cmake --install build5
}

package_phonon-qt6-mpv-git() {
  pkgdesc+=' for Qt6'
  depends+=(qt6-base phonon-qt6)
  provides=(phonon-qt6-backend)
  DESTDIR="$pkgdir" cmake --install build6
}
