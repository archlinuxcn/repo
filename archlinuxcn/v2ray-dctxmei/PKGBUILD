# Maintainer: Dct Mei <dctxmei@gmail.com>

pkgname=v2ray-dctxmei
_pkgname=v2ray
pkgver=4.34.0
pkgrel=1
pkgdesc="A platform for building proxies to bypass network restrictions"
arch=('x86_64')
url="https://github.com/v2fly/v2ray-core"
license=('MIT')
depends=('glibc' 'v2ray-domain-list-community' 'v2ray-geoip')
makedepends=('git' 'go')
provides=("v2ray=${pkgver}")
conflicts=('v2ray')
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/v2fly/v2ray-core/archive/v${pkgver}.tar.gz"
        "v2ray.sysusers"
        "v2ray.tmpfiles"
        "v2ray.service"
        "v2ray@.service")
sha256sums=('b250f569cb0369f394f63184e748f1df0c90500feb8a1bf2276257c4c8b81bee'
            'caca9d88eff50ce7cd695b6fcab4d253344e92c966780f7de6d5c531d48ed80e'
            'ae55077bcf7140a7460f192adb03009b4573ec4420af84f4cbe9828cf8ca8e06'
            'ffe8c832fac5772fd700e39460e41cf59bac544c7a16e0b9a7fcee53d4e4bb9d'
            '7d8cd2309f3cb54d888128e9e8150432756b60cf7e759da46ad8e848c0b0a386')

build() {
    cd "${srcdir}"/"v2ray-core-${pkgver}"/
    export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external"
    export CGO_LDFLAGS="${LDFLAGS}"
    export CGO_CFLAGS="${CFLAGS}"
    export CGO_CPPFLAGS="${CPPFLAGS}"
    go build -o v2ray ./main
    go build -o v2ctl ./infra/control/main
}

check() {
    cd "${srcdir}"/"v2ray-core-${pkgver}"/
    go test -p 1 -tags json -v -timeout 30m v2ray.com/core/...
}

package() {
    cd "${srcdir}"/"v2ray-core-$pkgver"/
    install -d "${pkgdir}"/etc/v2ray/
    install -Dm 755 v2ray -t "${pkgdir}"/usr/bin/
    install -Dm 755 v2ctl -t "${pkgdir}"/usr/bin/
    install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/v2ray/
    install -Dm 644 "${srcdir}"/v2ray.sysusers "${pkgdir}"/usr/lib/sysusers.d/v2ray.conf
    install -Dm 644 "${srcdir}"/v2ray.tmpfiles "${pkgdir}"/usr/lib/tmpfiles.d/v2ray.conf
    install -Dm 644 "${srcdir}"/v2ray.service -t "${pkgdir}"/usr/lib/systemd/system/
    install -Dm 644 "${srcdir}"/v2ray@.service -t "${pkgdir}"/usr/lib/systemd/system/
}
