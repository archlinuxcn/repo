# Maintainer: Dct Mei <dctxmei@yandex.com>

pkgname=v2ray-dctxmei
_pkgname=v2ray
pkgver=4.45.0
pkgrel=1
pkgdesc="A platform for building proxies to bypass network restrictions"
arch=('x86_64')
url="https://github.com/v2fly/v2ray-core"
license=('MIT')
depends=('glibc' 'v2ray-domain-list-community' 'v2ray-geoip')
makedepends=('git' 'go')
provides=("v2ray=${pkgver}")
conflicts=('v2ray')
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/v2fly/v2ray-core/archive/v${pkgver}.tar.gz"
        "v2ray.service"
        "v2ray@.service")
sha256sums=('fb8731295d8bbad9ef9157dbd3bc62655752d36544e0cc4077d4fa8a8a4ea295'
            '11d3f49d37518ea2a342ec69cd815ffac829d40405e20fbc2ae5a7f22332f6b1'
            'f3fc6f0bc3e08e84a4a6d4360bd8bf7169964e4894d17fad549ac1824c45373b')

build() {
    cd "${srcdir}"/"v2ray-core-${pkgver}"/
    export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external"
    export CGO_LDFLAGS="${LDFLAGS}"
    export CGO_CFLAGS="${CFLAGS}"
    export CGO_CPPFLAGS="${CPPFLAGS}"
    go build -o v2ray ./main
    go build -o v2ctl ./infra/control/main
}

check() {
    cd "${srcdir}"/"v2ray-core-${pkgver}"/
    go test -p 1 -tags json -v -timeout 30m ./...
}

package() {
    cd "${srcdir}"/"v2ray-core-$pkgver"/
    install -d "${pkgdir}"/etc/v2ray/
    install -Dm 755 v2ray -t "${pkgdir}"/usr/bin/
    install -Dm 755 v2ctl -t "${pkgdir}"/usr/bin/
    install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/v2ray/
    install -Dm 644 "${srcdir}"/v2ray.service -t "${pkgdir}"/usr/lib/systemd/system/
    install -Dm 644 "${srcdir}"/v2ray@.service -t "${pkgdir}"/usr/lib/systemd/system/
}
