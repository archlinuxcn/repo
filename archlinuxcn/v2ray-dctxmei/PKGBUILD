# Maintainer: Dct Mei <dctxmei@yandex.com>

pkgname=v2ray-dctxmei
_pkgname=v2ray
pkgver=4.40.1
pkgrel=1
pkgdesc="A platform for building proxies to bypass network restrictions"
arch=('x86_64')
url="https://github.com/v2fly/v2ray-core"
license=('MIT')
depends=('glibc' 'v2ray-domain-list-community' 'v2ray-geoip')
makedepends=('git' 'go')
provides=("v2ray=${pkgver}")
conflicts=('v2ray')
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/v2fly/v2ray-core/archive/v${pkgver}.tar.gz"
        "v2ray.service"
        "v2ray@.service")
sha256sums=('75b599cb9866c2469056b71f2c0c69cbdab08cf15f6bd2273c893cd0bd16f175'
            '7b11f58df7068639354a840d88e1220f33046aa1402f81da5f434097b03be874'
            'd1e2a2cd16ceaf78d65130e2a5d3d49d3ef4ee8647e102d6a30494fb139efba9')

build() {
    cd "${srcdir}"/"v2ray-core-${pkgver}"/
    export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external"
    export CGO_LDFLAGS="${LDFLAGS}"
    export CGO_CFLAGS="${CFLAGS}"
    export CGO_CPPFLAGS="${CPPFLAGS}"
    go build -o v2ray ./main
    go build -o v2ctl ./infra/control/main
}

check() {
    cd "${srcdir}"/"v2ray-core-${pkgver}"/
    go test -p 1 -tags json -v -timeout 30m ./...
}

package() {
    cd "${srcdir}"/"v2ray-core-$pkgver"/
    install -d "${pkgdir}"/etc/v2ray/
    install -Dm 755 v2ray -t "${pkgdir}"/usr/bin/
    install -Dm 755 v2ctl -t "${pkgdir}"/usr/bin/
    install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/v2ray/
    install -Dm 644 "${srcdir}"/v2ray.service -t "${pkgdir}"/usr/lib/systemd/system/
    install -Dm 644 "${srcdir}"/v2ray@.service -t "${pkgdir}"/usr/lib/systemd/system/
}
