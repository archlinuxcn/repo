# Maintainer: Hu Butui <hot123tea123@gmail.com>
# Contributor: Leo Mao <leomaoyw at gmail dot com>

_pkgname=cupy
pkgname=python-cupy
pkgver=9.2.0
pkgrel=1
pkgdesc='A NumPy-compatible array library accelerated by CUDA'
url='https://cupy.dev'
arch=('x86_64')
license=('MIT')
depends=(
  cuda
  cudnn
  nccl
  python-fastrlock
  python-numpy
  )
makedepends=(
  cython
  git
  python-setuptools
)
optdepends=('libcutensor')
source=("${_pkgname}-${pkgver}::git+https://github.com/cupy/cupy.git#tag=v${pkgver}")
sha256sums=('SKIP')

prepare() {
  cd "${_pkgname}-${pkgver}"
  git submodule update --init --recursive
  export CC=/opt/cuda/bin/gcc
  export CXX=/opt/cuda/bin/g++
}

build() {
  cd "${_pkgname}-${pkgver}"
  python setup.py build
}

package() {
  cd "${_pkgname}-${pkgver}"
  python setup.py install --skip-build --prefix=/usr --root="${pkgdir}" --optimize=1
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
# vim:set ts=2 sw=2 et:
