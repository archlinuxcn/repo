# Maintainer: Bruce Zhang <zttt183525594@gmail.com>

pkgname=pulseaudio-modules-bt
pkgver=1.3
pkgrel=4
pkgdesc="PulseAudio Bluetooth modules with SBC, AAC, APTX, APTX-HD, Sony LDAC (A2DP codec) support"
arch=("i686" "x86_64" "arm" "armv6h" "armv7h" "aarch64")
url="https://github.com/EHfive/pulseaudio-modules-bt"
license=('GPL3')
depends=("pulseaudio>=12.0" "bluez" "bluez-libs" "sbc" "libfdk-aac.so")
makedepends=("cmake>=3.0" "libavcodec.so>=58" "libldac" "git")
optdepends=("libavcodec.so>=58: aptX Classic, aptX HD support"
            "libldac: LDAC support"
            "fdk-aac-free: alternative library for AAC support")
provides=("pulseaudio-bluetooth")
conflicts=("pulseaudio-bluetooth")

source=("https://github.com/EHfive/pulseaudio-modules-bt/archive/v$pkgver.tar.gz"
        "git+https://github.com/pulseaudio/pulseaudio.git")

md5sums=('d94c150e6463b4c241dc003c1530fd17'
         'SKIP')

prepare() {
    cd "$pkgname-$pkgver"
    rm -r pa
    ln -sf -T "../pulseaudio" "pa"
    git -C pa checkout v`pkg-config libpulse --modversion|sed 's/[^0-9.]*\([0-9.]*\).*/\1/'`
}

build() {
    cd "$pkgname-$pkgver"
    cmake \
        -DCMAKE_BUILD_TYPE=Release \
        .
    make
}

package() {
    cd "$pkgname-$pkgver"
    make DESTDIR="$pkgdir" install
}
