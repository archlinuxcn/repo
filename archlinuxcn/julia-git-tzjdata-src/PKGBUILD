pkgname=julia-git-tzjdata-src
pkgver=1.2.0+2024a
_commit=4b34705e20ffc069061739c502c9521af0fd6e48
pkgrel=1
pkgdesc="TZJData.jl"
url="https://github.com/JuliaTime/TZJData.jl.git"
arch=('any')
license=('MIT')
makedepends=(git julia-pkg-scripts wget)
depends=(julia-git)
source=("git+https://github.com/JuliaTime/TZJData.jl.git#commit=$_commit")
sha256sums=('65e497282e2b40c4871425dacdcc58c8d57890164db167dee69d14b548f68cc2')

build() {
  cd TZJData.jl

  rm -rf gen

  local url
  url=$(sed -ne '/url *= *".*"/{s/ *url *= *"\(.*\)" */\1/p;q}' Artifacts.toml)

  wget -O tzjfile-tzdata.tar.gz "$url"

  (
    rm -rf tzjdata/
    mkdir tzjdata/
    cd tzjdata/
    tar xf ../tzjfile-tzdata.tar.gz
    rm ../tzjfile-tzdata.tar.gz
  )

  sed -i -e 's|artifact"tzjdata"|joinpath(@__DIR__, "../tzjdata")|' src/TZJData.jl
}

package() {
  cd TZJData.jl

  JULIA_INSTALL_SRCPKG=1 . /usr/lib/julia/julia-install-pkg.sh TZJData "${pkgdir}" "${pkgname}" julia-git
}
