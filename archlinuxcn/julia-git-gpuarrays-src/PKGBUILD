pkgbase=julia-git-gpuarrays-src
_subpkgs=(GPUArraysCore
          JLArrays)
pkgname=(julia-git-gpuarrays-src)
pkgver=11.3.3
_commit=0c08596a5e948aaa0ffa33d66ce4cd62328e5ee7
pkgrel=1
pkgdesc="GPUArrays.jl"
url="https://github.com/JuliaGPU/GPUArrays.jl.git"
arch=('any')
license=('MIT')
makedepends=(git julia-pkg-scripts)
depends=(julia-git)
source=("git+https://github.com/JuliaGPU/GPUArrays.jl.git#commit=$_commit")
sha256sums=('270c4168ffd853bae973bf9fc498f30b5dc757d8eb0b3bae2fe8bee20ba4e2f7')

package_julia-git-gpuarrays-src() {
  cd GPUArrays.jl

  JULIA_INSTALL_SRCPKG=1 . /usr/lib/julia/julia-install-pkg.sh GPUArrays "${pkgdir}" "${pkgname}" julia-git

  rm -rf "${dest_dir}"/lib
}

_subpackage() {
  local _subpkg=$1
  cd GPUArrays.jl/lib/${_subpkg}

  JULIA_INSTALL_SRCPKG=1 . /usr/lib/julia/julia-install-pkg.sh ${_subpkg} "${pkgdir}" "${pkgname}" julia-git
}

for _subpkg in "${_subpkgs[@]}"; do
  pkgname=("${pkgname[@]}" julia-git-${_subpkg,,}-src)
  eval "package_julia-git-${_subpkg,,}-src() { _subpackage ${_subpkg}; }"
done
