pkgbase=julia-git-gpuarrays-src
_subpkgs=(GPUArraysCore
          JLArrays)
pkgname=(julia-git-gpuarrays-src)
pkgver=11.3.1
_commit=796cfd895926d0ce24e57196f45383daccbae08c
pkgrel=1
pkgdesc="GPUArrays.jl"
url="https://github.com/JuliaGPU/GPUArrays.jl.git"
arch=('any')
license=('MIT')
makedepends=(git julia-pkg-scripts)
depends=(julia-git)
source=("git+https://github.com/JuliaGPU/GPUArrays.jl.git#commit=$_commit")
sha256sums=('c4f806eea8aceea6a838e25f035e0e477005cc7da08dfe780e5594d7bfe57d42')

package_julia-git-gpuarrays-src() {
  cd GPUArrays.jl

  JULIA_INSTALL_SRCPKG=1 . /usr/lib/julia/julia-install-pkg.sh GPUArrays "${pkgdir}" "${pkgname}" julia-git

  rm -rf "${dest_dir}"/lib
}

_subpackage() {
  local _subpkg=$1
  cd GPUArrays.jl/lib/${_subpkg}

  JULIA_INSTALL_SRCPKG=1 . /usr/lib/julia/julia-install-pkg.sh ${_subpkg} "${pkgdir}" "${pkgname}" julia-git
}

for _subpkg in "${_subpkgs[@]}"; do
  pkgname=("${pkgname[@]}" julia-git-${_subpkg,,}-src)
  eval "package_julia-git-${_subpkg,,}-src() { _subpackage ${_subpkg}; }"
done
