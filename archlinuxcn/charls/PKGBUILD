# Maintainer: Butui Hu <hot123tea123@gmail.com>

pkgname=charls
pkgver=2.2.0
pkgrel=1
pkgdesc='A C++ JPEG-LS library implementation'
arch=('x86_64')
url='https://github.com/team-charls/charls'
license=('BSD')
depends=('gcc-libs')
makedepends=('cmake')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/team-charls/charls/archive/${pkgver}.tar.gz")
sha512sums=('387293f89bc30b50a59de7d5af23d0047bcdba89bd48cdb81e0437adabbfc9e19e135749129334a9db5c3ae5ad0c75b489e6e6545ee0c8134fc432153413793e')

build() {
  echo "CXXFLAGS: ${CXXFLAGS}"
  echo "CPPFLAGS: ${CPPFLAGS}"
  export CXXFLAGS+=" ${CPPFLAGS}"
  cmake \
    -B "${pkgname}-${pkgver}/build" \
    -S "${pkgname}-${pkgver}" \
    -DCMAKE_INSTALL_PREFIX:PATH=/usr \
    -DBUILD_SHARED_LIBS:BOOL=ON \
    -Wno-dev
  make -C "${pkgname}-${pkgver}/build"
}

package() {
  make -C "${pkgname}-${pkgver}/build" DESTDIR="${pkgdir}" install
  install -D -m644 "${srcdir}/${pkgname}-${pkgver}/LICENSE.md" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
# for compatibility
  ln -s "/usr/include/charls" "${pkgdir}/usr/include/CharLS"
  ln -s "/usr/lib/libcharls.so" "${pkgdir}/usr/lib/libCharLS.so.2"
}
# vim:set ts=2 sw=2 et:

