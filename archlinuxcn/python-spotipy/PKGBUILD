# Maintainer: Roald Clark <roaldclark@gmail.com>
# Contributor: goetzc
# Contributor: James P. Harvey <jamespharvey20 at gmail dot com>
# Contributor: Vyacheslav Konovalov <ðŸ¦€vk@protonmail.com>

_name=spotipy
pkgname="python-${_name}"
pkgver=2.25.1
pkgrel=1
pkgdesc='A light weight Python library for the Spotify Web API'
arch=('any')
url="https://github.com/${_name}-dev/${_name}"
license=('MIT')
depends=(
    'python'
    'python-redis'
    'python-requests'
    'python-urllib3'
)
makedepends=(
    'python-build'
    'python-installer'
    'python-setuptools'
    'python-wheel'
)
optdepends=('python-pymemcache')
conflicts=("${pkgname}-git")
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha256sums=('1d525c070567a3aa36efda82dbde59e0a2ac7f395e5a88ea4f31da47fda07ff9')

build() {
    cd "${srcdir}/${_name}-${pkgver}"
    python -m build --wheel --no-isolation
}

package() {
    cd "${srcdir}/${_name}-${pkgver}"
    install -Dm0644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}"
    python -m installer --destdir="${pkgdir}" dist/*.whl
}
