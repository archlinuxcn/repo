# Maintainer: Amanoel Dawod <amoka at amanoel dot com>
# Contributor: Elliott Saille <me+aur@esaille.me>
# Contributor: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: unikum <unikum.pm@gmail.com>
# Contributor: speed145a <jonathan@tagchapter.com>

pkgname=firewalld-git
pkgver=r3601.405228a3
pkgrel=1
epoch=1
pkgdesc="Firewall daemon with D-Bus interface (from git)"
arch=(any)
url="https://firewalld.org/"
license=(GPL2)
depends=(
  dbus-python
  hicolor-icon-theme
  nftables
  python-gobject
)
makedepends=(
  desktop-file-utils
  docbook-xsl
  git
  glib2
  intltool
  libxslt
  podman
)
optdepends=(
  'iptables-nft: ebtables and iptables support'
  'ipset: ipset support'
  'libnotify: firewall panel applet support'
  'nm-connection-editor: firewall panel applet support'
  'python-capng: drop daemon capabilities'
  'python-pyqt5: firewall panel applet support'
)
provides=('firewalld')
conflicts=('firewalld')
backup=(
  etc/conf.d/firewalld
  etc/firewalld/firewalld.conf
)
source=(
  git+https://github.com/firewalld/firewalld.git
  firewalld-sysconfigdir.patch
)
sha256sums=('SKIP'
            '37860bd444e67741cf4a7817eccc9bdbbf56e68cc9448bb54f44a44050b72045')

pkgver() {
  cd firewalld
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd firewalld
  patch -Np1 -i ../firewalld-sysconfigdir.patch
  NOCONFIGURE=true ./autogen.sh
}

build() {
  cd firewalld
  ./configure \
    --prefix=/usr \
    --localstatedir=/var \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --disable-schemas-compile \
    --disable-sysconfig
  make
}

package() {
  make DESTDIR="${pkgdir}" -C firewalld install
  export PYTHONHASHSEED=0
  python -m compileall -d /usr/lib "$pkgdir/usr/lib"
  python -O -m compileall -d /usr/lib "$pkgdir/usr/lib"
}
