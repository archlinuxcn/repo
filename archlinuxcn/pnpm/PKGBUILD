# Maintainer: Tomasz Jakub Rup <tomasz.rup@gmail.com>
pkgname=pnpm
pkgver=6.16.1
pkgrel=1
pkgdesc="Fast, disk space efficient package manager"
arch=('any')
url="https://pnpm.js.org/"
license=('MIT')
depends=('nodejs')
makedepends=('npm')
source=("https://registry.npmjs.org/$pkgname/-/$pkgname-$pkgver.tgz")
noextract=("$pkgname-$pkgver.tgz")
sha256sums=('2a40a3d8a56eaa411819fb8aa8cdd4d9fe729f74571929311247922387925f5a')

package() {
  npm install -g --user root --prefix "$pkgdir"/usr "$srcdir/$pkgname-$pkgver.tgz"

  find "$pkgdir"/usr -type d -exec chmod 755 {} +
  chown -R root:root "$pkgdir"

  # delete source map files to reduce size
  find "$pkgdir"/usr/lib -depth -name '*.map' -delete

  install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "../../../lib/node_modules/$pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
