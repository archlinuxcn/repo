# Maintainer: Tomasz Jakub Rup <tomasz.rup@gmail.com>
pkgname=pnpm
pkgver=7.25.1
pkgrel=1
pkgdesc="Fast, disk space efficient package manager"
arch=('any')
url="https://pnpm.js.org/"
license=('MIT')
depends=('nodejs')
makedepends=('npm')
source=("https://registry.npmjs.org/$pkgname/-/$pkgname-$pkgver.tgz")
noextract=("$pkgname-$pkgver.tgz")
sha256sums=('2ae0888b3aae1cac089c7099f13282a1768786267c7c86886bb73215218f70a4')

package() {
  npm install -g --prefix "$pkgdir"/usr "$srcdir/$pkgname-$pkgver.tgz"

  find "$pkgdir"/usr -type d -exec chmod 755 {} +
  chown -R root:root "$pkgdir"

  # delete source map files to reduce size
  find "$pkgdir"/usr/lib -depth -name '*.map' -delete

  install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "../../../lib/node_modules/$pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
