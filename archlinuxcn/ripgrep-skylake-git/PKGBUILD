# shellcheck disable=SC2034,SC2164,SC2154
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Andrew Gallant <jamslam@gmail.com>
pkgname=ripgrep-skylake-git
pkgver=13.0.0.20221216
pkgrel=1
pkgdesc="A search tool that combines the usability of ag with the raw speed of grep. Git version, optimized for skylake CPUs"
arch=('x86_64')
url="https://github.com/BurntSushi/ripgrep"
license=('MIT' 'UNLICENSE')
depends=('gcc-libs' 'pcre2')
makedepends=('cargo-nightly' 'git' 'asciidoc')
options=('debug')
source=("https://github.com/BurntSushi/$pkgname/archive/$pkgver.tar.gz")
source=("git+$url")
sha256sums=('SKIP')
conflicts=(ripgrep ripgrep-broadwell-git)
replaces=(ripgrep-broadwell-git)
provides=(ripgrep=$pkgver ripgrep-git=$pkgver)

pkgver () {
  cd ripgrep
  # git describe | sed -E 's/([^-]*-g)/r\1/;s/-/./g'
  echo $(grep -oPm1 '(?<=^version = ")[^"]+' Cargo.toml).$(TZ=UTC git log -1 --pretty='%cd' --date=short-local | tr -d '-')
}

build () {
  cd ripgrep
  echo "lto = true" >> Cargo.toml
  RUSTFLAGS="-C target-cpu=skylake" cargo build --release --features pcre2
  git checkout Cargo.toml
}

package () {
  cd ripgrep

  install -Dm755 "target/release/rg" "$pkgdir/usr/bin/rg"

  install -Dm644 "target/release/build/ripgrep-"*/out/rg.bash "$pkgdir/usr/share/bash-completion/completions/rg"
  install -Dm644 "complete/_rg" "$pkgdir/usr/share/zsh/site-functions/_rg"

  install -Dm644 "target/release/build/ripgrep-"*/out/rg.1 "$pkgdir/usr/share/man/man1/rg.1"
  install -Dm644 "README.md" "$pkgdir/usr/share/doc/${pkgname}/README.md"

  install -Dm644 "COPYING" "$pkgdir/usr/share/licenses/${pkgname}/COPYING"
  install -Dm644 "LICENSE-MIT" "$pkgdir/usr/share/licenses/${pkgname}/LICENSE-MIT"
  install -Dm644 "UNLICENSE" "$pkgdir/usr/share/licenses/${pkgname}/UNLICENSE"
}
