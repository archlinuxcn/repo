# Maintainer: Paul <pb.orzel@proton.me>

pkgname=amdgpu_top
pkgver=0.6.0
pkgrel=1
pkgdesc="Tool that shows AMD GPU utilization"
arch=('i686' 'pentium4' 'x86_64' 'arm' 'armv7h' 'armv6h' 'aarch64')
url="https://github.com/Umio-Yasuno/${pkgname}"
license=("MIT")
depends=("libdrm")
makedepends=("cargo")
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('6fd4bf25737f1a3ac83993dc7bdaa5ed63593e25d4de1dc15ac98459d5b2bc0d')

prepare() {
	cd "${pkgname}-${pkgver}/"
	export RUSTUP_TOOLCHAIN=stable
	export CARGO_TARGET_DIR=target
	cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
	cd "${pkgname}-${pkgver}/"
	export RUSTUP_TOOLCHAIN=stable
	export CARGO_TARGET_DIR=target
	cargo build --frozen --release
}

package() {
	cd "${pkgname}-${pkgver}/"
	install -Dm755 target/release/amdgpu_top -t "${pkgdir}/usr/bin/"
	install -Dm644 assets/amdgpu_top.desktop -t "${pkgdir}/usr/share/applications/desktop/"
	install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
