# Maintainer: Adrián Pérez de Castro <aperez@igalia.com>
# Maintainer: Alexandre Macabies <web+oss@zopieux.com>
#
pkgname=gn-git
pkgdesc='Meta-build system which generates Ninja build files'
pkgver=r1639.f3c7676f
pkgrel=1
license=('BSD')
arch=('x86_64' 'i686')
conflicts=('gn-bin' 'gn')
provides=('gn')
depends=('gcc-libs')
makedepends=('python2' 'ninja' 'git')
url='https://gn.googlesource.com/gn'
source=("gn::git+${url}" gcc-support.patch)
sha512sums=('SKIP'
            'fe2576ac88859494f7829de8b4fec1dc87ba9a418ac5b80028dea52471ffdfb65570728f5b2e8a9328d404394e6e22a5ebfd3765660e27324918f7a23fea3854')

pkgver () {
	export GIT_DIR="${startdir}/gn"
	printf "r%s.%s" "$(git --bare rev-list --count HEAD)" "$(git --bare rev-parse --short HEAD)"
}

prepare () {
	cd gn
	patch -p1 < "${srcdir}/gcc-support.patch"
}

build () {
	cd gn
	CC=cc CXX=c++ AR=ar \
	CFLAGS="${CFLAGS} -pthread" \
	CXXFLAGS="${CXXFLAGS} -pthread" \
	LDFLAGS="${LDFLAGS} -pthread" \
		python2 build/gen.py --use-lto --no-strip
	ninja -C out
}

package () {
	install -Dm755 gn/out/gn "${pkgdir}/usr/bin/gn"

	# Documentation
	install -Dm644 -t "${pkgdir}/usr/share/doc/${pkgname}" \
		gn/docs/*.md gn/README.md

	install -Dm644 -t "${pkgdir}/usr/share/doc/${pkgname}/example" \
		gn/examples/simple_build/*.*
	install -Dm644 -t "${pkgdir}/usr/share/doc/${pkgname}/example/build" \
		gn/examples/simple_build/build/*.*
	install -Dm644 -t "${pkgdir}/usr/share/doc/${pkgname}/example/build/toolchain" \
		gn/examples/simple_build/build/toolchain/*.*

	# Vim support
	local item
	for item in autoload ftplugin ftdetect syntax ; do
		install -Dm 644 -t "${pkgdir}/usr/share/vim/vimfiles/${item}" \
			"gn/tools/gn/misc/vim/${item}"/*.vim
	done
		
	# License
	install -m 644 -D gn/LICENSE \
		"${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
