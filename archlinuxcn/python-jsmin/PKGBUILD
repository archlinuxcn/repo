_name=jsmin
pkgname=python-jsmin
pkgver=2.2.2
pkgrel=7
pkgdesc="JavaScript minifier."
arch=(any)
url="https://github.com/tikitu/jsmin/"
license=('MIT License')
depends=('python')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha256sums=('b6df99b2cd1c75d9d342e4335b535789b8da9107ec748212706ef7bbe5c2553b')

build() {
  cd "$srcdir/$_name-$pkgver"
  python3 setup.py build
}

package() {
  cd "$srcdir/$_name-$pkgver"
  python3 setup.py install --root=$pkgdir --optimize=1 --skip-build

  # make sure we don't install annoying files
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  rm -rf "$pkgdir/$_site_packages/tests/"
}

