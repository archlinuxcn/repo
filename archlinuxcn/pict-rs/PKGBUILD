# Maintainer: Integral <integral@member.fsf.org>
# Contributor: lifespirit <life@crabs.pro>

pkgname=pict-rs
pkgver=0.5.20
pkgrel=1
pkgdesc="A simple image hosting service"
arch=('x86_64')
url="https://git.asonix.dog/asonix/${pkgname}"
license=('AGPL-3.0-or-later')
depends=(
	'gcc-libs'
	'imagemagick'
	'ffmpeg'
	'perl-image-exiftool'
)
makedepends=('git' 'cargo' 'protobuf')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha256sums=('fe829a7deb279a236adb9eda08f3660e060dc119057e3c719db51081a93d4ecf')

prepare() {
	cd "${pkgname}/"
	cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
	cd "${pkgname}/"
	CFLAGS+=" -ffat-lto-objects" cargo build --frozen --release --all-features
}

check() {
	cd "${pkgname}/"
	CFLAGS+=" -ffat-lto-objects" cargo test --frozen --all-features
}

package() {
	cd "${pkgname}/"
	install -vDm755 "target/release/${pkgname}" -t "${pkgdir}/usr/bin/"
	install -vDm644 docs/* -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
