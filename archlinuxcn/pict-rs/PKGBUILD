# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=pict-rs
_pkgver=0.5.16
pkgver="${_pkgver/-/.}"
pkgrel=1
pkgdesc="A simple image hosting service"
arch=('x86_64')
url="https://git.asonix.dog/asonix/pict-rs"
license=('AGPL3')
depends=('glibc' 'gcc-libs' 'imagemagick' 'ffmpeg' 'perl-image-exiftool')
makedepends=('rust' 'protobuf')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$_pkgver.tar.gz")
b2sums=('978d8fbf642bf52514f4978fecfd750d246738f9a5584e4722eb9788ba0f9f4671f07d9ec25c9f61e555955842d1b60873d5a903c517f9fa417231dce2f12943')

prepare() {
  cd "$pkgname"

  # download dependencies
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname"
  cargo build --frozen --release --all-features
}

check() {
  cd "$pkgname"
  cargo test --frozen --all-features
}

package() {
  cd "$pkgname"
  install -vDm755 -t "$pkgdir/usr/bin" "target/release/$pkgname"
}
