# Maintainer: KokaKiwi <kokakiwi+aur@kokakiwi.net>

_pkgname=ImHex-Patterns
pkgname=${_pkgname,,}-git
pkgver=r214.f53be98
pkgrel=1
pkgdesc='Hex patterns, include patterns and other files for the use with the ImHex Hex Editor'
url='https://github.com/WerWolv/ImHex-Patterns'
license=('GPL2')
arch=('any')
depends=('bash' 'python')
makedepends=('git')
source=("${pkgname}::git+${url}.git"
        "yara::git+https://github.com/Yara-Rules/rules.git"
        )
sha256sums=('SKIP'
            'SKIP')
b2sums=('SKIP'
        'SKIP')
options=(!strip)

pkgver() {
  cd "${pkgname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${pkgname}"
  git submodule init
  git config submodule."yara/official_rules".url "${srcdir}/yara"
  git -c protocol.file.allow=always submodule update
}

package() {
  install -dm755 "$pkgdir/usr/share/imhex"
  shopt -u dotglob
  cp -r -t "$pkgdir/usr/share/imhex" \
    "$pkgname"/*/
  rm -rf "$pkgdir"/usr/share/imhex/tests
}
