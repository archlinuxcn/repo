# Maintainer: Kimiblock Moe
# Contributor: Francesco Masala <mail@francescomasala.me>
# Contributor: lotation <xlapsiu@gmail.com>

pkgname=bottles-bwrap
_pkgname=Bottles
pkgver=60.1
pkgrel=2
epoch=2
pkgdesc='Easily manage wine and proton prefix. Sandboxed by portable.'
arch=(any)
url="https://github.com/bottlesdevs/Bottles"
license=(GPL-3.0-only)
provides+=(bottles)
conflicts+=(bottles)
depends=(
	cabextract
	dconf
	gtk4
	gtksourceview5
	hicolor-icon-theme
	icoextract
	imagemagick
	libadwaita
	libportal-gtk4
	p7zip
	patool
	python
	python-chardet
	python-fvs
	python-gobject
	python-markdown
	python-orjson
	python-pathvalidate
	python-pycurl
	python-requests
	python-steamgriddb
	python-yaml
	python-yara
	webkit2gtk
	xorg-xdpyinfo
	vkbasalt-cli
	portable
	gamemode
)

optdepends=(
	gvfs
	lib32-gamemode
	lib32-gnutls
	lib32-vkd3d
	lib32-vulkan-icd-loader
	vkd3d
	vulkan-icd-loader
	wine
)
makedepends=(
	blueprint-compiler
	meson
	ninja
	git
)
source=(
	"Bottles::git+https://github.com/bottlesdevs/Bottles.git#tag=${pkgver}"
	remove-flatpak-checks.patch
	portable-config
	start.sh
)
sha256sums=('3b6203ff66b79ac4dab1f70ff6b05146629ac839b5a972b0e3fd8b1a56966068'
            '3efff1f50b7878115b9b46b4998fbb36cd749a0515c6984b12efd7f12044b8b2'
            '15b16b8fdc61e5da2e62c80643351c1cbe981e5df4474564446e159ef7249766'
            'e8151fe783b4c202c99e206535f6f2b3e025b070d7237cb6f8bbb1f23ad0eb94')

function prepare() {
	patch --forward --directory="${srcdir}/${_pkgname}" --strip=1 --input="${srcdir}/remove-flatpak-checks.patch"
}

build() {
	cd "${srcdir}/${_pkgname}"
	meson setup --prefix='/usr' build
	ninja -C build
}

package() {
	install -vDm755 "${srcdir}/portable-config" \
		"${pkgdir}/usr/lib/portable/info/com.usebottles.bottles/config"
	install -vDm755 "${srcdir}/start.sh" \
		"${pkgdir}/usr/bin/bottles-bwrap"
	cd "${srcdir}/${_pkgname}"
	DESTDIR="${pkgdir}" ninja -C build install
	install -d "${pkgdir}/usr/lib/bottles-bwrap"
	mv ${pkgdir}/usr/bin/bottles{,-cli} "${pkgdir}/usr/lib/bottles-bwrap"
	ln -srf "${pkgdir}/usr/bin/bottles-bwrap" "${pkgdir}/usr/bin/bottles"
}
