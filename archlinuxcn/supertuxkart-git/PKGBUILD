#Maintainer: heavysink (winstonwu91 at gmail)

pkgname=supertuxkart-git
pkgver=23144+18565
pkgrel=1
pkgdesc="A kart racing game featuring Tux and his friends - development version"
url="http://supertuxkart.sourceforge.net/"
license=("cc-by-sa-3.0")
arch=('i686' 'x86_64')

makedepends=("git" "subversion" "cmake")

depends=("hicolor-icon-theme" "sqlite" "python" "libvorbis" "freealut" "mcpp" "glut" "glew" "libraqm" "libxkbcommon" "libopenglrecorder" "sdl2" "bluez-libs")

conflicts=("supertuxkart")
replaces=("supertuxkart-cmakesvn" "supertuxkart-svn")

source=(
    "stk-code::git+https://github.com/supertuxkart/stk-code.git"

    # assets reside in subversion repository
    "stk-assets::svn+https://svn.code.sf.net/p/supertuxkart/code/stk-assets"
)
md5sums=('SKIP' 'SKIP')

pkgver() {
    cd "${srcdir}/stk-code"
    local _git_rev="$(git rev-list --count HEAD)"

    cd "${srcdir}/stk-assets"
    local _assets_rev="$(svnversion)"

    printf "%s+%s" "${_git_rev}" "${_assets_rev}"
}

build() {
    cd "${srcdir}/stk-code"

    [ -d "cmake_build" ] && rm -rf cmake_build
    mkdir cmake_build && cd cmake_build

    cmake -DCMAKE_INSTALL_PREFIX=/usr ..
    # -DBUILD_RECORDER=off     - disable in-game recorder (then you can remove the dependency `libopenglrecorder`)
    make
}

package() {
    cd "stk-code/cmake_build"
    make DESTDIR=${pkgdir} install

    install -dm 755 "${pkgdir}/usr/share/pixmaps"
    cd "${pkgdir}/usr/share/pixmaps"
    if [ ! -f supertuxkart.png ]; then
        ln -s supertuxkart{_128,}.png
    fi
}
