# Maintainer: Yichao Yu <yyc1992@gmail.com>

pkgname=sleef-git
_realver=3.6.1
epoch=1
pkgver=3.7.0.34.0.gb56eab1
pkgrel=1
pkgdesc='SIMD Library for Evaluating Elementary Functions, vectorized libm and DFT'
arch=('aarch64' 'i686' 'x86_64')
url="https://sleef.org"
license=('custom')
depends=()
makedepends=(cmake git)
provides=(
  "sleef=${_realver}")
conflicts=("sleef")
options=('debug' 'strip')
source=("git+https://github.com/shibatch/sleef")
md5sums=('SKIP')

pkgver() {
  cd sleef

  git describe --tags | sed -e 's/-/.0./g'
}

# prepare() {
#   cd sleef
# }

build() {
  cd sleef

  mkdir -p build
  cd build

  CFLAGS+=" -ffile-prefix-map=${srcdir}=${DBGSRCDIR:-/usr/src/debug}"
  export CFLAGS

  cmake .. \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DSLEEF_BUILD_TESTS=Off \
    -DSLEEF_BUILD_QUAD=On \
    -DBUILD_SHARED_LIBS=On
  make
}

package() {
  cd sleef/build

  make DESTDIR="$pkgdir/" install
}
