# Maintainer: Megumi_fox <i@megumifox.com>

pkgname=qliveplayer-git
pkgver=3.22.1.r0.ge0d6117
pkgrel=1
pkgdesc='Cute and useful Live Stream Player with danmaku support.'
arch=('x86_64')
url="https://github.com/IsoaSFlus/QLivePlayer"
license=('GPL2')
provides=('qliveplayer')
conflicts=('qliveplayer')
depends=('mpv' 
         'ffmpeg'
         'python-aiohttp'
         'qt5-base'
         'qt5-quickcontrols2'
         'qt5-graphicaleffects'
         'qt5-quickcontrols'
         'python')
makedepends=('cmake'
             'git'
             'ninja'
             'extra-cmake-modules' )
optdepends=('python-protobuf: for YouTube LiveChat support'
            'streamlink: for foreign streaming service support')

source=(
    "QLivePlayer::git+https://github.com/IsoaSFlus/QLivePlayer.git"
)

pkgver() {
    cd QLivePlayer
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

sha256sums=('SKIP')

build() {
    cd $srcdir/QLivePlayer
    mkdir -p build
    cd build
    cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_LIBDIR=lib ..
    ninja
}

package() {
    cd $srcdir/QLivePlayer/build
    DESTDIR="$pkgdir" ninja install
}
