# Maintainer: Atte Lautanala <atte@lautana.la>
# Contributor: Trevor Mosey <trevor dot mosey at gmail dot com>

pkgname=flyctl
pkgver=0.3.232
pkgrel=1
pkgdesc="Command line tools for fly.io services"
arch=('x86_64' 'aarch64' 'riscv64')
url="https://github.com/superfly/${pkgname}"
license=("Apache-2.0")
makedepends=('go')
checkdepends=('ruby-rake')
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('ac9a6938428cf68d1b6c86c7d2466d559889fedfc5d13ec68c5bd5d9bd2339b3')

prepare() {
	cd "${pkgname}-${pkgver}/"
	export GOPATH="${srcdir}"
	go mod download -modcacherw
}

build() {
	cd "${pkgname}-${pkgver}/"
	export CGO_CPPFLAGS="${CPPFLAGS}"
	export CGO_CFLAGS="${CFLAGS}"
	export CGO_CXXFLAGS="${CXXFLAGS}"
	export CGO_LDFLAGS="${LDFLAGS}"
	export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
	go build -o bin/flyctl
}

check() {
	cd "${pkgname}-${pkgver}/"
	go test ./...
}

package() {
	install -Dm755 "${pkgname}-${pkgver}/bin/flyctl" -t "${pkgdir}/usr/bin/"
	ln -s "/usr/bin/${pkgname}" "${pkgdir}/usr/bin/fly"
}
