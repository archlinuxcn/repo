# Maintainer: Benjamin Maisonnas <ben at wainei dot net>
# Author: Ondrej ÄŒerman

_pkgbase=zenpower
pkgname=zenpower-dkms-git
pkgver=46.5e2f56f
pkgrel=1
pkgdesc="Linux kernel driver for reading sensors for AMD Zen family CPUs"
arch=('x86_64' 'i686')
url="https://github.com/ocerman/zenpower"
license=('GPL')
depends=('make' 'gcc' 'linux' 'dkms')
makedepends=('git' 'linux-headers')
provides=('zenpower')

source=("$_pkgbase::git+$url.git"
        "Makefile"
        "dkms.conf"
        "$_pkgbase.conf")

sha256sums=('SKIP'
            'f7f2c87bfed0d4a921d196948b77f9c9d75357b6c3396b72ad9eac9db1720580'
            '9eb35ca8dcff02744dd3256238f22bfe6ebaf1636867aabed4c40a31073c4e1e'
            '92c26ef5cd6f8a632f2d8cb12e9da8c4452cb0432ffe543733f13e67d6531d04')

pkgver() {
  cd "$srcdir/$_pkgbase"
  printf "%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "$srcdir/$_pkgbase"
}

package() {
  cd "$srcdir/$_pkgbase"

  install -d "${pkgdir}"/usr/src/${_pkgbase}-${pkgver}/
  cp -r ${srcdir}/${_pkgbase}/* "${pkgdir}"/usr/src/${_pkgbase}-${pkgver}/

  install -Dm644 ${srcdir}/Makefile "${pkgdir}"/usr/src/${_pkgbase}-${pkgver}/Makefile
  install -Dm644 ${srcdir}/dkms.conf "${pkgdir}"/usr/src/${_pkgbase}-${pkgver}/dkms.conf

  sed -e "s/@_PKGBASE@/${_pkgbase}/" \
    -e "s/@PKGVER@/${pkgver}/" \
    -i "${pkgdir}"/usr/src/${_pkgbase}-${pkgver}/dkms.conf

  install -Dm644 ${srcdir}/${_pkgbase}.conf "${pkgdir}"/usr/lib/depmod.d/${_pkgbase}.conf
}
