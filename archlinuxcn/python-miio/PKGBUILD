# Maintainer: Kasei Wang <kasei@kasei.im>

pkgname=python-miio
pkgver=0.5.5
pkgrel=1
pkgdesc="Python library & console tool for controlling Xiaomi smart appliances"
url="https://github.com/rytilahti/python-miio"
arch=('any')
license=('GPL3')
depends=('python>=3.6' 'python-appdirs' 'python-attrs' 'python-click>=7' 'python-construct' 'python-cryptography' 'python-netifaces' 'python-pytz' 'python-tqdm' 'python-zeroconf')
makedepends=('git' 'python-setuptools' 'python-poetry')
optdepends=('python-android-backup-tools: Android backup extraction support')
source=("${pkgname}::git+https://github.com/rytilahti/${pkgname}.git#tag=${pkgver}")
sha512sums=('SKIP')

prepare() {
  cd "${pkgname}"
  git submodule update --init --recursive
}

build() {
	cd ${pkgname}
	poetry build
	tar -xvf "./dist/${pkgname}-${pkgver}.tar.gz"
}

package() {
	cd ${pkgname}/${pkgname}-${pkgver}
	python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
	install -D -m644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
