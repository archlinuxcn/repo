# Maintainer: Hao Long <aur@esd.cc>

pkgname=bgpq3-git
pkgver=0.1.35.r14.g4a86326
pkgrel=1
pkgdesc="bgp filtering automation tool"
arch=("x86_64" "i686")
url="https://github.com/snar/bgpq3"
license=('BSD')
provides=('bgpq3')
conflicts=('bgpq3')
depends=('glibc')
makedepends=('git')
source=("$pkgname::git://github.com/snar/bgpq3.git"
        "01-fix-man-location.patch")
sha256sums=('SKIP'
            '4723df01270b3c4b712768b9f867ac72c9070ab7a96469f0aa34d65769e8412c')

pkgver() {
  cd $pkgname
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd $pkgname
  patch -Np1 -i ${srcdir}/01-fix-man-location.patch
}

build() {
  cd $pkgname
  ./configure --prefix=$pkgdir/usr
  make -j $(nproc)
}

package() {
  cd $pkgname
  make install
  install -Dm644 COPYRIGHT "$pkgdir"/usr/share/licenses/$pkgname/COPYRIGHT
  install -Dm644 README.md "$pkgdir"/usr/share/doc/$pkgname/README.md
}
