# Maintainer: Chih-Hsuan Yen <yan12125@gmail.com>

# Modified from aur/fcitx5-chinese-addons-git. Original maintainers:
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: csslayer <wengxt AT gmail com>

_pkgname=fcitx5-chewing
pkgname=$_pkgname-git
pkgver=r110.67062fd
pkgrel=1
pkgdesc="Fcitx5 addon for Chewing"
arch=('i686' 'x86_64')
url="https://gitlab.com/fcitx/fcitx5-chewing"
license=('GPL')
depends=('libchewing-git' 'fcitx5-git' 'hicolor-icon-theme')
provides=("$_pkgname=$pkgver")
conflicts=("$_pkgname" 'fcitx-chewing')
makedepends=('extra-cmake-modules' 'git')
source=("git+https://github.com/fcitx/fcitx5-chewing.git")
sha512sums=('SKIP')

pkgver() {
  cd $_pkgname

  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd $_pkgname

  mkdir -p build
}

build(){
  cd $_pkgname/build

  cmake \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_INSTALL_LIBDIR=/usr/lib \
      ..

  make
}

package() {
  cd $_pkgname/build

  make DESTDIR="$pkgdir" install
}
