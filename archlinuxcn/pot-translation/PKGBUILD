# Maintainer: Pylogmon <pylogmon@outlook.com>
# Maintainer: Integral <integral@member.fsf.org>

pkgname=pot-translation
_pkgname=pot-desktop
pkgver=3.0.6
_pkgver=${pkgver/b/-beta}
pkgrel=1
pkgdesc="一个跨平台的划词翻译软件 | A cross-platform software for text translation."
arch=('x86_64' 'aarch64' 'riscv64')
url="https://github.com/pot-app/${_pkgname}"
license=('GPL-3.0-or-later')
depends=('webkit2gtk' 'gtk3' 'libayatana-appindicator' 'xdotool' 'libxcb' 'libxrandr' 'tesseract' 'tessdata')
makedepends=('nodejs' 'pnpm' 'cargo' 'dbus')
source=("${_pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/${_pkgver}.tar.gz")
sha512sums=('fc0a24cc59b52b7ae1d4405986a8346793d9fd9577c395b39fdf92fbcc881dc94d8f080cfcf26f30d34746bb58616a4d5a2953e60659f0cf8043b7cf0d0936ff')
options=('!lto')

prepare() {
	cd "${_pkgname}-${_pkgver}/"
	sed -i "s/\"version\".*/\"version\": \"${_pkgver}\"/g" "src-tauri/tauri.conf.json"
	pnpm i
}

build() {
	cd "${_pkgname}-${_pkgver}/"
	pnpm tauri build -b deb
}

package() {
	cd "${_pkgname}-${_pkgver}/"
	tar -xpf src-tauri/target/release/bundle/deb/pot_${_pkgver}_*/data.tar.gz -C "${pkgdir}"
	chown -R root:root "${pkgdir}"
}
