# Maintainer: Pylogmon <pylogmon@outlook.com>

pkgname=pot-translation
_pkgname=pot-desktop
pkgver=2.7.7
_pkgver=$(sed 's/b/-beta/' <<<${pkgver})
pkgrel=1
pkgdesc="一个跨平台的划词翻译软件 | A cross-platform software for text translation."
arch=('x86_64' 'aarch64' 'riscv64')
url="https://github.com/pot-app/pot-desktop"
license=('GPL-3.0-or-later')
depends=('webkit2gtk' 'gtk3' 'libayatana-appindicator' 'xdotool' 'libxcb' 'libxrandr' 'tesseract' 'tessdata')
makedepends=('nodejs' 'pnpm' 'rust' 'dbus')
source=("${_pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/${_pkgver}.tar.gz")
sha512sums=('6f9c48c4ab4184da3019ebc658a85d1f1c1c521dbddb8bafa5c4ba7ffe61a3af78081c41d08e6def75feb98894917d36f486cb721ac773033452e0c04c23d791')
options=('!lto')

build() {
	cd "${_pkgname}-${_pkgver}/"
	sed -i "s/\"version\".*/\"version\": \"${_pkgver}\"/g" "src-tauri/tauri.conf.json"
	pnpm i
	pnpm tauri build -b deb
}

package() {
	cd "${_pkgname}-${_pkgver}/"
	tar -xpf "src-tauri/target/release/bundle/deb/pot_${_pkgver}_amd64/data.tar.gz" -C ${pkgdir}
	chown -R root:root ${pkgdir}
}
