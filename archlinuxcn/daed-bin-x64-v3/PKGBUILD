# Maintainer: Integral <integral@member.fsf.org>

pkgname=daed-bin-x64-v3
_pkgname=daed
pkgver=0.4.0rc1
pkgrel=1
pkgdesc="A modern dashboard for dae, bundled with dae-wing (backend API server) and dae (core)."
url="https://daeuniverse.github.io/daed"
arch=('x86_64')
license=('AGPL-3.0-or-later AND MIT')
provides=('daed')
conflicts=('daed' 'daed-git')
source=("${pkgname}-${pkgver}.zip::https://github.com/daeuniverse/${_pkgname}/releases/download/v${pkgver}/${_pkgname}-linux-x86_64_v3_avx2.zip")
sha512sums=('2725b7308a8b0f799715f5867bdfd8a6b24639edf63a30c7df4839b9278311a11520ad7457b76cf5b67b1154b35e786fe035b7f74b0b5c0c8778ba4d7cee3912')

prepare() {
	if !(/lib/ld-linux-x86-64.so.2 --help | grep "x86-64-v3 (supported, searched)" &> /dev/null); then
		echo "Your CPU does NOT support x86-64-v3!"
		echo "Exiting..."
		exit 1
	fi
}

package() {
	# Install binary
	mv "daed-linux-x86_64_v3_avx2" "${_pkgname}"
	install -vDm755 "${_pkgname}" -t "${pkgdir}/usr/bin/"

	# Install systemd service
	install -vDm644 "daed.service" -t "${pkgdir}/usr/lib/systemd/system/"

	# Install geoip.dat & geosite.dat
	install -vDm644 "geoip.dat" -t "${pkgdir}/usr/share/${_pkgname}/"
	install -vDm644 "geosite.dat" -t "${pkgdir}/usr/share/${_pkgname}/"

	echo "After installation completed, open your browser and navigate to http://localhost:2023"
}
