# Maintainer: Josip Ponjavic <josipponjavic at gmail dot com>

pkgname=haruna-git
_pkgname=${pkgname%-git}
pkgver=1.3.3.r37.gf46aada
pkgrel=1
pkgdesc="A Qt/QML video player built on top of libmpv"
url="https://invent.kde.org/multimedia/${_pkgname}"
arch=('x86_64')
license=('GPL-3.0-or-later')
depends=('ffmpeg'
	'gcc-libs'
	'kcolorscheme'
	'kconfig'
	'kcoreaddons'
	'kcrash'
	'kdeclarative'
	'kfilemetadata'
	'ki18n'
	'kio'
	'kirigami'
	'kwindowsystem'
	'mpvqt'
	'qqc2-desktop-style'
	'qt6-5compat'
	'qt6-base'
	'qt6-declarative')
makedepends=('git' 'extra-cmake-modules')
optdepends=('yt-dlp: for video-sharing websites playback'
	'youtube-dl: for video-sharing websites playback')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=("git+${url}.git")
sha256sums=('SKIP')

pkgver() {
	cd "${_pkgname}/"
	git describe --long --tags | sed -r 's/([^-]*-g)/r\1/;s/-/./g;s/v//g'
}

build() {
	cmake -B build -S "${_pkgname}/" -DCMAKE_INSTALL_PREFIX=/usr
	cmake --build build
}

package() {
	DESTDIR="${pkgdir}" cmake --install build
}
