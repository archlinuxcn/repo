# Maintainer: Josip Ponjavic <josipponjavic at gmail dot com>

pkgname=haruna-git
_pkgname=${pkgname%-git}
pkgver=0.12.2.r26.g623664d
pkgrel=1
pkgdesc="A Qt/QML video player built on top of libmpv"
url="https://invent.kde.org/multimedia/haruna"
arch=('x86_64')
license=('GPL-3.0-or-later')
depends=('breeze' 'kfilemetadata5' 'kio-extras' 'mpv' 'qqc2-desktop-style5')
makedepends=('extra-cmake-modules' 'git' 'kdoctools5')
optdepends=('yt-dlp: for video-sharing websites playback'
	'youtube-dl: for video-sharing websites playback')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=("${_pkgname}::git+${url}.git")
sha256sums=('SKIP')

pkgver() {
	cd "${_pkgname}/"
	git describe --long --tags | sed -r 's/([^-]*-g)/r\1/;s/-/./g;s/v//g'
}

build() {
	cmake -B build -S "${_pkgname}/" -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=None
	cmake --build build
}

package() {
	DESTDIR="${pkgdir}" cmake --install build
}
