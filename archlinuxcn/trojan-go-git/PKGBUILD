pkgname=trojan-go-git
_pkgname=trojan-go
pkgver=0.6.0.r0.gb93b95f
pkgrel=2
pkgdesc="A Trojan proxy written in Go (git version)."
arch=(x86_64)
url="https://github.com/p4gefau1t/trojan-go"
license=(GPL3)
depends=(glibc v2ray-geoip v2ray-domain-list-community)
makedepends=(go git)
provides=(trojan-go)
conflicts=(trojan-go)
source=("$_pkgname::git+$url.git")
sha512sums=('SKIP')

pkgver() {
    cd "$srcdir"/$_pkgname
    git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {    
    cd "$srcdir"/$_pkgname
    
    # NOTE: uncomment the following line if you are in mainland china
    # export GOPROXY="https://goproxy.cn,direct"
    export CGO_ENABLED=1
    export GO111MODULE=on

    go build -v \
        -buildmode=pie -trimpath -mod=readonly -modcacherw \
        -tags "full" \
        -ldflags="-s -w -extldflags=-Wl,-z,now,-z,relro" \
        -o trojan-go .
}

package() {
    cd "$srcdir"/$_pkgname
    install -Dm755 "$srcdir"/$_pkgname/trojan-go -t "$pkgdir"/usr/lib/trojan-go/
    mkdir -p "$pkgdir"/usr/bin
    ln -sf /usr/lib/$_pkgname/trojan-go "$pkgdir"/usr/bin/trojan-go
    ln -sf /usr/lib/v2ray/geosite.dat "$pkgdir"/usr/lib/$_pkgname/geosite.dat
    ln -sf /usr/lib/v2ray/geoip.dat "$pkgdir"/usr/lib/$_pkgname/geoip.dat
}
