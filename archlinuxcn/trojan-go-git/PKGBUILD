pkgname=trojan-go-git
_pkgname=trojan-go
pkgver=0.8.0.r0.gfccba27
pkgrel=1
pkgdesc="A Trojan proxy written in Go (git version)."
depends=(glibc v2ray-geoip v2ray-domain-list-community)
provides=(trojan-go)
conflicts=(trojan-go)
makedepends=(go git hugo)
arch=(x86_64)
url="https://github.com/p4gefau1t/trojan-go"
license=(GPL3)
source=("$_pkgname::git+$url.git")
sha512sums=('SKIP')

pkgver() {
    cd "$srcdir"/$_pkgname
    git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {    
    # NOTE: uncomment this if you are in mainland china
    # export GOPROXY="https://goproxy.cn,direct"
    export CGO_ENABLED=1
    export GO111MODULE=on

    cd "$srcdir"/$_pkgname
    go build -v \
        -buildmode=pie -trimpath -mod=readonly -modcacherw \
        -tags "full" \
        -ldflags="-s -w -extldflags=-Wl,-z,now,-z,relro" \
        -o trojan-go .
    
    cd "$srcdir"/$_pkgname/docs/
    make hugo-build
}

package() {
    sed -i "10s/bin/lib\/trojan-go/g" "$srcdir"/$_pkgname/example/trojan-go{,@}.service
    
    install -Dm755 -t "$pkgdir"/usr/lib/trojan-go/      "$srcdir"/$_pkgname/trojan-go
    install -Dm644 -t "$pkgdir"/usr/lib/systemd/system/ "$srcdir"/$_pkgname/example/trojan-go{,@}.service
    install -Dm644 -t "$pkgdir"/etc/trojan-go/example/  "$srcdir"/$_pkgname/example/{client,server}.json
    
    mkdir -p "$pkgdir"/usr/bin
    ln -sf /usr/lib/trojan-go/trojan-go "$pkgdir"/usr/bin/trojan-go
    ln -sf /usr/lib/v2ray/geosite.dat   "$pkgdir"/usr/lib/trojan-go/geosite.dat
    ln -sf /usr/lib/v2ray/geoip.dat     "$pkgdir"/usr/lib/trojan-go/geoip.dat
    
    mkdir -p "$pkgdir"/usr/share/trojan-go
    ln -sf /usr/lib/v2ray/geosite.dat   "$pkgdir"/usr/share/trojan-go/geosite.dat
    ln -sf /usr/lib/v2ray/geoip.dat     "$pkgdir"/usr/share/trojan-go/geoip.dat
    
    mkdir -p "$pkgdir"/usr/share/doc/trojan-go
    cp -r "$srcdir"/$_pkgname/docs/public/* "$pkgdir"/usr/share/doc/trojan-go/
}
