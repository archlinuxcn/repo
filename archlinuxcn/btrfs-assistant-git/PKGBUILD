# Maintainer: Integral <integral@member.fsf.org>
# Contributor: dr460nf1r3 <dr460nf1r3 at garudalinux dot org>

pkgname=btrfs-assistant-git
_pkgname=${pkgname%-git}
pkgver=2.2.r2.g95e413d
pkgrel=1
pkgdesc="An application for managing BTRFS subvolumes and Snapper snapshots"
arch=('x86_64' 'aarch64')
url="https://gitlab.com/${_pkgname}/${_pkgname}"
license=('GPL-3.0-or-later')
depends=('qt6-base' 'qt6-svg' 'ttf-font' 'polkit' 'btrfs-progs' 'diffutils')
optdepends=('snapper' 'btrfsmaintenance')
makedepends=('git' 'cmake' 'qt6-tools')
conflicts=("${_pkgname}")
provides=("${_pkgname}")
backup=("etc/${_pkgname}.conf")
source=("git+${url}.git")
sha256sums=('SKIP')

pkgver() {
	cd "${_pkgname}/"
	git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
	cmake -B build -S "${_pkgname}/" -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=None
	cmake --build build
}

package() {
	DESTDIR="${pkgdir}" cmake --install build

	# License
	install -Dm644 "${_pkgname}/LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
