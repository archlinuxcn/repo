# Maintainer: Rocket Aaron <i at rocka dot me>
# Contributor: Jonathan Liu <net147@gmail.com>

pkgname=edid-decode-git
_gitname=v4l-utils
pkgver=1.28.0.r65.gc81300e1
pkgrel=1
pkgdesc="EDID decoder and conformance tester"
url="https://git.linuxtv.org/edid-decode.git/"
arch=('x86_64')
license=('MIT')
depends=('glibc' 'gcc-libs')
makedepends=('git' 'meson')
provides=('edid-decode')
source=('git+https://git.linuxtv.org/v4l-utils.git'
        'install-tag.patch')
sha256sums=('SKIP'
            '77dd420d8247af7464814887afdb459551769189f112992c1fabc75153c6a70a')

pkgver() {
  cd "$_gitname"
  git describe --long --tags | sed 's/v4l-utils-//g;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "$_gitname"
  git apply "$srcdir/install-tag.patch"
}

build() {
  arch-meson \
    -Dbpf=disabled \
    -Dqvidcap=disabled \
    -Ddoxygen-doc=disabled \
    -Dqv4l2=disabled \
    -Djpeg=disabled \
    "$srcdir/$_gitname" "$srcdir/build"
  meson compile -C "$srcdir/build" edid-decode
}

package() {
  meson install -C "$srcdir/build" --destdir "$pkgdir" --tags edid-decode
  install -Dm644 "$srcdir/$_gitname/utils/edid-decode/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
