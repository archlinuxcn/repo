# Maintainer: Rocket Aaron <i at rocka dot me>
# Contributor: Jonathan Liu <net147@gmail.com>

pkgname=edid-decode-git
_gitname=v4l-utils
pkgver=1.30.0.r26.gc655bd27
pkgrel=1
pkgdesc="EDID decoder and conformance tester"
url="https://git.linuxtv.org/v4l-utils.git/tree/utils/edid-decode"
arch=('x86_64')
license=('MIT')
depends=('glibc' 'gcc-libs')
makedepends=('git' 'meson')
provides=('edid-decode')
conflicts=('v4l-utils')
source=('git+https://git.linuxtv.org/v4l-utils.git'
        'install-tag.patch')
sha256sums=('SKIP'
            '9fde9b36b083d2b475394aac8c58bba691331136a83bb23b1fa59672bec41c56')

pkgver() {
  cd "$_gitname"
  git describe --long --tags | sed 's/v4l-utils-//g;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "$_gitname"
  git apply "$srcdir/install-tag.patch"
}

build() {
  arch-meson \
    -Dbpf=disabled \
    -Ddoxygen-doc=disabled \
    -Dqvidcap=disabled \
    -Dv4l2-tracer=disabled \
    -Dqv4l2=disabled \
    -Dgconv=disabled \
    -Djpeg=disabled \
    -Dlibdvbv5=disabled \
    "$srcdir/$_gitname" "$srcdir/build"
  meson compile -C "$srcdir/build" edid-decode
}

package() {
  meson install -C "$srcdir/build" --destdir "$pkgdir" --tags edid-decode
  install -Dm644 "$srcdir/$_gitname/utils/edid-decode/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
