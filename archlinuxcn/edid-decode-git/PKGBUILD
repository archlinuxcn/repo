# Maintainer: Rocket Aaron <i at rocka dot me>
# Contributor: Jonathan Liu <net147@gmail.com>

pkgname=edid-decode-git
_gitname=edid-decode
pkgver=r719.4bdd779
pkgrel=1
pkgdesc="EDID decoder and conformance tester"
url="https://git.linuxtv.org/edid-decode.git/"
arch=('x86_64')
license=('MIT')
depends=('glibc' 'gcc-libs')
makedepends=('git' 'meson')
provides=('edid-decode')
source=('git+https://git.linuxtv.org/edid-decode.git')
sha256sums=('SKIP')

pkgver() {
  cd "$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  arch-meson "$srcdir/$_gitname" "$srcdir/build"
  meson compile -C "$srcdir/build"
}

package() {
  meson install -C "$srcdir/build" --destdir "$pkgdir"
  install -Dm644 "$srcdir/$_gitname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
