# Maintainer: Kimiblock Moe
pkgname=zen-browser-portable
pkgver=1.14.5b
pkgrel=1
pkgdesc="Zen Browser sandboxed by portable"
arch=('any')
url="https://github.com/Kraftland/portable"
license=(GPL-3.0-or-later)
groups=()
options=(!debug !strip)

makedepends+=(git)

depends=(
	"portable"
)

optdepends=(
	"zen-browser-i18n-zh-cn: Language pack for Zen Browser (zh-CN)"
	"zen-browser-i18n-zh-tw: Language pack for Zen Browser (zh-TW)"
	"zen-browser-i18n-ja: Language pack for Zen Browser (ja)"
)

provides=("zen-browser=${pkgver}")
conflicts=(zen-browser)

makedepends+=(
	"zen-browser"
)

checkdepends=()

source=(
	portable-config
	start.sh
	desktop.file
	)

function prepare() {
	pacman -Ql zen-browser >file.list
}

function package() {
	while IFS= read -r line; do
		file="$(echo "$line" | awk '{print $2}')"
		if [[ -d ${file} ]]; then
			echo "Omitting Directory"
		else
			install -Dm755 "${file}" "${pkgdir}/${file}"
		fi
	done < file.list
	install -Dm755 \
		portable-config \
		"${pkgdir}/usr/lib/portable/info/org.mozilla.zen/config"
	rm "${pkgdir}/usr/share/applications"/*
	rm "${pkgdir}/usr/bin"/*
	install -Dm755 \
		"${srcdir}/start.sh" \
		"${pkgdir}/usr/bin/zen"
	install -Dm755 \
		"${srcdir}/start.sh" \
		"${pkgdir}/usr/bin/zen-browser"
	install -Dm644 \
		"${srcdir}/desktop.file" \
		"${pkgdir}/usr/share/applications/org.mozilla.zen.desktop"
	echo '''[Desktop Entry]
Type=Application
Name=Zen Browser
GenericName=Stub for MPRIS
Icon=zen-browser
TryExec=portable
Exec=env _portableConfig=org.mozilla.zen portable -- %u
Terminal=false
NoDisplay=true''' >"${pkgdir}/usr/share/applications/zen-browser.desktop"
}
sha256sums=('e79bba18a1cc0997cab92a1d6cb7d9c8c6c5263de5b1f350b2ab45b1e8ce5d75'
            'b1df66c4246e6584efef74f5c0ae74b7c51c32823bf28bd92a3ef3c938dcaa09'
            'b1366506d4df221581e67544edd00728b9793da1cb8609beec3cf138d4a75eaf')
