# contributor: zhuqin <zhuqin83@gmail.com>
# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=emacs-org-mode-git
pkgver=9.3.7.r638.g6882478ca
pkgrel=1
epoch=1
pkgdesc="Emacs Org Mode from git"
arch=('any')
url="http://orgmode.org/"
depends=('emacs')
makedepends=('git' 'texlive-core' 'perl' 'awk')
optdepends=('java-environment: for using ditaa.jar in the contrib directory'
            'zsh: for using dir2org.zsh in the contrib directory')
license=('GPL')
provides=("emacs-org-mode=$pkgver")
conflicts=('emacs-org-mode')
install=emacs-org-mode.install
source=(git+https://code.orgmode.org/bzg/org-mode.git)
md5sums=('SKIP')

pkgver() {
  cd org-mode

  git describe --tag | sed -e 's/^[^0-9]*//' -e 's/_/./g' -e 's/-/.r/' -e 's/-/./g'
}

build() {
  cd org-mode

  make
}

package () {
  cd org-mode

  make DESTDIR="$pkgdir" install
  mv "$pkgdir"/usr/share/info/org "$pkgdir"/usr/share/info/orgmode
  install -d "$pkgdir"/usr/share/emacs/site-lisp/org_contrib
  cp -r contrib/{lisp,scripts} "$pkgdir"/usr/share/emacs/site-lisp/org_contrib
}
