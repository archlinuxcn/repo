# Maintainer: Puqns67 <me@puqns67.icu>
# Contributor: Danct12 <danct12@disroot.org>

# This PKGBUILD is base on the AUR repo python-gbinder, by Danct12 <danct12@disroot.org>

pkgname='python-gbinder'
pkgver=1.1.2
pkgrel=3
pkgdesc='Python bindings for libgbinder'
arch=('i686' 'x86_64' 'armv7h' 'aarch64')
url='https://github.com/erfanoabdi/gbinder-python'
_reponame="${url##*/}"
license=('GPL-3.0-or-later')
depends=('libgbinder')
makedepends=('cython' 'python' 'python-setuptools')
source=("${_reponame}-${pkgver}.tar.gz"::"${url}/archive/refs/tags/${pkgver}.tar.gz")
sha512sums=('6f076a5cb265eed193d2ac2623921e76ced923230ca2131460efea941182f26b770d657f5155f6a29fa54a314f6dc62a8d5b96d14ee90a359389e60318a38b71')

build() {
  cd "${srcdir}/${_reponame}-${pkgver}"
  python setup.py build --cython
}

package() {
  cd "${srcdir}/${_reponame}-${pkgver}"
  python setup.py install --prefix='/usr' --root="${pkgdir}"
}
