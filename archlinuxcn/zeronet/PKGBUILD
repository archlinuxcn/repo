# Maintainer: Sakuraba Amane <tobiichiamane@archlinuxcn.org>

pkgname='zeronet'
pkgver=0.7.1
pkgrel=2
arch=('any')
url="https://zeronet.io/"
depends=('python'
         'python-gevent'
         'python-msgpack')
optdepends=('tor: anonymity')
license=('GPL2')
pkgdesc="Decentralized websites using Bitcoin crypto and the BitTorrent network."
source=("https://github.com/HelloZeroNet/ZeroNet-linux/archive/dist-linux64/ZeroNet-py3-linux64.tar.gz"
        "zeronet.conf"
        "zeronet.service")
sha256sums=('4173c2ee78d53cf83211f3ef81811083ef90c44aa048a5faad4009d9bb99686a'
            'ea735e82dbb10a2c1fda7abfeb2f38c2429044d8254f9e2396c50cecb6f778f8'
            '1952e99997cb081f05657563e147143e6c7408195d5fe984b726e3bb9a8675df')
install="zeronet.install"
backup=("etc/zeronet.conf")
options=(!strip) # ignore test binaries in the depsendency libs that fail strip

# Upstream uses camel case
_pkgarchive="ZeroNet-linux-dist-linux64"

package() {
   mkdir -p "$pkgdir/opt/zeronet"

   # There is no setup.py shipped, so brute-force copy
   cp -a "$srcdir/$_pkgarchive/." "$pkgdir/opt/zeronet/"

   install -D -m644 "$srcdir/zeronet.conf" "$pkgdir/etc/zeronet.conf"
   install -D -m644 "$srcdir/zeronet.service" "$pkgdir/usr/lib/systemd/system/zeronet.service"

   install -D -m644 "$srcdir/$_pkgarchive/core/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
