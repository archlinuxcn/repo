# Maintainer: Rocket Aaron <i at rocka dot me>

_repo=uswitch
pkgname=plasma6-applets-uswitch
# TODO remove this when new version releases
_ref=c10cb79d1c1a1e1983e1cda62f6284b827fbabc8
pkgver=1.3.0
pkgrel=4
pkgdesc='Modified version of User Switch plasmoid'
url="https://gitlab.com/divinae/uswitch"
license=('GPL-3.0-only')
arch=('x86_64')
depends=('plasma-workspace')
makedepends=('cmake' 'extra-cmake-modules')
source=("$_repo-$pkgver.tar.gz::https://gitlab.com/divinae/$_repo/-/archive/$_ref/archive.tar.gz"
        "kde-install-dirs.patch")
sha256sums=('04694d8b807a5d722d823f877fe720b93437513c016051ac7d8963c00c50fc9a'
            'd55ac2ce5bd6f352335ce2c3e158c833fb199bf6087b3cf325143cc2dd7eddee')

prepare(){
  cd "${srcdir}/${_repo}-${_ref}"
  patch -Np1 -i "${srcdir}/kde-install-dirs.patch"
}

build() {
  local _src="$_repo-$_ref"
  cmake -S "$_src" -B build \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
