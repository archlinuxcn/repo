# Maintainer: Yichao Yu <yyc1992@gmail.com>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>

USE_PYPY=1

pkgname=pypy3
pkgver=2.4.0
pkgrel=1
pkgdesc="A Python3 implementation written in Python, JIT enabled"
url="http://pypy.org"
arch=('i686' 'x86_64')
depends=('libffi' 'openssl' 'expat' 'ncurses' 'zlib' 'bzip2')
# Force cpython2 to be installed for mercurial
makedepends=('python2>=2.7' 'mercurial' 'gdbm' 'sqlite' 'tcl' 'tk')
if ((USE_PYPY)); then
    makedepends+=('pypy')
fi
provides=()
optdepends=('sqlite: sqlite3 module'
            'gdbm: dbm module'
            'tk: tkinter module'
            'tcl: tkinter module'
            'pypy3-readline: better readline module')
options=('debug' 'strip' '!emptydirs')
license=('custom:MIT')
source=("https://bitbucket.org/pypy/pypy/downloads/$pkgname-$pkgver-src.tar.bz2")
md5sums=('96ba72916114d16904e12562b5d84e51')

_check_python() {
  _PYTHON=python2
  if ((USE_PYPY)) && which pypy &> /dev/null; then
      _PYTHON=pypy
  fi
}

build() {
  cd ${pkgname}-${pkgver}-src/pypy/goal

  _check_python

  CFLAGS+=' -O3 -g'
  export CFLAGS

  $_PYTHON ../../rpython/bin/rpython -Ojit --shared --output=pypy3-c \
           targetpypystandalone
}

package_pypy3() {
  cd "${srcdir}"
  mkdir -p licenses/{libbz2-dev,openssl}
  ln -sf /usr/share/licenses/bzip2/LICENSE licenses/libbz2-dev/copyright
  ln -sf /usr/share/licenses/openssl/LICENSE licenses/openssl/copyright

  cd ${pkgname}-${pkgver}-src/pypy/tool/release

  _check_python

  CFLAGS+=' -O3 -g'
  export CFLAGS

  LD_LIBRARY_PATH="${LD_LIBRARY_PATH}:../../goal/" $_PYTHON package.py \
                 --license_base "${srcdir}"/licenses --override_pypy_c \
                 ../../goal/pypy3-c ../../../ pypy3 pypy3-c \
                 "${srcdir}"/${pkgname}.tar.bz2

  mkdir -p "${pkgdir}"/opt
  tar x -C "${pkgdir}"/opt -f "${srcdir}"/${pkgname}.tar.bz2

  mkdir -p "${pkgdir}"/opt/pypy3/lib
  cp ../../goal/libpypy3-c.so "${pkgdir}"/opt/pypy3/lib/
  mkdir -p "${pkgdir}"/usr/lib
  ln -s /opt/pypy3/lib/libpypy3-c.so "${pkgdir}"/usr/lib/libpypy3-c.so

  mkdir -p "${pkgdir}"/usr/bin
  ln -s /opt/pypy3/bin/pypy3-c "${pkgdir}"/usr/bin/pypy3

  install -Dm644 "${pkgdir}"/opt/pypy3/LICENSE \
          "${pkgdir}"/usr/share/licenses/pypy3/LICENSE

  export LD_LIBRARY_PATH
  LD_LIBRARY_PATH="${pkgdir}/usr/lib:${pkgdir}/opt/pypy3/lib:${LD_LIBRARY_PATH}"

  # Generate bytecode
  # The `|| true` is necessary so that the test compilation that are expected
  # to fail does not break the build.
  "${pkgdir}"/opt/pypy3/bin/pypy3-c -m compileall "${pkgdir}"/opt/pypy3 || true
  # "${pkgdir}"/opt/pypy3/bin/pypy3-c -O \
  #            -m compileall "${pkgdir}"/opt/pypy3 || true

  pypyabi=$("${pkgdir}"/opt/pypy3/bin/pypy3-c -c 'import sys; ver = sys.pypy_version_info; print("pypy3-%s%s" % (ver[0], ver[1]))')
  provides+=("${pypyabi}")
}
