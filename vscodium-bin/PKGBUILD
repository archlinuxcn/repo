# Maintainer: Plague-doctor <plague <at>> privacyrequired <<dot>> com >

_pkgname=vscodium
pkgname=$_pkgname-bin
pkgver=1.30.2
pkgrel=3
pkgdesc="Binary releases of VS Code without MS branding/telemetry/licensing."
arch=('x86_64')
url="https://github.com/VSCodium/vscodium"
license=('MIT')
depends=(
        fontconfig libxtst gtk3 python cairo alsa-lib nss gcc-libs libnotify libxss gconf
        'glibc>=2.28-4'
        )
optdepends=(
        'gvfs: For move to trash functionality'
        'libdbusmenu-glib: For KDE global menu'
)
provides=('code')
conflicts=('visual-studio-code-bin' 'code')
replaces=($_pkgname)
source=(
        vscodium.desktop
        ${_pkgname}-${pkgver}-${pkgrel}.tar.gz::${url}/releases/download/${pkgver}/VSCode-linux-x64-${pkgver}.tar.gz
       )


sha256sums=('e4f3503d6c6eb9d967f9d35e58f5f801da98c4ccf7bd31dc752d1cef05781717'
            '081783f0c186129040b6741a4aa64da27127ed9040b1d82db22706b19fd32ea9')


package() {
    install -d -m755 ${pkgdir}/usr/bin
    install -d -m755 ${pkgdir}/usr/share/{vscodium,applications,pixmaps}
    # copy over all extracted files
    cp -Pr ${srcdir}/* ${pkgdir}/usr/share/vscodium
    # remove extra source symlinks
    find ${pkgdir}/usr/share/vscodium/ -type l -delete
    ln -s /usr/share/${_pkgname}/bin/vscodium ${pkgdir}/usr/bin/${_pkgname}
    install -D -m644 vscodium.desktop ${pkgdir}/usr/share/applications/vscodium.desktop
    install -D -m644 ${srcdir}/resources/app/resources/linux/code.png \
            ${pkgdir}/usr/share/pixmaps/vscodium.png
}
