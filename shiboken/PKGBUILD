# Maintainer: Hugo Osvaldo Barrera <hugo@osvaldobarrera.com.ar>
# Contributor: Matthias Maennich <arch@maennich.net>
pkgname=shiboken
pkgver=1.2.2
pkgrel=1
pkgdesc="Support library for Python bindings"
arch=('i686' 'x86_64')
license=('LGPL')
url="http://www.pyside.org"
depends=("python2" "python" "qt4>=4.8" 'libxslt')
optdepends=("python-sphinx: Documentation")
makedepends=('cmake')
conflicts=("libshiboken")
provides=("libshiboken" "shiboken")
source=("http://download.qt-project.org/official_releases/pyside/$pkgname-$pkgver.tar.bz2")

build(){
    cd $srcdir/shiboken-$pkgver
    mkdir -p build_python2 && cd build_python2
    cmake ../ -DCMAKE_INSTALL_PREFIX=/usr  \
              -DCMAKE_BUILD_TYPE=Release   \
              -DBUILD_TESTS=OFF            \
              -DPYTHON_EXECUTABLE=/usr/bin/python2 \
              -DPYTHON_LIBRARY=/usr/lib/libpython2.7.so \
              -DPYTHON_INCLUDE_DIR=/usr/include/python2.7 \
              -DQT_QMAKE_EXECUTABLE=qmake4
    make

    cd $srcdir/shiboken-$pkgver
    mkdir -p build && cd build
    cmake ../ -DCMAKE_INSTALL_PREFIX=/usr  \
              -DCMAKE_BUILD_TYPE=Release   \
              -DBUILD_TESTS=OFF            \
              -DUSE_PYTHON3=yes            \
              -DQT_QMAKE_EXECUTABLE=qmake4
    make
}

package(){
    cd $srcdir/shiboken-$pkgver/build
    make DESTDIR=$pkgdir install
    _pyverflags=$(python -c 'import sysconfig; print(sysconfig.get_config_var("SOABI"))')

    cd generator
    install -D -m 755 ../data/ShibokenConfig.cmake "$pkgdir/usr/lib/cmake/Shiboken-$pkgver"
    install -D -m 755 ../data/ShibokenConfigVersion.cmake "$pkgdir/usr/lib/cmake/Shiboken-$pkgver/"
    install -D -m 755 ../data/ShibokenConfig.${_pyverflags}.cmake "$pkgdir/usr/lib/cmake/Shiboken-$pkgver/"
    install -D -m 755 ../data/shiboken.pc $pkgdir/usr/lib/pkgconfig/

    cd $srcdir/shiboken-$pkgver/build_python2
    make DESTDIR=$pkgdir install

    cd generator
    install -D -m 755 ../data/ShibokenConfig-python2.7.cmake "$pkgdir/usr/lib/cmake/Shiboken-$pkgver/"
}

md5sums=('9f5bee9d414ce51be07ff7a20054a48d')
