# Maintainer: Stephen Zhang <zsrkmyn at gmail dot com>
pkgname=nvchecker-git
pkgver=v0.3.r56.g9de0eb2
pkgrel=1
pkgdesc="a tool for checking if a new version of some software has been released."
arch=('any')
url="https://github.com/lilydjwg/nvchecker"
license=('MIT')
depends=('git' 'mercurial' 'python' 'python-tornado')
makedepends=('python-setuptools')
optdepends=('python-pycurl: better HTTP client support')
source=('git://github.com/lilydjwg/nvchecker.git')
md5sums=('SKIP')

_gitroot=nvchecker

pkgver() {
  cd "$srcdir/$_gitroot"
  git describe --long | sed -r 's/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  cd $srcdir/$_gitroot
  python3 setup.py build
}

package() {
  cd $srcdir/$_gitroot
  python3 setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

# vim:set ts=2 sw=2 et:
