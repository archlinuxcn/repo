# Maintainer: Yichao Yu <yyc1992@gmail.com>
# Contributor: Felix Yan <felixonmars@gmail.com>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>

_pypyname=pypy-hg
_pypyabi=26
_pypy3name=pypy3-hg
_pypy3abi=25

pkgbase=${_pypyname}-pytools
pkgname=(${_pypy3name}-pytools ${_pypyname}-pytools)
pkgver=2014.3.1
pkgrel=3
pkgdesc="A collection of tools for Python"
arch=('any')
url="http://mathema.tician.de/software/pytools"
license=('MIT')
makedepends=(pypy3-${_pypy3abi} pypy-setuptools pypy3-${_pypy3abi}-setuptools)
source=(http://pypi.python.org/packages/source/p/pytools/pytools-${pkgver}.tar.gz)
sha512sums=('2bcd02c50ef389dae7cc98c0a16cd16b6a13ab71200304c8a97a816bf6385b616ff4fc15a7a2172450ea6c56895ec220929a2296318814e097c264c92833e58d')

build() {
  cp -a pytools-${pkgver}{,-python2}

  (cd pytools-${pkgver}
   pypy3 setup.py build)

  (cd pytools-${pkgver}-python2
   pypy setup.py build)
}

package_pypy-hg-pytools() {
  depends=(pypy pypy-decorator)
  provides=(pypy-pytools=${pkgver}-${pkgrel})
  conflicts=(pypy-pytools)
  cd pytools-${pkgver}-python2

  pypy setup.py install --root="${pkgdir}" --skip-build --optimize=1
}

package_pypy3-hg-pytools(){
  depends=(pypy3-${_pypy3abi} pypy3-${_pypy3abi}-decorator)
  provides=(pypy3-${_pypy3abi}-pytools=${pkgver}-${pkgrel}
            pypy3-pytools=${pkgver}-${pkgrel})
  conflicts=(pypy3-pytools)
  cd pytools-${pkgver}

  pypy3 setup.py install --root="${pkgdir}" --skip-build --optimize=1
}
