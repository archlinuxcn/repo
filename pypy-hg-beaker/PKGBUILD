# Maintainer: Yichao Yu <yyc1992@gmail.com>

_pypyname=pypy-hg
_pypyabi=26
_pypy3name=pypy3-hg
_pypy3abi=25

pkgbase=${_pypyname}-beaker
pkgname=(${_pypy3name}-beaker ${_pypyname}-beaker)
pkgver=1.6.4
pkgrel=7
arch=('any')
license=('custom')
pkgdesc="Caching and sessions WSGI middleware for use with web applications and stand-alone Python scripts and applications, pypy version."
url="http://beaker.groovie.org/"
makedepends=(pypy-setuptools pypy3-${_pypy3abi}-setuptools sqlite
             pypy3-${_pypy3abi})
source=("http://cheeseshop.python.org/packages/source/B/Beaker/Beaker-${pkgver}.tar.gz")
md5sums=('c2e102870ed4c53104dec48ceadf8e9d')

build() {
  cp -r Beaker-${pkgver} python2-Beaker-${pkgver}

  (cd Beaker-${pkgver}
   sed -i "s#/usr/bin/python#/usr/bin/pypy3#" beaker/crypto/pbkdf2.py
   pypy3 setup.py build)

  (cd python2-Beaker-${pkgver}
   sed -i "s#/usr/bin/python#/usr/bin/pypy#" beaker/crypto/pbkdf2.py
   pypy setup.py build)
}

package_pypy3-hg-beaker() {
  depends=(pypy3-${_pypy3abi})
  provides=(pypy3-beaker=${pkgver}
            pypy3-${_pypy3abi}-beaker=${pkgver})
  conflicts=(pypy3-beaker)
  cd Beaker-${pkgver}

  pypy3 setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/pypy3-beaker/LICENSE"
}

package_pypy-hg-beaker() {
  depends=(pypy)
  provides=(pypy-beaker=${pkgver})
  conflicts=(pypy-beaker)
  cd python2-Beaker-${pkgver}

  pypy setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/pypy-beaker/LICENSE"
}
