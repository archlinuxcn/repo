# $Id: PKGBUILD 194676 2013-09-21 18:35:37Z eric $
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=imagemagick-fftw
pkgver=6.8.6.10
pkgrel=1
arch=('i686' 'x86_64')
url="http://www.imagemagick.org/"
license=('custom')
makedepends=('libltdl' 'lcms2' 'libxt' 'fontconfig' 'libxext' 'ghostscript' \
             'openexr' 'libwmf' 'librsvg' 'libxml2' 'jasper' 'liblqr' \
             'opencl-headers' 'libcl' 'libwebp' 'fftw')
#source=(http://www.imagemagick.org/download/ImageMagick-${pkgver%.*}-${pkgver##*.}.tar.xz \
source=(ftp://ftp.sunet.se/pub/multimedia/graphics/ImageMagick/ImageMagick-${pkgver%.*}-${pkgver##*.}.tar.xz \
        perlmagick.rpath.patch)
sha1sums=('807380b2221b5fed789bc895e8e4c3758237be9a'
          'e143cf9d530fabf3b58023899b5cc544ba93daec')

prepare() {
  cd ImageMagick-${pkgver%.*}-${pkgver##*.}
  sed '/AC_PATH_XTRA/d' -i configure.ac
  autoreconf --force --install
  patch -p0 -i "${srcdir}/perlmagick.rpath.patch"
}

build() {
  cd ImageMagick-${pkgver%.*}-${pkgver##*.}
  ./configure --prefix=/usr --sysconfdir=/etc --with-modules --disable-static \
    --enable-hdri --with-wmf --with-openexr --with-xml --with-lcms2 --with-jp2 \
    --with-webp --with-gslib --with-gs-font-dir=/usr/share/fonts/Type1 \
    --with-perl --with-perl-options="INSTALLDIRS=vendor" --with-lqr --with-rsvg \
    --enable-opencl --without-gvc --without-djvu --without-autotrace \
    --without-jbig --without-fpx --without-dps --with-fftw
 make
}

check() {
  cd ImageMagick-${pkgver%.*}-${pkgver##*.}
 # make check
}

package() {
  pkgdesc="An image viewing/manipulation program"
  depends=('perl' 'libltdl' 'lcms2' 'libxt' 'fontconfig' 'libxext' 'liblqr' 'libcl' 'fftw')
  optdepends=('ghostscript: for Ghostscript support'
              'openexr: for OpenEXR support'
              'libwmf: for WMF support'
              'librsvg: for SVG support'
              'libxml2: for XML support'
              'jasper: for JPEG-2000 support'
              'libpng: for PNG support'
	      'libwebp: for WEBP support')
  backup=("etc/ImageMagick-${pkgver%%.*}/coder.xml"
          "etc/ImageMagick-${pkgver%%.*}/colors.xml"
          "etc/ImageMagick-${pkgver%%.*}/delegates.xml"
          "etc/ImageMagick-${pkgver%%.*}/log.xml"
          "etc/ImageMagick-${pkgver%%.*}/magic.xml"
          "etc/ImageMagick-${pkgver%%.*}/mime.xml"
          "etc/ImageMagick-${pkgver%%.*}/policy.xml"
          "etc/ImageMagick-${pkgver%%.*}/quantization-table.xml"
          "etc/ImageMagick-${pkgver%%.*}/thresholds.xml"
          "etc/ImageMagick-${pkgver%%.*}/type.xml"
          "etc/ImageMagick-${pkgver%%.*}/type-dejavu.xml"
          "etc/ImageMagick-${pkgver%%.*}/type-ghostscript.xml"
          "etc/ImageMagick-${pkgver%%.*}/type-windows.xml")
  options=('!docs' 'libtool' '!emptydirs')
  provides=("imagemagick=$pkgver")
  conflicts=('imagemagick')

  cd ImageMagick-${pkgver%.*}-${pkgver##*.}
  make -j1 DESTDIR="${pkgdir}" install
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/imagemagick/LICENSE"
  install -Dm644 NOTICE "${pkgdir}/usr/share/licenses/imagemagick/NOTICE"

#Cleaning
  rm -f "${pkgdir}"/usr/lib/*.la
}
