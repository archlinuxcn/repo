# Maintainer: Yichao Yu <yyc1992@gmail.com>

_pypyname=pypy-hg
_pypyabi=26
_pypy3name=pypy3-hg
_pypy3abi=25

pkgbase=${_pypyname}-mako
pkgname=(${_pypy3name}-mako ${_pypyname}-mako)
pkgver=1.0.0
pkgrel=4
pkgdesc="Hyperfast and lightweight templating for the Python platform"
arch=('any')
url="http://www.makotemplates.org/"
license=('MIT')
makedepends=(pypy-setuptools pypy3-${_pypy3abi}-setuptools
             'python>=3.4' # for 2to3
             pypy-markupsafe pypy3-${_pypy3abi}-markupsafe
             pypy-beaker pypy3-${_pypy3abi}-beaker
             pypy3-${_pypy3abi})
source=(https://pypi.python.org/packages/source/M/Mako/Mako-$pkgver.tar.gz{,.asc})
sha1sums=('580b3a8043833e3c3340d4b661a33f6ccc6a35d5'
          'SKIP')
options=('!emptydirs')

prepare() {
  cp -r Mako-$pkgver python2-Mako-$pkgver
}

build() {
  (cd Mako-$pkgver
   pypy3 setup.py build)

  (cd python2-Mako-$pkgver
   pypy setup.py build)
}

package_pypy3-hg-mako() {
  depends=(pypy3-${_pypy3abi} pypy3-${_pypy3abi}-markupsafe
           pypy3-${_pypy3abi}-beaker)
  provides=(pypy3-mako=${pkgver}
            pypy3-${_pypy3abi}-mako=${pkgver})
  conflicts=(pypy3-mako)
  cd Mako-$pkgver

  pypy3 setup.py install --skip-build --root="$pkgdir" --optimize=1
  install -D LICENSE "$pkgdir/usr/share/licenses/pypy3-mako/COPYING"
  install -m755 -d "${pkgdir}/usr/bin/"
  mv "${pkgdir}"/opt/pypy3/bin/mako-render \
     "${pkgdir}/usr/bin/pypy3-mako-render"
}

package_pypy-hg-mako() {
  depends=(pypy-markupsafe pypy-beaker pypy)
  provides=(pypy-mako=${pkgver})
  conflicts=(pypy-mako)
  cd python2-Mako-$pkgver

  pypy setup.py install --skip-build --root="$pkgdir" --optimize=1
  install -D LICENSE "$pkgdir/usr/share/licenses/pypy-mako/COPYING"
  install -m755 -d "${pkgdir}/usr/bin/"
  mv "${pkgdir}"/opt/pypy/bin/mako-render \
     "${pkgdir}/usr/bin/pypy-mako-render"
}
