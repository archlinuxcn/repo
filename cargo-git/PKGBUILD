# Maintainer: Daniel Micay <danielmicay@gmail.com>
_pkgname=cargo
pkgname=cargo-git
pkgver=r2452.9780093
pkgrel=1
pkgdesc='Rust package manager'
arch=('i686' 'x86_64')
url="http://crates.io/"
license=('MIT' 'Apache')
makedepends=('cmake' 'python')
depends=('git' 'rust')
provides=('cargo')
conflicts=('cargo')
source=('git+https://github.com/rust-lang/cargo.git'
        'git+https://github.com/rust-lang/rust-installer.git')
md5sums=('SKIP'
         'SKIP')

pkgver() {
  cd cargo
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd cargo
  git submodule init
  git config submodule.src/rust-installer.url "$srcdir/rust-installer"
  git submodule update
}

build() {
  cd cargo
  ./configure --prefix=/usr --disable-verify-install --enable-optimize
  make
}

package() {
  cd cargo
  make DESTDIR="$pkgdir/" install
  mv "$pkgdir/usr/etc" "$pkgdir"
  install -D -m 644 LICENSE-MIT "$pkgdir/usr/share/licenses/$pkgname/LICENSE-MIT"
  cd "$pkgdir/usr/lib"
  rm rustlib/{install.log,uninstall.sh}
}
