# Maintainer: lilydjwg <lilydjwg@gmail.com>
# Maintainer: Oliver Mangold <o.mangold at gmail dot com>
pkgname=bedup-git
pkgver=0.10.0.2.g7283da4
pkgrel=4
pkgdesc="Offline deduplication utility for the BTRFS filesystem"
arch=('i686' 'x86_64')
url="https://github.com/g2p/bedup"
source=("git+$url")
md5sums=('SKIP')
license=('GPL')
depends=('python' 'python-setuptools' 'python-alembic' 'python-cffi'
         'python-sqlalchemy' 'python-xdg' 'python-mako')
makedepends=('gcc' 'btrfs-progs' 'git')
conflicts=('bedup')
provides=('bedup')
epoch=1
_repo_name=bedup

pkgver() {
  cd "$srcdir/$_repo_name"
  git describe --tags | sed -e 's/v//' -e 's/-/./g'
}

prepare() {
  cd "$srcdir/$_repo_name"
  git reset --hard
}

build() {
  cd "$srcdir/$_repo_name"
  python3 setup.py build
}

package() {
  cd "$srcdir/$_repo_name"
  python3 setup.py install --root "${pkgdir}" --optimize=1
}
