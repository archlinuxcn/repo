# Maintainer: lilydjwg <lilydjwg@gmail.com>
# Maintainer: Oliver Mangold <o.mangold at gmail dot com>
pkgname=bedup-git
pkgver=0.9.0.34.g3c4381a
pkgrel=1
pkgdesc="Offline deduplication utility for the BTRFS filesystem"
arch=('i686' 'x86_64')
url="https://github.com/g2p/bedup"
source=('bedup-contextlib2.patch'
        "git+$url")
md5sums=('0603c504dd948caf814ce3f74c2f18b1'
         'SKIP')
license=('GPL')
depends=('python' 'python-setuptools' 'python-alembic' 'python-cffi'
         'python-sqlalchemy' 'python-xdg' 'python-mako')
makedepends=('gcc' 'btrfs-progs' 'git')
conflicts=('bedup')
provides=('bedup')
epoch=1
_repo_name=bedup

pkgver() {
  cd "$srcdir/$_repo_name"
  git describe --tags | sed -e 's/v//' -e 's/-/./g'
}

prepare() {
  cd "$srcdir/$_repo_name"
  git reset --hard
  git apply ../bedup-contextlib2.patch
  sed -i '/contextlib2/d' setup.py
}

build() {
  cd "$srcdir/$_repo_name"
  python3 setup.py build
}

package() {
  cd "$srcdir/$_repo_name"
  python3 setup.py install --root "${pkgdir}" --optimize=1
}
