# $Id: PKGBUILD 108505 2014-03-27 14:48:31Z fyan $
# Maintainer: Yichao Yu <yyc1992@gmail.com>
# Contributor: Felix Yan <felixonmars@gmail.com>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>
# Contributor: Peter Garceau <RockyChimp@gmail.com>

_pypyname=pypy-hg
_pypyabi=26
_pypy3name=pypy3-hg
_pypy3abi=25

pkgbase=${_pypyname}-sympy
pkgname=(${_pypy3name}-sympy ${_pypyname}-sympy)
pkgver=0.7.5
pkgrel=6
arch=('any')
pkgdesc='Symbolic manipulation package (Computer Algebra System), written in pure Python'
url='http://sympy.org/en/index.html'
license=('BSD')
makedepends=(pypy pypy3-${_pypy3abi})
source=("https://github.com/sympy/sympy/releases/download/sympy-${pkgver}/sympy-${pkgver}.tar.gz")
md5sums=('7de1adb49972a15a3dd975e879a2bea9')

prepare() {
  cp -r sympy-${pkgver} py3-sympy-${pkgver}

  sed -i -e 's|#!/usr/bin/env python|#!/usr/bin/env pypy|' \
      -e 's|#!/usr/bin/python|#!/usr/bin/pypy|' \
      sympy-${pkgver}/sympy/mpmath/tests/{runtests.py,test_eigen.py,test_levin.py,test_eigen_symmetric.py} \
      sympy-${pkgver}/sympy/mpmath/matrices/{eigen.py,eigen_symmetric.py} \
      sympy-${pkgver}/sympy/utilities/tests/diagnose_imports.py

  sed -i -e 's|#!/usr/bin/env python|#!/usr/bin/env pypy3|' \
      -e 's|#!/usr/bin/python|#!/usr/bin/pypy3|' \
      py3-sympy-${pkgver}/sympy/mpmath/tests/{runtests.py,test_eigen.py,test_levin.py,test_eigen_symmetric.py} \
      py3-sympy-${pkgver}/sympy/mpmath/matrices/{eigen.py,eigen_symmetric.py} \
      py3-sympy-${pkgver}/sympy/utilities/tests/diagnose_imports.py
}

build() {
  (cd sympy-${pkgver}
   pypy setup.py build)

  (cd py3-sympy-${pkgver}
   pypy3 setup.py build)
}

package_pypy-hg-sympy() {
  depends=(pypy)
  conflicts=(pypy-sympy)
  optdepends=("${_pypyname}-pyglet: plotting"
              "${_pypyname}-ipython: user friendly interface for isympy")

  cd sympy-${pkgver}

  pypy setup.py install --skip-build --root "${pkgdir}" --optimize=1

  install -m755 -d "${pkgdir}/usr"
  mv "$pkgdir/opt/pypy/"{share,bin} "$pkgdir/usr/"

  # rename files that exists in both 'python2-sympy' and 'python-sympy'
  mv "${pkgdir}"/usr/bin/isympy{,-pypy}
  mv "${pkgdir}"/usr/share/man/man1/isympy{,-pypy}.1

  install -D -m644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

package_pypy3-hg-sympy() {
  depends=(pypy3-${_pypy3abi})
  conflicts=(pypy3-sympy)
  provides=(pypy3-${_pypy3abi}-sympy=${pkgver}-${pkgrel}
            pypy3-sympy=${pkgver}-${pkgrel})
  optdepends=("${_pypy3name}-ipython: user friendly interface for isympy")

  cd py3-sympy-${pkgver}

  pypy3 setup.py install --skip-build --root "${pkgdir}" --optimize=1

  install -m755 -d "${pkgdir}/usr"
  mv "$pkgdir/opt/pypy3/"{share,bin} "$pkgdir/usr/"

  # rename files that exists in both 'python2-sympy' and 'python-sympy'
  mv "${pkgdir}"/usr/bin/isympy{,-pypy3}
  mv "${pkgdir}"/usr/share/man/man1/isympy{,-pypy3}.1

  install -D -m644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
