# Maintainer: Yichao Yu <yyc1992@gmail.com>

_pypyname=pypy-hg
_pypyabi=26
_pypy3name=pypy3-hg
_pypy3abi=25

_libname=certifi
pkgbase=${_pypyname}-${_libname}
pkgname=(${_pypy3name}-${_libname} ${_pypyname}-${_libname})
pkgver=14.05.14
pkgrel=4
pkgdesc="Mozilla's SSL Certs"
arch=(any)
url="http://pypi.python.org/pypi/certifi"
license=('GPL')
makedepends=(pypy3-${_pypy3abi}-setuptools pypy-setuptools pypy3-${_pypy3abi})
source=("http://pypi.python.org/packages/source/${_libname:0:1}/$_libname/$_libname-$pkgver.tar.gz")
sha512sums=('5edba0cc432c27212e31085fb3c2f2ded6fa177d716116111517c25b980d9fc206a9a6d47e2c001762e951ff50203b269d0eb245a039df20188b1136f3b700b2')

prepare() {
  cp -a $_libname-$pkgver{,-py2}

  (cd $_libname-$pkgver-py2
   sed -i '1s|python$|pypy|' certifi/core.py)

  (cd $_libname-$pkgver
   sed -i '1s|python$|pypy3|' certifi/core.py)
}

build() {
  (cd $_libname-$pkgver
   pypy3 setup.py build)

  (cd $_libname-$pkgver-py2
   pypy setup.py build)
}

package_pypy3-hg-certifi() {
  depends=(pypy3-${_pypy3abi})
  provides=(pypy3-${_libname}=${pkgver}
            pypy3-${_pypy3abi}-${_libname}=${pkgver})
  conflicts=(pypy3-${_libname})
  cd $_libname-$pkgver

  pypy3 setup.py install --skip-build -O1 --root="$pkgdir"
  install -m0644 -D "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_pypy-hg-certifi() {
  depends=(pypy)
  provides=(pypy-${_libname}=${pkgver})
  conflicts=(pypy-${_libname})
  cd $_libname-$pkgver-py2

  pypy setup.py install --skip-build -O1 --root="$pkgdir"
  install -m0644 -D "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
