# Maintainer: Daniel Micay <danielmicay@gmail.com>
# Contributor: Gordin <9ordin @t gmail dot com>
pkgname=android-sdk
pkgver=r22.6.3
pkgrel=2
pkgdesc='Google Android SDK'
arch=('i686' 'x86_64')
url="http://developer.android.com/sdk/index.html"
license=('custom')
depends=('java-environment' 'alsa-lib' 'openal' 'libstdc++5' 'libxv'
         'ncurses' 'sdl' 'swt' 'zlib')
if [[ $CARCH = x86_64 ]]; then
  depends=('java-environment' 'lib32-alsa-lib' 'lib32-openal' 'lib32-libstdc++5'
           'lib32-libxv' 'lib32-mesa' 'lib32-ncurses' 'lib32-sdl' 'lib32-zlib' 'swt')
fi
install=$pkgname.install
optdepends=('android-udev: udev rules for Android devices'
            'android-sdk-platform-tools: adb, aapt, aidl, dexdump and dx')
source=(https://dl-ssl.google.com/android/repository/tools_$pkgver-linux.zip
        $pkgname.png
        $pkgname.desktop
        $pkgname.sh
        $pkgname.csh
        $pkgname.conf
        license.html)
sha1sums=('3410a13c9eb7d1667e9a735bfecb0a7e2944087a'
          'c249134310ae91dccce1ad839b3985211674ab71'
          'e834f53e13d5926ec7be3fd775fa80aa6f328eb6'
          '78f8574e651c9bf8b7515ecb30c7ef93edbc4a96'
          '08c85aab7523e22b298891c7047bc0e7adbf3437'
          '145bdf3eb41a56574b289c1577a24bc47097ec83'
          'bfb91be7e0b602d765b7a1fcaf0ce1b7e1a93faa')

package() {
  cd "$pkgdir"
  install -Dm755 "$srcdir/$pkgname.sh" etc/profile.d/$pkgname.sh
  install -Dm755 "$srcdir/$pkgname.csh" etc/profile.d/$pkgname.csh
  install -Dm644 "$srcdir/$pkgname.conf" etc/ld.so.conf.d/$pkgname.conf
  mkdir -p opt/$pkgname
  mkdir opt/$pkgname/platforms opt/$pkgname/add-ons
  cp -a "$srcdir/tools" opt/$pkgname
  install -Dm644 "$srcdir/$pkgname.png" usr/share/pixmaps/$pkgname.png
  install -Dm644 "$srcdir/$pkgname.desktop" \
    usr/share/applications/$pkgname.desktop
  install -Dm644 "$srcdir/license.html" \
    usr/share/licenses/$pkgname/license.html

  if [[ $CARCH = i686 ]]; then
    rm -rf ${pkgdir}/opt/android-sdk/tools/lib/{monitor-,}x86_64 \
      ${pkgdir}/opt/android-sdk/tools/lib/lib64* \
      ${pkgdir}/opt/android-sdk/tools/emulator64-*
  fi

  # Fix broken permissions
  chmod -R o=g opt/$pkgname
}
