# Maintainer: lilydjwg <lilydjwg@gmail.com>
# Maintainer: gborzi <gborzi@ieee.org>
# Contributor: Greg von Nessi <greg.vonnessi@gmail.com>

_pkgname=screenruler
pkgname=gnome-$_pkgname
pkgver=0.9.6
pkgrel=2
pkgdesc="ScreenRuler lets you measure objects on your screen using six different metrics."
arch=('any')
url="https://launchpad.net/screenruler"
license=('GPL')
depends=('ruby-gtk2' 'ruby-cairo' 'ruby-gettext')
makedepends=('intltool')
conflicts=('gruler')
source=(https://launchpad.net/screenruler/trunk/$pkgver/+download/$_pkgname-$pkgver.tar.gz $pkgname.desktop path_patch.diff)

md5sums=('0caefe6835501beae8daffe2a8069e54' 
         '4f0e90bb3f2a241c64de7e5b1ddefdd4'
         'a54e055c1b37c65dec3062e90be76508')

build() {
  cd $srcdir/$_pkgname

  # Fix path
  patch -Np0 -i $srcdir/path_patch.diff
}

package() {
  cd $srcdir/$_pkgname
  # no compile for installation
  mkdir -p $pkgdir/usr/share/{applications,pixmaps,$_pkgname/utils}
  install -m644 *.glade $pkgdir/usr/share/$_pkgname
  install -m644 preferences_window.rb help_window.rb \
                ruler_popup_menu.rb ruler_window.rb utils/*.rb \
                $pkgdir/usr/share/$_pkgname/utils
  install -Dm755 screenruler.rb $pkgdir/usr/bin/$_pkgname

  # creation of menu entry
  install -m644 $srcdir/$pkgname.desktop $pkgdir/usr/share/applications/
  install -m644 $_pkgname-icon-*.png $pkgdir/usr/share/pixmaps/
}
