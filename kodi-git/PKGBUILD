# Maintainer: Lenovsky <lenovsky@pm.me>
# Contributor: BlackIkeEagle < ike DOT devolder AT gmail DOT com >
# Contributor: DonVla <donvla@users.sourceforge.net>
# Contributor: Ulf Winkelvos <ulf [at] winkelvos [dot] de>
# Contributor: Ralf Barth <archlinux dot org at haggy dot org>
# Contributor: B & monty - Thanks for your hints :)
# Contributor: marzoul
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: [vEX] <niechift.dot.vex.at.gmail.dot.com>
# Contributor: Zeqadious <zeqadious.at.gmail.dot.com>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Maxime Gauduin <alucryd@gmail.com>
#
# Original credits go to Edgar Hucek <gimli at dark-green dot com>
# for his xbmc-vdpau-vdr PKGBUILD at https://archvdr.svn.sourceforge.net/svnroot/archvdr/trunk/archvdr/xbmc-vdpau-vdr/PKGBUILD

pkgbase=kodi-git
pkgname=('kodi-git' 'kodi-eventclients-git' 'kodi-tools-texturepacker-git' 'kodi-dev-git')
_gitname='xbmc'
pkgver=18.0b4.r133.g1e6207326fa
pkgrel=1
arch=('i686' 'x86_64')
url="http://kodi.tv"
license=('GPL2')
makedepends=('fstrcmp' 'flatbuffers' 'afpfs-ng' 'bluez-libs' 'boost' 'cmake' 'curl' 'cwiid' 'doxygen'
             'git' 'ffmpeg' 'fmt' 'glew' 'gperf' 'hicolor-icon-theme' 'jasper' 'java-runtime=8'
             'libaacs' 'libass' 'libbluray' 'libcdio' 'libcec' 'libgl' 'libmariadbclient'
             'libmicrohttpd' 'libmodplug' 'libmpeg2' 'libnfs' 'libplist' 'libxrandr'
             'libxslt' 'lirc' 'lzo' 'mesa' 'nasm' 'nss-mdns' 'python2-pillow'
             'python2-pybluez' 'python2-simplejson' 'rapidjson' 'rtmpdump' 'shairplay'
             'smbclient' 'swig' 'taglib' 'tinyxml' 'upower')
source=("$_gitname::git+https://github.com/xbmc/xbmc.git#branch=master")
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  git describe --long --tags | sed 's/\([^-]*-\)/r/2;s/-/./g'
}

prepare() {
  [[ -d kodi-build ]] && rm -rf kodi-build
  mkdir kodi-build
}

build() {
  cd "$_gitname"
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DENABLE_EVENTCLIENTS=ON \
    -DLIRC_DEVICE=/run/lirc/lircd \
    -DENABLE_INTERNAL_FFMPEG="no" \
    -DWITH_FFMPEG="yes" \
    .
  make
  make preinstall
}

# kodi
# components: kodi, kodi-bin

package_kodi-git() {
  pkgdesc="A software media player and entertainment hub for digital media (master branch)"
  depends=('curl' 'bluez-libs' 'flatbuffers' 'desktop-file-utils' 'ffmpeg' 'glew'
           'hicolor-icon-theme' 'libcdio' 'libcec' 'libmariadbclient' 'libmicrohttpd'
           'libxrandr' 'libxslt' 'lzo' 'mesa' 'python2-pillow' 'python2-simplejson'
           'smbclient' 'fstrcmp' 'taglib' 'tinyxml' 'xorg-xdpyinfo')
  optdepends=('gdb: for meaningful backtraces in case of trouble - STRONGLY RECOMMENDED'
              'afpfs-ng: Apple shares support'
              'bluez: Blutooth support'
              'python2-pybluez: Bluetooth support'
              'libnfs: NFS shares support'
              'libplist: AirPlay support'
              'lirc: Remote controller support'
              'lsb-release: log distro information in crashlog'
              'pulseaudio: PulseAudio support'
              'shairplay: AirPlay support'
              'unrar: Archives support'
              'unzip: Archives support'
              'upower: Display battery level')
  provides=("kodi=${pkgver}" "xbmc=${pkgver}")
  conflicts=('kodi' 'xbmc')
  replaces=('xbmc-git' 'xbmc-svn')

  _components=('kodi' 'kodi-bin')

  cd "$_gitname"
  # install eventclients
  for _cmp in ${_components[@]}; do
    DESTDIR="$pkgdir" /usr/bin/cmake \
      -DCMAKE_INSTALL_COMPONENT="$_cmp" \
      -P cmake_install.cmake
  done

  # Licenses
  install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
  for licensef in LICENSE.md; do
    mv "$pkgdir/usr/share/doc/kodi/$licensef" \
      "$pkgdir/usr/share/licenses/$pkgname"
  done

  # python2 is being used
  cd "$pkgdir"
  grep -lR '#!.*python' * | while read file; do sed -s 's/\(#!.*python\)/\12/g' -i "$file"; done
}

# kodi-eventclients
# components: kodi-eventclients-common kodi-eventclients-ps3 kodi-eventclients-wiiremote kodi-eventclients-xbmc-send

package_kodi-eventclients-git() {
  pkgdesc="Kodi Event Clients (master branch)"
  provides=("kodi-eventclients=${pkgver}")
  conflicts=('kodi-eventclients')
  depends=('cwiid')

  _components=('kodi-eventclients-common' 'kodi-eventclients-ps3' 'kodi-eventclients-wiiremote'
               'kodi-eventclients-xbmc-send')

  cd "$_gitname"
  # install eventclients
  for _cmp in ${_components[@]}; do
    DESTDIR="$pkgdir" /usr/bin/cmake \
      -DCMAKE_INSTALL_COMPONENT="$_cmp" \
      -P cmake_install.cmake
  done

  # python2 is being used
  cd "$pkgdir"
  grep -lR '#!.*python' * | while read file; do sed -s 's/\(#!.*python\)/\12/g' -i "$file"; done
}

# kodi-tools-texturepacker
# components: kodi-tools-texturepacker

package_kodi-tools-texturepacker-git() {
  pkgdesc="Kodi Texturepacker tool (master branch)"
  provides=("kodi-tools-texturepacker=${pkgver}")
  conflicts=('kodi-tools-texturepacker')
  depends=('giflib' 'libjpeg-turbo' 'libpng' 'lzo')

  _components=('kodi-tools-texturepacker')

  cd "$_gitname"
  # install eventclients
  for _cmp in ${_components[@]}; do
    DESTDIR="$pkgdir" /usr/bin/cmake \
      -DCMAKE_INSTALL_COMPONENT="$_cmp" \
      -P cmake_install.cmake
  done
}

# kodi-dev
# components: kodi-addon-dev kodi-audio-dev kodi-eventclients-dev kodi-game-dev kodi-inputstream-dev kodi-peripheral-dev kodi-pvr-dev kodi-screensaver-dev kodi-visualization-dev

package_kodi-dev-git() {
  pkgdesc="Kodi dev files (master branch)"
  provides=("kodi-dev=${pkgver}")
  conflicts=('kodi-dev')
  depends=('kodi' 'python2')

  _components=('kodi-addon-dev' 'kodi-audio-dev' 'kodi-eventclients-dev' 'kodi-game-dev'
               'kodi-inputstream-dev' 'kodi-peripheral-dev' 'kodi-pvr-dev' 'kodi-screensaver-dev'
               'kodi-visualization-dev')

  cd "$_gitname"
  # install eventclients
  for _cmp in ${_components[@]}; do
    DESTDIR="$pkgdir" /usr/bin/cmake \
      -DCMAKE_INSTALL_COMPONENT="$_cmp" \
      -P cmake_install.cmake
  done

  # python2 is being used
  cd "$pkgdir"
  grep -lR '#!.*python' * | while read file; do sed -s 's/\(#!.*python\)/\12/g' -i "$file"; done
}
