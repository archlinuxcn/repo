# Maintainer: Johann Weging <johann@weging.com>

pkgname=godef-git
pkgver=344.20181105
pkgrel=1
pkgdesc="Find symbol information in Go source"
arch=('i686' 'x86_64')
provides=('godef')
conflicts=('godef')
url="https://github.com/rogpeppe/godef"
license=('BSD')
depends=('glibc')
makedepends=('go' 'git')
source=('git+https://github.com/rogpeppe/godef')
md5sums=('SKIP')

pkgver() {
	cd "${srcdir}/godef"
    printf "%s.%s" "$(git rev-list --count HEAD)" "$(git log -1 --format=%cd --date=short | tr -d -)"
}

prepare() {
    mkdir -p "${srcdir}/src/github.com/rogpeppe/"
    ln -sf "${srcdir}/godef" "${srcdir}/src/github.com/rogpeppe/"
}

build() {
    export GOPATH="${srcdir}"
    cd "${srcdir}/src/github.com/rogpeppe/godef"
    go build -o godef
}

package() {
    cd "${srcdir}/src/github.com/rogpeppe/godef"
	install -Dm755 godef "${pkgdir}/usr/bin/godef"
}
