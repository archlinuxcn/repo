# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Hilton Medeiros <medeiros.hilton@gmail.com>
# Contributor: Dave Reisner <d@falconindy.com>

pkgname=libgit2-julia
pkgver=0.26.0
pkgrel=1
epoch=1
pkgdesc='A linkable library for Git'
arch=(i686 x86_64 armv7h aarch64)
url='https://libgit2.github.com/'
depends=(zlib openssl libssh2 curl http-parser)
makedepends=(cmake python)
provides=("libgit2=$epoch:$pkgver")
conflicts=(libgit2)
license=('GPL2')
source=("$pkgname-$pkgver.tar.gz::https://github.com/libgit2/libgit2/archive/v${pkgver}.tar.gz"
        libgit2-agent-nonfatal.patch)
sha1sums=('7e9792e3d2ee88719f0d7cb59737256bfc1cddbb'
          '5b333adbd3bfa5e546053d2439d15f684cb0c44f')
options=(!strip)

prepare() {
  cd "libgit2-$pkgver"

  patch -p1 < ../libgit2-agent-nonfatal.patch
}

build() {
  cd "libgit2-$pkgver"
  export LANG=en_US.UTF-8
  export CFLAGS+=' -g'
  export CXXFLAGS+=' -g'
  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr -DTHREADSAFE=ON
  make
}

package() {
  cd "libgit2-$pkgver"
  make DESTDIR="$pkgdir" install
}
