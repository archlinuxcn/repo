# Maintainer: Boqun Feng <boqun.feng@gmail.com>

pkgname=beignet-git
pkgver=1.0.0.0.338.gb03a780
pkgrel=1
epoch=1
pkgdesc="OpenCL Implemenation for Intel IvyBridge and Haswell"
arch=('i686' 'x86_64')
url="http://cgit.freedesktop.org/beignet/"
license=('LGPL')
options=('debug' 'strip')
depends=('clang>=3.3' 'llvm>=3.3' 'ncurses' 'ocl-icd' 'libsm' 'libice' 'libx11'
  'libxext' 'libxfixes' 'libdrm' 'libgl' 'gcc-libs' 'zlib' 'libffi')
makedepends=('git' 'cmake>=2.6' 'python2' 'opencl-headers20')
source=(git://anongit.freedesktop.org/beignet)
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/beignet"

  git describe --tags | sed -e 's/^[^0-9]*//g' -e 's/-/.0./' -e 's/-/./g'
}

build() {
  cd "${srcdir}/beignet"

  # For clang
  # CFLAGS=${CFLAGS//-fvar-tracking-assignments}
  # CXXFLAGS=${CXXFLAGS//-fvar-tracking-assignments}

  mkdir -p include.bak

  for f in include/CL/*; do
    [[ $f =~ intel ]] && continue
    mv -i "$f" include.bak/
  done
  mkdir -p build
  cd build
  cmake .. \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DPYTHON_EXECUTABLE=/usr/bin/python2 \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd "${srcdir}/beignet/build"

  make DESTDIR="$pkgdir/" install
}
