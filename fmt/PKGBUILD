# Maintainer: Mihai Bi≈üog <mihai.bisog at [gmail] d0t com>
pkgname=fmt
pkgver=5.0.0
pkgrel=1
pkgdesc="Open-source formatting library for C++."
arch=("i686" "x86_64")
url="http://fmtlib.net"
license=("BSD")
makedepends=("cmake")

source=("https://github.com/fmtlib/fmt/archive/$pkgver.tar.gz")
md5sums=('4c33de20d7ed4a4d8040328200f685f8')

build() {
    cd "$pkgname-$pkgver"
    mkdir -p build && cd build

    cmake -DCMAKE_BUILD_TYPE=Release      \
          -DCMAKE_INSTALL_PREFIX=/usr     \
          -DCMAKE_INSTALL_LIBDIR=/usr/lib \
          -DFMT_DOC=OFF                   \
  -DCMAKE_INSTALL_LIBDIR=/usr/lib ..
    make
}

check() {
    cd "$pkgname-$pkgver/build"
    make test
}

package() {
    cd "$pkgname-$pkgver/build"
    make DESTDIR="$pkgdir" install
    install -D -m644 ../LICENSE.rst "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    install -D -m644 ../ChangeLog.rst "${pkgdir}/usr/share/doc/${pkgname}/ChangeLog.rst"
}
