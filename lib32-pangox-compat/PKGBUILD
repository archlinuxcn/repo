# Maintainer: drrossum <d.r.vanrossum ag gmx.de>
# Contributor: josephgbr <rafael.f.f1@gmail.com>

_pkgbase=pangox-compat
pkgname=lib32-${_pkgbase}
pkgver=0.0.2
pkgrel=1
pkgdesc="X Window System font support for Pango (32 bit)"
arch=('x86_64')
license=('LGPL')
depends=('lib32-pango' "${_pkgbase}")
makedepends=('gcc-multilib')
options=('!libtool')
source=(http://ftp.gnome.org/pub/gnome/sources/${_pkgbase}/${pkgver:0:3}/${_pkgbase}-${pkgver}.tar.xz)
url="http://www.pango.org/"
sha256sums=('552092b3b6c23f47f4beee05495d0f9a153781f62a1c4b7ec53857a37dfce046')

build() {
  export CC='gcc -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  
  cd ${_pkgbase}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --libdir=/usr/lib32
  make
}

package() {
  make -j1 -C ${_pkgbase}-${pkgver} DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/{etc,usr/include}
}
