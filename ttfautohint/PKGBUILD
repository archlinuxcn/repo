# Maintainer: Arthur Țițeică arthur.titeica/gmail/com

pkgname=ttfautohint
pkgver=1.3
pkgrel=1
pkgdesc="Provides automated hinting process for web fonts"
arch=('i686' 'x86_64')
url="http://www.freetype.org/ttfautohint/"
license=('GPL' 'custom')
depends=('freetype2' 'qt4')
# makedepends=('qt4')
optdepends=('texlive-bin: generate docs'
            'pandoc: generate docs'
            'ghc: pandoc filter')
source=(http://download.savannah.gnu.org/releases/freetype/$pkgname-$pkgver.tar.gz)
sha256sums=('c39fa03790b2dfe711f66137cf0f0324eb19872932cef91da9c0bddf9e4ce104')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  export QMAKE='/usr/bin/qmake-qt4'
  export MOC='/usr/bin/moc-qt4'
  export UIC='/usr/bin/uic-qt4'
  export RCC='/usr/bin/rcc-qt4'
  # export QTDIR='/usr/lib/qt4'
  # CFLAGS="-I/usr/include/qt4"
  ./configure --prefix=/usr --with-qt=/usr/lib/qt4
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make -k check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install

  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
  install -Dm644 FTL.TXT "${pkgdir}/usr/share/licenses/${pkgname}/FTL.TXT"
}

# vim:set ts=2 sw=2 et:
