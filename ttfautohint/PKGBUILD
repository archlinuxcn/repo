# Maintainer: Mario Finelli <mario dot finelli at yahoo dot com>
# Contributor: Arthur Titeica <arthur.titeica at gmail dot com>

pkgname=ttfautohint
pkgver=1.4.1
pkgrel=1
pkgdesc="Provides automated hinting process for web fonts."
arch=('i686' 'x86_64')
url="http://www.freetype.org/ttfautohint/"
license=('GPL' 'custom')
depends=('freetype2' 'qt4')
optdepends=('texlive-bin: generate docs'
            'pandoc: generate docs'
            'ghc: pandoc filter')
source=(http://download.savannah.gnu.org/releases/freetype/$pkgname-$pkgver.tar.gz{,.sig})
sha256sums=('46667e0abf9cd0e3db0abb2a46569d251948708f93c82c2b62f25eb46892ce15'
            'SKIP')
validpgpkeys=('58E0C111E39F5408C5D3EC76C1A60EACE707FDA5')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  export QMAKE='/usr/bin/qmake-qt4'
  export MOC='/usr/bin/moc-qt4'
  export UIC='/usr/bin/uic-qt4'
  export RCC='/usr/bin/rcc-qt4'
  # export QTDIR='/usr/lib/qt4'
  # CFLAGS="-I/usr/include/qt4"
  ./configure --prefix=/usr --with-qt=/usr/lib/qt4
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make -k check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install

  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
  install -Dm644 FTL.TXT "${pkgdir}/usr/share/licenses/${pkgname}/FTL.TXT"
}
validpgpkeys=(58E0C111E39F5408C5D3EC76C1A60EACE707FDA5)
