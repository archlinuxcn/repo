# Maintainer: Aleksey Filippov <sarum9in@gmail.com>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Thomas S Hatch <thatch45@gmail.com>
# Contributor: Geoffroy Carrier <geoffroy@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

_gtestver=1.7.0
_gmockver=1.7.0

_pkgbase="protobuf"
pkgname=('protobuf3' 'python2-protobuf3' 'python-protobuf3')
pkgver=3.0.0
_pkgver=$(echo $pkgver | tr _ -)
pkgrel=1
pkgdesc="Protocol Buffers - Google's data interchange format"
arch=('i686' 'x86_64')
url='https://developers.google.com/protocol-buffers/'
license=('BSD')
depends=('gcc-libs' 'zlib')
makedepends=('unzip' 'python-setuptools' 'python2-setuptools' 'clang')
source=(
  "https://github.com/google/${_pkgbase}/archive/v${_pkgver}.tar.gz"
  "gtest-${_gtestver}.tar.gz::https://github.com/google/googletest/archive/release-${_gtestver}.tar.gz"
  "gmock-${_gmockver}.tar.gz::https://github.com/google/googlemock/archive/release-1.7.0.tar.gz"
)
md5sums=('d4f6ca65aadc6310b3872ee421e79fa6'
         '4ff6353b2560df0afecfbda3b2763847'
         '13c3b4a57ad575763deb73fc0ad96e07')

prepare() {
  cd $_pkgbase-$_pkgver
  rm -rf gmock && cp -r "$srcdir/googlemock-release-${_gtestver}" "gmock"
  rm -rf gmock/gtest && cp -r "$srcdir/googletest-release-${_gtestver}" "gmock/gtest"
  sed -r 's|/usr/bin/env python|/usr/bin/env python2|' -i gmock/gtest/scripts/*.py
}

build() {
  # GCC is stuck on src/google/protobuf/util/internal/protostream_objectsource_test.cc
  # and src/google/protobuf/util/internal/protostream_objectwriter_test.cc
  export CC=clang CXX=clang++

  cd "$srcdir/$_pkgbase-$_pkgver"
  ./autogen.sh

  echo "Building gtest"
  cd "$srcdir/$_pkgbase-$_pkgver/gmock/gtest"
  ./configure
  make $MAKEFLAGS

  echo "Building gmock"
  cd "$srcdir/$_pkgbase-$_pkgver/gmock"
  ./configure
  make $MAKEFLAGS

  echo "Building protobuf"
  cd "$srcdir/$_pkgbase-$_pkgver"
  ./configure --prefix=/usr
  make $MAKEFLAGS
}

check() {
  make -C "$srcdir/$_pkgbase-$_pkgver" check
}

package_protobuf3() {
  conflicts=('protobuf' 'protobuf-cpp')
  provides=('protobuf' 'protobuf-cpp')
  replaces=('protobuf-cpp')

  cd "$srcdir/$_pkgbase-$_pkgver"
  make DESTDIR="$pkgdir" install
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  install -Dm644 editors/protobuf-mode.el \
    "$pkgdir"/usr/share/emacs/site-lisp/protobuf-mode.el
}

package_python2-protobuf3() {
  pkgdesc='Python 2 bindings for Google Protocol Buffers'
  depends=('python2' 'python2-six' "protobuf3=${pkgver}")
  conflicts=('python2-protobuf')
  provides=('python2-protobuf')

  cd "$srcdir/$_pkgbase-$_pkgver/python"
  python2 setup.py install --root="$pkgdir"

  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s /usr/share/licenses/$_pkgbase/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/
}

package_python-protobuf3() {
  pkgdesc='Python 3 bindings for Google Protocol Buffers'
  depends=('python' 'python-six' "protobuf3=${pkgver}")
  conflicts=('python-protobuf')
  provides=('python-protobuf')

  cd "$srcdir/$_pkgbase-$_pkgver/python"
  python3 setup.py install --root="$pkgdir"

  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s /usr/share/licenses/$_pkgbase/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/
}
