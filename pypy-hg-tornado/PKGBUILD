# Maintainer: Yichao Yu <yyc1992@gmail.com>
# Contributor: Felix Yan <felixonmars@gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >

_pypyname=pypy-hg
_pypyabi=26
_pypy3name=pypy3-hg
_pypy3abi=25

pkgbase=${_pypyname}-tornado
pkgname=(${_pypy3name}-tornado ${_pypyname}-tornado)
pkgver=4.0.2
pkgrel=7
pkgdesc='open source version of the scalable, non-blocking web server and tools'
arch=('any')
url='http://www.tornadoweb.org/'
license=('Apache')
makedepends=(pypy-setuptools pypy3-${_pypy3abi}-setuptools
             pypy-backports.ssl_match_hostname git pypy3-${_pypy3abi})
source=("git+https://github.com/facebook/tornado.git#tag=v$pkgver"
        use_system_ca_certificates.patch)
sha512sums=('SKIP'
            '6e50e9ecf361d54d9f67e1f12185cf58863ad0eae72fbe7cc24e8eaf94874255009a030249bb51adf06e98c7ed0b17d8c6d9ee65190ebc341d6857c0efbc7840')

prepare() {
  (cd tornado
   patch -p1 -i ../use_system_ca_certificates.patch)

  cp -a tornado{,-py2}

  # python -> pypy rename
  find tornado-py2 -name '*py' \
       -exec sed -e 's_#!/usr/bin/env python_#!/usr/bin/env pypy_' -i {} \;
  find tornado -name '*py' \
       -exec sed -e 's_#!/usr/bin/env python_#!/usr/bin/env pypy3_' -i {} \;
}

build() {
  (cd tornado
   pypy3 setup.py build)

  (cd tornado-py2
   pypy setup.py build)
}

package_pypy3-hg-tornado() {
  depends=(pypy3-${_pypy3abi})
  provides=(pypy3-${_pypy3abi}-tornado=${pkgver}-${pkgrel}
            pypy3-tornado=${pkgver}-${pkgrel})
  conflicts=(pypy3-tornado)
  cd tornado

  pypy3 setup.py install --skip-build --root="${pkgdir}" --optimize=1
}

package_pypy-hg-tornado() {
  depends=(pypy pypy-backports.ssl_match_hostname)
  provides=(pypy-tornado=${pkgver}-${pkgrel})
  conflicts=(pypy-tornado)
  cd tornado-py2

  pypy setup.py install --skip-build --root="${pkgdir}" --optimize=1
}
