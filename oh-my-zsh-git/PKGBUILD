# Maintainer: jcsiv <siviter dot jamie at gmx dot co dot uk>
# Contributor: ThinCarrotShrimp <christoph.r.martin+arch at gmail dot com>

pkgname=oh-my-zsh-git
pkgver=4712.b4007b540
pkgrel=1
pkgdesc="A community-driven framework for managing your zsh configuration."
arch=('any')
url='https://github.com/robbyrussell/oh-my-zsh'
license=('MIT')
depends=('zsh')
makedepends=('git')
optdepends=('ruby: for some plugin functionality')
install='oh-my-zsh-git.install'

_gitname=oh-my-zsh
source=("$_gitname::git://github.com/robbyrussell/oh-my-zsh.git")
md5sums=('SKIP')

pkgver() {
  cd "$_gitname"
  printf "%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
  cd $_gitname

  mkdir -p "${pkgdir}/usr/share/oh-my-zsh"

  # Fix the path to oh-my-zsh, and disable auto-updating.
  sed -e 's/ZSH=.*/ZSH=\/usr\/share\/oh-my-zsh/1'\
      -e 's/# \(DISABLE_AUTO_UPDATE="true"\)/\1/'\
      templates/zshrc.zsh-template > zshrc

  # Update zshrc with a per user writable cache (thanks apjanke!)
  echo "" >> zshrc
  echo "ZSH_CACHE_DIR=\$HOME/.oh-my-zsh-cache" >> zshrc
  echo "if [[ ! -d \$ZSH_CACHE_DIR ]]; then" >> zshrc
  echo "  mkdir \$ZSH_CACHE_DIR" >> zshrc
  echo "fi" >> zshrc

  # Move any sourcing to the end of the file
  sed -i '/^source/{H;$!d;s/.*//};$G;s/\n*//' zshrc

  # We don't need anything related to git in the package.
  rm -rf .git*

  # We don't need {check_for_,}upgrade.sh, {un,}install.sh
  rm -f tools/{upgrade,uninstall,install,check_for_upgrade}.sh

  # Copy everything else.
  cp -r . "${pkgdir}/usr/share/oh-my-zsh/"
}

# vim: set ts=2 sw=2 et:
