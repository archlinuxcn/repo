# Contributor: Alexander 'hatred' Drozdov <adrozdoff@gmail.com>
# Maintainer: Erico Nunes <nunes dot erico at gmail>

_target="arm-linux-gnueabihf"
pkgname=${_target}-glibc
pkgver=2.21
pkgrel=2
pkgdesc="GNU C Library (${_target})"
arch=(any)
url="http://www.gnu.org/software/libc"
license=('GPL' 'LGPL')
depends=("${_target}-linux-api-headers")
makedepends=("${_target}-gcc-stage2" 'gperf')
provides=("${_target}-eglibc=${pkgver}" "${_target}-glibc-headers=${pkgver}"
          "${_target}-eglibc-headers=${pkgver}")
conflicts=("${_target}-eglibc" "${_target}-glibc-headers")
replaces=("${_target}-eglibc" "${_target}-glibc-headers")
options=(!strip 'staticlibs')
source=(http://ftp.gnu.org/gnu/libc/glibc-${pkgver}.tar.xz
        glibc-2.21-roundup.patch)
md5sums=('9cb398828e8f84f57d1f7d5588cf40cd'
         'bf9d96b11c76b113606aae102da63d9d')

prepare() {
  cd glibc-${pkgver}

  # glibc-2.21..75adf430
  patch -p1 -i ../glibc-2.21-roundup.patch

  rm -rf "${srcdir}"/glibc-build
  mkdir -p "${srcdir}"/glibc-build
}

build() {
  cd glibc-${pkgver}

  # Don not build timezones
  sed -i 's/timezone rt/rt/' Makeconfig

  cd "${srcdir}"/glibc-build

  export CFLAGS="-U_FORTIFY_SOURCE -mlittle-endian -mhard-float -O2"
  export CPPFLAGS="-U_FORTIFY_SOURCE -O2"
  unset LD_LIBRARY_PATH

  export BUILD_CC=gcc
  export CC=${_target}-gcc
  export CXX=${_target}-g++
  export AR=${_target}-ar
  export RANLIB=${_target}-ranlib

  ../glibc-${pkgver}/configure \
    --prefix=/ \
    --target=${_target} \
    --host=${_target} \
    --build=${CHOST} \
    --with-headers=/usr/${_target}/include \
    --enable-add-ons \
    --enable-obsolete-rpc \
    --enable-kernel=2.6.32 \
    --enable-bind-now \
    --disable-profile \
    --enable-stackguard-randomization \
    --enable-lock-elision \
    --enable-shared \
    --with-tls \
    --with-__thread \
    --without-cvs \
    --without-gd \
    --disable-werror

  make
}

package() {
  cd glibc-build

  make "install_root=${pkgdir}/usr/${_target}" install

  mkdir -p "${pkgdir}/usr/${_target}/usr"
  ln -s ../{include,lib} "${pkgdir}/usr/${_target}/usr"
  # Remove unneeded for compilation files
  rm -rf "${pkgdir}"/usr/${_target}/{bin,sbin,etc,share,var}

  for f in "${pkgdir}"/usr/${_target}/lib/lib{c,pthread}.so; do
    sed -i "$f" -e "s://lib/l:l:g"
  done
}
