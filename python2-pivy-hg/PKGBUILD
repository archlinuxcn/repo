# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor/Submitter: <p.janouch@gmail.com>

pkgname=python2-pivy-hg
pkgver=610.d8c4fefe5a19
pkgrel=1
pkgdesc="Coin binding for Python"
arch=('i686' 'x86_64')
url="http://pivy.coin3d.org"
license=('custom')
depends=(python2 soqt)
makedepends=('swig' 'mercurial' 'glu')
provides=(python2-pivy pivy)
conflicts=(python2-pivy pivy)
replaces=(pivy-hg)
source=('pivy::hg+https://bitbucket.org/Coin3D/pivy')
sha256sums=('SKIP')
options=('debug' 'strip')

pkgver() {
  cd pivy/

  hg tip | head -n1 | tr -d ' ' | cut -d: --output-delimiter=. -f 2,3
}

build() {
  cd pivy/

  python2 setup.py build
}

package() {
  cd pivy/

  python2 setup.py install --skip-build --root="${pkgdir}"
  install -D -m 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
