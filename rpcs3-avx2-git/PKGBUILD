# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=rpcs3-avx2-git
pkgver=0.0.5.r1011.75221a607
pkgrel=1
pkgdesc='A Sony PlayStation 3 emulator (Require AVX2)'
arch=('x86_64')
url='https://github.com/RPCS3/rpcs3'
license=('GPL2')
depends=('alsa-lib' 'glew' 'glu' 'libevdev' 'libgl' 'libice' 'libpng'
         'libpulse' 'libsm' 'libx11' 'libxext' 'openal' 'qt5-base'
         'qt5-declarative' 'vulkan-icd-loader' 'zlib'
         'libavcodec.so' 'libavformat.so' 'libavutil.so' 'libncursesw.so'
         'libswscale.so' 'libudev.so')
makedepends=('boost' 'cereal' 'cmake' 'ffmpeg' 'git' 'libglvnd' 'python'
             'vulkan-validation-layers')
provides=('rpcs3' 'rpcs3-git')
conflicts=('rpcs3' 'rpcs3-git')
options=('!emptydirs')
source=('git+https://github.com/RPCS3/rpcs3.git'
        'rpcs3-common::git+https://github.com/RPCS3/common.git'
        'rpcs3-hidapi::git+https://github.com/RPCS3/hidapi.git'
        'rpcs3-llvm::git+https://github.com/RPCS3/llvm.git'
        'git+https://github.com/kobalicek/asmjit.git'
        'git+https://github.com/Microsoft/GSL.git'
        'git+https://github.com/KhronosGroup/glslang.git'
        'git+https://github.com/zeux/pugixml.git'
        'git+https://github.com/Cyan4973/xxHash.git'
        'git+https://github.com/jbeder/yaml-cpp.git')
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP')

pkgver() {
  cd rpcs3

  echo "$(git describe --tags | sed 's/^v//; s/-/.r/; s/-g/./')"
}

prepare() {
  pushd rpcs3

  git submodule init 3rdparty/{GSL,hidapi,pugixml,xxHash,yaml-cpp} asmjit llvm Vulkan/glslang
  git config submodule.asmjit.url ../asmjit
  git config submodule.glslang.url ../glslang
  git config submodule.GSL.url ../GSL
  git config submodule.hidapi.url ../rpcs3-hidapi
  git config submodule.llvm.url ../rpcs3-llvm
  git config submodule.pugixml.url ../pugixml
  git config submodule.xxHash ../xxHash
  git config submodule.yaml-cpp ../yaml-cpp
  git submodule update 3rdparty/{GSL,hidapi,pugixml,xxHash,yaml-cpp} asmjit llvm Vulkan/glslang

  popd

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd build

  cmake ../rpcs3 \
    -DCMAKE_BUILD_TYPE='Release' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCMAKE_SKIP_RPATH='ON' \
    -DUSE_SYSTEM_FFMPEG='ON' \
    -DUSE_SYSTEM_LIBPNG='ON' \
    -DUSE_NATIVE_INSTRUCTIONS='OFF'
  make
}

package() {
  make DESTDIR="${pkgdir}" -C build install
}
