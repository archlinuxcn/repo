# Maintainer: Yichao Yu <yyc1992@gmail.com>
# Contributor: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Timm Preetz <timm@preetz.us>

_pypyname=pypy-hg
_pypyabi=26
_pypy3name=pypy3-hg
_pypy3abi=25

pkgbase=${_pypyname}-pygments
pkgname=(${_pypy3name}-pygments ${_pypyname}-pygments)
pkgver=1.6
pkgrel=7
pkgdesc="Python syntax highlighter"
arch=('any')
url="http://pygments.org/"
license=('BSD')
makedepends=(pypy3-${_pypy3abi}-setuptools pypy-setuptools pypy3-${_pypy3abi})
options=('!emptydirs')
source=(https://pypi.python.org/packages/source/P/Pygments/Pygments-$pkgver.tar.gz
        make-pygments.lexers.guess_lexer_for_filename-py3-compatible.patch)
sha256sums=('799ed4caf77516e54440806d8d9cd82a7607dfdf4e4fb643815171a4b5c921c0'
            '15cf8323ce343d32cbf78d35ab20001ce3204abe90b68a80607c7aea41499f07')

prepare() {
  cd Pygments-$pkgver

  patch -Np1 -i \
        ../make-pygments.lexers.guess_lexer_for_filename-py3-compatible.patch
}

build() {
  rm -rf pypy{,3}-build
  for pypy_name in pypy{,3}; do
    (cp -r Pygments-$pkgver ${pypy_name}-build
     cd ${pypy_name}-build
     ${pypy_name} setup.py build)
  done
}

package_pypy3-hg-pygments() {
  depends=(pypy3-${_pypy3abi})
  provides=(pypy3-${_pypy3abi}-pygments=${pkgver}-${pkgrel}
            pypy3-pygments=${pkgver}-${pkgrel})
  conflicts=(pypy3-pygments)

  cd pypy3-build

  pypy3 setup.py install --root="$pkgdir" -O1

  rm "$pkgdir/opt/pypy3/bin/pygmentize"

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_pypy-hg-pygments() {
  depends=(pypy)
  provides=(pypy-pygments=${pkgver}-${pkgrel})
  conflicts=(pypy-pygments)

  cd pypy-build

  pypy setup.py install --root="$pkgdir" -O1

  install -dm755 "${pkgdir}/usr/bin"
  mv "${pkgdir}/opt/pypy/bin/pygmentize" "${pkgdir}/usr/bin/pygmentize-pypy"

  sed -i '1s,^#! \?/usr/bin/\(env \|\)python$,#!/usr/bin/pypy,' \
    "${pkgdir}/usr/bin/pygmentize-pypy"

  install -Dm644 external/pygments.bashcomp \
    "$pkgdir/usr/share/bash-completion/completions/pygmentize-pypy"

  sed -i 's/pygmentize/pygmentize-pypy/g' \
    "$pkgdir/usr/share/bash-completion/completions/pygmentize-pypy"

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
