# $Id$
# Maintainer: Eric Bélanger <eric@archlinux.org>

pkgname=wget-lily
pkgver=1.19.2
pkgrel=1
pkgdesc="Network utility to retrieve files from the Web"
arch=('i686' 'x86_64')
url="https://www.gnu.org/software/wget/wget.html"
license=('GPL3')
depends=('gnutls' 'libidn' 'libutil-linux' 'libpsl' 'pcre')
checkdepends=('perl-http-daemon' 'perl-io-socket-ssl' 'python')
optdepends=('ca-certificates: HTTPS downloads')
conflicts=('wget')
provides=("wget=$pkgver")
backup=('etc/wgetrc')
source=(https://ftp.gnu.org/gnu/wget/wget-${pkgver}.tar.gz{,.sig} wget.patch)
sha256sums=('4f4a673b6d466efa50fbfba796bd84a46ae24e370fa562ede5b21ab53c11a920'
            'SKIP' aa28cf8532f6b7cc1fe689eb253b0453102990eb725a6583704ca4f3e665c9b0)
validpgpkeys=('AC404C1C0BF735C63FF4D562263D6DF2E163E1EA'
              '1CB27DBC98614B2D5841646D08302DB6A2670428') # Tim Rühsen <tim.ruehsen@gmx.de>

prepare() {
  cd wget-${pkgver}
  cat >> doc/sample.wgetrc <<EOF

# default root certs location
ca_certificate=/etc/ssl/certs/ca-certificates.crt
EOF
  patch -p1 -i "${srcdir}/wget.patch"
}

build() {
  cd wget-${pkgver}
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --disable-rpath \
    --enable-nls \
    --with-ssl=gnutls
  make
}

check() {
  cd wget-${pkgver}
  make check < /dev/null
}

package() {
  cd wget-${pkgver}
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
