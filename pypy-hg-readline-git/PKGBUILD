# Maintainer: Yichao Yu <yyc1992@gmail.com>

_pypyname=pypy-hg
_pypyabi=26
_pypy3name=pypy3-hg
_pypy3abi=25

pkgbase=${_pypyname}-readline-git
pkgname=(${_pypy3name}-readline-git ${_pypyname}-readline-git)
makedepends=('cmake' 'git' 'readline' pypy pypy3-${_pypy3abi})
pkgver=0.2.0.2.gc71facf
pkgrel=4
epoch=1
pkgdesc="Full function readline module for pypy."
arch=('i686' 'x86_64')
url="http://github.com/yuyichao/pyreadline-cffi"
license=('custom')
options=('debug' 'strip')
source=('git://github.com/yuyichao/pyreadline-cffi')
md5sums=('SKIP')

pkgver() {
  cd pyreadline-cffi

  git describe | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

build() {
  cd pyreadline-cffi

  mkdir -p build{,3}

  (cd build3
   cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DPYTHON_EXECUTABLE=/usr/bin/pypy3 \
         -DPYREADLINE_DIR=/opt/pypy3/lib_pypy/pyrepl
   make)

  (cd build
   cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DPYTHON_EXECUTABLE=/usr/bin/pypy \
         -DPYREADLINE_DIR=/opt/pypy/lib_pypy/pyrepl
   make)
}

package_pypy3-hg-readline-git() {
  depends=(pypy3-${_pypy3abi})
  provides=(pypy3-${_pypy3abi}-readline
            pypy3-readline)
  conflicts=(pypy3-readline)
  cd pyreadline-cffi/build3

  make install DESTDIR="${pkgdir}"
}

package_pypy-hg-readline-git() {
  depends=(pypy)
  provides=(pypy-readline)
  conflicts=(pypy-readline)
  cd pyreadline-cffi/build

  make install DESTDIR="${pkgdir}"
}
