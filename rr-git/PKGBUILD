# Maintainer: dequis <dx@dxzone.com.ar>
# Previous maintainer: Joel Teichroeb <joel@teichroeb.net>

pkgname=rr-git
pkgver=5.2.0.r82.g07ff7425
pkgrel=1
pkgdesc='a nondeterministic debugger'
arch=(i686 x86_64)
url='http://rr-project.org/'
license=('custom')
depends=(python2 python-pexpect gdb capnproto)
makedepends=('git' 'cmake' 'capnproto')
if [[ $CARCH = x86_64 ]]; then
    makedepends+=('gcc-multilib')
fi
source=('git://github.com/mozilla/rr')
sha1sums=('SKIP')
provides=('rr')
conflicts=('rr')
options=('debug' '!strip')

pkgver() {
  cd rr
  git describe --long --tags | sed -r 's/([^-]*-g)/r\1/;s/-/./g'
}

prepare() {
  cd rr
  # patch -Np1 < ../
  sed -i -e 's/std=c++11/std=c++14/' CMakeLists.txt
}

build() {
  cd rr
  mkdir -p build

  cd build
  # Debug build has `Werror` on
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=lib -DCMAKE_BUILD_TYPE=Release ..
  make
}

package() {
  cd rr/build
  make DESTDIR="${pkgdir}" install
  install -D ../LICENSE "${pkgdir}/usr/share/licenses/rr/LICENSE"
}
