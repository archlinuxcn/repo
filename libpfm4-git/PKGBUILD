# Maintainer: Benjamin Chretien <chretien at lirmm dot fr>
# Contributor: HÃ¥vard Espeland <espeland@acm.org>
pkgname=libpfm4-git
pkgver=4.7.1.r1.g756658b
provides=('libpfm4')
conflicts=('libpfm3' 'libpfm4')
pkgrel=1
pkgdesc="Library to encode performance events for use by perf tool"
arch=('x86_64' 'i686')
url="http://perfmon2.sourceforge.net/"
license=('MIT')
depends=('glibc')
makedepends=('git')
source=("git://git.code.sf.net/p/perfmon2/libpfm4")
md5sums=('SKIP')

pkgver() {
  cd libpfm4
  git describe --long --tags | sed -r -e 's/([^-]*-g)/r\1/;s/-/./g;s/^v//'
}

build() {
  cd libpfm4
  sed -i 's|PREFIX=/usr/local|PREFIX=/usr|g' config.mk
  sed -i 's|LDCONFIG=ldconfig|LDCONFIG=true|g' config.mk
  make
}

check () {
  cd libpfm4/tests
  ./validate
}

package() {
  cd libpfm4

  # Install libraries
  make DESTDIR="$pkgdir/" install

  # Install useful example files
  make DESTDIR="$pkgdir/" install_examples

  # Install license
  install -D -m0644 COPYING "${pkgdir}"/usr/share/licenses/libpfm4/LICENSE
}
