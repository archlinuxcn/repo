# Maintainer: Vic Luo <vicluo96 at gmail.com>

pkgname=cquery-git
_pkgname=cquery
pkgver=1625.0f181a6f
pkgrel=1
pkgdesc='Low-latency vscode language server for large C++ code-bases, powered by libclang.'
arch=('any')
url='https://github.com/cquery-project/cquery/'
license=('MIT')
depends=('clang')
makedepends=("git" "python" "llvm")
source=('git+https://github.com/cquery-project/cquery.git')
md5sums=(
    'SKIP'
)

pkgver() {
    cd $_pkgname
    printf "%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    cd $_pkgname
    git submodule update --init
}

build() {
    cd $_pkgname
    # --variant=custom will not add extra CXXFLAGS
    python waf configure --variant=custom --prefix="$pkgdir/usr" --llvm-config=/usr/bin/llvm-config
    python waf build --variant=custom
}

#check() {
#    cd $_pkgname
#    yes | build/custom/bin/cquery --test-unit --test-index --clang-sanity-check
#}

package() {
    cd $_pkgname
    python waf install --variant=custom
}
