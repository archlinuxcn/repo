# Maintainer: rafaelff <rafaelff@gnome.org>

pkgname=teeworlds-ddnet
pkgver=10.2.1
pkgrel=2
pkgdesc="A customized version by DDRaceNetwork of this 2D shooting game"
arch=('i686' 'x86_64')
url="https://ddnet.tw"
license=('custom')
depends=('alsa-lib' 'glu' 'sdl2' 'freetype2' 'opusfile' 'curl')
makedepends=('bam' 'imagemagick' 'gendesk' 'python')
optdepends=('teeworlds-ddnet-skins: more skins for your tee'
            'teeworlds-ddnet-maps-git: mainly important for DDNet Server')
provides=('teeworlds')
conflicts=('teeworlds')
source=("ddnet-$pkgver.tar.gz::https://github.com/ddnet/ddnet/archive/$pkgver.tar.gz"
        "0001-updated-build-files-for-bam-0.5.patch")
source_i686=("https://ddnet.tw/downloads/GraphicsTools-linux_x86.tar.gz")
source_x86_64=("https://ddnet.tw/downloads/GraphicsTools-linux_x86_64.tar.gz")
md5sums=('c888a2835932f9c55fc738647bf01b14'
         'caf0f0c06ff54672e12829b1cbedd5f6')
md5sums_i686=('566354c3b4510b032af7d891381ee711')
md5sums_x86_64=('fc32ca52ae9be02f68b6c257153dbd37')

prepare() {
      # Client
    convert "ddnet-$pkgver/other/icons/DDNet.ico" "$srcdir/$pkgname.png"
    gendesk -f -n --pkgname "$pkgname" --pkgdesc "$pkgdesc" \
        --name 'Teeworlds' --categories 'Game;ArcadeGame'
      
      # Server
    convert "ddnet-$pkgver/other/icons/DDNet-Server.ico" "$srcdir/${pkgname}_srv.png"
      # This desktop file, combined with 'teeworlds-ddnet-maps-git' pkg will
      # run DDNet Server with all votes, maps etc. -- no score/ranking, though
    gendesk -f -n --pkgname "${pkgname}_srv" --pkgdesc "DDNet Server" --terminal=true \
        --exec='sh -c "cd /usr/share/teeworlds/data && teeworlds-ddnet_srv"' \
        --name 'DDNet Server' --categories 'Game;ArcadeGame'     

  cd ddnet-$pkgver
  patch -p1 -i "$srcdir/0001-updated-build-files-for-bam-0.5.patch"

}

build() {
  cd ddnet-$pkgver
  bam release
}

package() {
  cd ddnet-$pkgver
  
    # Install DDNet client/server binaries
  install -Dm755 DDNet "$pkgdir"/usr/bin/teeworlds-ddnet
  install -Dm755 DDNet-Server "$pkgdir"/usr/bin/teeworlds-ddnet_srv
  install -Dm755 config_store "$pkgdir"/usr/bin/ddnet-config_store
  install -Dm755 config_retrieve "$pkgdir"/usr/bin/ddnet-config_retrieve
    # Install Graphic Tools binaries
  install -Dm755 ../dilate "$pkgdir"/usr/bin/dilate
  install -Dm755 ../tileset_borderadd "$pkgdir"/usr/bin/tileset_borderadd
  install -Dm755 ../tileset_borderfix "$pkgdir"/usr/bin/tileset_borderfix
  install -Dm755 ../tileset_borderrem "$pkgdir"/usr/bin/tileset_borderrem
  install -Dm755 ../tileset_borderset "$pkgdir"/usr/bin/tileset_borderset
  
    # Install data files
  mkdir -p "$pkgdir"/usr/share/teeworlds/data
  cp -r data/* "$pkgdir"/usr/share/teeworlds/data
  
    # Install desktop and icon files
  install -Dm644 ../teeworlds-ddnet.desktop "$pkgdir"/usr/share/applications/teeworlds-ddnet.desktop
  install -Dm644 ../teeworlds-ddnet_srv.desktop "$pkgdir"/usr/share/applications/teeworlds-ddnet_srv.desktop
  install -Dm644 ../teeworlds-ddnet-5.png   "$pkgdir"/usr/share/pixmaps/teeworlds-ddnet.png
  install -Dm644 ../teeworlds-ddnet_srv-8.png   "$pkgdir"/usr/share/pixmaps/teeworlds-ddnet_srv.png
  
    # Install license files
  install -Dm644 license.txt "$pkgdir"/usr/share/licenses/$pkgname/license.txt
}
