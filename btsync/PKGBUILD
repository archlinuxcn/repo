# Contributor: Dalton Miller
# Contributor: Kilian Lackhove kilian@lackhove.de
# Contributor: Justin Patera serialhex@gmail.com
# Maintainer: ava1ar <mail(at)ava1ar(dot)me>

pkgname=btsync
pkgver=1.4.110
pkgrel=1
pkgdesc="BitTorrent Sync - automatically sync files via secure, distributed technology"
license=("custom:btsync")
arch=('i686' 'x86_64' 'arm' 'armv6h' 'armv7h')
url="http://www.bittorrent.com/sync"
install=btsync.install
backup=('etc/btsync.conf')
optdepends=("btsync-autoconfig: Auto-create users' config files if needed") 
source=(btsync.service btsync_user.service btsync.conf)
source_arm+=("http://syncapp.bittorrent.com/${pkgver}/btsync_arm-${pkgver}.tar.gz")
source_armv6h+=("http://syncapp.bittorrent.com/${pkgver}/btsync_arm-${pkgver}.tar.gz")
source_armv7h+=("http://syncapp.bittorrent.com/${pkgver}/btsync_arm-${pkgver}.tar.gz")
source_i686+=("http://syncapp.bittorrent.com/${pkgver}/btsync_i386-${pkgver}.tar.gz")
source_x86_64+=("http://syncapp.bittorrent.com/${pkgver}/btsync_x64-${pkgver}.tar.gz")
sha1sums=('05038b03a673dd0207b2758a2e01db5d28c409d1'
          'd30deb7e41ba1d163b4a5e442b8d8118758d312a'
          'bf7d6bd7ffe2d23cce51a51e92fac3b82a7c87bb')
sha1sums_i686=('60e36a8b3738235ae04d430da898621ce7da5dd1')
sha1sums_x86_64=('459d25511dd4bb15ae36d5927576f574c27d54d1')
sha1sums_arm=('02d2e44935403693d7f30df6fdd107bf9a6ae149')
sha1sums_armv6h=('02d2e44935403693d7f30df6fdd107bf9a6ae149')
sha1sums_armv7h=('02d2e44935403693d7f30df6fdd107bf9a6ae149')
# temporary to enforce upgrade to pacman 4.2
makedepends=('pacman>=4.2')

package() {
	# install main binary
	install -Dm755 "${srcdir}"/btsync "${pkgdir}"/usr/bin/btsync

	# generate and install system-wide config
	mkdir -p "${pkgdir}"/etc
	./btsync --dump-sample-config \
		| sed 's:/home/user/\.sync:/var/lib/btsync:' \
		| sed 's:\/\/ "pid_file":  "pid_file":' \
		| sed 's:\/\/ "storage_path":  "storage_path":' \
		> "${pkgdir}"/etc/btsync.conf

	# install systemd config files
	install -Dm644 "${srcdir}"/btsync.service "${pkgdir}"/usr/lib/systemd/system/btsync.service
	install -Dm644 "${srcdir}"/btsync.conf "${pkgdir}"/usr/lib/tmpfiles.d/btsync.conf
	install -Dm644 "${srcdir}"/btsync_user.service "${pkgdir}"/usr/lib/systemd/user/btsync.service

	# install license
	install -Dm644 "${srcdir}"/LICENSE.TXT "${pkgdir}"/usr/share/licenses/${pkgname}/license.txt
}
