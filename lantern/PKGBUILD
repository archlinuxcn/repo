# Submitter: neo.shadowsocks<neo.shadowsocks AT gmx.com>
# Maintainer: Kars Wang <jaklsy g-mail>

pkgname=lantern
pkgver=2.2.5
pkgrel=1
pkgdesc='Lantern is a free desktop application that delivers fast, reliable and secure access to the open Internet. (Stable Channel)'
arch=('i686' 'x86_64')
url='https://getlantern.org'
license=('Apache')

# neo: I got it by ldd the lantern-binary as deb metadata doesn't provides it,
# and remove duplicated, like atk required by gtk3
depends=('hicolor-icon-theme' 'bzip2' 'dbus' 'glib2' 'gtk3' 'libappindicator-gtk3' 'mesa' 'xcb-util' 'xdg-utils' 'zlib')
options=('emptydirs' '!strip' '!docs')
install=$pkgname.install
source=('lantern.service')
source_i686=("lantern-installer-32-bit-$pkgver.deb::https://raw.githubusercontent.com/getlantern/lantern-binaries/master/lantern-installer-32-bit.deb")
source_x86_64=("lantern-installer-64-bit-$pkgver.deb::https://raw.githubusercontent.com/getlantern/lantern-binaries/master/lantern-installer-64-bit.deb")
sha512sums=('47535c7aedbb9758fdfc476914c790e333b1645d0c0184335d1b4740f58014c61b245e6879ed435d0a87e217226500f5467e914a0d738c079368ebfab433640e')
sha512sums_i686=('c0bddfc677631075f51edd2ea04e34fc47c02eabbe8ae99e33a24d07a42736e170197b6c4628bdeafdd1c6a5014f2b791df71d6f103b52df53acdc197bc71292')
sha512sums_x86_64=('f7142f24f9391649082f7607e56a6541c3da93f1f2db642bc5ad7f048a28aa61c67d467e957a29f52a6bc588d091a77437124b9c6652fab225cc5abf8c4c6f4e')

package() {
    msg2 'Extracting the data.tar.xz...'
    bsdtar -xf data.tar.xz -C "${pkgdir}/"

    msg2 'Install systemd service file...'
    install -Dm644 "${srcdir}/lantern.service" "${pkgdir}/usr/lib/systemd/system/lantern.service"
    install -dm700 "$pkgdir/var/lib/lantern"
}
