# Maintainer: alucryd <alucryd at gmail dot com>
# Contributor: jarda-wien <xstej70 at gmail dot com>
# Contributor: Jason Melton <jason dot melton at gmail dot com>
# Contributor: Atilla ÖNTAŞ <tarakbumba at gmail dot com>

pkgname=nvidia-dkms
pkgver=331.20
pkgrel=1
pkgdesc='NVIDIA kernel module sources (DKMS)'
arch=('i686' 'x86_64')
[[ $CARCH == i686 ]] && _arch=x86 && _pkg=NVIDIA-Linux-${_arch}-${pkgver} && md5sums=('801aa04a087891690f1cac09575b2ba9')
[[ $CARCH == x86_64 ]] && _arch=x86_64 && _pkg=NVIDIA-Linux-${_arch}-${pkgver}-no-compat32 && md5sums=('28295eed56c2ca996401c0093279620f')
url="http://www.nvidia.com/"
license=('custom')
depends=('dkms' "nvidia-utils=${pkgver}")
optdepends=('linux-headers: Build the module against Arch kernel'
            'linux-lts-headers: Build the module against LTS Arch kernel')
provides=("nvidia=${pkgver}")
conflicts=('nvidia')
install="${pkgname}.install"
source=("http://us.download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")

prepare() {
  if [[ -d ${_pkg} ]] ; then
    rm -rf ${_pkg}
  fi

  sh ${_pkg}.run --extract-only
}

package() {
  cd ${_pkg}

  mkdir -p "${pkgdir}"/usr/src
  cp -dr --no-preserve=ownership kernel "${pkgdir}"/usr/src/nvidia-${pkgver}

# Blacklist nouveau driver
  mkdir -p "${pkgdir}"/usr/lib/modprobe.d
  echo "blacklist nouveau" > "${pkgdir}"/usr/lib/modprobe.d/nvidia.conf

# Fix permissions
  find "${pkgdir}" -type d -exec chmod 755 {} +
  find "${pkgdir}" -type f -exec chmod 644 {} +
  chmod 755 "${pkgdir}"/usr/src/nvidia-${pkgver}/conftest.sh

# License
  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s nvidia "${pkgdir}"/usr/share/licenses/${pkgname}
}

# vim: ts=2 sw=2 et:
