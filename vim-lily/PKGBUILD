# Maintainer: lilydjwg <lilydjwg@gmail.com>

pkgbase=vim-lily
pkgname=(gvim-lily vim-runtime-lily vim-lily)
pkgver=8.1.513
pkgrel=1
_versiondir="vim$(echo $pkgver | awk -F. '{print $1$2}')"
arch=('i686' 'x86_64')
license=('custom:vim')
url="http://www.vim.org"
makedepends=('gpm' 'gtk2' 'perl' 'python2' 'python' 'ruby' 'gettext'
             'pkg-config' 'sed' 'luajit' 'git' 'tar' 'desktop-file-utils' 'libxt')
source=('tmpfiles.conf'
        'vimdoc.hook'
        "vim-repo::git://localhost/vim")
sha1sums=('f4d0ccb87d0add6bd76280ed82f401e9a91aabc5'
          'adc4c82b6c4097944e5a767270a772721455eb8c'
          'SKIP')
options=('debug')

pkgver() {
  cd vim-repo
  echo 8.1.$(sed -n '/ included_patches\[\]/,+3p' src/version.c | sed '/[^,]$/d;s/\s*\|,//g')
}

build() {
  cd vim-repo

  # define the place for the global (g)vimrc file (set to /etc/vimrc)
  sed -i 's|^.*\(#define SYS_.*VIMRC_FILE.*"\) .*$|\1|' \
    src/feature.h
  sed -i 's|^.*\(#define VIMRC_FILE.*"\) .*$|\1|' \
    src/feature.h

  for name in vim gvim; do
    mkdir -p "${srcdir}"/$name-build
    git archive all | tar x -C ../$name-build
  done

  cd "${srcdir}"/vim-build
  rm -f src/auto/config.cache
  make clean || true
  make distclean || true
  CFLAGS='-g -O2' STRIP=true ./configure --prefix=/usr --localstatedir=/var/lib/vim \
    --mandir=/usr/share/man --with-luajit \
    --enable-acl --disable-gui --without-x --disable-fontset --disable-xsmp \
    --enable-python3interp --enable-rubyinterp=dynamic --enable-pythoninterp \
    --enable-luainterp=dynamic --enable-perlinterp=dynamic \
    --enable-cscope --enable-multibyte --with-features=huge \
    --enable-terminal \
    --enable-fail-if-missing \
    --with-compiledby=百合仙子 --with-modified-by=百合仙子

  msg2 'Building...'
  make

  sleep 1
  msg2 'Rebuilding...'
  sed -i 's/compiled_sys.*/compiled_sys = (char_u *)"次美好的百合森林";/' src/auto/pathdef.c
  make

  cd "${srcdir}"/gvim-build
  rm -f src/auto/config.cache
  make clean || true
  make distclean || true
  CFLAGS='-g -O2' STRIP=true ./configure --prefix=/usr --localstatedir=/var/lib/vim \
    --mandir=/usr/share/man --with-luajit \
    --enable-acl --enable-gui=gtk2 --enable-fontset --enable-xsmp \
    --enable-python3interp --enable-rubyinterp=dynamic --enable-pythoninterp \
    --enable-luainterp=dynamic --enable-perlinterp=dynamic \
    --enable-cscope --enable-multibyte --with-features=huge \
    --enable-terminal \
    --enable-fail-if-missing \
    --with-compiledby=百合仙子 --with-modified-by=百合仙子

  msg2 'Building...'
  make

  sleep 1
  msg2 'Rebuilding...'
  sed -i 's/compiled_sys.*/compiled_sys = (char_u *)"次美好的百合森林";/' src/auto/pathdef.c
  make

}

package_vim-lily() {
  pkgdesc='Vi Improved, a highly configurable, improved version of the vi text editor with additional scripting support'
  depends=('gpm' 'vim-runtime-lily' 'acl')
  optdepends=('luajit' 'ruby' 'python' 'python2' 'perl')
  conflicts=('vim' 'gvim' 'vim-python3' 'gvim-python3' 'vim-minimal')
  provides=("vim=${pkgver}-${pkgrel}" "vim-python3=${pkgver}-${pkgrel}")

  cd "${srcdir}"/vim-build
  make -j1 VIMRCLOC=/etc DESTDIR="${pkgdir}" install

  # provided by (n)vi in core
  rm ${pkgdir}/usr/bin/{ex,view}

  cd ${pkgdir}/usr/share/vim/${_versiondir}/lang
  for i in *utf-8*; do
    ln -s $i `echo $i|tr -d '-'`
  done

  # delete some manpages
  find ${pkgdir}/usr/share/man -type d -name 'man1' 2>/dev/null | \
    while read _mandir; do
    cd ${_mandir}
    rm -f ex.1 view.1 # provided by (n)vi
  done

  # Runtime provided by runtime package
  rm -r "${pkgdir}"/usr/share/vim

  # license
  install -Dm644 "${srcdir}"/vim-repo/runtime/doc/uganda.txt \
    "${pkgdir}"/usr/share/licenses/${pkgname}/license.txt
  install -D -m644 "${srcdir}"/tmpfiles.conf "${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf"
}

package_gvim-lily() {
  pkgdesc='Vi Improved, a highly configurable, improved version of the vi text editor (with advanced features, such as a GUI) with additional scripting support'
  depends=('gpm' 'gtk2' 'vim-runtime-lily' 'desktop-file-utils' 'libxt')
  optdepends=('luajit' 'ruby' 'python' 'python2' 'perl')
  conflicts=('vim' 'gvim' 'vim-python3' 'gvim-python3' 'vim-minimal')
  provides=("vim=${pkgver}-${pkgrel}" "gvim=${pkgver}-${pkgrel}" "gvim-python3=${pkgver}-${pkgrel}")

  cd "${srcdir}"/gvim-build
  make -j1 VIMRCLOC=/etc DESTDIR="${pkgdir}" install

  # provided by (n)vi in core
  rm ${pkgdir}/usr/bin/{ex,view}

  cd ${pkgdir}/usr/share/vim/${_versiondir}/lang
  for i in *utf-8*; do
    ln -s $i `echo $i|tr -d '-'`
  done

  # delete some manpages
  find ${pkgdir}/usr/share/man -type d -name 'man1' 2>/dev/null | \
    while read _mandir; do
    cd ${_mandir}
    rm -f ex.1 view.1 # provided by (n)vi
  done

  # Move the runtime for later packaging
  mv "${pkgdir}"/usr/share/vim "${srcdir}"/runtime-install

  # freedesktop links
  install -Dm644 "${srcdir}"/vim-repo/runtime/vim.desktop "${pkgdir}"/usr/share/applications/vim.desktop
  install -Dm644 "${srcdir}"/vim-repo/runtime/gvim.desktop "${pkgdir}"/usr/share/applications/gvim.desktop
  install -Dm644 "${srcdir}"/vim-repo/runtime/vim48x48.png "${pkgdir}"/usr/share/pixmaps/gvim.png

  # license
  install -Dm644 "${srcdir}"/vim-repo/runtime/doc/uganda.txt \
    "${pkgdir}"/usr/share/licenses/${pkgname}/license.txt
  install -D -m644 "${srcdir}"/tmpfiles.conf "${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf"
}

package_vim-runtime-lily() {
  if [ ! -d "${srcdir}"/runtime-install ]; then
    package_gvim-lily
    rm -rf "$pkgdir"
  fi

  # set variables late
  pkgdesc='Runtime for vim and gvim'
  arch=('any')
  conflicts=('vim-runtime')
  provides=("vim-runtime=${pkgver}-${pkgrel}")
  # reset
  depends=()
  optdepends=()

  # Install the runtime split from gvim
  install -dm755 "${pkgdir}"/usr/share
  mv "${srcdir}"/runtime-install "${pkgdir}"/usr/share/vim

  # Don't forget logtalk.dict
  install -Dm644 "${srcdir}"/gvim-build/runtime/ftplugin/logtalk.dict \
    "${pkgdir}"/usr/share/vim/${_versiondir}/ftplugin/logtalk.dict

  # fix FS#17216
  sed -i 's|messages,/var|messages,/var/log/messages.log,/var|' \
    "${pkgdir}"/usr/share/vim/${_versiondir}/filetype.vim

  # patch filetype.vim for better handling of pacman related files
  sed -i "s/rpmsave/pacsave/;s/rpmnew/pacnew/;s/,\*\.ebuild/\0,PKGBUILD*,*.install/" \
    "${pkgdir}"/usr/share/vim/${_versiondir}/filetype.vim
  sed -i "/find the end/,+3{s/changelog_date_entry_search/changelog_date_end_entry_search/}" \
    "${pkgdir}"/usr/share/vim/${_versiondir}/ftplugin/changelog.vim

  # rgb.txt file
  install -Dm644 "${srcdir}"/vim-repo/runtime/rgb.txt \
    "${pkgdir}"/usr/share/vim/${_versiondir}/rgb.txt

  # license
  install -dm755 "${pkgdir}"/usr/share/licenses/vim-runtime
  ln -s /usr/share/vim/${_versiondir}/doc/uganda.txt \
    "${pkgdir}"/usr/share/licenses/vim-runtime/license.txt

  # The official package puts it in vim-runtime so we have to do the same to avoid conflicts
  install -Dm644 "${srcdir}"/vimdoc.hook "${pkgdir}"/usr/share/libalpm/hooks/vimdoc.hook
}

# vim:set sw=2 sts=2 et ft=sh:
