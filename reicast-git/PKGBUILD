# Maintainer: carstene1ns <arch carsten-teibes de> - http://git.io/ctPKG
# Contributor: Holzhaus <jan.holthuis@ruhr-uni-bochum.de>

pkgname=reicast-git
pkgver=r2360.4b24d223
pkgrel=1
pkgdesc="A multiplatform Sega Dreamcast emulator (development version)"
arch=('i686' 'x86_64' 'armv7h')
url="http://reicast.com/"
license=('GPL2' 'BSD' 'MIT')
conflicts=('reicast')
provides=('reicast')
makedepends=('git')
depends=('libgl' 'alsa-lib' 'python')
source=(reicast::"git+https://github.com/reicast/reicast-emulator.git")
md5sums=('SKIP')

pkgver() {
  cd reicast
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build () {
  make -C reicast/shell/linux PREFIX=/usr
}

package () {
  make -C reicast/shell/linux DESTDIR="$pkgdir/" PREFIX=/usr install
}
