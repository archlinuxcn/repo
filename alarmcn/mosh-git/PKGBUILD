pkgname=mosh-git
epoch=1
pkgver=1.4.0.r3.gc16108f
pkgrel=1
pkgdesc='Mobile shell, surviving disconnects with local echo and line editing. Git version'
arch=(x86_64 aarch64)
url='https://mosh.org/'
license=(GPL3)
depends=(libutempter openssh openssl perl-io-tty protobuf)
makedepends=(git)
conflicts=(mosh)
provides=(mosh)
options=(!emptydirs)
source=("git+https://github.com/mobile-shell/mosh")
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/mosh"
  git describe | sed -E 's/([^-]*-g)/r\1/;s/-/./g;s/^mosh\.//'
}

build() {
  cd "$srcdir/mosh"
  ./autogen.sh
  ./configure --enable-ufw --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "$srcdir/mosh"
  DESTDIR="$pkgdir" make install
  install -Dm644 "conf/bash-completion/completions/mosh" \
    "$pkgdir/usr/share/bash-completion/completions/mosh"
}
