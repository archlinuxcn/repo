# Maintainer: Chih-Hsuan Yen <yan12125@gmail.com>
# Maintainer: Integral <integral@member.fsf.org>

pkgname=apfsprogs-git
_pkgname=${pkgname%-git}
pkgver=0.2.1.r0.g3721463
pkgrel=1
pkgdesc="Experimental APFS tools for linux"
arch=('x86_64' 'aarch64' 'riscv64')
license=('GPL-2.0-only')
url="https://github.com/linux-apfs/${_pkgname}"
makedepends=('git')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=("git+${url}.git")
sha256sums=('SKIP')
_progs=('mkapfs' 'apfs-snap' 'apfsck' 'apfs-label')

pkgver() {
	cd "${_pkgname}/"
	git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/v//'
}

build() {
	cd "${_pkgname}/"

	for prog in "${_progs[@]}"; do
		make -C "${prog}"
	done
}

package() {
	cd "${_pkgname}/"

	for prog in "${_progs[@]}"; do
		make DESTDIR="${pkgdir}" BINDIR=/usr/bin MANDIR=/usr/share/man/man8/ -C "${prog}" install
	done
}
