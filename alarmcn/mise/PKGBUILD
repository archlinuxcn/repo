# Maintainer: Integral <integral@member.fsf.org>
# Contributor: Jeff Dickey <releases at mise dot jdx dot dev>

pkgname=mise
pkgver=2025.2.1
pkgrel=1
pkgdesc="The front-end to your dev env"
arch=('x86_64' 'aarch64' 'riscv64')
url="https://github.com/jdx/${pkgname}"
license=('MIT')
depends=('openssl' 'bzip2' 'zlib')
makedepends=('cargo')
optdepends=('usage: completion support')
conflicts=('rtx' 'rtx-bin')
replaces=('rtx')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha256sums=('9fe245786b34eca166f2001358f8ddac9632c38739fdc559f378d2536fff559e')
options=('!lto')

prepare() {
	cd "${pkgname}-${pkgver}/"
	cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
	cd "${pkgname}-${pkgver}/"
	cargo build --frozen --release --all-features
}

check() {
	cd "${pkgname}-${pkgver}/"
	cargo test --frozen --all-features
}

package() {
	cd "${pkgname}-${pkgver}/"
	install -Dm755 "target/release/${pkgname}" -t "${pkgdir}/usr/bin/"
	install -Dm644 "man/man1/${pkgname}.1" -t "${pkgdir}/usr/share/man/man1/"
	install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"

	install -d "${pkgdir}/usr/share/doc/${pkgname}/"
	cp -r docs/* "${pkgdir}/usr/share/doc/${pkgname}/"

	# Delete broken symlink
	rm "${pkgdir}/usr/share/doc/mise/settings.toml"

	cd completions
	install -Dm644 "${pkgname}.bash" "${pkgdir}/usr/share/bash-completion/completions/mise"
	install -Dm644 "${pkgname}.fish" -t "${pkgdir}/usr/share/fish/vendor_completions.d/"
	install -Dm644 "_${pkgname}" -t "$pkgdir/usr/share/zsh/site-functions/"
}
