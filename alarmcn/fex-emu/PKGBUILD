# Maintainer: Eric Engestrom <aur@engestrom.ch>

pkgname=fex-emu
pkgver=2509
pkgrel=1
pkgdesc='Fast usermode x86 and x86-64 emulator for Arm64'
url=https://fex-emu.com
arch=(aarch64)
license=(MIT)
makedepends=(git cmake ninja clang llvm python-setuptools)
depends=(qt5-declarative qt5-quickcontrols squashfs-tools squashfuse erofs-utils)
source=("git+https://github.com/FEX-Emu/FEX#tag=FEX-$pkgver"
        "git+https://github.com/catchorg/Catch2"
        "git+https://github.com/KhronosGroup/Vulkan-Headers"
        "git+https://github.com/Sonicadvance1/cpp-optparse"
        "git+https://github.com/FEX-Emu/drm-headers"
        "git+https://github.com/FEX-Emu/fex-gcc-target-tests-bins"
        "git+https://github.com/FEX-Emu/fex-gvisor-tests-bins"
        "git+https://github.com/FEX-Emu/fex-posixtest-bins"
        "git+https://github.com/fmtlib/fmt"
        "git+https://github.com/Sonicadvance1/imgui"
        "git+https://github.com/FEX-Emu/jemalloc"
        "git+https://github.com/FEX-Emu/robin-map"
        "git+https://github.com/FEX-Emu/vixl"
        "git+https://github.com/herumi/xbyak"
        "git+https://github.com/Cyan4973/xxhash"
        )
sha256sums=('b3c14621eb08f5752967684ebc7aa7869d14bc6cb32ce3679894e0522ce25cdd'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP')

prepare() {
  cd FEX
  git submodule init
  for f in \
    Catch2 \
    Vulkan-Headers \
    drm-headers \
    fex-gcc-target-tests-bins \
    fex-gvisor-tests-bins \
    fex-posixtest-bins \
    fmt \
    imgui \
    jemalloc \
    robin-map \
    vixl \
    xbyak \
    xxhash \
    ;
  do
    git config submodule."External/$f".url "$srcdir/$f"
  done

  git config submodule."External/jemalloc_glibc".url "$srcdir/jemalloc"

  for f in \
    cpp-optparse \
    ;
  do
    git config submodule."Source/Common/$f".url "$srcdir/$f"
  done

  git -c protocol.file.allow=always submodule update
}

build() {
  FEX_OPTIONS=()
  FEX_OPTIONS+=(-D ENABLE_LTO=True)
  FEX_OPTIONS+=(-D BUILD_TESTS=False)

  if command -V ld.mold &>/dev/null
  then
    FEX_OPTIONS+=(-D USE_LINKER=True)
  elif command -V ld.lld &>/dev/null
  then
    FEX_OPTIONS+=(-D USE_LINKER=lld)
  fi

  CC=clang \
  CXX=clang++ \
  cmake \
    -S FEX -B build -G Ninja \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D CMAKE_BUILD_TYPE=Release \
    -D FMT_INSTALL=False \
    "${FEX_OPTIONS[@]}"
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install

  install -Dm644 "$srcdir"/FEX/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
