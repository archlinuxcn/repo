# Maintainer: Jos Dehaes <jos.dehaes@gmail.com>
# Maintainer: Matthias Kurz <m.kurz@irregular.at>

_name=asahi-installer
pkgname=asahi-fwextract
pkgver=0.6.9
pkgrel=3
pkgdesc='Asahi Linux firmware extractor'
arch=('any')
url='http://asahilinux.org'
license=('MIT')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/AsahiLinux/${_name}/archive/refs/tags/v${pkgver}.tar.gz")
install=asahi-fwextract.install
sha256sums=('def0c0ad698fcdc8b8efaac4df6822ce16e1266ea7b8e774bcb414248dc4a618')
b2sums=('7e3cf000f92081169ba937fb03455d902b1d14fd1b7bce55718cb037e75e02fa984bf7d4e549dcb64bc01fddaa7977f3b3f6bfa5ceef357babb98820899f6f41')
depends=(python "asahi-scripts>=20230606-1" lzfse)
makedepends=(python-build python-installer python-wheel python-setuptools)

build() {
    cd "$_name-$pkgver"
    python -m build --wheel --no-isolation
}

package() {
    cd "$_name-$pkgver"
    python -m installer --destdir="$pkgdir" dist/*.whl
}
