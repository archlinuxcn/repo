# Maintainer: Hector Martin <marcan@marcan.st>

_name=asahi-installer
pkgname=asahi-fwextract
pkgver=0.5pre2
pkgrel=3
pkgdesc='Asahi Linux firmware extractor'
arch=('any')
url='http://asahilinux.org'
license=('MIT')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/AsahiLinux/${_name}/archive/refs/tags/v${pkgver}.tar.gz")
install=asahi-fwextract.install
sha256sums=('6a8569d023d14e5cde1f78486392920e99988ec65e808ab3f694d387e9543556')
b2sums=('628e687ae90df0f3eea0c2f2a81accb17bd892bb09dc08bb7f36c0f406dac9f2ec39fadb9a3b109725b4c8978d61bd42935859f40a9c596eb4c931f0eeed44b3')
depends=(python asahi-scripts lzfse)
makedepends=(python-build python-installer python-wheel python-setuptools)

build() {
    cd "$_name-$pkgver"
    python -m build --wheel --no-isolation
}

package() {
    cd "$_name-$pkgver"
    python -m installer --destdir="$pkgdir" dist/*.whl
}
