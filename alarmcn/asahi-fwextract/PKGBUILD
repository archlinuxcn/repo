# Maintainer: Jos Dehaes <jos.dehaes@gmail.com>
# Maintainer: Matthias Kurz <m.kurz@irregular.at>

_name=asahi-installer
pkgname=asahi-fwextract
pkgver=0.7.9
pkgrel=2
pkgdesc='Asahi Linux firmware extractor'
arch=('any')
url='http://asahilinux.org'
license=('MIT')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/AsahiLinux/${_name}/archive/refs/tags/v${pkgver}.tar.gz")
install=asahi-fwextract.install
sha256sums=('b41f39360ce6cd6de299c4f25cd69718530a5619b16fb7a1eed666083bd909e1')
b2sums=('1b6754652116da28d0ad3ba7c26f0d41f269da7c4d11bd776953088ba5765546add0e853106b1c39da5238be0f0e01e8be838630f0c03ec52c035aabbfaff68c')
depends=(python "asahi-scripts>=20230606-1" lzfse)
makedepends=(python-build python-installer python-wheel python-setuptools)

build() {
    cd "$_name-$pkgver"
    python -m build --wheel --no-isolation
}

package() {
    cd "$_name-$pkgver"
    python -m installer --destdir="$pkgdir" dist/*.whl
}
