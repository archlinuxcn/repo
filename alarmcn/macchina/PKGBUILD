# Maintainer: grtcdr <ba.tahaaziz@gmail.com>

pkgname=macchina
pkgver=6.2.1
pkgrel=1
pkgdesc="A system information fetcher, with an (unhealthy) emphasis on performance."
optdepends=('wmctrl: window manager support')
makedepends=('rust' 'cargo' 'libgit2' 'git')
arch=('x86_64' 'armv7h' 'aarch64')
license=('MIT')
url="https://github.com/Macchina-CLI/macchina"
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('87a38bde067fadd96615899d6a8b9efdb238a4bd3859008be47b3e4c2a02c607')
options=('!lto')

build() {
    cd "${pkgname}-${pkgver}/"
    cargo build --release --locked --target-dir=target
}

package() {
    cd "${pkgname}-${pkgver}/"
    install -Dm 755 "target/release/${pkgname}" -t "${pkgdir}/usr/bin"

    install -Dm 644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    install -Dm 644 "doc/${pkgname}.1" -t "${pkgdir}/usr/share/man/man1"
    install -Dm 644 "doc/${pkgname}.7" -t "${pkgdir}/usr/share/man/man7"

    install -d "${pkgdir}/usr/share/${pkgname}/themes"
    install -m 644 contrib/themes/* "${pkgdir}/usr/share/${pkgname}/themes"
}
