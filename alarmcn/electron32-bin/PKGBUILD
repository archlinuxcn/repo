# Maintainer: zxp19821005 <zxp19821005 at 163 dot com>
# Co-maintainer: Yurii Kolesnykov <root@yurikoles.com>
# Based on extra/electron* by
# Bruno Pagani <archange@archlinux.org>
# Caleb Maclennan <caleb@alerque.com>
_projectname=electron
_major=32
_pkgname="${_projectname}${_major}"
pkgname="${_pkgname}"-bin
_subver="3.3"
_pkgver="${_major}.${_subver}"
pkgver="${_pkgver/-/.}"
pkgrel=1
pkgdesc="Build cross platform desktop apps with web technologies â€” prebuilt"
arch=(
  'aarch64'
  'armv7h'
  'x86_64'
)
url='https://electronjs.org'
_ghurl="https://github.com/electron/electron"
license=(
  'MIT'
  'LicenseRef-custom'
)
provides=(
  "${_pkgname}=${pkgver}"
)
conflicts=("${_pkgname}")
source_aarch64=(
  "${_pkgname}-chromedriver-${pkgver}-aarch64.zip::${_ghurl}/releases/download/v${_pkgver}/chromedriver-v${_pkgver}-linux-arm64.zip"
  "${_pkgname}-${pkgver}-aarch64.zip::${_ghurl}/releases/download/v${_pkgver}/electron-v${_pkgver}-linux-arm64.zip"
)
source_armv7h=(
  "${_pkgname}-chromedriver-${pkgver}-armv7h.zip::${_ghurl}/releases/download/v${_pkgver}/chromedriver-v${_pkgver}-linux-armv7l.zip"
  "${_pkgname}-${pkgver}-armv7h.zip::${_ghurl}/releases/download/v${_pkgver}/electron-v${_pkgver}-linux-armv7l.zip"
)
source_x86_64=(
  "${_pkgname}-chromedriver-${pkgver}-x86_64.zip::${_ghurl}/releases/download/v${_pkgver}/chromedriver-v${_pkgver}-linux-x64.zip"
  "${_pkgname}-${pkgver}-x86_64.zip::${_ghurl}/releases/download/v${_pkgver}/electron-v${_pkgver}-linux-x64.zip"
)
sha256sums_aarch64=('2535230e99cf356285c19af32586940e3416c22546bea0b38d71e6dd2579fa8f'
                    '6b18f435855284852be2b2c1b49e58df380a56784d78b358a13ea77bbace4a8a')
sha256sums_armv7h=('bda58c490006c5df0e5a87038e38943ec2e8e222496bfb1ac590f4732e5ffcf6'
                   'a067329d55cc6648e9f783fd8b01b93da45ac21892b8096d758b30a87505c1a7')
sha256sums_x86_64=('adfb974ccb29edc2365b244e3c4d06dae6a0bd4f5cf280ff62271959a8aad10e'
                   '0e1f1540492e48e3b8805f87c5096c3b99995c4c1b581ee57e9c836538bae813')
package() {
  depends=(
    'alsa-lib'
    'gtk3'
    'nss'
  )
  optdepends=(
    'kde-cli-tools: file deletion support (kioclient5)'
    'pipewire: WebRTC desktop sharing under Wayland'
    'qt6-base: enable Qt6 with --enable-features=AllowQt'
    'gtk4: for --gtk-version=4 (GTK4 IME might work better on Wayland)'
    'trash-cli: file deletion support (trash-put)'
    "xdg-utils: open URLs with desktop's default (xdg-email, xdg-open)"
  )
  install -Dm755 -d "${pkgdir}/usr/"{bin,lib/"${_pkgname}"}
  find "${srcdir}" -mindepth 1 -maxdepth 1 -type f ! -name "*.zip" ! -name "LICENSE*" -exec cp -r --no-preserve=ownership --preserve=mode -t "${pkgdir}/usr/lib/${_pkgname}/." {} +
  cp -r --no-preserve=ownership --preserve=mode "${srcdir}/"{locales,resources} "${pkgdir}/usr/lib/${_pkgname}"
  chmod u+s "${pkgdir}/usr/lib/${_pkgname}/chrome-sandbox"
  ln -nfs "/usr/lib/${_pkgname}/${_projectname}" "${pkgdir}/usr/bin/${_pkgname}"
  install -Dm644 "${srcdir}/LICENSE"* -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
