# Maintainer: zxp19821005 <zxp19821005 at 163 dot com>
# Co-maintainer: Yurii Kolesnykov <root@yurikoles.com>
# Based on extra/electron* by
# Bruno Pagani <archange@archlinux.org>
# Caleb Maclennan <caleb@alerque.com>
_projectname=electron
_major=32
_pkgname="${_projectname}${_major}"
pkgname="${_pkgname}"-bin
_subver="0.1"
_pkgver="${_major}.${_subver}"
pkgver="${_pkgver/-/.}"
pkgrel=1
pkgdesc="Build cross platform desktop apps with web technologies â€” prebuilt"
arch=(
  'aarch64'
  'armv7h'
  'x86_64'
)
url='https://electronjs.org'
_ghurl="https://github.com/electron/electron"
license=(
  'MIT'
  'LicenseRef-custom'
)
provides=(
  "${_pkgname}=${pkgver}"
)
conflicts=("${_pkgname}")
source_aarch64=(
  "${_pkgname}-chromedriver-${pkgver}-aarch64.zip::${_ghurl}/releases/download/v${_pkgver}/chromedriver-v${_pkgver}-linux-arm64.zip"
  "${_pkgname}-${pkgver}-aarch64.zip::${_ghurl}/releases/download/v${_pkgver}/electron-v${_pkgver}-linux-arm64.zip"
)
source_armv7h=(
  "${_pkgname}-chromedriver-${pkgver}-armv7h.zip::${_ghurl}/releases/download/v${_pkgver}/chromedriver-v${_pkgver}-linux-armv7l.zip"
  "${_pkgname}-${pkgver}-armv7h.zip::${_ghurl}/releases/download/v${_pkgver}/electron-v${_pkgver}-linux-armv7l.zip"
)
source_x86_64=(
  "${_pkgname}-chromedriver-${pkgver}-x86_64.zip::${_ghurl}/releases/download/v${_pkgver}/chromedriver-v${_pkgver}-linux-x64.zip"
  "${_pkgname}-${pkgver}-x86_64.zip::${_ghurl}/releases/download/v${_pkgver}/electron-v${_pkgver}-linux-x64.zip"
)
sha256sums_aarch64=('79d8a68e363357d578322290dc3d1544b429f6b86dce8f7b26fa8e6b2ede15a9'
                    'a4f2e36b22a8aebd385a21012cfdb480de781240aafe5ddc4248565a10579242')
sha256sums_armv7h=('6de4a0d2141ef592fab3f98e30856ce2fc12adaff3debbf2f8c5f9c9778f7422'
                   'fb28562242c38dac9ec2c7d3bc24993482174662d78820d2bffeb1cf8e72b7a7')
sha256sums_x86_64=('be87aaeb50372c6bc4686e3b0b5d263025501a59ad940497da1ef9d4af3db30b'
                   '97d91b496a5eb6faf617d754fe6d6cdae29bf5684463c2b1b3a8c9b106977b85')
package() {
  depends=(
    'alsa-lib'
    'gtk3'
    'nss'
  )
  optdepends=(
    'kde-cli-tools: file deletion support (kioclient5)'
    'pipewire: WebRTC desktop sharing under Wayland'
    'qt6-base: enable Qt6 with --enable-features=AllowQt'
    'gtk4: for --gtk-version=4 (GTK4 IME might work better on Wayland)'
    'trash-cli: file deletion support (trash-put)'
    "xdg-utils: open URLs with desktop's default (xdg-email, xdg-open)"
  )
  install -Dm755 -d "${pkgdir}/usr/"{bin,lib/"${_pkgname}"}
  find "${srcdir}" -mindepth 1 -maxdepth 1 -type f ! -name "*.zip" ! -name "LICENSE*" -exec cp -r --no-preserve=ownership --preserve=mode -t "${pkgdir}/usr/lib/${_pkgname}/." {} +
  cp -r --no-preserve=ownership --preserve=mode "${srcdir}/"{locales,resources} "${pkgdir}/usr/lib/${_pkgname}"
  chmod u+s "${pkgdir}/usr/lib/${_pkgname}/chrome-sandbox"
  ln -nfs "/usr/lib/${_pkgname}/${_projectname}" "${pkgdir}/usr/bin/${_pkgname}"
  install -Dm644 "${srcdir}/LICENSE"* -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
