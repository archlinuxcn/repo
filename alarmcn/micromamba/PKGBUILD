# Maintainer: Guillaume Dolle  <dev at gdolle.com>
pkgname=micromamba
pkgver=2.0.3
pkgrel=1
pkgdesc="C++ tiny version of mamba, the fast conda package installer"
arch=('aarch64' 'armv7h' 'i686' 'x86_64')
url="https://github.com/mamba-org/mamba"
license=('MIT')
depends=('libarchive.so' 'libcurl.so' 'libsolv' 'libfmt.so' 'libsimdjson.so'
         'libspdlog.so' 'libyaml-cpp.so' 'libreproc.so' 'libreproc++.so')
makedepends=('gtest' 'termcolor' 'cmake' 'cli11' 'nlohmann-json'
             'ghc-filesystem' 'tl-expected' 'python')
source=(micromamba-${pkgver}.tar.gz::https://github.com/mamba-org/mamba/archive/refs/tags/micromamba-${pkgver}.tar.gz)
sha256sums=('564e9ffc91015e8b1699f2de1baa59449ab037f27e4c19355680a0f145fd2b9d')
provides=('micromamba')
conflicts=('micromamba' 'micromamba-bin')

build() {
  cd mamba-micromamba-${pkgver}

  cmake -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_POSITION_INDEPENDENT_CODE=ON \
        -DBUILD_LIBMAMBA=ON \
        -DBUILD_LIBMAMBAPY=OFF \
        -DBUILD_LIBMAMBA_TESTS=OFF \
        -DBUILD_MAMBA_PACKAGE=OFF \
        -DBUILD_MAMBA=ON \
        -DBUILD_SHARED=ON \
        -DBUILD_STATIC=OFF \
        -B build
  cmake --build build
}

check() {
  cd mamba-micromamba-${pkgver}/build
  ctest
}

package() {
  cd mamba-micromamba-${pkgver}
  DESTDIR="${pkgdir}" cmake --install build

  mv "${pkgdir}"/usr/bin/{mamba,micromamba}
}
