# Maintainer: Guillaume Dolle  <dev at gdolle.com>
pkgname=micromamba
pkgver=2.3.2
pkgrel=2
pkgdesc="C++ tiny version of mamba, the fast conda package installer"
arch=('aarch64' 'armv7h' 'i686' 'x86_64')
url="https://github.com/mamba-org/mamba"
license=('MIT')
depends=('libarchive.so' 'libcurl.so' 'libsolv' 'fmt' 'libsimdjson.so'
         'libspdlog.so' 'libyaml-cpp.so' 'libreproc.so' 'libreproc++.so')
makedepends=('gtest' 'termcolor' 'cmake' 'cli11' 'nlohmann-json'
             'ghc-filesystem' 'tl-expected' 'python')
source=(micromamba-${pkgver}.tar.gz::https://github.com/mamba-org/mamba/archive/refs/tags/${pkgver}.tar.gz)
sha256sums=('c969d189b0263218467b9e3b8922fcad8f7023bd8b5a981edc37e0da27cf953f')
conflicts=('micromamba-bin')

build() {
  cd mamba-${pkgver}

  cmake -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_POSITION_INDEPENDENT_CODE=ON \
        -DBUILD_LIBMAMBA=ON \
        -DBUILD_LIBMAMBAPY=OFF \
        -DBUILD_LIBMAMBA_TESTS=OFF \
        -DBUILD_MAMBA_PACKAGE=OFF \
        -DBUILD_MAMBA=ON \
        -DBUILD_SHARED=ON \
        -DBUILD_STATIC=OFF \
        -DMAMBA_WARNING_AS_ERROR=OFF \
        -B build
  cmake --build build
}

# check() {
#   cd mamba-${pkgver}/build
#   ctest
# }

package() {
  depends+=('libfmt.so')
  cd mamba-${pkgver}
  DESTDIR="${pkgdir}" cmake --install build

  mv "${pkgdir}"/usr/bin/{mamba,micromamba}
}
