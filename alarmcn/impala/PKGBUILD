# Maintainer: orhun <orhunparmaksiz@gmail.com>

pkgname=impala
pkgver=0.2.3
pkgrel=1
pkgdesc="TUI for managing wifi"
url="https://github.com/pythops/${pkgname}"
depends=('gcc-libs' 'iwd')
makedepends=('cargo')
arch=('x86_64' 'aarch64' 'riscv64')
license=('GPL-3.0-or-later')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha512sums=('74ff0c213b11614a0bb632dd88cc2f66753e2bab0a3c0a3e798bd658336d1fa3a0b90d06aaa8b182e17fbae2eb6053fe64506294032d47a1f6da4ff64b36ce16')

prepare() {
	cd "${pkgname}-${pkgver}/"
	cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
	cd "${pkgname}-${pkgver}/"
	cargo build --release --frozen
}

check() {
	cd "${pkgname}-${pkgver}/"
	cargo test --frozen
}

package() {
	cd "${pkgname}-${pkgver}/"
	install -Dm755 "target/release/${pkgname}" -t "${pkgdir}/usr/bin/"
	install -Dm644 Readme.md -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
