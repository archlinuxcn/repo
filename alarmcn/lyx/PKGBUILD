# Maintainer: Carlos Aznar√°n <caznaranl@uni.pe>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: Jason Chu <jason@archlinux.org>
pkgname=lyx
pkgver=2.5.0
pkgrel=2
pkgdesc="An advanced WYSIWYM document processor & LaTeX front-end"
arch=(aarch64 x86_64)
url="https://www.lyx.org"
depends=(qt6-svg python imagemagick enchant hunspell libmythes
         file hicolor-icon-theme desktop-file-utils)
makedepends=(git boost bc)
optdepends=('rcs: built-in version control system'
            'texlive-latexextra: float wrap support'
            'python: support for python scripts')
license=(GPL-2.0-or-later)
backup=('etc/lyx/lyxrc.dist')
options=('emptydirs')
source=("git+https://codeberg.org/LyX-org/lyx.git#tag=$pkgver"
        lyxrc.dist)
sha512sums=('d6bd2ae76f00f323ff06eebfe5809a412d41a38eea89096328e36c71b01b1d1a1c74bf55df8a6b4fc8342280c13e30764e4aff55610c12b126681733e3927439'
            'eef777cf6033a7b1e04700f33068b07309f8d5c6931c16927305dafb3a00fd46d4b536149349ab56b7455e7dea195c8889da2b6fbf9caa9e76bc0557f9358bc3')

build() {
  cd lyx
  ./autogen.sh
  ./configure \
    --prefix=/usr \
    --enable-qt6 \
    --without-included-boost \
    --without-included-mythes
  make
}

package() {
  cd lyx
  make DESTDIR="${pkgdir}" install

  # install default config file
  install -Dm644 "${srcdir}/lyxrc.dist" "${pkgdir}/etc/lyx/lyxrc.dist"
  ln -sf /etc/lyx/lyxrc.dist "${pkgdir}/usr/share/lyx/lyxrc.dist"
}
