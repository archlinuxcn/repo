# Maintainer: Linus Dierheimer <Linus@Dierheimer.de>

pkgname=fastfetch-git
_pkgname=${pkgname%-git}
pkgver=2.8.3.r19.g5fb4b6d
pkgrel=1
pkgdesc="Like neofetch, but much faster because written in c"
arch=('x86_64' 'aarch64' 'riscv64')
url="https://github.com/${_pkgname}-cli/${_pkgname}"
license=('MIT')
makedepends=(
	"git"
	"cmake"
	"pciutils"
	"vulkan-headers"
	"vulkan-icd-loader"
	"wayland"
	"libxcb"
	"libxrandr" # Depends on libX11, which headers are also needed
	"dconf"     # Depends on glib2, which headers are also needed
	"imagemagick"
	"chafa"
	"zlib"
	"dbus"
	"xfconf"
	"mesa"
	"opencl-headers"
	"ocl-icd"
)
optdepends=(
	"pciutils: GPU output"
	"libdrm: Improved AMD GPU output"
	"vulkan-icd-loader: GPU fallback"
	"wayland: Improved Wayland support (resolution + multi monitor)"
	"libxcb: Improved X11 support (resolution + multi monitor)"
	"libx11: Improved X11 support (resolution)"
	"libxrandr: Improved X11 support (resolution + multi monitor)"
	"glib2: Output for values that are only stored in GSettings"
	"dconf: Output for values that are only stored in DConf"
	"dbus: Media player and song output"
	"imagemagick: Image output using sixel or kitty graphics protocol"
	"chafa: Image output as ascii art"
	"zlib: Faster image output when using kitty graphics protocol"
	"xfconf: XFWM theme + xfce-terminal font"
	"mesa: OpenGL module"
	"ocl-icd: OpenCL module"
	"nix: self contained nix package count"
)

provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=("git+${url}.git")
sha256sums=('SKIP')

pkgver() {
	cd "${_pkgname}/"
	git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
	cmake -B build \
		-S "${_pkgname}/" \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DENABLE_SQLITE3=OFF \
		-DENABLE_RPM=OFF \
		-DENABLE_IMAGEMAGICK6=OFF \
		-DENABLE_LIBCJSON=OFF \
		-Wno-dev

	cmake --build build
}

package() {
	DESTDIR="${pkgdir}" cmake --install build
	install -Dm644 "${_pkgname}/LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
