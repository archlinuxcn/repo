# Maintainer: Morteza NourelahiAlamdari <m at 0t1.me>
# Contributor: Andy Weidenbaum <archbaum at gmail.com>
# Contributor: Brian Knox <taotetek at gmail.com>

pkgname=czmq-git
_pkgname=${pkgname%-git}
pkgver=4.2.1.r164.gf22f6572
pkgrel=1
epoch=1
pkgdesc="High-level C binding for ZeroMQ"
arch=('i686' 'x86_64' 'aarch64')
depends=('zeromq' 'curl' 'lz4' 'systemd-libs' 'util-linux-libs')
makedepends=('git')
url="https://github.com/zeromq/${_pkgname}"
license=('MPL-2.0')
source=("git+${url}")
sha256sums=('SKIP')
provides=("${_pkgname}=${pkgver%.r*}" libczmq.so)
conflicts=("${_pkgname}")

pkgver() {
	git -C "${_pkgname}" describe --long --abbrev=7 | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
	cd "${_pkgname}/"

	./autogen.sh
	./configure \
		--prefix=/usr \
		--sbindir=/usr/bin \
		--libexecdir=/usr/lib/czmq \
		--sysconfdir=/etc \
		--sharedstatedir=/usr/share/czmq \
		--localstatedir=/var/lib/czmq \
		--disable-czmq_selftest \
		--disable-zmakecert \
		--with-gnu-ld \
		--with-nss=no

	make
}

package() {
	cd "${_pkgname}/"
	make DESTDIR="${pkgdir}" install
}
