# Maintainer: dbgbgtf <dudududumaxver@outlook.com>
# Maintainer: RocketDev <ma2014119@outlook.com>
pkgname=ceccomp
pkgver=3.5
pkgrel=1
pkgdesc="A C-based seccomp analysis tool"
arch=(x86_64 aarch64)
url="https://github.com/dbgbgtf1/Ceccomp"
license=('GPL-3.0-or-later')
depends=(
    libseccomp
    glibc
)
makedepends=(
    asciidoctor # for doc generation
    python # for configure
    git
    util-linux # for flock to display a progress
    gettext # for i18n/l10n
)

source=("$pkgname"::git+https://github.com/dbgbgtf1/Ceccomp.git#tag=v${pkgver}?signed)
b2sums=('59391f15cd86625044fd16ce51506f9a7c1d92ffacea86445d3f7f4a98586fc9941eb0e83ac802f4300b48dca19ac6fa4a0d2af5f669aa42e4f76d60077d77ec')

validpgpkeys=(
    '0816A179BB09248F30468BD6542A0969B5CEDCDB' # dbgbgtf1 <dudududumaxver@outlook.com>
    'A7ACCC386C15E3C554D34B3EAB08F98092A456BB' # RocketDev <ma2014119@outlook.com>
)

build() {
    # lilac always has clean root build
    cd "$srcdir/$pkgname"
    ./configure --destdir="$pkgdir" --packager="Arch Linux CN"
    make
}

package() {
    cd "$srcdir/$pkgname"
    make install
}
