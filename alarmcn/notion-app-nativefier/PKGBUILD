# Maintainer: Alec Mev <alec@mev.earth>

pkgname=notion-app-nativefier
pkgver=2022.07.30
pkgrel=9
pkgdesc='Notion in shared Electron runtime'
arch=('x86_64' 'aarch64')
url='https://www.notion.so'
license=('MIT')
_nativefier=49
_electronv=19
depends=("electron${_electronv}")
makedepends=(
  'curl'
  'gendesk'
  'yarn'
)
source=(notion-app.png)
sha256sums=('18605a3db87a2dd2628cf15166a04e7f3449d3c1f9b501ee922750fc3ea24024')

_name=Notion

prepare() {
  cat > "${pkgname}" <<EOF
#!/usr/bin/env bash
exec electron${_electronv} /usr/share/${pkgname} "\$@"
EOF
  gendesk \
    --pkgname "${pkgname}" \
    --pkgdesc "${pkgdesc}" \
    --name "${_name}" \
    --categories "Network;Office;ProjectManagement;Utility;TextTools" \
    -n \
    -f
  cp notion-app.png "${pkgname}.png"
}

build() {
  yarn add "nativefier@${_nativefier}"
  yarn nativefier \
       --name "${_name}" \
       --icon "${pkgname}.png" \
       --user-agent safari \
       --single-instance \
       --verbose \
       --tray start-in-tray \
       https://www.notion.so
}

package() {
  mkdir -p "${pkgdir}/usr/share"
  local _x=`echo "${_name}-linux-"*`
  cp -r "${_x}/resources/app" "${pkgdir}/usr/share/${pkgname}"
  install -Dm755 -t "${pkgdir}/usr/bin/" "${pkgname}"
  install -Dm644 -t "${pkgdir}/usr/share/applications/" "${pkgname}.desktop"
  install -Dm644 -t "${pkgdir}/usr/share/licenses/${pkgname}" "${_x}/LICENSE"
  install -Dm644 -t "${pkgdir}/usr/share/pixmaps/" "${pkgname}.png"
}
