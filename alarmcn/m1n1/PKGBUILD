# Maintainer: Hector Martin <marcan@marcan.st>

pkgname=m1n1
pkgver=1.5.0
pkgrel=1
pkgdesc='Asahi Linux bootloader'
arch=('aarch64')
url='http://asahilinux.org'
license=('MIT')
makedepends=(git imagemagick)
source=(
  "git+https://github.com/AsahiLinux/m1n1#tag=v${pkgver}"
)
sha256sums=('1937b5bd4adff1d6734547f176144f574e80505b8690a83fbe8425502d47daf7')
b2sums=('4b0a8c5fde1e8747c98dacb379a16dfd603b5f830027a84af170fa9372d79bc486818002de0aa5399358685dcbd5d8bbef15de98db9407943221b8e597335e9a')

prepare() {
  cd m1n1
  git submodule update --init --recursive
}

build() {
  cd m1n1
  make ARCH= RELEASE=1 -j1
}

package() {
  install -Dt "$pkgdir/usr/lib/asahi-boot" -m644 "m1n1/build/m1n1.bin"
  install -Dm644 m1n1/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
