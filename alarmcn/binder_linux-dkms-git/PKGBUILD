# Maintainer: Sick Codes <info at sick dot codes>
# Maintainer: Christian Hoff <https://github.com/choff>
# Contributor: Tobias Martin <tm-x at gmx dot net>

pkgname=binder_linux-dkms-git
_pkgname=${pkgname%-git}
pkgver=r60.7e31e5d
pkgrel=1
arch=('any')
_srcname=anbox-modules
url="https://github.com/choff/${_srcname}"
pkgdesc="Android kernel driver fork by @choff in DKMS format, binder only"
license=('GPL-2.0-only')
conflicts=("${_pkgname}" "anbox-modules-dkms")
replaces=("${_pkgname}")
depends=('dkms')
makedepends=('git')
source=("git+${url}.git")
sha256sums=('SKIP')

pkgver() {
	cd "${_srcname}/"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

package() {
	install -dm755 "${pkgdir}/usr/src/binder-${pkgver}/"
	cp -r ${_srcname}/binder/* "${pkgdir}/usr/src/binder-${pkgver}/"
}
