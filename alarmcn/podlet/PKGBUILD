# Maintainer: Integral <integral@member.fsf.org>

pkgname=podlet
pkgver=0.3.1
pkgrel=1
pkgdesc="Generate Podman Quadlet files from a Podman command, compose file, or existing object"
arch=('x86_64' 'aarch64' 'riscv64')
url="https://github.com/containers/${pkgname}"
license=('MPL-2.0')
depends=('gcc-libs')
makedepends=('cargo')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha256sums=('9dd2ce4a618563f8cb607106eb8082744b228575627bcc5e144c9f076e4be691')

prepare() {
	cd "${pkgname}-${pkgver}/"
	cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
	cd "${pkgname}-${pkgver}/"
	cargo build --frozen --release --all-features
}

check() {
	cd "${pkgname}-${pkgver}/"
	cargo test --frozen --all-features
}

package() {
	cd "${pkgname}-${pkgver}/"
	install -Dm755 "target/release/${pkgname}" -t "${pkgdir}/usr/bin"
	install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
