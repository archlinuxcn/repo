#

pkgname=cubature-git
pkgver=v1.0.4.r3.g4ba70a7
pkgrel=1
pkgdesc='multi-dimensional adaptive integration (cubature) in C'
arch=(aarch64 armv7h i686 x86_64)
url='https://github.com/stevengj/cubature'
license=('GPLv2+')
makedepends=('git' 'cmake')
source=('git+https://github.com/stevengj/cubature')
sha1sums=('SKIP')
provides=('cubature')
conflicts=('cubature')

pkgver() {
  cd cubature
  git describe --long --tags | sed -r 's/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  cd cubature
  mkdir -p build

  cd build
  export CFLAGS+=' -g -flto -O3'
  export LDFLAGS+=' -g -flto -O3 -Wl,-Bsymbolic-functions'
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=lib \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_POLICY_VERSION_MINIMUM=3.5 ..
  make
}

package() {
  cd cubature/build
  make DESTDIR="${pkgdir}" install
}
