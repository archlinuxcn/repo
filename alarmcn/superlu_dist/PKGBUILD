# Maintainer: Carlos Aznar√°n <caznaranl@uni.pe>
# Maintainer: Gianluca Pettinello <g_pet at hotmail dot com>
# Contributor: Christian Pfeiffer <cpfeiffer at live dot de>
pkgname=superlu_dist
pkgver=9.2.0
pkgrel=1
pkgdesc="Distributed memory, MPI based SuperLU"
arch=(aarch64 x86_64)
url="https://github.com/xiaoyeli/superlu_dist"
license=('custom')
depends=(blas lapack parmetis libmpi.so)
optdepends=(python)
makedepends=(cmake gcc-fortran python)
provides=(libsuperlu_dist.so)
source=(superlu_dist-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz)
sha512sums=('9aba137cde807b468d6a3bbb405a7a8d227f6ddcc16965421f59781592dba6878da78609a71f1d73adcc5d7b70b355df1dabb7fd453bf79066b4118992bb24e8')
options=('!emptydirs')

build() {
  cmake \
    -S superlu_dist-${pkgver} \
    -B build \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -Denable_examples=OFF \
    -DBUILD_SHARED_LIBS=ON \
    -DCMAKE_CXX_STANDARD=14 \
    -DCMAKE_C_COMPILER=mpicc \
    -DCMAKE_CXX_COMPILER=mpicxx \
    -DXSDK_ENABLE_Fortran=ON \
    -Denable_doc=OFF \
    -Denable_single=ON \
    -Denable_double=ON \
    -DTPL_PARMETIS_INCLUDE_DIRS="/usr/include" \
    -DTPL_PARMETIS_LIBRARIES="/usr/lib/libparmetis.so" \
    -DTPL_BLAS_LIBRARIES="/usr/lib/libblas.so" \
    -DTPL_LAPACK_LIBRARIES="/usr/lib/liblapack.so" \
    -DTPL_ENABLE_LAPACKLIB=ON \
    -DTPL_ENABLE_PARMETISLIB=ON \
    -DTPL_ENABLE_COMBBLASLIB=OFF \
    -DTPL_ENABLE_CUDALIB=OFF \
    -DTPL_ENABLE_HIPLIB=OFF \
    -DTPL_ENABLE_INTERNAL_BLASLIB=OFF \
    -DCMAKE_INSTALL_INCLUDEDIR=include/superlu_dist \
    -Wno-dev
  cmake --build build --target all
}

package() {
  DESTDIR="${pkgdir}" cmake --build build --target install

  rm "${pkgdir}"/usr/lib/PYTHON/pddrive.py
  local _sitedir=$(python -c 'import site; print(site.getsitepackages()[0])')
  mkdir -p "${pkgdir}/${_sitedir}"
  mv "${pkgdir}"/usr/lib/PYTHON/* "${pkgdir}/${_sitedir}"
  rmdir "${pkgdir}"/usr/lib/PYTHON/

  python -m compileall "${pkgdir}/${_sitedir}" -s "${pkgdir}" -o 0 -o 1 -o 2

  install -Dm644 superlu_dist-${pkgver}/README.md \
          "${pkgdir}/usr/share/doc/superlu_dist/README.md"
  install -Dm644 superlu_dist-${pkgver}/DOC/ug.pdf \
          "${pkgdir}/usr/share/doc/superlu_dist/ug.pdf"
  install -Dm644 superlu_dist-${pkgver}/License.txt \
          "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
