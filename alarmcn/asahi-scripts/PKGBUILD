# Maintainer: Hector Martin <marcan@marcan.st>

pkgname=asahi-scripts
pkgver=20240623
pkgrel=2
pkgdesc='Asahi Linux maintenance scripts'
arch=('any')
url='http://asahilinux.org'
license=('MIT')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/AsahiLinux/${pkgname}/archive/refs/tags/${pkgver}.tar.gz")
install=asahi-scripts.install
sha256sums=('1431149b3bfd91468fe41d7e05062d8d5d0ce40edeab2b2e9983feee1af4570d')
b2sums=('b69a3d697ba60fc74bc41de8c850bda2427d039142e1d0a1b354a0fb63b74091f65298dc3b325f711a4743f5aabf3cc1a724a121045d49396ab014b32e5c9c2c')
backup=(etc/m1n1.conf)

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make PREFIX=/usr DESTDIR=${pkgdir} all
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make PREFIX=/usr DESTDIR=${pkgdir} install-arch

  install -Dm644 "$srcdir/${pkgname}-${pkgver}/LICENSE" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
