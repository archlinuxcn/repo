pkgname=libcxxwrap_julia
pkgver=0.14.9
_commit=51193e0f1d7f03cb081e57af0574e5952b9ec6b1
pkgrel=3
pkgdesc=" C++ library for backing CxxWrap.jl "
url="https://github.com/JuliaInterop/libcxxwrap-julia"
arch=('aarch64' 'x86_64')
license=('MIT')
makedepends=(git cmake)
depends=(julia-git)
source=("git+https://github.com/JuliaInterop/libcxxwrap-julia#commit=${_commit}")
sha256sums=('c3df011dee0b09921c8b0db9bac43bc7227b83dff4311baab90c037dd8d84161')
# LTO breaks unique linkage of guard variable
options=('!lto')

build() {
  cd libcxxwrap-julia

  mkdir -p build
  cd build
  export CXXFLAGS+=' -Djl_function_t=jl_value_t'
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DJulia_PREFIX=/usr \
        -DJLCXX_BUILD_TESTS=Off \
        -DJLCXX_BUILD_EXAMPLES=Off
  make
}

package() {
  cd libcxxwrap-julia/build

  make install DESTDIR="${pkgdir}"
}
