pkgname=libcxxwrap_julia
pkgver=0.14.5
pkgrel=1
pkgdesc=" C++ library for backing CxxWrap.jl "
url="https://github.com/JuliaInterop/libcxxwrap-julia"
arch=('aarch64' 'x86_64')
license=('MIT')
makedepends=(git cmake)
depends=(julia-git)
source=("git+https://github.com/JuliaInterop/libcxxwrap-julia#tag=v${pkgver}")
sha256sums=('3777d177f97913e2305adde190b56da33c2ab05fa4527993e4cd2e0a323e8cfd')
# LTO breaks unique linkage of guard variable
options=('!lto')

build() {
  cd libcxxwrap-julia

  mkdir -p build
  cd build
  export CXXFLAGS+=' -Djl_function_t=jl_value_t'
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DJulia_PREFIX=/usr \
        -DJLCXX_BUILD_TESTS=Off \
        -DJLCXX_BUILD_EXAMPLES=Off
  make
}

package() {
  cd libcxxwrap-julia/build

  make install DESTDIR="${pkgdir}"
}
