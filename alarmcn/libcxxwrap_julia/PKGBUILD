pkgname=libcxxwrap_julia
pkgver=0.14.7
pkgrel=2
pkgdesc=" C++ library for backing CxxWrap.jl "
url="https://github.com/JuliaInterop/libcxxwrap-julia"
arch=('aarch64' 'x86_64')
license=('MIT')
makedepends=(git cmake)
depends=(julia-git)
if [[ $pkgver == 0.14.7 ]]; then
  source=("git+https://github.com/JuliaInterop/libcxxwrap-julia#commit=a2c35fa39983bd9246eb7d9cda4a7a2369334799")
else
  source=("git+https://github.com/JuliaInterop/libcxxwrap-julia#tag=v${pkgver}")
fi
sha256sums=('e16b8b3273e7ae439628300052c12d9b1e8a5233fd5a076de61d7c2c8421aa3b')
# LTO breaks unique linkage of guard variable
options=('!lto')

build() {
  cd libcxxwrap-julia

  mkdir -p build
  cd build
  export CXXFLAGS+=' -Djl_function_t=jl_value_t'
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DJulia_PREFIX=/usr \
        -DJLCXX_BUILD_TESTS=Off \
        -DJLCXX_BUILD_EXAMPLES=Off
  make
}

package() {
  cd libcxxwrap-julia/build

  make install DESTDIR="${pkgdir}"
}
