# AArch64 Apple Silicon (Asahi Linux)
# Maintainer: Janne Grunau <j@jannau.net>

buildarch=8

_rcver=6.6
#_rcrel=3
_asahirel=15
pkgrel=2

_m1n1_version=1.4.11

pkgbase=linux-asahi
#_commit_id=asahi-${_nextver}-${_asahirel}
_commit_id=asahi-${_rcver}${_rcrel+-rc}${_rcrel}-${_asahirel}
_srcname=linux-${_commit_id}
_kernelname=${pkgbase#linux}
_desc="AArch64 Apple Silicon (M1 development kernel)"
pkgver="${_rcver}${_rcrel:+rc}${_rcrel}.asahi${_asahirel}"
arch=('aarch64')
url="http://www.kernel.org/"
license=('GPL2')

makedepends=(
  bc dtc kmod libelf pahole cpio perl rustup tar xz xmlto clang
)
options=('!strip')
source=(
  https://github.com/AsahiLinux/linux/archive/${_commit_id}.tar.gz
  config         # the main kernel config file
  config.edge    # overrides for linux-asahi-edge
)
sha256sums=('1d8d82dcfd87fb7153d81f52d01789be226c79637898de2b7407019ec7d35c8e'
            '46917e449d51ce37675442cd280892f6521b719bdb75a05b558d456ad7fc9193'
            '5cbdda1262f989a5247f4ef70690b569f9be8212bf8f576242bf6ac69036bddf')
b2sums=('f0cdce0421bb8637d52e82d2ff0b7e07c29e828783c7cb8733f1582916be81bfd14a0b6a98f51f633ee9da3d10aa1fff6b27c5779bd9fa55c1d04615173d6d4b'
        '8b3089d22d0b68e9b0c20a5e8f7ebe2626ef43cdc7053a94590ea3ca25417e76866e220ae98ff184e4912033d877eb89fd3c64bbe476889696220c5dd3b05e97'
        '01a8b3b7e3c7bd528f6d07d2b581b8a56688305d42fdd0b1555fd26319c4369e2357bcaa8434a1a5e3123aa9fdc6f1a24de4813b819397af89ba8d0c5e896228')
export KBUILD_BUILD_HOST=archlinux
export KBUILD_BUILD_USER=$pkgbase
export KBUILD_BUILD_TIMESTAMP="$(date -Ru${SOURCE_DATE_EPOCH:+d @$SOURCE_DATE_EPOCH})"

prepare() {
  cd $_srcname

  rustup install 1.71.1
  rustup component add rust-src
  _path=$PATH
  PATH="$HOME/.rustup/toolchains/1.71.1-$(uname -m)-unknown-linux-gnu/bin:$_path"
  cargo install --locked --version 0.65.1 bindgen-cli
  PATH="$HOME/.cargo/bin:$HOME/.rustup/toolchains/1.71.1-$(uname -m)-unknown-linux-gnu/bin:$_path"

  echo "Setting version..."
  echo "-$_asahirel-$pkgrel" > localversion.10-pkgrel

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done

  echo "Creating build directories..."
  mkdir -p build/base build/edge

  echo "Setting config (base)..."
  cp ../config build/base/.config
  make olddefconfig prepare O=$PWD/build/base
  diff -u ../config build/base/.config || :
  make -s kernelrelease O=$PWD/build/base > build/base/version

  echo "Setting config (edge)..."
  cat build/base/.config ../config.edge > build/edge/.config
  make olddefconfig prepare O=$PWD/build/edge
  make -s kernelrelease O=$PWD/build/edge > build/edge/version

  echo "Prepared $pkgbase version $(<build/base/version)"
}

build() {
  PATH="$HOME/.cargo/bin:$HOME/.rustup/toolchains/1.71.1-$(uname -m)-unknown-linux-gnu/bin:$PATH"

  cd $_srcname
  echo "Building base..."
  make all O=$PWD/build/base
  echo "Copying objects..."
  cp build/edge/.config{,.save}
  touch build/base/.config
  cp -urT build/{base,edge}
  rm build/edge/init/version.o
  make O=$PWD/build/edge oldconfig prepare
  cp build/edge/.config{.save,}
  echo "Building edge..."
  make O=$PWD/build/edge oldconfig prepare
  make O=$PWD/build/edge all
}

_package_kernel() {
  PATH="$HOME/.cargo/bin:$HOME/.rustup/toolchains/1.71.1-$(uname -m)-unknown-linux-gnu/bin:$PATH"

  pkgdesc="The $pkgdesc kernel and modules"
  depends=(coreutils kmod initramfs $2 "m1n1>=$_m1n1_version")
  optdepends=('crda: to set the correct wireless channels of your country'
              'linux-firmware: firmware images needed for some devices')
  provides=(WIREGUARD-MODULE linux=${pkgver})
  replaces=(wireguard-arch)

  local kernver="$(<$O/version)"
  local modulesdir="$pkgdir/usr/lib/modules/$kernver"

  echo "Installing boot image..."
  # systemd expects to find the kernel here to allow hibernation
  # https://github.com/systemd/systemd/commit/edda44605f06a41fb86b7ab8128dcf99161d2344
  install -Dm644 "$O"/arch/arm64/boot/Image "$modulesdir/vmlinuz"

  # Used by mkinitcpio to name the kernel
  echo "$1" | install -Dm644 /dev/stdin "$modulesdir/pkgbase"

  echo "Installing modules..."
  make O="$O" INSTALL_MOD_PATH="$pkgdir/usr" INSTALL_MOD_STRIP=1 modules_install

  echo "Installing device trees..."
  install -Dt "$modulesdir/dtbs" "$O"/arch/arm64/boot/dts/apple/*.dtb

  # remove build and source links
  rm "$modulesdir"/{source,build}
}

_package() {
  cd $_srcname
  export O="$PWD/build/base"
  _package_kernel "$pkgbase"
}

_package-edge() {
  cd $_srcname
  export O="$PWD/build/edge"
  _package_kernel "$pkgbase-edge" "$pkgbase=$pkgver"
}

_package-headers() {
  pkgdesc="Headers and scripts for building modules for the $pkgdesc kernel"
  depends=(pahole)

  KARCH=arm64

  cd $_srcname
  O=build/base
  local builddir="$pkgdir/usr/lib/modules/$(<${O}/version)/build"

  echo "Installing build files..."
  install -Dt "$builddir" -m644 $O/.config Makefile $O/Module.symvers $O/System.map \
    localversion.* $O/version $O/vmlinux
  install -Dt "$builddir/kernel" -m644 kernel/Makefile
  cp -t "$builddir" -a scripts $O/scripts

  # required when STACK_VALIDATION is enabled
  #install -Dt "$builddir/tools/objtool" tools/objtool/objtool

  # required when DEBUG_INFO_BTF_MODULES is enabled
  #install -Dt "$builddir/tools/bpf/resolve_btfids" tools/bpf/resolve_btfids/resolve_btfids

  echo "Installing headers..."
  cp -t "$builddir" -a include $O/include

  install -Dt "${builddir}/arch/${KARCH}" -m644 arch/${KARCH}/Makefile
  install -Dt "${builddir}/arch/${KARCH}/kernel" -m644 $O/arch/${KARCH}/kernel/asm-offsets.s
  cp -t "${builddir}/arch/${KARCH}" -a arch/${KARCH}/include $O/arch/${KARCH}/include

  install -Dt "$builddir/drivers/md" -m644 drivers/md/*.h
  install -Dt "$builddir/net/mac80211" -m644 net/mac80211/*.h

  # https://bugs.archlinux.org/task/13146
  install -Dt "$builddir/drivers/media/i2c" -m644 drivers/media/i2c/msp3400-driver.h

  # https://bugs.archlinux.org/task/20402
  install -Dt "$builddir/drivers/media/usb/dvb-usb" -m644 drivers/media/usb/dvb-usb/*.h
  install -Dt "$builddir/drivers/media/dvb-frontends" -m644 drivers/media/dvb-frontends/*.h
  install -Dt "$builddir/drivers/media/tuners" -m644 drivers/media/tuners/*.h

  # https://bugs.archlinux.org/task/71392
  install -Dt "$builddir/drivers/iio/common/hid-sensors" -m644 drivers/iio/common/hid-sensors/*.h

  echo "Installing KConfig files..."
  find . -name 'Kconfig*' -exec install -Dm644 {} "$builddir/{}" \;

  echo "Removing unneeded architectures..."
  local arch
  for arch in "$builddir"/arch/*/; do
    [[ $arch = */${KARCH}/ ]] && continue
    echo "Removing $(basename "$arch")"
    rm -r "$arch"
  done

  echo "Removing documentation..."
  rm -r "$builddir/Documentation"

  echo "Removing broken symlinks..."
  find -L "$builddir" -type l -printf 'Removing %P\n' -delete

  echo "Removing loose objects..."
  find "$builddir" -type f -name '*.o' -printf 'Removing %P\n' -delete

  echo "Removing cmd files..."
  find "$builddir" -type f -name '*.cmd' -printf 'Removing %P\n' -delete

  echo "Stripping build tools..."
  local file
  while read -rd '' file; do
    case "$(file -bi "$file")" in
      application/x-sharedlib\;*)      # Libraries (.so)
        strip -v $STRIP_SHARED "$file" ;;
      application/x-archive\;*)        # Libraries (.a)
        strip -v $STRIP_STATIC "$file" ;;
      application/x-executable\;*)     # Binaries
        strip -v $STRIP_BINARIES "$file" ;;
      application/x-pie-executable\;*) # Relocatable binaries
        strip -v $STRIP_SHARED "$file" ;;
    esac
  done < <(find "$builddir" -type f -perm -u+x ! -name vmlinux -print0)

  echo "Stripping vmlinux..."
  strip -v $STRIP_STATIC "$builddir/vmlinux"

  echo "Adding symlink..."
  mkdir -p "$pkgdir/usr/src"
  ln -sr "$builddir" "$pkgdir/usr/src/$pkgbase"

  O=build/edge
  local builddir2="$pkgdir/usr/lib/modules/$(<${O}/version)/build"
  mkdir -p $builddir2
  cp -rl $builddir/* $builddir2
  cp --remove-destination $O/{.config,System.map,version,Module.symvers,vmlinux} $builddir2
  rm -rf $builddir2/include/config
  cp -r $O/include/config $builddir2/include/config
}

pkgname=("$pkgbase" "$pkgbase-headers" "$pkgbase-edge")
for _p in "${pkgname[@]}"; do
  eval "package_$_p() {
    $(declare -f "_package${_p#$pkgbase}")
    _package${_p#$pkgbase}
  }"
done

# vim:set ts=8 sts=2 sw=2 et:
