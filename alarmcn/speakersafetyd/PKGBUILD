# Maintainer: Hector Martin <marcan@marcan.st>

pkgname=speakersafetyd
pkgver=1.0.2
pkgrel=1
pkgdesc='Asahi Linux speaker safety daemon'
arch=('aarch64')
url='http://asahilinux.org'
depends=('alsa-lib')
makedepends=('rust')
license=('MIT')
source=(
  "speakersafetyd-${pkgver}.tar.gz::https://github.com/AsahiLinux/speakersafetyd/archive/refs/tags/${pkgver}.tar.gz"
)
sha256sums=('844ae3719c029e826f58c3799b6e358d189b0c42ade7a91f6c35b960cae35919')

build() {
  cd "${srcdir}/speakersafetyd-${pkgver}"
  make
}

package() {
  cd "${srcdir}/speakersafetyd-${pkgver}"
  make DESTDIR="${pkgdir}" UNITDIR=/usr/lib/systemd/system UDEVDIR=/usr/lib/udev/rules.d install
}
