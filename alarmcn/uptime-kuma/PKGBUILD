# Maintainer: Coelacanthus <CoelacanthusHex@gmail.com>

pkgname=uptime-kuma
pkgver=1.19.6
pkgrel=1
pkgdesc="A free and open source uptime monitoring solution"
arch=('aarch64' 'armv7h')
url="https://github.com/louislam/uptime-kuma"
license=('MIT')
depends=('nodejs' 'sqlite')
makedepends=('npm' 'chromium')
optdepends=(
            'cloudflared: reverse proxy with Cloudflare tunnel'
)
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/${pkgver}.tar.gz"
        "uptime-kuma.service"
)
b2sums=('9de4f2ad8f180eae2f39c36268efa075c76b2fc255132a721e4cfc5b956262f1f00cad0fb61b33400eca4b1585972320dac3d15719c47a19005b814ac21c2bc3'
        '4a0d02dc226a6d80400d16300f672e7fcf50bdb112e2629e1f1c8177cac499e2ca23f0e3fee4c314f50610b2daea3a98a839c04cd960aaba94765399ffd1b30a')

build() {
    cd ${srcdir}/${pkgname}-${pkgver}
    export PUPPETEER_SKIP_CHROMIUM_DOWNLOAD=true
    npm ci
    ./node_modules/.bin/vite build --config ./config/vite.config.js
}

package() {
    npm install \
        --build-from-source --sqlite=/usr \
        -g --prefix "${pkgdir}/usr" "${srcdir}/${pkgname}-${pkgver}.tar.gz"

    # npm gives ownership of ALL FILES to build user
    # https://bugs.archlinux.org/task/63396
    chown -R root:root "${pkgdir}"
    
    # Remove references to $pkgdir
    find "$pkgdir" -type f -name package.json -print0 | xargs -0 sed -i "/_where/d"
    
    install -d ${pkgdir}/usr/lib/node_modules/uptime-kuma/dist/
    cp -r ${srcdir}/${pkgname}-${pkgver}/dist/* -t ${pkgdir}/usr/lib/node_modules/uptime-kuma/dist/
    
    install -d ${pkgdir}/usr/lib/systemd/system
    install -Dm644 "${srcdir}/uptime-kuma.service" ${pkgdir}/usr/lib/systemd/system/uptime-kuma.service
    install -Dm644 "${srcdir}/${pkgname}-${pkgver}/LICENSE" ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
