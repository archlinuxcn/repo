# Maintainer: Evangelos Foutras <foutrelis@archlinux.org>

pkgname=7zip
pkgver=26.00
pkgrel=1
pkgdesc='File archiver for extremely high compression'
arch=(aarch64 x86_64)
url='https://www.7-zip.org'
license=('LGPL-2.1-or-later AND LicenseRef-UnRAR AND BSD-3-Clause AND BSD-2-Clause')
depends=(
  sh
  libgcc
  libstdc++
  glibc
)
makedepends_x86_64=(uasm)
provides=(p7zip)
conflicts=(p7zip)
_replaces=(p7zip)
source=(https://7-zip.org/a/7z${pkgver//./}-src.tar.xz)
sha512sums=('2374b3d90388838391ef35a95a81e97639478598011d46adee9ddb84ed8c287fad47f6d2d794c22b0fc5c1f7dec4bfaaa25b6e747c5e78100eb63818fe85b897')
b2sums=('4ac1ecf98aa3bdcc968a75e250b467b0c38eb27dd6752fdc7c025d6d1b73cca648a12ba8a26691443cf929c22ef560c211959693f82acdff5d27cf0f8d159645')

build() {
  local _platform_flags=()

  case $CARCH in
    x86_64)
      _platform_flags=(PLATFORM=x64 IS_X64=1 MY_ASM=uasm USE_ASM=1)
      ;;
    i686)
      _platform_flags=(PLATFORM=x86 IS_X86=1 MY_ASM=uasm USE_ASM=1)
      ;;
    aarch64)
      _platform_flags=(PLATFORM=arm64 IS_ARM64=1 USE_ASM=1)
      ;;
  esac

  for component in Bundles/{Alone,Alone7z,Format7zF,SFXCon} UI/Console; do
    make -C CPP/7zip/$component -f ../../cmpl_gcc.mak "${_platform_flags[@]}" \
      LFLAGS_STRIP= \
      CC="cc $CPPFLAGS $CFLAGS $LDFLAGS" \
      CXX="g++ $CPPFLAGS $CXXFLAGS $LDFLAGS"
  done
}

package() {
  install -Dt "$pkgdir/usr/lib/7zip" \
    CPP/7zip/Bundles/Alone/b/g/7za \
    CPP/7zip/Bundles/Alone7z/b/g/7zr \
    CPP/7zip/Bundles/Format7zF/b/g/7z.so \
    CPP/7zip/UI/Console/b/g/7z
  install -D CPP/7zip/Bundles/SFXCon/b/g/7zCon "$pkgdir/usr/lib/7zip/7zCon.sfx"

  for _prog in 7za 7zr 7z; do
    printf '#!/bin/sh\nexec /usr/lib/7zip/%s "$@"\n' "$_prog" \
    | install -D /dev/stdin "$pkgdir/usr/bin/$_prog"
  done

  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" DOC/{,unRar}License.txt
}

# vim:set ts=2 sw=2 et:
