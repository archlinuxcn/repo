# Copyright (C) 2019 Denis 'GNUtoo' Carikli <GNUtoo@cyberdimension.org>
# This program is free software: you can redistribute it and/or modify
# it under the terms of the CC0 1.0 License.

pkgname=qdl
pkgver=2.1
pkgrel=3
pkgdesc="Tool to communicate with Qualcomm System On a Chip bootroms to install or execute code"
arch=(armv7h aarch64 i686 x86_64)
url='https://github.com/linux-msm/qdl'
license=('BSD3')
depends=(libxml2.so zlib xz libicui18n.so libicuuc.so libicudata.so libusb-1.0.so)
source=("https://github.com/andersson/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('f1a23653271498efd442f6787b33d6142b3cb75db74bdb0600b05b3c584d9b1bad7b883cecff252d2df6b9871888b824a8d236833bce38f288d399c6ece6562c')

build(){
  cd "${srcdir}/${pkgname}-${pkgver}"
  make CFLAGS="$CFLAGS -include stdlib.h $(pkgconf libusb-1.0 --cflags) -O2 -Wall -g $(xml2-config --cflags)"
}

package(){
  cd "${srcdir}/${pkgname}-${pkgver}"
  make prefix="/usr" DESTDIR="${pkgdir}" install

  # Package license
  install -d "${pkgdir}/usr/share/licenses/${pkgname}/"
  install LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/"

  # Package documentation
  install -d "${pkgdir}/usr/share/doc/${pkgname}/"
  install README.md "${pkgdir}/usr/share/doc/${pkgname}/"
}
