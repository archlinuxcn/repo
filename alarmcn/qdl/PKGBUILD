# Copyright (C) 2019 Denis 'GNUtoo' Carikli <GNUtoo@cyberdimension.org>
# This program is free software: you can redistribute it and/or modify
# it under the terms of the CC0 1.0 License.

pkgname=qdl
pkgver=1.0
pkgrel=10
pkgdesc="Tool to communicate with Qualcomm System On a Chip bootroms to install or execute code"
arch=(armv7h aarch64 i686 x86_64)
url='https://github.com/andersson/qdl'
license=('BSD3')
depends=(libxml2.so zlib xz libicui18n.so libicuuc.so libicudata.so libusb-1.0.so)
source=("https://github.com/andersson/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('46c18ba3b602c4df0ecede3c926e5a8ef3736adfa788a79b4406054840acb1efe1cd25e7d34ce681e7cda4f3ba66ebde8fc3367981bf1caa84ae5b74e8ef9bb5')

build(){
  cd "${srcdir}/${pkgname}-${pkgver}"
  make CFLAGS="$CFLAGS -include stdlib.h $(pkgconf libusb-1.0 --cflags) -O2 -Wall -g $(xml2-config --cflags)"
}

package(){
  cd "${srcdir}/${pkgname}-${pkgver}"
  make prefix="/usr" DESTDIR="${pkgdir}" install

  # Package license
  install -d "${pkgdir}/usr/share/licenses/${pkgname}/"
  install LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/"

  # Package documentation
  install -d "${pkgdir}/usr/share/doc/${pkgname}/"
  install README "${pkgdir}/usr/share/doc/${pkgname}/"
}
