# Maintainer: Integral <integral@member.fsf.org>
# Contributor: tarball <bootctl@gmail.com>
# Contributor: √Årni Dagur <arnidg@protonmail.ch>

pkgname=xcp
pkgver=0.23.0
pkgrel=1
pkgdesc="An extended 'cp'"
arch=('i686' 'x86_64' 'armv7h' 'aarch64')
url="https://github.com/tarka/${pkgname}"
license=('GPL-3.0-only')
depends=('gcc-libs')
makedepends=('cargo')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha256sums=('386815c98475defc5b3823ea6c7a08ba661036364581a82865d9f8672968d40a')

prepare() {
	cd "${pkgname}-${pkgver}/"
	cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
	cd "${pkgname}-${pkgver}/"
	cargo build --frozen --release --all-features
}

check() {
	cd "${pkgname}-${pkgver}/"
	cargo test --frozen --all-features
}

package() {
	cd "${pkgname}-${pkgver}/"
	install -Dm755 "target/release/${pkgname}" -t "${pkgdir}/usr/bin/"
	install -Dm644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}/"

	cd completions
	install -Dm644 "${pkgname}.bash" "$pkgdir/usr/share/bash-completion/completions/${pkgname}"
	install -Dm644 "${pkgname}.fish" -t "$pkgdir/usr/share/fish/vendor_completions.d/"
	install -Dm644 "${pkgname}.zsh" "$pkgdir/usr/share/zsh/site-functions/_${pkgname}"
}
