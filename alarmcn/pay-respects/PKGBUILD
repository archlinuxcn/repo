# Maintainer: Integral <integral@member.fsf.org>
# Contributor: iff <iff@ik.me>

pkgname=pay-respects
pkgver=0.7.0
pkgrel=1
pkgdesc="Terminal command suggestion, alternative to thefuck written in Rust with AI support"
arch=('x86_64' 'aarch64')
url="https://codeberg.org/iff/${pkgname}"
license=('AGPL-3.0-or-later')
makedepends=('cargo')
optdepends=('curl: for fetching AI suggestions')
replaces=('pay_respects')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha256sums=('a054d3e0ef6d99bfc84153a5023a4d8d91a64d9695cda1f4147720cfddfc19e2')

prepare() {
	cd "${pkgname}/"
	cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
	cd "${pkgname}/"
	export _DEF_PR_LIB="/usr/lib/${pkgname}/"
	cargo build --frozen --release --all-features --workspace
}

package() {
	cd "${pkgname}/"
	install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"

	cd target/release
	install -Dm755 "${pkgname}" -t "${pkgdir}/usr/bin/"
	install -Dm755 _${pkgname}-{module-100-runtime-rules,fallback-100-request-ai} -t "${pkgdir}/usr/lib/${pkgname}/"
}
