# Maintainer: Integral <integral@member.fsf.org>
# Contributor: iff <iff@ik.me>

pkgname=pay-respects
pkgver=0.6.1
pkgrel=1
pkgdesc="Terminal command suggestion, alternative to thefuck written in Rust with AI support"
arch=('x86_64' 'aarch64')
url="https://codeberg.org/iff/${pkgname}"
license=('AGPL-3.0-or-later')
makedepends=('cargo')
optdepends=('curl: for fetching AI suggestions')
replaces=('pay_respects')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha256sums=('2b54b11f76e0471424b4490ff7257f7093e9d07ce33972bc20e5e42301893bac')

prepare() {
	cd "${pkgname}/"
	cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
	cd "${pkgname}/"
	cargo build --frozen --release --all-features
}

package() {
	cd "${pkgname}/"
	install -Dm755 "target/release/${pkgname}" -t "${pkgdir}/usr/bin/"
	install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
