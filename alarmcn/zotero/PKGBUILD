# Maintainer: Aaron Keesing <agkphysics at gmail dot com>

pkgname=zotero
pkgver=7.0.11
pkgrel=3
pkgdesc="A free, easy-to-use tool to help you collect, organize, cite, and share your research sources."
arch=('x86_64' 'i686' 'aarch64')
url="https://github.com/zotero/zotero"
license=('AGPL-3.0-or-later')
depends=('dbus-glib' 'gtk3' 'nss' 'libxt')
makedepends=('npm' 'git' 'zip' 'unzip' 'perl' 'python>=3' 'curl' 'wget' 'rsync' 'nodejs' 'patch')
source=("zotero.desktop"
        "https://ftp.mozilla.org/pub/firefox/nightly/2024/06/2024-06-09-20-51-51-mozilla-central/firefox-128.0a1.en-US.linux-aarch64.tar.bz2"
        "zotero-client::git+https://github.com/zotero/zotero.git#tag=${pkgver}"
        "zotero-translators::git+https://github.com/zotero/translators.git"
        "zotero-styles::git+https://github.com/zotero/bundled-styles.git"
        "zotero-pdf-worker::git+https://github.com/zotero/pdf-worker.git"
        "zotero-note-editor::git+https://github.com/zotero/note-editor.git"
        "zotero-reader::git+https://github.com/zotero/reader.git"
        "zotero-schema::git+https://github.com/zotero/zotero-schema.git"
        "zotero-SingleFile::git+https://github.com/gildas-lormeau/SingleFile.git"
        "zotero-utilities::git+https://github.com/zotero/utilities.git"
        "zotero-translate::git+https://github.com/zotero/translate.git"
        "zotero-csl::git+https://github.com/citation-style-language/locales.git"
        "zotero-libreoffice-integration::git+https://github.com/zotero/zotero-libreoffice-integration.git"
        "zotero-epub-js::git+https://github.com/zotero/epub.js.git"
        "disable-updater.patch"
        0001-A-more-generic-way-to-specify-the-archs-to-build.patch)
noextract=(firefox-128.0a1.en-US.linux-aarch64.tar.bz2)
sha256sums=('eab76db7a56a4d9aaa17baaf240b82fcf57944a4ddf8ef1b58cc64182426cedc'
            '6ef76929cb6db1ffef92e99935dfaac700beb3a49f6a72cd173cb53167e6441f'
            'def569fb936524d192e3939225e1619c374abd3bcd5226fa6100db36782f8ebb'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'dc1894ac2e1520c3dae8e9cd5e09608f4bb3298bdede2891a77118187edffa9d'
            '181a2ee9bd6ecdf7078a3a5f399b1720824dc6d2bbc7b520e314fd294f6345c2')

prepare() {
  cd "$srcdir/zotero-client"

  # Lie about the version for now since we need fx128 to use the nightly build
  echo "$pkgver.SOURCE" > version

  cp "app/update-packaging/removed-files_linux-x86_64" \
     "app/update-packaging/removed-files_linux-aarch64"
  (
    mkdir -p app/xulrunner
    . app/config.sh
    cp "$srcdir/firefox-128.0a1.en-US.linux-aarch64.tar.bz2" \
       app/xulrunner/firefox-$GECKO_VERSION_LINUX-aarch64.tar.bz2
  )
  sed -i -e 's/MinVersion=.*/&a0/' app/assets/application.ini

  patch -N -p1 < "$srcdir/disable-updater.patch"
  patch -N -p1 < "$srcdir/0001-A-more-generic-way-to-specify-the-archs-to-build.patch"

  npm i --legacy-peer-deps

  git submodule init
  git config submodule.translators.url "$srcdir/zotero-translators"
  git config submodule.styles.url "$srcdir/zotero-styles"
  git config submodule.pdf-worker.url "$srcdir/zotero-pdf-worker"
  git config submodule.note-editor.url "$srcdir/zotero-note-editor"
  git config submodule.reader.url "$srcdir/zotero-reader"
  git config submodule.resource/schema/global.url "$srcdir/zotero-schema"
  git config submodule.resource/SingleFile.url "$srcdir/zotero-SingleFile"
  git config submodule.chrome/content/zotero/xpcom/utilities.url "$srcdir/zotero-utilities"
  git config submodule.chrome/content/zotero/xpcom/translate.url "$srcdir/zotero-translate"
  git config submodule.chrome/content/zotero/locale/csl.url "$srcdir/zotero-csl"
  git config submodule.app/modules/zotero-libreoffice-integration.url "$srcdir/zotero-libreoffice-integration"
  git -c protocol.file.allow=always submodule update

  cd "$srcdir/zotero-client/chrome/content/zotero/xpcom/utilities"
  git config submodule.resource/schema/global.url "$srcdir/zotero-schema"
  git -c protocol.file.allow=always submodule update

  cd "$srcdir/zotero-client/chrome/content/zotero/xpcom/translate/modules/utilities"
  git config submodule.resource/schema/global.url "$srcdir/zotero-schema"
  git -c protocol.file.allow=always submodule update

  cd "$srcdir/zotero-client/reader"
  git submodule init
  git config submodule.epubjs/epub.js.url "$srcdir/zotero-epub-js"
  git -c protocol.file.allow=always submodule update

  cd "$srcdir/zotero-client/pdf-worker"
  git submodule init
  git -c protocol.file.allow=always submodule update
}

build() {
  cd "$srcdir/zotero-client"
  NODE_OPTIONS=--openssl-legacy-provider npm run build
  app/scripts/dir_build -q -a 'x86_64 aarch64'
}

package() {
  install -dDm755 "$pkgdir"/usr/{bin,lib/zotero}
  cp -r "$srcdir/zotero-client/app/staging/Zotero_linux-$CARCH"/* "$pkgdir/usr/lib/zotero"
  ln -s /usr/lib/zotero/zotero "$pkgdir/usr/bin/zotero"
  install -Dm644 "$srcdir/zotero.desktop" "$pkgdir/usr/share/applications/zotero.desktop"

  # Copy zotero icons to a standard location
  install -Dm644 "$pkgdir/usr/lib/zotero/icons/icon32.png" "$pkgdir/usr/share/icons/hicolor/32x32/apps/zotero.png"
  install -Dm644 "$pkgdir/usr/lib/zotero/icons/icon64.png" "$pkgdir/usr/share/icons/hicolor/64x64/apps/zotero.png"
  install -Dm644 "$pkgdir/usr/lib/zotero/icons/icon128.png" "$pkgdir/usr/share/icons/hicolor/128x128/apps/zotero.png"
  install -Dm644 "$pkgdir/usr/lib/zotero/icons/symbolic.svg" "$pkgdir/usr/share/icons/hicolor/symbolic/apps/zotero-symbolic.svg"

  # Close shell when launching
  sed -i -r 's:^("\$CALLDIR/zotero-bin" -app "\$CALLDIR/application.ini" "\$@"):exec \1:' "$pkgdir/usr/lib/zotero/zotero"
}
