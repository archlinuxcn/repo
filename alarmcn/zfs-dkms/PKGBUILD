# Maintainer: Kevin Stolp <kevinstolp@gmail.com>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Iacopo Isimbaldi <isiachi@rhye.it>

pkgname=zfs-dkms
pkgver=2.2.7
pkgrel=1
pkgdesc="Kernel modules for the Zettabyte File System."
arch=('any')
url="https://zfsonlinux.org/"
license=('CDDL-1.0')
depends=("zfs-utils" 'dkms' 'perl')
provides=("ZFS-MODULE=${pkgver}" "SPL-MODULE=${pkgver}" "zfs")
source=("https://github.com/zfsonlinux/zfs/releases/download/zfs-${pkgver}/zfs-${pkgver}.tar.gz"
	"0001-only-build-the-module-in-dkms.conf.patch")
sha256sums=('b2b8e3bfabf2a6407a0132243726cb6762547a5bd095b1b1f37eaf2a9d8f7672'
            '8d5c31f883a906ab42776dcda79b6c89f904d8f356ade0dab5491578a6af55a5')

prepare() {
	cd "zfs-${pkgver}/"

	patch -p1 -i ../0001-only-build-the-module-in-dkms.conf.patch

	# remove unneeded sections from module build
	sed -ri "/AC_CONFIG_FILES/,/]\)/{
/AC_CONFIG_FILES/n
/]\)/n
/^\s*(module\/.*|zfs.release|Makefile)/!d
}" configure.ac

	./autogen.sh
}

build() {
	cd "zfs-${pkgver}/"

	./scripts/dkms.mkconf -n zfs -v ${pkgver} -f dkms.conf
	./scripts/make_gitrev.sh include/zfs_gitrev.h
}

package() {
	cd "zfs-${pkgver}/"

	dkmsdir="${pkgdir}/usr/src/zfs-${pkgver}"
	install -d ${dkmsdir}/{config,scripts}
	cp -a configure dkms.conf Makefile.in META zfs_config.h.in zfs.release.in include/ module/ "${dkmsdir}/"
	install -Dm644 config/compile config/config.* config/missing config/*sh "${dkmsdir}/config/"
}
