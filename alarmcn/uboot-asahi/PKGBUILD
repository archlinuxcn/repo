# Maintainer: Hector Martin <marcan@marcan.st>

pkgname=uboot-asahi
_ver=2022.07
_asahirel=2

pkgver=${_ver}_rc100.asahi${_asahirel}
pkgrel=2
pkgdesc='U-Boot for Apple Silicon Macs'
_commit_id=asahi-v${_ver}-${_asahirel}
_srcname=u-boot-${_commit_id}
arch=('aarch64')
url='http://asahilinux.org'
license=('MIT' 'GPL2')
makedepends=( bc imagemagick )
source=(
  "u-boot-${_commit_id}.tar.gz::https://github.com/AsahiLinux/u-boot/archive/${_commit_id}.tar.gz"
)
sha256sums=('97060aa18a854bbf99b33816fbb97e87ab6644a57fc70d00cec1e52b60746405')
b2sums=('f19f0d03fb7f8481d1d4369a11982668c2ce88cce3a4e30d91863741c836c5a0c0f80787a7a1642aace98ab7a16ed7ceabe467d2afa48cd30fe1704d456549a7')

prepare() {
  cd "${srcdir}/$_srcname"
  make apple_m1_defconfig
}

build() {
  cd "${srcdir}/$_srcname"
  make
}

package() {
  cd "${srcdir}"

  tgtdir="$pkgdir/usr/lib/asahi-boot"

  install -Dt "$tgtdir" -m644 "$_srcname/u-boot-nodtb.bin"
  install -Dt "$tgtdir/dtb" -m644 "$_srcname/arch/arm/dts/"t[86]*.dtb
}
