# Maintainer: Hector Martin <marcan@marcan.st>

pkgname=uboot-asahi
_ver=2023.04
_asahirel=2

pkgver=${_ver}.asahi${_asahirel}
pkgrel=4
pkgdesc='U-Boot for Apple Silicon Macs'
_commit_id=asahi-v${_ver}-${_asahirel}
_srcname=u-boot-${_commit_id}
arch=('aarch64')
url='http://asahilinux.org'
license=('MIT' 'GPL2')
makedepends=( bc imagemagick )
source=(
  "u-boot-${_commit_id}.tar.gz::https://github.com/AsahiLinux/u-boot/archive/${_commit_id}.tar.gz"
)
sha256sums=('262d0102c90da9469993fc40c22d99c1db5f3e0eef0117cb34740448bfa55cf1')
b2sums=('9f235ba95acc007a2226315a6cf224495d92ce51d3a4188a946c4e387dac765c0092787265d58250315a30e6c804980701058183abaa12f7e6fe765d0ef8ee63')

prepare() {
  cd "${srcdir}/$_srcname"
  make apple_m1_defconfig
}

build() {
  cd "${srcdir}/$_srcname"
  make
}

package() {
  cd "${srcdir}"

  tgtdir="$pkgdir/usr/lib/asahi-boot"

  install -Dt "$tgtdir" -m644 "$_srcname/u-boot-nodtb.bin"
  install -Dt "$tgtdir/dtb" -m644 "$_srcname/arch/arm/dts/"t[86]*.dtb
}
