# Maintainer: Hector Martin <marcan@marcan.st>

pkgname=uboot-asahi
_ver=2025.10
_asahirel=1

pkgver=${_ver}.asahi${_asahirel}
pkgrel=6
pkgdesc='U-Boot for Apple Silicon Macs'
_commit_id=asahi-v${_ver}-${_asahirel}
_srcname=u-boot-${_commit_id}
arch=('aarch64')
url='http://asahilinux.org'
license=('MIT' 'GPL2')
makedepends=( bc imagemagick )
source=(
  "u-boot-${_commit_id}.tar.gz::https://github.com/AsahiLinux/u-boot/archive/${_commit_id}.tar.gz"
)
sha256sums=('8c763e0f8912a4e991da73100f9d9d9c1c723a733e4004d9435b0a1681c0560a')
b2sums=('d42dc640483f3392efaacf3c29cecb1dadbc539524ae482918ab963dda2c7845bb83d7f8bc44a147503866169afd75c264d79f9fc03c3c1f6931a33bc1275a44')

prepare() {
  cd "${srcdir}/$_srcname"
  make apple_m1_defconfig
}

build() {
  cd "${srcdir}/$_srcname"
  make
}

package() {
  cd "${srcdir}"

  tgtdir="$pkgdir/usr/lib/asahi-boot"

  install -Dt "$tgtdir" -m644 "$_srcname/u-boot-nodtb.bin"
  install -Dt "$tgtdir/dtb" -m644 "$_srcname/arch/arm/dts/"t[86]*.dtb
}
