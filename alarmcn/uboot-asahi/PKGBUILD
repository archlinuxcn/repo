# Maintainer: Hector Martin <marcan@marcan.st>

pkgname=uboot-asahi
_ver=2024.10
_asahirel=1

pkgver=${_ver}.asahi${_asahirel}
pkgrel=3
pkgdesc='U-Boot for Apple Silicon Macs'
_commit_id=asahi-v${_ver}-${_asahirel}
_srcname=u-boot-${_commit_id}
arch=('aarch64')
url='http://asahilinux.org'
license=('MIT' 'GPL2')
makedepends=( bc imagemagick )
source=(
  "u-boot-${_commit_id}.tar.gz::https://github.com/AsahiLinux/u-boot/archive/${_commit_id}.tar.gz"
)
sha256sums=('c226191d52d3f853dd7d4e2c241e4259b90bd0aecb4c56e324936cc61b1bb53e')
b2sums=('dbedf75b0efdc418b24afebc221bfddc357a72b524071d9b706cdccaff2ea22217483de3cc29c5c005635d4cd73dee71a5c39fa4de44b5afe8d1dc06851b67fe')

prepare() {
  cd "${srcdir}/$_srcname"
  make apple_m1_defconfig
}

build() {
  cd "${srcdir}/$_srcname"
  make
}

package() {
  cd "${srcdir}"

  tgtdir="$pkgdir/usr/lib/asahi-boot"

  install -Dt "$tgtdir" -m644 "$_srcname/u-boot-nodtb.bin"
  install -Dt "$tgtdir/dtb" -m644 "$_srcname/arch/arm/dts/"t[86]*.dtb
}
