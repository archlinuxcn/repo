# Maintainer: Hector Martin <marcan@marcan.st>

pkgname=uboot-asahi
_ver=2025.10
_asahirel=2

pkgver=${_ver}.asahi${_asahirel}
pkgrel=7
pkgdesc='U-Boot for Apple Silicon Macs'
_commit_id=asahi-v${_ver}-${_asahirel}
_srcname=u-boot-${_commit_id}
arch=('aarch64')
url='http://asahilinux.org'
license=('MIT' 'GPL2')
makedepends=( bc imagemagick )
source=(
  "u-boot-${_commit_id}.tar.gz::https://github.com/AsahiLinux/u-boot/archive/${_commit_id}.tar.gz"
)
sha256sums=('899895de62aeef3d6c7360133396089bdf85ac09490429b2b3f69a4dfdfcb415')
b2sums=('50f8233ced5927267a3b345e89927223ba811523830c61c05e8641693b79aa4dbe3bf5abde1cdf932b159141540d40be0aa01baeeba8e1843c85205a0a64c3bc')

prepare() {
  cd "${srcdir}/$_srcname"
  make apple_m1_defconfig
}

build() {
  cd "${srcdir}/$_srcname"
  make
}

package() {
  cd "${srcdir}"

  tgtdir="$pkgdir/usr/lib/asahi-boot"

  install -Dt "$tgtdir" -m644 "$_srcname/u-boot-nodtb.bin"
  install -Dt "$tgtdir/dtb" -m644 "$_srcname/arch/arm/dts/"t[86]*.dtb
}
