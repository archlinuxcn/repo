# Maintainer: Hector Martin <marcan@marcan.st>

pkgname=uboot-asahi
_ver=2022.07
_asahirel=3

pkgver=${_ver}_rc100.asahi${_asahirel}
pkgrel=3
pkgdesc='U-Boot for Apple Silicon Macs'
_commit_id=asahi-v${_ver}-${_asahirel}
_srcname=u-boot-${_commit_id}
arch=('aarch64')
url='http://asahilinux.org'
license=('MIT' 'GPL2')
makedepends=( bc imagemagick )
source=(
  "u-boot-${_commit_id}.tar.gz::https://github.com/AsahiLinux/u-boot/archive/${_commit_id}.tar.gz"
)
sha256sums=('8d1e517ccf80ad066c88d17a27c29125497d7f8883ed60c9ade7bf0bd85482ad')
b2sums=('1ba928bdab4189b663d7fa1b0f67a67e68779339f15ed371947fc6743f83da7579e93b4447b247d170bd23a448f8c08f1c37aa432036a73ce04e24dd3011d2b3')

prepare() {
  cd "${srcdir}/$_srcname"
  make apple_m1_defconfig
}

build() {
  cd "${srcdir}/$_srcname"
  make
}

package() {
  cd "${srcdir}"

  tgtdir="$pkgdir/usr/lib/asahi-boot"

  install -Dt "$tgtdir" -m644 "$_srcname/u-boot-nodtb.bin"
  install -Dt "$tgtdir/dtb" -m644 "$_srcname/arch/arm/dts/"t[86]*.dtb
}
