# Maintainer: eolianoe <eolianoe [at] gmail [DoT] com>
# Contributor: Aaron Lindsay <aaron@aclindsay.com>
# Contributor: Edvinas Valatka <edacval@gmail.com>
# Contributor: Adrian HÃ¼hn <adrian.huehn@web.de>

pkgname=libsearpc
epoch=2
pkgver=3.3.0
# tagname=$pkgver
_tagname=3.3-latest
pkgrel=3
pkgdesc="A simple C language RPC framework (including both server side & client side)"
arch=('i686' 'x86_64' 'armv7h' 'armv6h' 'aarch64')
url="https://github.com/haiwen/libsearpc"
license=('Apache')
depends=("glib2>=2.26.0" libgobject-2.0.so libglib-2.0.so "jansson>=2.2.1")
provides=(libsearpc.so)
makedepends=(python)
# upstream seems to change tag so let's rename the download...
source=("libsearpc-${pkgver}-7.tar.gz::${url}/archive/v${_tagname}.tar.gz")
sha256sums=('3d6bef12f5ce957309886c2effa83775ded8b568584c555b2746474afab96d5e')

prepare () {
  cd ${pkgname}-${_tagname}
  sed -i 's|(DESTDIR)@prefix@|@prefix@|' './libsearpc.pc.in'
}

build () {
  echo "Building python3 version"
  cd ${pkgname}-${_tagname}
  ./autogen.sh
  ./configure --prefix=/usr PYTHON=/usr/bin/python --disable-compile-demo
  make
}

package () {
  cd ${pkgname}-${_tagname}
  make DESTDIR="${pkgdir}" install
}
