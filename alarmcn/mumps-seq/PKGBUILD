# Maintainer : eolianoe <eolianoe At GoogleMAIL DoT CoM>
# Contributor: Michele Mocciola <mickele>
# Contributor: Guillaume Doll√© <dolle dot guillaume at gmail dot com >

pkgname=mumps-seq
_pkgname=mumps
_PKGNAME=MUMPS
pkgver=5.6.2
pkgrel=2
pkgdesc="Sparse solver library using Gaussian elimination (sequential version)"
url="http://mumps-solver.org"
license=('custom')
# The build system only links to blas but uses lapack function
# Just use openblas for now but this could probably be fixed fairly easily...
depends=('gcc-libs' 'lapack' 'metis')
makedepends=(gcc-fortran)
arch=('aarch64' 'x86_64')
source=("http://mumps-solver.org/${_PKGNAME}_${pkgver}.tar.gz")
sha256sums=('13a2c1aff2bd1aa92fe84b7b35d88f43434019963ca09ef7e8c90821a8f1d59a')
options=(staticlibs '!lto')

prepare() {
  cd ${_PKGNAME}_${pkgver}

  sed -e 's/-lpord/-lpord_seq/g' -e 's/-lmpiseq/-lmpiseq_seq/g' \
      Make.inc/Makefile.G95.SEQ > Makefile.inc
}

build() {
  cd ${_PKGNAME}_${pkgver}

  FFLAGS+=' -fallow-argument-mismatch'
  export FFLAGS

  make_args=(OPTF=-O3
             OPTL=-O3
             OPTC=-O3
             CDEFS=-DAdd_
             LMETIS="-lmetis"
             ORDERINGSF="-Dpord -Dmetis"
             LIBEXT_SHARED=".so"
             SONAME="-soname"
             PLAT="_seq"
             CC="gcc -fPIC ${CFLAGS}"
             FC="gfortran -fPIC ${FFLAGS}"
             FL="gfortran -fPIC"
             RANLIB="ranlib"
             LIBBLAS="-lblas"
             LAPACK="-llapack")

  make allshared "${make_args[@]}"
}

package(){
  cd ${_PKGNAME}_${pkgver}

  # Install all headers
  install -dm755 "${pkgdir}/usr/include/mumps-seq"
  install -Dm644 -- include/*.h "${pkgdir}/usr/include/mumps-seq"

  # Install all libraries
  install -dm755 "${pkgdir}/usr/lib"
  install -Dm644 -- lib/*.so "${pkgdir}/usr/lib"

  # Install mpiseq headers
  install -dm755 "${pkgdir}/usr/include/mpiseq"
  install -Dm644 -- libseq/*.h "${pkgdir}/usr/include/mpiseq"

  # Install mpiseq libraries
  install -m755 libseq/libmpiseq_seq.so "${pkgdir}/usr/lib"
  ln -sf libmpiseq_seq.so "${pkgdir}/usr/lib/libmpiseq.so"

  # Install license
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # Install examples
  install -dm755 "${pkgdir}/usr/share/doc/mumps-seq/examples"
  cp -r examples/* "${pkgdir}/usr/share/doc/mumps-seq/examples"
  rm -- "${pkgdir}/usr/share/doc/mumps-seq/examples/"*.o
}
