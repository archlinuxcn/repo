pkgname=libllvmextra_jll
pkgver=9.4.6
_commit=4d6741a7727aecf72352cdfe63a98014c97813b5
pkgrel=1
pkgdesc="LLVM.jl"
url="https://github.com/maleadt/LLVM.jl.git"
arch=(aarch64 armv7h x86_64)
license=('MIT')
makedepends=(git cmake llvm-julia-git)
source=("git+https://github.com/maleadt/LLVM.jl.git#commit=$_commit"
        0001-Support-LLVM-21-in-LLVMExtra.patch
        0002-Generate-wrappers-for-LLVM-21.patch)
sha256sums=('a65dc47d308cb49879c383912f7c0edaf28c21343d05f74ec986d757dc78bab7'
            '784dd1e8c28f4a455cd38f1e696969d3f57ca014ba2f6e5f9efadb37d1837405'
            '1f5d4ff81200780bfc5d21e5613dcebc8d431218217d78af91c4a7293d69eaaf')
options=('debug')

_versioned_llvm=

prepare() {
  cd LLVM.jl

  patch -Np1 --no-backup-if-mismatch < ../0001-Support-LLVM-21-in-LLVMExtra.patch
  patch -Np1 --no-backup-if-mismatch < ../0002-Generate-wrappers-for-LLVM-21.patch
}

build() {
  cd LLVM.jl/deps/LLVMExtra

  mkdir -p build
  cd build
  if [[ -n $_versioned_llvm ]]; then
    export CMAKE_PREFIX_PATH=/usr/lib/llvm$_versioned_llvm/
  fi
  cmake .. -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  if [[ -n $_versioned_llvm ]]; then
    _llvm_ver=$(llvm-config-${_versioned_llvm} --version)
  else
    _llvm_ver=$(llvm-config --version)
  fi
  [[ $_llvm_ver =~ ^([0-9]*).* ]]
  llvm_maj_ver=${BASH_REMATCH[1]}
  ((next_llvm_maj=llvm_maj_ver + 1))

  _deps=("llvm-libs-julia-git>=${llvm_maj_ver}.0"
         "llvm-libs-julia-git<${next_llvm_maj}.0")

  depends+=("${_deps[@]}")

  cd LLVM.jl/deps/LLVMExtra/build

  make install DESTDIR="${pkgdir}"

  mkdir "${pkgdir}/usr/lib/julia"
  mv "${pkgdir}/usr/lib/"libLLVMExtra-* "${pkgdir}/usr/lib/julia"
}
