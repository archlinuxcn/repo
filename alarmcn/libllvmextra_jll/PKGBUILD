pkgname=libllvmextra_jll
pkgver=9.4.4
_commit=90917c8d4f7ab8d6ba6acf0f0d583486b8b02610
pkgrel=1
pkgdesc="LLVM.jl"
url="https://github.com/maleadt/LLVM.jl.git"
arch=(aarch64 armv7h x86_64)
license=('MIT')
makedepends=(git cmake llvm-julia-git)
source=("git+https://github.com/maleadt/LLVM.jl.git#commit=$_commit"
        0001-Support-LLVM-21-in-LLVMExtra.patch)
sha256sums=('1e825342574806972fad84e5c88ad8f50d0b3dd231aba79c53eea287a12638ad'
            'c7a91ccd3c55e90d027832dc5ebbdae09e4ca96ecbd22e370ed674e181111443')
options=('debug')

_versioned_llvm=

prepare() {
  cd LLVM.jl

  patch -Np1 --no-backup-if-mismatch < ../0001-Support-LLVM-21-in-LLVMExtra.patch
}

build() {
  cd LLVM.jl/deps/LLVMExtra

  mkdir -p build
  cd build
  if [[ -n $_versioned_llvm ]]; then
    export CMAKE_PREFIX_PATH=/usr/lib/llvm$_versioned_llvm/
  fi
  cmake .. -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  if [[ -n $_versioned_llvm ]]; then
    _llvm_ver=$(llvm-config-${_versioned_llvm} --version)
  else
    _llvm_ver=$(llvm-config --version)
  fi
  [[ $_llvm_ver =~ ^([0-9]*).* ]]
  llvm_maj_ver=${BASH_REMATCH[1]}
  ((next_llvm_maj=llvm_maj_ver + 1))

  _deps=("llvm-libs-julia-git>=${llvm_maj_ver}.0"
         "llvm-libs-julia-git<${next_llvm_maj}.0")

  depends+=("${_deps[@]}")

  cd LLVM.jl/deps/LLVMExtra/build

  make install DESTDIR="${pkgdir}"

  mkdir "${pkgdir}/usr/lib/julia"
  mv "${pkgdir}/usr/lib/"libLLVMExtra-* "${pkgdir}/usr/lib/julia"
}
