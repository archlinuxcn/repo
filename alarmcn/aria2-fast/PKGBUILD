# Maintainer: Pandada8 < pandada8@gmail.com >
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=aria2-fast
_pkgname=${pkgname%-fast}
pkgver=1.37.0
pkgrel=1
pkgdesc="Aria2 Download utility with little patch to maximize aria2 download speed"
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
url="http://${_pkgname}.github.io"
license=('GPL-2.0-or-later AND (OpenSSL OR LicenseRef-SSLeay)')
depends=('gnutls' 'libxml2' 'sqlite' 'c-ares' 'ca-certificates' 'libssh2')
checkdepends=('cppunit')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=("https://github.com/${_pkgname}/${_pkgname}/releases/download/release-${pkgver}/${_pkgname}-${pkgver}.tar.xz" "${pkgname}.patch")
sha256sums=('60a420ad7085eb616cb6e2bdf0a7206d68ff3d37fb5a956dc44242eb2f79b66b'
            'd4cd567e98a009c2c24b77d5f4eb42ce3b0576af9315eee475a778f71a7786d2')

build() {
	cd "${_pkgname}-${pkgver}/"

	patch -Np1 <"../${pkgname}.patch"

	./configure \
		--prefix=/usr \
		--enable-libaria2 \
		--with-ca-bundle=/etc/ssl/certs/ca-certificates.crt

	make
}

package() {
	cd "${_pkgname}-${pkgver}/"

	make DESTDIR=${pkgdir} install

	# add bash completion (aria2 automatically installs to a temporary target directory)
	install -d "${pkgdir}/usr/share/bash-completion/completions"
	install -m644 "${pkgdir}/usr/share/doc/${_pkgname}/bash_completion/aria2c" \
		"${pkgdir}/usr/share/bash-completion/completions"

	rm -rf "${pkgdir}/usr/share/doc/${_pkgname}/bash_completion"
	install -Dm644 {COPYING,LICENSE.OpenSSL} -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
