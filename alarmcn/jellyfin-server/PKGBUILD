# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=jellyfin-server
_pkgname="${pkgname%-server}"
pkgver=10.10.1
pkgrel=1
pkgdesc="Jellyfin server backend"
arch=('x86_64' 'aarch64')
url="https://${_pkgname}.org"
_url="https://github.com/${_pkgname}/${_pkgname}"
license=('GPL-2.0-or-later')
_dotnet_runtime=linux-arm64
depends=(
	'aspnet-runtime'
	'bash'
	'sqlite'
	'fontconfig'
	'jellyfin-ffmpeg'
	'libvpl'
)
makedepends=(
	'git'
	'dotnet-sdk'
)
optdepends=('jellyfin-web: to run web-app on the same machine')
backup=(
	"etc/${_pkgname}/logging.json"
	"etc/${_pkgname}/${_pkgname}.env"
)
options=('!debug')
source=(
	"${_pkgname}-${pkgver}.tar.gz::${_url}/archive/refs/tags/v${pkgver}.tar.gz"
	"jellyfin-packaging::git+${_url}-packaging.git"
	"sysusers.conf"
	"tmpfiles.conf"
	"fix-envfile-path.patch"
)
sha512sums=('48772ee5f769afe4fd1831344bd95d13728d9a54ddd3df939efb656f5c0e7be6557d2fbd1ca4ecd6bdc727ab1322e63f966cb73cd50ac8c02b43478f2413bef3'
            'SKIP'
            '9f62481faa5f6a2fad75b0a5bb727c74f46ada991aff938db225dee0232a2c948ba3e22f56b8fdaf960901b37d8cd6c292665dac1c954c6bc4b5e104da45ef6f'
            '3e12ec3d3fcb15975d5f86bc3ce3363ae89b0e9e0b2580c29fc8a612c0220a74a067138b15c48ae27bb3c5777eca33055f10651949678a1ee7bd094293f6abb6'
            '26d4a2c081c5a2563863fb21a427e6b5ff0f551549b7ba77075054e81fbda064991e3443ec3f11f42ffdcc8adef0e20363f00d248d7967d327656280db919672')

prepare() {
	cd "${_pkgname}-packaging/"

	# fix envfile path in systemd service
	patch -p1 -i ../fix-envfile-path.patch
}

build() {
	cd "${_pkgname}-${pkgver}/"

	# disable dotnet telemetry
	export DOTNET_CLI_TELEMETRY_OPTOUT=1
	export DOTNET_SKIP_FIRST_TIME_EXPERIENCE=1
	export DOTNET_NOLOGO=1

	dotnet \
		publish \
		Jellyfin.Server \
		--configuration Release \
		--output builddir \
		--self-contained false --runtime "${_dotnet_runtime}" \
		-p:DebugSymbols=false \
		-p:DebugType=none
}

package() {
	cd "${_pkgname}-${pkgver}/"

	# install binaries
	install -vd ${pkgdir}/usr/{lib,bin}
	cp -r builddir "${pkgdir}/usr/lib/jellyfin"
	ln -sf /usr/lib/jellyfin/jellyfin "${pkgdir}/usr/bin/jellyfin"

	# ensure binaries have correct permissions
	chmod 755 "${pkgdir}/usr/lib/${_pkgname}/jellyfin"

	# use upstream provided packaging files
	cd ../jellyfin-packaging/debian/conf

	# systemd integration
	install -vDm644 jellyfin.service -t "${pkgdir}/usr/lib/systemd/system/"
	install -vDm644 "${srcdir}/sysusers.conf" "${pkgdir}/usr/lib/sysusers.d/${_pkgname}.conf"
	install -vDm644 "${srcdir}/tmpfiles.conf" "${pkgdir}/usr/lib/tmpfiles.d/${_pkgname}.conf"
	install -vDm640 jellyfin "${pkgdir}/etc/${_pkgname}/${_pkgname}.env"
	install -vDm640 logging.json -t "${pkgdir}/etc/${_pkgname}"
}
