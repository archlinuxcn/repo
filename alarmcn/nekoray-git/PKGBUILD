# Maintainer: Chi_Tang <me@chitang.dev>
# Maintainer: Integral <integral@member.fsf.org>

pkgname=nekoray-git
_pkgname=${pkgname%-git}
pkgver=4.0.1.r0.gadef6cd
pkgrel=6
pkgdesc="Qt based cross-platform GUI proxy configuration manager (backend: sing-box)"
arch=('x86_64' 'aarch64' 'riscv64')
url="https://matsuridayo.github.io"
license=('GPL-3.0-or-later')
makedepends=('cmake' 'git' 'go')
depends=(
	'qt6-base' 'qt6-svg' 'qt6-tools'
	'protobuf' 'yaml-cpp' 'zxing-cpp'
	'abseil-cpp'
)
optdepends=(
	'sing-geoip: geoip data for NekoBox'
	'sing-geosite: geosite data for NekoBox'
)
conflicts=("${_pkgname}")
provides=("${_pkgname}")
source=(
	"git+https://github.com/MatsuriDayo/${_pkgname}.git"
	"git+https://github.com/Skycoder42/QHotkey.git"
	"nekobox.sh"
	"nekobox.desktop"
)
sha512sums=('SKIP'
            'SKIP'
            '3a65300520462bd6a5667c50b7db1ccbf7107b9d5c65959d00451a68cdd77d9e13288da03ccb0d712cd4fb076cdfb3c635490d6148268f920cde7ba11b7237a9'
            'f99795f5f82fca74bbc36569d3595bc141c3fef00a0c6076585da683f0a5144dbfdbbe4525cf82576e9010262f74f462ae40f5a7b95dafc01033763a9b1711bd')

pkgver() {
	cd "${_pkgname}/"
	git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
	cd "${_pkgname}/"

	git submodule init
	git config submodule.3rdparty/QHotkey.url "${srcdir}/QHotkey"
	git -c protocol.file.allow=always submodule update

	./libs/get_source.sh
}

build() {
	cd "${_pkgname}/"

	cmake -B build -DQT_VERSION_MAJOR=6 -DNKR_PACKAGE=ON
	cmake --build build

	export GOARCH

	case $CARCH in
	x86_64) GOARCH=amd64 ;;
	aarch64) GOARCH=arm64 ;;
	riscv64) GOARCH=riscv64 ;;
	esac

	GOOS=linux ./libs/build_go.sh
}

package() {
	# Assets
	install -Dm644 nekobox.desktop -t "${pkgdir}/usr/share/applications/"
	install -Dm644 "${_pkgname}/res/public/nekobox.png" -t "${pkgdir}/usr/share/pixmaps/"

	# Core
	local core_srcdir=linux

	case $CARCH in
	x86_64) core_srcdir+=64 ;;
	aarch64) core_srcdir+=-arm64 ;;
	riscv64) core_srcdir+=-riscv64 ;;
	esac

	install -Dm755 "${_pkgname}/deployment/${core_srcdir}/nekobox_core" -t "${pkgdir}/usr/lib/nekobox/"

	# Binary file
	install -Dm755 "${_pkgname}/build/nekobox" -t "${pkgdir}/usr/lib/nekobox/"

	# Launcher
	install -Dm755 nekobox.sh "${pkgdir}/usr/bin/nekobox"
}
