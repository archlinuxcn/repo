# Maintainer: Chi_Tang <me@chitang.dev>
# Maintainer: Integral <integral@member.fsf.org>

pkgname=nekoray-git
_pkgname=${pkgname%-git}
pkgver=4.3.7.r26.gfd421d0
pkgrel=1
pkgdesc="Qt based cross-platform GUI proxy configuration manager (backend: sing-box)"
arch=('x86_64' 'aarch64' 'riscv64')
url="https://github.com/throneproj/${_pkgname}"
license=('GPL-3.0-or-later')
makedepends=('cmake' 'git' 'go' 'qt6-tools')
depends=('qt6-base' 'qt6-charts' 'protobuf' 'yaml-cpp' 'zxing-cpp' 'abseil-cpp' 'cpr')
optdepends=(
	'sing-geoip-db: geoip database for NekoBox'
	'sing-geosite-db: geosite database for NekoBox'
)
conflicts=("${_pkgname}")
provides=("${_pkgname}")
source=(
	"git+${url}.git"
	Throne.{sh,desktop}
)
install="${pkgname}.install"
sha512sums=('SKIP'
            '626eaef62bbbed77d2c5c3181b2968f7ac22d35408f3941ddf85e246913db85ff8c4b243f6f32fa305fae6bd260fb68733b8b6aecf37eb5fc4c2606281e35e15'
            '932b2a5ea5b08a460cae1e42e3b364c57e3673c74a10b0e8aa6d9c1f7ea503254dc5d260e03283bc173a3ee016e90e3ea685498653e6b24d845f6b1a0db3c5f8')

pkgver() {
	git -C "${_pkgname}/" describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
	cd "${_pkgname}/"

	cmake -B build \
		-DCMAKE_BUILD_TYPE=None \
		-DCMAKE_INSTALL_PREFIX=/usr

	cmake --build build

	export CGO_CPPFLAGS="${CPPFLAGS}"
	export CGO_CFLAGS="${CFLAGS}"
	export CGO_CXXFLAGS="${CXXFLAGS}"
	export CGO_LDFLAGS="${LDFLAGS}"

	cd core/server
	VERSION_SINGBOX=$(go list -m -f '{{.Version}}' github.com/sagernet/sing-box)
	go build -o ../../build/ \
		-buildmode=pie \
		-trimpath \
		-ldflags "-linkmode=external -w -s -X 'github.com/sagernet/sing-box/constant.Version=${VERSION_SINGBOX}'" \
		-mod=readonly \
		-modcacherw \
		-tags "with_clash_api,with_gvisor,with_quic,with_wireguard,with_utls,with_ech,with_dhcp"
}

package() {
	install -Dm644 Throne.desktop -t "${pkgdir}/usr/share/applications/"
	install -Dm755 Throne.sh "${pkgdir}/usr/bin/Throne"

	cd "${_pkgname}/"
	install -Dm644 "res/public/Throne.png" -t "${pkgdir}/usr/share/pixmaps/"
	install -Dm755 build/{Throne,Core} -t "${pkgdir}/usr/lib/Throne/"
}
