# Maintainer: Integral <integral@member.fsf.org>
# Contributor: Devin Lin <devin@kde.org>

pkgname=plasma-mobile
pkgver=6.4.4
pkgrel=3
pkgdesc="Plasma Mobile shell components."
arch=('x86_64' 'i686' 'i486' 'pentium4' 'arm' 'armv6h' 'armv7h' 'aarch64')
url="https://invent.kde.org/plasma/${pkgname}"
license=('LGPL-2.0-or-later')
depends=(
	'plasma-nm'
	'plasma-pa'
	'powerdevil'
	'modemmanager-qt'
	'qqc2-breeze-style'
	'kuserfeedback'
	'kirigami-addons'
	'maliit-keyboard'
	'kpipewire'
)
optdepends=(
	'plasma-mobile-nm: Mobile networking settings modules for WiFi, etc.'
	'plasma-settings: Settings application for Plasma Mobile'
	'plasma-dialer: Phone application'
	'plasma-workspace-wallpapers: A large wallpaper selection for Plasma'
	'plasma-mobile-sounds: Plasma Mobile sound theme'
)
makedepends=('cmake' 'extra-cmake-modules')
source=("https://download.kde.org/stable/plasma/${pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('8481ca63a93575fa443248be0b86e0536f4452538479cddfa6cf958e0f9112f1')

build() {
	cmake -B build -S "${pkgname}-${pkgver}" \
		-DCMAKE_BUILD_TYPE=None \
		-DCMAKE_INSTALL_PREFIX=/usr

	cmake --build build
}

package() {
	DESTDIR="${pkgdir}" cmake --install build
}
