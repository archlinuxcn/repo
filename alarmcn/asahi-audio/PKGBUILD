# Maintainer: Hector Martin <marcan@marcan.st>

pkgname=asahi-audio
pkgver=2.1
pkgrel=1
pkgdesc='Linux Userspace Audio Configuration'
arch=('aarch64')
url='http://asahilinux.org'
license=('MIT')
makedepends=(wireplumber pipewire)
source=(
  "asahi-audio-${pkgver}.tar.gz::https://github.com/AsahiLinux/asahi-audio/archive/refs/tags/v${pkgver}.tar.gz"
)
sha256sums=('5cda5d8b29b73e5c07d253db552d802bc2d9ecac4efa8e61929038f92ba6ab9a')

build() {
  cd "${srcdir}/asahi-audio-${pkgver}"
  make
}

package() {
  depends=('wireplumber>=0.4.16' pipewire bankstown lsp-plugins-lv2 speakersafetyd)
  cd "${srcdir}/asahi-audio-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
