pkgname=julia-git-pardiso-src
pkgver=1.1.2
_commit=9329f428699edb5b0e2c7780df478d40e7417aa4
pkgrel=1
pkgdesc="Pardiso.jl"
url="https://github.com/JuliaSparse/Pardiso.jl.git"
arch=(aarch64 armv7h i686 x86_64)
license=('MIT')
makedepends=(git julia-pkg-scripts)
depends=(julia-git)
depends_x86_64=(intel-mkl)
source=("git+https://github.com/JuliaSparse/Pardiso.jl.git#commit=$_commit")
sha256sums=('c651bb5a39e7a5136e6aa18ee4ecc5543dee1bf16043c5da0985476843410476')

prepare() {
  cd Pardiso.jl

  if [[ $CARCH != x86_64 ]]; then
    sed -i -e '/function  *__init__()/a\ \ \ \ return' src/Pardiso.jl
  fi
}

build() {
  cd Pardiso.jl/deps

  JULIA_DEPOT_PATH="${srcdir}/.julia" julia --startup-file=no build.jl
}

package() {
  cd Pardiso.jl

  JULIA_INSTALL_SRCPKG=1 . /usr/lib/julia/julia-install-pkg.sh Pardiso "${pkgdir}" "${pkgname}" julia-git
}
