# $Id$
# Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Guillaume ALAUX <guillaume@archlinux.org>
# Contributor: Florian Pritz <bluewind at jabber dot ccc dot de>
pkgname=wireshark-gtk2
pkgbase=wireshark
pkgver=2.2.1
pkgrel=1
arch=('i686' 'x86_64')
license=('GPL2')
makedepends=('c-ares' 'geoip' 'qt5-tools' 'qt5-multimedia' 'gtk3' 'krb5' 'libpcap' 'bash' 'libssh'
             'gnutls' 'portaudio' 'lua52' 'python' 'desktop-file-utils' 'hicolor-icon-theme')
options=(!emptydirs)
url='https://www.wireshark.org/'
source=(https://www.wireshark.org/download/src/${pkgbase}-${pkgver}.tar.bz2
        do_not_use_svn_version.patch)
sha1sums=('fe07ab3582f39111ce5c78f5de6f44a8667000ac'
          '489a176a7b90f7ae6f0985510c0c9038de54d9d6')

prepare() {
  cd ${pkgbase}-${pkgver}

  # Avoid ugly "svn rev unknown from unknown" version string
  patch -p1 < "$srcdir"/do_not_use_svn_version.patch
}

build() {
  cd ${pkgbase}-${pkgver}

  ./autogen.sh
  ./configure \
      --prefix=/usr \
      --with-gtk2 \
      --with-pcap \
      --with-zlib \
      --with-lua \
      --with-portaudio \
      --with-ssl \
      --with-krb5 \
      --with-c-ares \
      --with-geoip
  make all
}



package() {
  pkgdesc='a free network protocol analyzer for Unix/Linux and Windows - GTK 2 frontend'
  depends=('desktop-file-utils' 'gtk2' 'portaudio' 'wireshark-cli' 'wireshark-common')

  cd ${pkgbase}-${pkgver}

  install -Dm755 .libs/wireshark-gtk "${pkgdir}/usr/bin/wireshark-gtk2"
  install -Dm644 wireshark-gtk.desktop "${pkgdir}/usr/share/applications/wireshark-gtk2.desktop"
  sed -i -e 's/GTK+/&2/' -e 's/-gtk/&2/' \
         "${pkgdir}/usr/share/applications/wireshark-gtk2.desktop"
}

