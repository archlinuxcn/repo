# Maintainer: Lari Tikkanen <lartza@wippies.com>

pkgname=youtube-dl-git
_gitname="youtube-dl"
pkgver=2016.12.22.r10.51378d359
pkgrel=1
pkgdesc="A small command-line program to download videos from YouTube.com and a few more sites (git version)"
arch=('any')
url="https://github.com/rg3/youtube-dl"
license=('custom')
depends=('python' 'python-setuptools')
makedepends=('git' 'pandoc')
optdepends=('ffmpeg: for video post-processing'
            'rtmpdump: for rtmp streams support'
            'atomicparsley: for embedding thumbnails into m4a files')
provides=("youtube-dl")
conflicts=("youtube-dl")
source=('git+https://github.com/rg3/youtube-dl.git')
md5sums=('SKIP')

pkgver() {
  cd $srcdir/$_gitname
  printf "%s" "$(git describe --long | sed 's/\([^-]*-\)g/r\1/;s/-/./g')"
}

prepare() {
  cd $srcdir/$_gitname
  sed -i 's|etc/bash_completion.d|share/bash-completion/completions|' setup.py
  sed -i 's|etc/fish/completions|share/fish/completions|' setup.py
}

build() {
  cd $srcdir/$_gitname
  make pypi-files zsh-completion
}

package() {
  cd $srcdir/$_gitname
  python setup.py install --root="${pkgdir}/" --optimize=1
  mv "${pkgdir}/usr/share/bash-completion/completions/youtube-dl.bash-completion" \
     "${pkgdir}/usr/share/bash-completion/completions/youtube-dl"
  install -Dm644 youtube-dl.zsh "${pkgdir}/usr/share/zsh/site-functions/_youtube-dl"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
