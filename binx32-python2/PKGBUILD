# $Id: PKGBUILD 216192 2014-07-02 07:39:22Z fyan $
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Jason Chu <jason@archlinux.org>
# x32 Maintainer: Fantix King <fantix.king at gmail.com>

_basepkgname=python2
pkgname=binx32-python2
pkgver=2.7.8
pkgrel=1
_pybasever=2.7
pkgdesc="A high-level scripting language (x32 ABI)"
arch=('x86_64')
license=('PSF')
url="http://www.python.org/"
depends=("${_basepkgname}=${pkgver}" 'libx32-bzip2' 'libx32-gdbm' 'libx32-openssl' 'libx32-zlib' 'libx32-expat' 'libx32-sqlite' 'libx32-libffi')
makedepends=('binx32-tk' 'bluez-libs')
optdepends=('binx32-tk: for IDLE')
conflicts=('python2-x32')
replaces=('python2-x32')
provides=('python2-x32')
source=(
    http://www.python.org/ftp/python/${pkgver%rc?}/Python-${pkgver}.tar.xz
    Python-2.7.3-multilib-1.patch
    pyconfig-stub.h
)
sha1sums=('9c6281eeace0c3646fa556c8087bb1b7e033c9c4'
          '533eb9f7d78feff1f5d73887a968ddc5ec486bc2'
          '0ad4a8f98b0d111b603323aad7327fcc349927bc')

prepare() {
  cd Python-${pkgver}

  # http://cblfs.cross-lfs.org/index.php/Python
  patch -p1 -i ${srcdir}/Python-2.7.3-multilib-1.patch
  sed -i -e "s|@@MULTILIB_DIR@@|libx32|g" Lib/distutils/command/install.py \
       Lib/distutils/sysconfig.py \
       Lib/pydoc.py \
       Lib/site.py \
       Lib/sysconfig.py \
       Lib/test/test_dl.py \
       Lib/test/test_site.py \
       Lib/trace.py \
       Makefile.pre.in \
       Modules/getpath.c \
       setup.py
  sed -i "s@/usr/X11R6@/usr@g" setup.py
  sed -i 's@/lib64@/libx32@g' Modules/_ctypes/libffi/m4/libtool.m4 \
      Modules/_ctypes/libffi/configure setup.py
  sed -i "s/'lib64'/'libx32'/g" setup.py
  ln -sf pyconfig.h pyconfig-x32.h

  # Temporary workaround for FS#22322
  # See http://bugs.python.org/issue10835 for upstream report
  sed -i "/progname =/s/python/python${_pybasever}/" Python/pythonrun.c

  # Enable built-in SQLite module to load extensions (fix FS#22122)
  sed -i "/SQLITE_OMIT_LOAD_EXTENSION/d" setup.py

  # FS#23997
  sed -i -e "s|^#.* /usr/local/bin/python|#!/usr/bin/python2-x32|" Lib/cgi.py

  sed -i "s/python2.3/python2-x32/g" Lib/distutils/tests/test_build_scripts.py \
     Lib/distutils/tests/test_install_scripts.py

  # Ensure that we are using the system copy of various libraries (expat, zlib and libffi),
  # rather than copies shipped in the tarball
  rm -r Modules/expat
  rm -r Modules/zlib
  rm -r Modules/_ctypes/{darwin,libffi}*

  # clean up #!s
  find . -name '*.py' | \
    xargs sed -i "s|#[ ]*![ ]*/usr/bin/env python$|#!/usr/bin/env python2-x32|"

  # Workaround asdl_c.py error after we touched the shebangs
  touch Include/Python-ast.h Python/Python-ast.c
}

build() {
  cd "${srcdir}/Python-${pkgver}"

  export OPT="${CFLAGS}"
  export CC="gcc -mx32"
  export CXX="g++ -mx32"
  ./configure --prefix=/usr \
              --enable-shared \
              --with-threads \
              --enable-ipv6 \
              --enable-unicode=ucs4 \
              --with-system-expat \
              --with-system-ffi \
              --with-dbmliborder=gdbm:ndbm --libdir=/usr/libx32

  make
}

check() {
  # As of 2.7.7, 1 test failed:
  # test_uuid
  # Besides, test_thread disabled as it will stuck

  cd Python-${pkgver}
  LD_LIBRARY_PATH="${srcdir}/Python-${pkgver}":${LD_LIBRARY_PATH} \
    "${srcdir}/Python-${pkgver}/python" -m test.regrtest -uall -x test_thread || warning "Tests failed"
}

package() {
  install="${pkgname}.install"

  cd Python-${pkgver}
  make DESTDIR="${pkgdir}" altinstall maninstall
  mv ${pkgdir}/usr/bin/python${_pybasever} ${pkgdir}/usr/bin/python${_pybasever}-x32
  mv ${pkgdir}/usr/bin/python${_pybasever}-config ${pkgdir}/usr/bin/python${_pybasever}-x32-config

  rm "${pkgdir}"/usr/share/man/man1/python.1

  ln -sf python${_pybasever}-x32        "${pkgdir}"/usr/bin/python2-x32
  ln -sf python${_pybasever}-x32-config "${pkgdir}"/usr/bin/python2-x32-config

  # FS#33954
  ln -sf python-${_pybasever}.pc    "${pkgdir}"/usr/libx32/pkgconfig/python2-x32.pc

  ln -sf ../../libpython${_pybasever}.so "${pkgdir}"/usr/libx32/python${_pybasever}/config/libpython${_pybasever}.so

  mv "${pkgdir}"/usr/bin/smtpd.py "${pkgdir}"/usr/libx32/python${_pybasever}/

  # some useful "stuff"
  install -dm755 "${pkgdir}"/usr/libx32/python${_pybasever}/Tools/{i18n,scripts}
  install -m755 Tools/i18n/{msgfmt,pygettext}.py "${pkgdir}"/usr/libx32/python${_pybasever}/Tools/i18n/
  install -m755 Tools/scripts/{README,*py} "${pkgdir}"/usr/libx32/python${_pybasever}/Tools/scripts/

  # clean up redundant scripts
  mv "${pkgdir}"/usr/bin/idle{,2-x32}
  rm "${pkgdir}"/usr/bin/pydoc
  rm "${pkgdir}"/usr/bin/2to3

  sed -i "s|#[ ]*![ ]*/usr/bin/python${_pybasever}$|#!/usr/bin/python${_pybasever}-x32|" \
    "${pkgdir}/usr/bin/python${_pybasever}-x32-config" "${pkgdir}/usr/bin/idle2-x32"

  # clean-up reference to build directory
  sed -i "s#${srcdir}/Python-${pkgver}:##" "${pkgdir}"/usr/libx32/python${_pybasever}/config/Makefile

  mv "${pkgdir}/usr/include/python${_pybasever}/pyconfig.h" "${srcdir}/pyconfig-x32.h"
  rm -rf "${pkgdir}/usr/"{include,share}
  install -Dm644 "${srcdir}/pyconfig-x32.h" "${pkgdir}/usr/include/python${_pybasever}/pyconfig-x32.h"
  install -Dm644 "${srcdir}/pyconfig-stub.h" "${pkgdir}/usr/include/python${_pybasever}/pyconfig-stub.h"

  # install license
  install -dm755 "$pkgdir"/usr/share/licenses
  ln -s ${_basepkgname} "${pkgdir}"/usr/share/licenses/${pkgname}
}
