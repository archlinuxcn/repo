# Maintainer: Daniel Micay <danielmicay@gmail.com>

_gitname=YouCompleteMe
pkgname=vim-youcompleteme-git
pkgver=564.7cef111
pkgrel=1
epoch=1
pkgdesc='A code-completion engine for Vim'
arch=(i686 x86_64)
url='http://valloric.github.com/YouCompleteMe/'
license=(GPL3)
groups=(vim-plugins)
depends=(gvim clang)
makedepends=(git cmake)
provides=(vim-youcompleteme)
conflicts=(vim-youcompleteme)
install=vimdoc.install
source=('git://github.com/Valloric/YouCompleteMe.git')
md5sums=(SKIP)

pkgver() {
  cd $_gitname
  echo $(git rev-list --count master).$(git rev-parse --short master)
}

build() {
  cd $_gitname/cpp
  cmake -G "Unix Makefiles" -DUSE_SYSTEM_LIBCLANG=ON
  make ycm_core
}

package() {
  cd $_gitname
  mkdir -p "$pkgdir/usr/share/vim/vimfiles"
  cp -a autoload doc plugin python "$pkgdir/usr/share/vim/vimfiles"
  ln -sf /usr/lib/llvm/libclang.so "$pkgdir/usr/share/vim/vimfiles/python/"
}
