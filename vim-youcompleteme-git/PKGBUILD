# Maintainer: Daniel Micay <danielmicay@gmail.com>
# Packager: phoenixlzx <phoenixlzx at archlinuxcn.org>

pkgname=vim-youcompleteme-git
pkgver=20130222
pkgrel=1
pkgdesc='A code-completion engine for Vim'
arch=(i686 x86_64)
url='http://valloric.github.com/YouCompleteMe/'
license=(GPL3)
groups=(vim-plugins)
depends=(vim clang)
makedepends=(git cmake)
provides=(vim-youcompleteme)
conflicts=(vim-youcompleteme)
install=vimdoc.install

_gitroot=https://github.com/Valloric/YouCompleteMe.git
_gitname=YouCompleteMe

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  cd cpp

  cmake -G "Unix Makefiles" -DUSE_SYSTEM_LIBCLANG=ON
  make ycm_core
}

package() {
  cd "$srcdir/$_gitname-build"
  mkdir -p "$pkgdir/usr/share/vim/vimfiles"
  cp -a autoload doc plugin python "$pkgdir/usr/share/vim/vimfiles"
  ln -sf /usr/lib/llvm/libclang.so "$pkgdir/usr/share/vim/vimfiles/python/"
}
