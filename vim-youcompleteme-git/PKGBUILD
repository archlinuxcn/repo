# Maintainer: Stephen Zhang <zsrkmyn at gmail dot com>
# Contributor: Erik Beran <eberan AT_gmail_DOT com>
# Contributor: Thor K. H. <thor at roht dot no>
# Contributor: Babken Vardanyan <483ken 4tgma1l
# Contributor: mikezackles
# Contributor: z33ky
# Contributor: stykr
# Contributor: Svenstaro
# Contributor: KaiSforza
# Contributor: Simon Gomizelj <simongmzlj@gmail.com>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: shmilee
# Contributor: foobster
# Contributor: archdria
# Contributor: Andy Weidenbaum <archbaum@gmail.com>

pkgname=vim-youcompleteme-git
pkgver=2428.ff0a62bd
_CLANG_VERSION_MAX=7.1
_CLANG_VERSION_MIN=7.0
pkgrel=1
pkgdesc="A code-completion engine for Vim"
arch=('i686' 'x86_64')
url='http://valloric.github.com/YouCompleteMe/'
license=('GPL3')
#groups=('vim-plugins')
depends=('python' 'vim' "clang>=$_CLANG_VERSION_MIN" "clang<$_CLANG_VERSION_MAX"
         'python-bottle' 'python-future' 'python-requests'
         'python-waitress' 'python-jedi')
optdepends=('mono: for C# completer'
            'nodejs: for nodejs completer'
            'rust: for rust completer'
            'racerd: for rust completer'
            'go: for go completer'
            'godef: for go completer'
            'gocode: for go completer'
            'typescript: for typescript completer')
makedepends=('cmake' 'git' 'make' 'mono' 'npm')
#checkdepends=('python-psutil' 'python-nose' 'python-mock'
#              'python-pyhamcrest' 'python-webtest' 'flake8'
#              'mono' 'nodejs' 'rust' 'racerd' 'go' 'godef'
#              'gocode' 'typescript')
source=('git+https://github.com/Valloric/YouCompleteMe.git'
        'git+https://github.com/ross/requests-futures.git'
        'git+https://github.com/Valloric/ycmd.git'
        'frozendict::git+https://github.com/slezica/python-frozendict.git'
        'git+https://github.com/nosami/OmniSharpServer.git'
        'git+https://github.com/icsharpcode/NRefactory.git'
        'git+https://github.com/jbevain/cecil.git'
        'use-system-completer.patch')
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'bc221a81c964da13c47fe9aabbcaff22c023c58ac46c167825f23e4fa2c40d13')
_COMPLETER="USE_CLANG_COMPLETER"
_SYSTEM_LIBCLANG="USE_SYSTEM_LIBCLANG"

pkgver() {
  cd "$srcdir/YouCompleteMe"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  msg2 'Setting up Git submodules...'

  cd "$srcdir/YouCompleteMe"
  git submodule init
  for _m in requests-futures ycmd; do
    git config submodule.third_party/$_m.url "$srcdir/$_m"
    git submodule update third_party/$_m
  done

  cd "$srcdir/YouCompleteMe/third_party/ycmd"
  patch -Np1 -i "$srcdir/use-system-completer.patch"
  git submodule init
  for _m in OmniSharpServer frozendict; do
    git config submodule.third_party/$_m.url "$srcdir/$_m"
    git submodule update third_party/$_m
  done

  cd "$srcdir/YouCompleteMe/third_party/ycmd/third_party/OmniSharpServer"
  git submodule init
  git config submodule.NRefactory.url "$srcdir/NRefactory"
  git config submodule.cecil.url "$srcdir/cecil"
  git submodule update
}

build() {
  # https://github.com/Valloric/ycmd/blob/master/build.py {{{

  msg2 'Building ycmd...' # BuildYcmdLibs()
  mkdir -p "$srcdir/ycmd_build"
  cd "$srcdir/ycmd_build"
  cmake -G "Unix Makefiles" -DUSE_PYTHON2=0 -D${_COMPLETER}=1 -D${_SYSTEM_LIBCLANG}=1 . "$srcdir/YouCompleteMe/third_party/ycmd/cpp"
  make ycm_core

  msg2 'Building OmniSharp completer...' # BuildOmniSharp()
  cd "$srcdir/YouCompleteMe/third_party/ycmd/third_party/OmniSharpServer"
  xbuild /property:Configuration=Release

  msg2 'Building Tern completer...' # SetUpTern()
  cd "$srcdir/YouCompleteMe/third_party/ycmd/third_party/tern_runtime"
  npm install --production
  # }}}

  cd "$srcdir/YouCompleteMe/third_party/requests-futures"
  python3 -m compileall -x 'setup.py' .

  for _m in frozendict; do
    cd "$srcdir/YouCompleteMe/third_party/ycmd/third_party/$_m"
    python3 -m compileall -x '(tests/|setup.py)' .
  done

  cd "$srcdir/YouCompleteMe/third_party/ycmd/ycmd"
  python3 -m compileall -x 'tests/' .

  cd "$srcdir/YouCompleteMe/python/ycm/"
  python3 -m compileall -x 'tests/' .
}

#check() {
#  export LANG=en_US.utf8
#  cd "$srcdir/YouCompleteMe/third_party/ycmd/"
#  python run_tests.py --skip-build
#  cd "$srcdir/YouCompleteMe/"
#  python run_tests.py --skip-build
#}

package() {
  for _f in .git .gitignore .gitmodules .hgignore .hgtags .travis.yml; do
    find "$srcdir" -name "$_f" -exec rm -rf {} +
  done
  rm -fr "$srcdir/YouCompleteMe/third_party/requests-futures/setup.py"
  rm -fr "$srcdir/YouCompleteMe/third_party/ycmd/third_party/frozendict/setup.py"
  rm -fr "$srcdir/YouCompleteMe/third_party/ycmd/ycmd/tests"
  rm -fr "$srcdir/YouCompleteMe/python/ycm/tests"

  mkdir -p "$pkgdir/usr/share/vim/vimfiles/third_party/ycmd/third_party/OmniSharpServer/OmniSharp/bin/Release"

  cp -r "$srcdir/YouCompleteMe/"{autoload,doc,plugin,python} \
    "$pkgdir/usr/share/vim/vimfiles"
  cp -r "$srcdir/YouCompleteMe/third_party/requests-futures" \
    "$pkgdir/usr/share/vim/vimfiles/third_party"
  cp -r "$srcdir/YouCompleteMe/third_party/ycmd/"{ycmd,ycm_core.so,CORE_VERSION} \
    "$pkgdir/usr/share/vim/vimfiles/third_party/ycmd"

  ln -s "/usr/lib/clang/${_CLANG_VERSION_MIN}"* \
    "$pkgdir/usr/share/vim/vimfiles/third_party/ycmd/clang_includes"

  cp -r "$srcdir/YouCompleteMe/third_party/ycmd/third_party/"{frozendict,tern_runtime} \
    "$pkgdir/usr/share/vim/vimfiles/third_party/ycmd/third_party"

  cp -r "$srcdir/YouCompleteMe/third_party/ycmd/third_party/OmniSharpServer/OmniSharp/bin/Release" \
    "$pkgdir/usr/share/vim/vimfiles/third_party/ycmd/third_party/OmniSharpServer/OmniSharp/bin"
}
# vim:set ts=2 sw=2 et:
