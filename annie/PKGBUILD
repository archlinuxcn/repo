# Maintainer: metal A-wing <1 at 233 dot email>
# Contributor:  Joaquin Garmendia <joaquingc123 at gmail dot com>

pkgname=annie
pkgver=0.6.5
pkgrel=1
arch=('x86_64' 'i686')
pkgdesc="A fast, simple and clean video downloader written in Go"
url="https://github.com/iawia002/annie"
license=("MIT")
makedepends=("go")
depends=("ffmpeg")
conflicts=("annie")
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/iawia002/annie/archive/${pkgver}.tar.gz")
sha256sums=('9f6029d9c372a174d372913ab3e856eb73075602baa956520e78639b0e151e5a')

prepare(){
	mkdir -p "$srcdir/src/github.com/iawia002/annie"
	cp -r "$srcdir/$pkgname-$pkgver/"{downloader,config,extractors,parser,request,static,utils,vendor} \
		"$srcdir/src/github.com/iawia002/annie"
}


build(){
	cd "${pkgname}-${pkgver}"
	GOPATH="$srcdir" go build
}

package(){
	install -Dm755 "${srcdir}/${pkgname}-${pkgver}/${pkgname}-${pkgver}" "${pkgdir}/usr/bin/${pkgname}"
	install -Dm644 "${srcdir}/${pkgname}-${pkgver}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

