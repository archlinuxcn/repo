# Maintainer: Yichao Yu <yyc1992@gmail.com>
# Contributer: Jelle van der Waa <jelle@vdwaa.nl>
# Contributer: Allan McRae <allan@archlinux.org>

_pypyname=pypy-hg
_pypyabi=26
_pypy3name=pypy3-hg
_pypy3abi=25

pkgbase=${_pypyname}-six
pkgname=(${_pypyname}-six ${_pypy3name}-six)
pkgver=1.8.0
pkgrel=4
pkgdesc="Python 2 and 3 compatibility utilities"
arch=('any')
url="http://pypi.python.org/pypi/six/"
license=('MIT')
makedepends=(pypy3-${_pypy3abi} pypy)
source=(http://pypi.python.org/packages/source/s/six/six-$pkgver.tar.gz)
md5sums=('1626eb24cc889110c38f7e786ec69885')

build() {
  cp -r six-$pkgver six2-$pkgver
}

check() {
  (cd six-$pkgver
   pypy3 setup.py check)

  (cd six2-$pkgver
   pypy setup.py check)
}

package_pypy3-hg-six() {
  depends=(pypy3-${_pypy3abi})
  conflicts=(pypy3-six)
  provides=(pypy3-${_pypy3abi}-six=${pkgver}-${pkgrel}
            pypy3-six=${pkgver}-${pkgrel})

  cd six-$pkgver
  pypy3 setup.py install --root "${pkgdir}" --optimize=1
  install -Dm644 "${srcdir}/six-${pkgver}/LICENSE" \
          "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_pypy-hg-six() {
  depends=(pypy)
  conflicts=(pypy-six)
  provides=(pypy-six=${pkgver}-${pkgrel})

  cd six2-$pkgver
  pypy setup.py install --root "${pkgdir}" --optimize=1
  install -Dm644 "${srcdir}/six-${pkgver}/LICENSE" \
          "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
