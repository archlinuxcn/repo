# Maintainer: Stephen Zhang <zsrkmyn at gmail dot com>

pkgdesc='Lua interface to QT library'
pkgname='torch7-qtlua-git'
pkgver=r83.8b80419
pkgrel=1
makedepends=('cmake' 'git')
depends=('torch7-git>=r819' 'qt4')
conflicts=('torch7-qtlua')
provides=('torch7-qtlua')
arch=('x86_64' 'i686')
url='https://github.com/torch/qtlua'
license=('BSD')
source=("${pkgname}::git+${url}")
sha512sums=('SKIP')

pkgver () {
	cd "${pkgname}"
	(
		set -o pipefail
		git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
		printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
	)
}

prepare() {
	cd "${pkgname}"
	sed -i -e '24s#cmake#cmake/${PROJECT_NAME}#' cmake/QtLuaPaths.cmake
	sed -i \
		-e '7iSET(QTLUA_LIB_DIR "@CMAKE_INSTALL_PREFIX@/@QtLua_INSTALL_LIB_SUBDIR@")' \
		-e '9iINCLUDE_DIRECTORIES(${QTLUA_INCLUDE_DIR})\nLINK_DIRECTORIES(${QTLUA_LIB_DIR})' \
		-e '2d' qtlua/QtLuaConfig.cmake.in
	sed -i -e '57s#)# @ONLY)#' qtlua/CMakeLists.txt
}

build () {
	cd "${pkgname}"
	[ ! -d build ] && mkdir build
	cd build
	cmake .. \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DLUA_INCDIR=/usr/include/luajit-2.0 \
		-DLUA_LIBDIR=/usr/lib/lua/5.1 \
		-DLUA=luajit \
		-DLUA_BINDIR=/usr/bin \
		-DLUALIB=/usr/lib/libluajit-5.1.so \
		-DLUADIR=/usr/share/lua/5.1 \
		-DLIBDIR=/usr/lib/lua/5.1 \
		-DCONFDIR=/usr/share
	make
}

package () {
	cd "${pkgname}/build"
	make DESTDIR="${pkgdir}" install
}
