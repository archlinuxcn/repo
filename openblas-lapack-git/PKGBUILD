# Maintainer: all3fox <all3fox [at] gmail __DoT_ com>
# Contributor: eolianoe <eolianoe [at] gmail [DoT] com>
# Contributor: Jiaxi Hu <sftrytry _AT_ gmail _DOT_ com>
# Contributor: Giuseppe Borzi <gborzi _AT_ ieee _DOT_ org>

pkgbase=openblas-git
pkgname=(openblas-git--dummy-for-debug openblas-git openblas-lapack-git)
_pkgname=OpenBLAS
pkgver=v0.2.20.r712.g544b069e
pkgrel=1
pkgdesc="Optimized BLAS library based on GotoBLAS2 1.13 BSD"
arch=('i686' 'x86_64' 'armv7h' 'aarch64')
url="http://xianyi.github.com/OpenBLAS/"
license=('BSD')
makedepends=('git' 'perl' 'gcc-fortran')
options=(!emptydirs debug strip)
source=("git://github.com/xianyi/OpenBLAS")
md5sums=('SKIP')
_lapack_ver=3.7.1

_config=(
  'SMP=1'
  'USE_OPENMP=1'
  'USE_THREAD=1'
  'NUM_THREADS=8'
  'MAJOR_VERSION=3'
  'NO_LAPACK=0'
  'GEMM_MULTITHREADING_THRESHOLD=50'
  'NO_AFFINITY=1'
)

case "$CARCH" in
  x86_64)
    _config=("${_config[@]}" 'DYNAMIC_ARCH=1' 'NUM_THREAD=16')
    ;;
  i686)
    _config=("${_config[@]}" 'DYNAMIC_ARCH=1' 'NUM_THREAD=8')
    ;;
  armv7h)
    _config=("${_config[@]}" 'TARGET=CORTEXA9' 'NUM_THREAD=8')
    ;;
  aarch64)
    _config=("${_config[@]}" 'TARGET=CORTEXA57' 'NUM_THREAD=8')
    ;;
  *)
    echo "Unknown CARCH: ${CARCH}"
    ;;
esac

pkgver() {
  cd "${_pkgname}"
  git describe --long | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd ${_pkgname}

  # patch -Np1 < ../.patch
}

build() {
  cd "${_pkgname}"

  export FFLAGS="-g -O2"

  # The OpenBLAS build system doesn't handle update very well.
  git clean -fdx
  make "${_config[@]}" libs netlib shared 2> error.log || {
    cat error.log
    return 1
  }
}

package_openblas-git--dummy-for-debug() {
  provides=()
  conflicts=()
  # This dummy package is provided to work around FS#59378
}

package_openblas-git() {
  depends=('gcc-libs')
  provides=('openblas')
  conflicts=('openblas')

  cd "${_pkgname}"

  make "${_config[@]}" PREFIX="${pkgdir}/usr" install &> install.log || {
    cat install.log
    return 1
  }

  # Install license
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # Remove reference to ${pkgdir}
  sed -i -e "s%${pkgdir}%%" \
      "${pkgdir}/usr/lib/cmake/openblas/OpenBLASConfig.cmake"

  rm "${pkgdir}/usr/include/"{lapacke,cblas}*
}

package_openblas-lapack-git() {
  depends=('openblas-git')
  provides=('openblas-lapack' "blas=${_lapack_ver}" "lapack=${_lapack_ver}"
            "cblas=${_lapack_ver}" "lapacke=${_lapack_ver}")
  conflicts=('blas' 'lapack' 'cblas')
  rm -rf pkg
  mkdir -p "${pkgdir}/usr/"{include,lib}/
  cd "${_pkgname}"

  make "${_config[@]}" PREFIX="${srcdir}/pkg" install

  mv "${srcdir}/pkg/include/"{lapacke,cblas}* "${pkgdir}/usr/include/"

  # Symlink to provide blas, cblas and lapack
  cd "${pkgdir}/usr/lib/"
  for lib in blas cblas lapack lapacke; do
    ln -sf libopenblas.so lib$lib.so
    ln -sf libopenblas.so lib$lib.so.${_lapack_ver:0:1}
    ln -sf libopenblas.so lib$lib.so.${_lapack_ver}
  done
}
