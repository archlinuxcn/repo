# Maintainer: SÃ©bastien Luttringer
# Contributor: Lawrence Lee <valheru@facticius.net>
# Contributor: Phillip Marvin <phillip.marvin@gmail.com>
# Contributor: keystone <phillip.marvin@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - build v5 with system libatomic_ops

pkgname=libunwind-git
pkgver=1.2.rc1.90.g55eb47d
pkgrel=1
pkgdesc='Portable and efficient C programming interface (API) to determine the call-chain of a program'
arch=('i686' 'x86_64' 'armv7h' 'aarch64')
url='http://www.nongnu.org/libunwind/'
license=('GPL')
depends=('glibc' 'xz')
makedepends=('git')
source=('git://git.savannah.gnu.org/libunwind.git'
        0001-Support-dynamic-unwind-info-on-ARM.patch
        0002-Handle-non-signal-frame-unwind-info-lookup-in-ARM-ex.patch
        0003-Allow-DWARF-version-4.patch
        0004-Prefer-EXTBL-unwinding-on-ARM.patch)
md5sums=('SKIP'
         '92933c002bbb516cf55b98d5643a2eb6'
         '99b17c615e8412677c229fdef7b02141'
         'ddfcb6da4750296122c611fc935a88f8'
         '91caf07fb0c2d574cd7755956b1272d0')
options=('debug' '!strip')
provides=(libunwind)
conflicts=(libunwind)

# HACK!!! remove after the damange on the build server is repaired.
if [[ -f ${SRCDEST:-${PWD}}/libunwind/config ]] && \
       grep 'url = http://git\.savannah\.gnu\.org/' \
            ${SRCDEST:-${PWD}}/libunwind/config &> /dev/null; then
    rm -rf ${SRCDEST:-${PWD}}/libunwind &> /dev/null || true
fi
if [[ -f ${SRCDEST:-${PWD}}/libunwind-git/config ]] && \
       grep 'url = git://git\.sv\.gnu\.org/libunwind\.git' \
            ${SRCDEST:-${PWD}}/libunwind-git/config &> /dev/null; then
    rm -rf ${SRCDEST:-${PWD}}/libunwind-git &> /dev/null || true
fi

pkgver() {
  cd libunwind

  git describe --tags | sed 's/^v//;s/-/./g'
}

prepare() {
  cd libunwind

  patch -Np1 < ../0001-Support-dynamic-unwind-info-on-ARM.patch
  patch -Np1 < ../0002-Handle-non-signal-frame-unwind-info-lookup-in-ARM-ex.patch
  patch -Np1 < ../0003-Allow-DWARF-version-4.patch
  patch -Np1 < ../0004-Prefer-EXTBL-unwinding-on-ARM.patch
}

build() {
  cd libunwind

  autoreconf -fi
  [[ $CARCH == "arm" ]] && LDFLAGS+=" -latomic_ops" && CFLAGS+=" -DAO_USE_PTHREAD_DEFS=1"

  ./configure CFLAGS="$CFLAGS -U_FORTIFY_SOURCE" --prefix=/usr
  make
}

# check() {
#   cd libunwind

#   # This function is ``supposed'' to fail. Upstream know, but haven't fixed it.
#   make check || return 0
# }

package() {
  cd libunwind

  make DESTDIR="$pkgdir/" install
  cat >> "$pkgdir/usr/include/libunwind.h" <<EOF
#ifndef JL_UNW_HAS_FORMAT_IP
#define JL_UNW_HAS_FORMAT_IP 1
#endif
EOF
}
