# Maintainer: SÃ©bastien Luttringer
# Contributor: Lawrence Lee <valheru@facticius.net>
# Contributor: Phillip Marvin <phillip.marvin@gmail.com>
# Contributor: keystone <phillip.marvin@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - build v5 with system libatomic_ops

pkgname=libunwind-git
pkgver=1.3.rc1.34.g748a2df
pkgrel=1
pkgdesc='Portable and efficient C programming interface (API) to determine the call-chain of a program'
arch=('i686' 'x86_64' 'armv7h' 'aarch64')
url='http://www.nongnu.org/libunwind/'
license=('GPL')
depends=('glibc' 'xz')
makedepends=('git')
source=('git://git.savannah.gnu.org/libunwind.git'
        0001-Prefer-EXTBL-unwinding-on-ARM.patch)
md5sums=('SKIP'
         '7cb666de58048d759e06a31debf0bb6e')
options=('debug' '!strip')
provides=(libunwind)
conflicts=(libunwind)

pkgver() {
  cd libunwind

  git describe --tags | sed 's/^v//;s/-/./g'
}

prepare() {
  cd libunwind

  patch -Np1 < ../0001-Prefer-EXTBL-unwinding-on-ARM.patch
}

build() {
  cd libunwind

  autoreconf -fi
  ./configure CFLAGS="$CFLAGS -U_FORTIFY_SOURCE" --prefix=/usr
  make
}

package() {
  cd libunwind

  make DESTDIR="$pkgdir/" install
}
