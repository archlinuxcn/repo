pkgname=ydcv-rs-git
_pkgname="ydcv-rs"
pkgdesc="A Rust version of ydcv."
pkgver=0.3.4.76
pkgrel=1
arch=('i686' 'x86_64')
provides=("ydcv")
url="https://github.com/farseerfc/ydcv-rs"
license=('MIT' 'GPL2')
depends=("openssl" "libdbus" "libxcb")
optdepends=()
makedepends=('git' 'cargo')
source=('git://github.com/farseerfc/ydcv-rs.git')
sha256sums=('SKIP')

pkgver() {
	cd $_pkgname
	echo $(grep '^version =' Cargo.toml|head -n1|cut -d\" -f2).$(git rev-list --count HEAD)
}

build() {
	cd $_pkgname
	cargo build --no-default-features --features "notify-rust" --release
}

check() {
	cd $_pkgname
	cargo test --no-default-features --features "notify-rust" --release
}

package() {
	cd $_pkgname
	install -D -m755 "$srcdir/$_pkgname/target/release/ydcv-rs" "$pkgdir/usr/bin/ydcv"
}
