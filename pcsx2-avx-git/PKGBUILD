# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: josephgbr <rafael.f.f1@gmail.com>
# Contributor: Themaister <maister@archlinux.us>

pkgname=pcsx2-avx-git
pkgver=1.5.0.r2356.dc5327816
pkgrel=1
pkgdesc='A Sony PlayStation 2 emulator (AVX instruction set, Sandy Bridge and up CPU required)'
arch=('x86_64')
url='http://www.pcsx2.net'
license=('GPL2' 'GPL3' 'LGPL2.1' 'LGPL3')
depends=('lib32-glew' 'lib32-libaio' 'lib32-libcanberra' 'lib32-libjpeg-turbo'
         'lib32-libpulse' 'lib32-nvidia-cg-toolkit' 'lib32-portaudio'
         'lib32-sdl2' 'lib32-soundtouch' 'lib32-wxgtk2')
makedepends=('cmake' 'git' 'png++')
provides=('pcsx2' 'pcsx2-git')
conflicts=('pcsx2' 'pcsx2-git')
source=('git+https://github.com/PCSX2/pcsx2.git')
sha256sums=('SKIP')

pkgver() {
  cd pcsx2

  git describe --tags | sed 's/^v//; s/-dev//; s/-/.r/; s/-g/./'
}

prepare() {
  # Disable ZZOgl-PG
  rm -rf pcsx2/plugins/zzogl-pg
  sed -i 's/-march=native/-march=sandybridge/g' pcsx2/cmake/BuildParameters.cmake
}

build() {
  cd pcsx2

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. \
    -DCMAKE_BUILD_TYPE='Release' \
    -DCMAKE_TOOLCHAIN_FILE='cmake/linux-compiler-i386-multilib.cmake' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCMAKE_LIBRARY_PATH='/usr/lib32' \
    -DPLUGIN_DIR='/usr/lib32/pcsx2' \
    -DGAMEINDEX_DIR='/usr/share/pcsx2' \
    -DEXTRA_PLUGINS='TRUE' \
    -DREBUILD_SHADER='TRUE' \
    -DGLSL_API='TRUE' \
    -DPACKAGE_MODE='TRUE' \
    -DXDG_STD='TRUE'

  make
}

package() {
  cd pcsx2/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
