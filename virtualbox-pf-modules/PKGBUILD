# Maintainer: Zuyi Hu <hzy068808@gmail.com>
# Contributer: graysky <graysky AT archlinux DOT us>
# Contributer: Ionut Biru <ibiru@archlinux.org>
# Contributer: SÃ©bastien Luttringer <seblu@aur.archlinux.org>

pkgbase=(virtualbox-pf-modules)
pkgname=(virtualbox-pf-host-modules virtualbox-pf-guest-modules)
pkgver=5.0.4
pkgrel=3
goodver=4.2
badver=4.3
arch=('i686' 'x86_64')
url='http://virtualbox.org'
license=('GPL')
makedepends=('linux-pf-headers' "virtualbox-host-dkms>=$pkgver" "virtualbox-guest-dkms>=$pkgver" 'dkms')

_extramodules=extramodules-$goodver-pf
_kernver="$(cat /usr/lib/modules/${_extramodules}/version)"

build() {
	# dkms need modification to be run as user
	cp -r /var/lib/dkms .
	echo "dkms_tree='$srcdir/dkms'" > dkms.conf
	# build host modules
	msg2 'Host modules'
	dkms --dkmsframework dkms.conf build "vboxhost/$pkgver" -k "$_kernver"
	# build guest modules
	msg2 'Guest modules'
	dkms --dkmsframework dkms.conf build "vboxguest/$pkgver" -k "$_kernver"
}

package_virtualbox-pf-host-modules() {
	_Hpkgdesc='Host kernel modules for VirtualBox running under Linux-pf.'
	pkgdesc="${_Hpkgdesc}"
	provides=("virtualbox-host-modules")
	depends=("linux-pf>=$goodver" "linux-pf<$badver")
	install=host.install

	install -dm755 "$pkgdir/usr/lib/modules/$_extramodules"
	cd "dkms/vboxhost/$pkgver/$_kernver/$CARCH/module"
	install -m644 * "$pkgdir/usr/lib/modules/$_extramodules"
	find "$pkgdir" -name '*.ko' -exec gzip -9 {} +
	sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='$_extramodules'/" "$startdir/host.install"
}

package_virtualbox-pf-guest-modules() {
	_Gpkgdesc='Guest kernel modules for VirtualBox running under Linux-pf.'
	pkgdesc="${_Gpkgdesc}"
	license=('GPL')
	provides=("virtualbox-guest-modules")
	depends=("linux-pf>=$goodver" "linux-pf<$badver")
	install=guest.install

	install -dm755 "$pkgdir/usr/lib/modules/$_extramodules"
	cd "dkms/vboxguest/$pkgver/$_kernver/$CARCH/module"
	install -m644 * "$pkgdir/usr/lib/modules/$_extramodules"
	find "$pkgdir" -name '*.ko' -exec gzip -9 {} +
	sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='$_extramodules'/" "$startdir/guest.install"
}
pkgdesc='Kernel modules for VirtualBox running under Linux-pf. No need for dkms with this package!'


