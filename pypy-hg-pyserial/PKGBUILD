# Maintainer: Yichao Yu <yyc1992@gmail.com>

_pypyname=pypy-hg
_pypyabi=26
_pypy3name=pypy3-hg
_pypy3abi=25

pkgbase=${_pypyname}-pyserial
pkgname=(${_pypy3name}-pyserial ${_pypyname}-pyserial)
pkgver=2.7
pkgrel=6
pkgdesc="Multiplatform Serial Port Module for Python"
arch=('any')
url="http://pyserial.sf.net"
license=('custom:PYTHON')
makedepends=(pypy pypy3-${_pypy3abi})
source=(http://pypi.python.org/packages/source/p/pyserial/pyserial-$pkgver.tar.gz)
md5sums=('794506184df83ef2290de0d18803dd11')

build() {
  cp -a $srcdir/pyserial-$pkgver $srcdir/py2serial-$pkgver
}

package_pypy3-hg-pyserial() {
  depends=(pypy3-${_pypy3abi})
  provides=(pypy3-pyserial=${pkgver}
            pypy3-${_pypy3abi}-pyserial=${pkgver})
  conflicts=(pypy3-pyserial)
  cd pyserial-$pkgver

  pypy3 setup.py install --root="$pkgdir"
  install -D -m644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt

  install -dm755 "${pkgdir}/usr"
  mv "${pkgdir}/opt/pypy3/bin" "${pkgdir}/usr"
  mv "$pkgdir"/usr/bin/miniterm.py "$pkgdir"/usr/bin/miniterm-pypy3.py
}

package_pypy-hg-pyserial() {
  depends=(pypy)
  provides=(pypy-pyserial=${pkgver})
  conflicts=(pypy-pyserial)
  cd py2serial-$pkgver

  pypy setup.py install --root="$pkgdir"
  install -D -m644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt

  install -dm755 "${pkgdir}/usr"
  mv "${pkgdir}/opt/pypy/bin" "${pkgdir}/usr"
  mv "$pkgdir"/usr/bin/miniterm.py "$pkgdir"/usr/bin/miniterm-pypy.py
}
