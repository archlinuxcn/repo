# Maintainer: Tom Richards <tom@tomrichards.net>
# Contributor: Andy Weidenbaum <archbaum@gmail.com>

pkgname=telegram-git
pkgver=r479.6800cf4
pkgrel=1
pkgdesc="Telegram messenger CLI"
arch=('i686' 'x86_64')
url="https://github.com/vysheng/tg"
license=('GPL2')
depends=('libconfig' 'libevent' 'lua')
makedepends=('git')
conflicts=('telegram')
provides=('telegram')

source=("$pkgname"::'git+https://github.com/vysheng/tg')
sha256sums=('SKIP')

pkgver() {
    cd "$srcdir/$pkgname"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd "$srcdir/$pkgname"
    ./configure
    make
}

package() {
    cd "$srcdir/$pkgname"
    install -Dm644 tg-server.pub ${pkgdir}/etc/telegram-cli/server.pub
    install -Dm755 bin/telegram-cli ${pkgdir}/usr/bin/telegram-cli
}
