# Contributor: Iru Dog <mytbk920423 at gmail.com>
pkgname=fqterm-git
pkgver=0.9.8.3.r1.26.g7cc618d
pkgrel=1
pkgdesc='a terminal emulator used for Telnet-based BBS, with Telnet and SSH support'
arch=('i686' 'x86_64')
url='https://github.com/mytbk/fqterm/'
license=('GPL2')
depends=('qt4' 'openssl')
makedepends=('git' 'cmake')
provides=('fqterm')
conflicts=('fqterm' 'fqterm-svn')
source=('git+https://github.com/mytbk/fqterm.git')
md5sums=('SKIP')

pkgver(){
  cd "${srcdir}/fqterm"
  git describe --always | tr '-' '.'
}

build() {
  mkdir -p "${srcdir}/build"
  cd "${srcdir}/build"

  sed -i "s#0.9.8#0.9.8 (Built with $(pacman -Q qt4), ArchLinux ${CARCH})#g" \
      ../fqterm/res/userconf/fqterm.cfg.orig
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_BUILD_TYPE=Release \
      -DQT_QMAKE_EXECUTABLE=qmake-qt4 \
      -DCMAKE_C_FLAGS="${CFLAGS}" \
      -DCMAKE_CXX_FLAGS="${CXXFLAGS}" \
      ../fqterm
  make
}

package(){
  cd "${srcdir}/build"
  make DESTDIR="$pkgdir/" install
}

