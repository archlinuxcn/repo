# $Id$
# Maintainer: Jack Meow <ellrywych@gmail.com>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch> # Contributor: Christopher Reimer <c.reimer1993@gmail.com>

pkgname=texmacs
pkgver=1.0.7.21
pkgrel=1
pkgdesc="Free scientific text editor, inspired by TeX and GNU Emacs. WYSIWYG editor TeX-fonts and CAS-interface (Giac, GTybalt, Macaulay 2, Maxima, Octave, Pari, Qcl, R and Yacas) in one."
arch=('i686' 'x86_64')
url="http://www.texmacs.org/"
license=('GPL')
depends=('perl' 'guile1.8' 'texlive-core' 'python2' 'libxext' 'freetype2' 'shared-mime-info'
         'desktop-file-utils' 'qt4')
# do not remove texlive-core dependency, as it is needed!
optdepends=('transfig: convert images using fig2ps'
            'gawk: conversion of some files')
#source=(ftp://ftp.texmacs.org/pub/TeXmacs/targz/TeXmacs-${pkgver}-src.tar.gz)
source=(http://www.texmacs.org/Download/ftp/tmftp/source/TeXmacs-${pkgver}-src.tar.gz)
install=texmacs.install
options=('!emptydirs')
sha1sums=('e8086acd4b7c1bb3aaccf75b6b0199aa612cb66c')

build() {
  cd ${srcdir}/TeXmacs-${pkgver}-src
  sed -i 's/env python/env python2/' \
    plugins/{mathematica/bin/realpath.py,python/bin/tm_python,sage/bin/tm_sage} \
    TeXmacs/misc/inkscape_plugin/texmacs_reedit.py
  sed -i 's/"python"/"python2"/' plugins/python/progs/init-python.scm
  sed -i '/^LDPATH/d' src/makefile.in
  sed -i -e 's/guile /guile1.8 /g' \
         -e 's/guile-config/guile-config1.8/g' \
         configure

 # Don't generate icon-cache and mime-database (namcap tells that they should not be in a package)
  sed -i '/update-mime-database/d' Makefile.in
  sed -i '/gtk-update-icon-cache/d' Makefile.in
  sed -i '\/icons\/gnome 2>\/dev\/null/d' Makefile.in
  
  export QMAKE=qmake-qt4
  export MOC=moc-qt4
  export UIC=uic-qt4
  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --libexecdir=/usr/lib \
              --enable-pdf-render \
              --enable-optimize \
              --with-imlib2 \
              --with-qt
  make
}

package() {
  cd ${srcdir}/TeXmacs-${pkgver}-src
  make DESTDIR=${pkgdir} install

 # fix fig2ps script
 sed -i 's|${prefix}|/usr|' ${pkgdir}/usr/bin/fig2ps
}
