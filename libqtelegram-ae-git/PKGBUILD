# Maintainer: farseerfc <farseerfc@gmail.com> 
# Contributor: nylocx <just18@gmx.de>
# Contributor: Llumex03 <l.lumex03@gmail.com>
# Original uploader in CCR: FranzMari 
# Original uploader in AUR: á¸¶á¸·umex03
# Fixes in AUR by: ogarcia

pkgname=libqtelegram-ae-git
epoch=3
pkgver=10.0.0.stable.r19.g87681bf
pkgrel=1
pkgdesc="Telegram library written in Qt based on telegram-cli code"
arch=('x86_64' 'i686')
license=('GPL3')
url="https://github.com/Aseman-Land/libqtelegram-aseman-edition"
provides=("libqtelegram-ae=$epoch:$pkgver")
conflicts=('libqtelegram-ae')
depends=('qt5-base' 'qt5-multimedia')
makedepends=('cmake' 'git')
source=("$pkgname"::"git+https://github.com/Aseman-Land/libqtelegram-aseman-edition.git")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$pkgname"
  ( set -o pipefail
    git describe --long --tags 2>/dev/null | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

prepare() {
  cd $pkgname
  mkdir -p build
}

build() {

  cd $pkgname
  ## copied from ./init with modification

  rm -rf libqtelegram-code-generator
  git clone https://github.com/Aseman-Land/libqtelegram-code-generator

  BUILD_DIR="build.init"

  rm -rf "$BUILD_DIR"
  mkdir "$BUILD_DIR"
  cd "$BUILD_DIR"

  qmake-qt5 -r ../libqtelegram-code-generator
  make

  ./libqtelegram-generator 54 ../scheme/scheme-54.tl ..
  cd ..
  ## end of ./init

  cd build
  qmake-qt5 -r QMAKE_CFLAGS_ISYSTEM= CONFIG+=typeobjects ..
  make
}

package() {
  cd $pkgname/build
  make INSTALL_ROOT="$pkgdir" install
}
