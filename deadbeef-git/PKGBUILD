# Maintainer: Heavysink <winstonwu91@gmail.com>
# Maintainer: Soukyuu <chrno-sphered at hotmail dot com>
# Contributor: archtux <antonio dot arias99999 at gmail dot com>
pkgname=deadbeef-git
pkgver=r7938.d3ff62fd4
pkgrel=1
pkgdesc="A GTK+ audio player for GNU/Linux (devel branch)"
url="http://deadbeef.sourceforge.net"
arch=('x86_64')
url='http://deadbeef.sourceforge.net'
license=('GPL2')
depends=('alsa-lib' 'hicolor-icon-theme' 'desktop-file-utils' 'jansson')
makedepends=('libvorbis' 'libmad' 'flac' 'curl' 'imlib2' 'wavpack' 'libsndfile' 'libcdio' 'libcddb'
             'libx11' 'faad2' 'zlib' 'intltool' 'pkgconfig' 'libpulse' 'libzip' 'libsamplerate'
             'yasm' 'ffmpeg' 'gtk2' 'gtk3' 'git' 'intltool')
optdepends=('gtk2: for the GTK2 interface'
            'gtk3: for the GTK3 interface'
            'libsamplerate: for Resampler plugin'
            'libvorbis: for Ogg Vorbis playback'
            'libmad: for MP1/MP2/MP3 playback'
            'mpg123: for MP1/MP2/MP3 playback'
            'flac: for FLAC playback'
            'curl: for Last.fm scrobbler, SHOUTcast, Icecast, Podcast support'
            'imlib2: for artwork plugin'
            'wavpack: for WavPack playback'
            'libsndfile: for Wave playback'
            'libcdio: audio cd plugin'
            'libcddb: audio cd plugin'
            'faad2: for AAC/MP4 support'
            'dbus: for OSD notifications support'
            'pulseaudio: for PulseAudio output plugin'
            'libx11: for global hotkeys plugin'
            'zlib: for Audio Overload plugin'
            'libzip: for vfs_zip plugin'
            'ffmpeg: for ffmpeg plugin')
conflicts=('deadbeef')
provides=('deadbeef')
source=('git+https://github.com/DeaDBeeF-Player/deadbeef')
md5sums=('SKIP')

build() {
  cd "$srcdir/deadbeef"
  ./autogen.sh
  ./configure --prefix=/usr

  make
}

package() {
  cd "$srcdir/deadbeef"

  make DESTDIR="$pkgdir" install
}

pkgver() {
  cd "$srcdir/deadbeef"
  echo r$(git rev-list --count master).$(git rev-parse --short master)
}
