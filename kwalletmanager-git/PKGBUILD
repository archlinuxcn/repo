# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kwalletmanager-git
pkgver=r818.bd78050
pkgrel=1
pkgdesc='Wallet management tool'
arch=('i686' 'x86_64')
url='http://www.kde.org/applications/system/kwalletmanager/'
license=('LGPL')
depends=('kwallet' 'hicolor-icon-theme' 'kdelibs4support')
makedepends=('extra-cmake-modules' 'git' 'python' 'kdoctools' 'kcmutils' 'kdoctools')
conflicts=('kwalletmanager')
provides=('kwalletmanager')
source=('git://anongit.kde.org/kwalletmanager.git#branch=frameworks')
install=$pkgname.install
md5sums=('SKIP')

pkgver() {
  cd kwalletmanager
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kwalletmanager \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
