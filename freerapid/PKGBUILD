# Maintainer: Vojtech Kral <vojtech -at- kral -dot- hk>
# Contributor:  Arkham <arkham at archlinux dot us>
# Contributor: Christoph Zeiler <archNOSPAM_at_moonblade.dot.org>

pkgname=freerapid
pkgver='0.9u4'
pkgrel=1
pkgdesc="A Java-based download manager for Rapidshare and other file sharing archives"
arch=('any')
url="http://wordrider.net/freerapid/"
license=('GPL')
depends=('java-runtime' 'gsettings-desktop-schemas')
provides=('frd')
source=(http://files.kral.hk/frd/FreeRapid-${pkgver}.zip
        $pkgname.sh
        $pkgname.desktop)

sha256sums=('f26d570b63e9591e438a625f87eee80480b160f077a4d7aae433c8d3e0d20ab9'
            '43e453a212e179a7dd6892fff51b10464a798300f6d4105c8163949cba037dc0'
            '5bbca23e1d4225f86aecc84d50ebe100c32b42522cb684a55feecab1ca21e13c')

package()
{
    cd $srcdir/FreeRapid-$pkgver

    # Copy data and fix permissions
    install -d "$pkgdir/usr/share/$pkgname"
    cp -rf frd.jar lib/ lookandfeel/ search/ objectdb.conf "$pkgdir/usr/share/$pkgname/"
    find "$pkgdir/usr/share/$pkgname" -type f -exec chmod 644 '{}' +

    # Install bin, icon and desktop file
    install -Dm 755 "$srcdir/$pkgname.sh" "$pkgdir/usr/bin/$pkgname"
    ln -sf "$pkgname" "$pkgdir/usr/bin/frd"
    install -Dm 644 frd.png "$pkgdir/usr/share/pixmaps/$pkgname.png"
    install -Dm 644 "$srcdir/$pkgname.desktop" "$pkgdir/usr/share/applications/$pkgname.desktop"
}
