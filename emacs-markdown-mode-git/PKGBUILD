# Maintainer: Noel Maersk <veox wemakethings net>
# Contributor: emhs <emhs08 gmail com>
# Contributor: Nils <mail n-sch de>

pkgname=emacs-markdown-mode-git
pkgver=2.3.r200.ga1e1ecc
pkgrel=1
pkgdesc='Emacs markdown-mode'
arch=('any')
url='http://jblevins.org/projects/markdown-mode/'
license=('GPL')
makedepends=('git')
depends=('emacs')
install=${pkgname}.install
provides=('emacs-markdown-mode')
conflicts=('emacs-markdown-mode')
source=(git://github.com/jrblevin/markdown-mode)
md5sums=('SKIP')

pkgver() {
  cd markdown-mode
  # cutting off 'v' prefix that presents in the git tag
  git describe --long | sed -r 's/^v//;s/([^-]*-g)/r\1/;s/-/./g'
}

package() {
  cd markdown-mode
  mkdir -p "$pkgdir"/usr/share/emacs/site-lisp/markdown-mode
  install -Dm644 *.el "$pkgdir"/usr/share/emacs/site-lisp/markdown-mode
}
