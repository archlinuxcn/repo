# Maintainer: lilydjwg <lilydjwg@gmail.com>
# Contributor: Bruno Pagani (a.k.a. ArchangeGabriel) <bruno.n.pagani@gmail.com>
# Contributor: Cedric MATHIEU <me.xenom @ gmail.com>

_name=thunderbird
_channel=nightly
pkgbase=thunderbird-nightly
pkgname=()
_version=66.0a1
pkgver=66.0a1.20181226.10
pkgrel=1
arch=('x86_64')
license=('MPL' 'GPL' 'LGPL')
depends=('alsa-lib' 'cairo' 'dbus-glib' 'desktop-file-utils' 'fontconfig'
         'freetype2' 'gtk2' 'hicolor-icon-theme' 'hunspell' 'libevent' 'libjpeg'
         'libmng' 'libpng' 'libvpx' 'libxt' 'mozilla-common' 'nspr' 'nss'
         'shared-mime-info' 'sqlite' 'startup-notification')
_url="https://ftp.mozilla.org/pub/${_name}/nightly/latest-comm-central-l10n"
_filename_prefix="20181226.10-"
source=("${pkgbase}.desktop")
sha512sums=('ef9cf2a1aecda97587095c2096e6e7d27840da80ec0071341421cbb4bf4d2e06a01b5c95dac3f3c8ea520700035328f8e6becffa0243c1a8da8c6c710126d5a4'
            '68b1eb7ca2f26582599ceb8caca6a7c6cb57b4ecfa169c7baef558aae970dcde4a13997ec257143f11b73094f6c71acf76dc0ebe16e3dd314001df9060c04d8e'
            'SKIP'
            '8d6e5bc86902afa081759f84e0d0bae74a4d8afcc321e5455ebbbcda5ba18b639af7fa6504a1f33894ce948e49841396cf6ec6a392f33e0ef9db930c9d920126'
            'SKIP'
            '1332200dfbb5679ddb3972963f689c2ddbdabd899212e84c8bf8c8551baf269944faac43d3fd8af32295dd4522b56477a022c5b6d7952c1dfe7c6ddae19fd3f5'
            'SKIP'
            '75875c2769f52c41a507d55b839f384e6696ba057dccf3755ea7bb8788ffdb726f80b74e41d646023704978f7c6f245c571e5808032a4ac15ba8a2eba508cf42'
            'SKIP')
validpgpkeys=('14F26682D0916CDD81E37B6D61B7B526D98F0353') # Mozilla’s GnuPG release key

_languages=(en-US zh-CN ja zh-TW)

for _lang in "${_languages[@]}"; do
  _locale=${_lang}
  _pkgname=thunderbird-nightly-${_locale,,}
  _src="${_name}-${_version}.${_lang}.linux"
  _filename="${_filename_prefix}${_src}-x86_64.tar.bz2"

  pkgname+=($_pkgname)
  source+=("${_filename}"::"${_url}/${_src}-x86_64.tar.bz2"
           "${_filename}.asc"::"${_url}/${_src}-x86_64.tar.bz2.asc")
  eval "package_$_pkgname() {
    msg2 'Removing old thunderbird directory...'
    rm -rf thunderbird
    msg2 'Extract ${_filename}...'
    bsdtar -xf ${_filename}
    _package $_lang
  }"
done

# Don't extract anything because they'll override each other
noextract=(${source[@]%%::*})

_package() {
  OPT_PATH="opt/${pkgbase}"
  pkgdesc="Standalone Web Browser from Mozilla — Nightly build ($1)"
  url="https://www.mozilla.org/$1/${_name}/${_channel}"
  provides=(thunderbird-${_channel}=$pkgver)
  conflicts=(thunderbird-${_channel} thunderbird-${_channel}-i18n-${1,,})
  replaces=(thunderbird-${_channel}-i18n-${1,,})

  # Install the package files
  install -d "${pkgdir}"/{usr/bin,opt}
  cp -r ${_name} "${pkgdir}"/${OPT_PATH}
  ln -s "/${OPT_PATH}/${_name}" "${pkgdir}"/usr/bin/${pkgbase}

  # Install .desktop files
  install -Dm644 "${srcdir}"/${pkgbase}.desktop -t "${pkgdir}"/usr/share/applications

  # Install icons
  SRC_LOC="${srcdir}"/${_name}
  DEST_LOC="${pkgdir}"/usr/share/icons/hicolor
  for i in 16 32 48 64 128; do
      install -Dm644 "${SRC_LOC}"/chrome/icons/default/default${i}.png "${DEST_LOC}"/${i}x${i}/apps/${pkgbase}.png
  done

  cat <<EOF | install -Dm644 /dev/stdin "${pkgdir}"/${OPT_PATH}/defaults/preferences/vendor.js
pref("intl.locale.requested", "");
EOF

  # Disable auto-updates
  cat <<EOF | install -Dm644 /dev/stdin "${pkgdir}"/${OPT_PATH}/distribution/policies.json
{"policies": {"DisableAppUpdate": true}}
EOF

  # Use system-provided dictionaries
  rm -rf "${pkgdir}"/${OPT_PATH}/{dictionaries,hyphenation}
  ln -sf /usr/share/hunspell "${pkgdir}"/${OPT_PATH}/dictionaries
  ln -sf /usr/share/hyphen "${pkgdir}"/${OPT_PATH}/hyphenation
}
