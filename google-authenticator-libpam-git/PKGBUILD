# Maintainer: Alexander GÃ¶rtz <aur@nyloc.de>
# Contributer: Dylan Cali <calid1984@gmail.com>

pkgname='google-authenticator-libpam-git'

pkgver=r209.9d858ec
pkgver() {
  cd "$srcdir/$pkgname"
  ( set -o pipefail
    git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

pkgrel=1
pkgdesc='PAM module for google authenticator app'
arch=('i686' 'x86_64' 'armv7h')
url='https://github.com/google/google-authenticator'
license=('APACHE')
depends=('pam')
makedepends=('git')
optdepends=('qrencode: scannable QR codes for google auth phone app')
provides=('google-authenticator-libpam')
conflicts=('google-authenticator-libpam')
source=("$pkgname"::'git+https://github.com/google/google-authenticator.git')
md5sums=(SKIP)

build() {
  cd "${srcdir}/${pkgname}/libpam"
  ./bootstrap.sh
  ./configure --prefix=/usr
  make
}


package() {
  cd "${srcdir}/${pkgname}/libpam"
  make DESTDIR="${pkgdir}/" install
}
