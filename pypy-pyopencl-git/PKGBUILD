# Maintainer: Yichao Yu <yyc1992@gmail.com>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>

_pypyname=pypy
_pypyabi=24
_pypy3name=pypy3
_pypy3abi=24

# pyopencl requires numpy
BUILD_PYPY3=0

pkgbase=${_pypyname}-pyopencl-git
pkgname=(${_pypyname}-pyopencl-git)
makedepends=("pypy-${_pypyabi}" 'pypy-setuptools' 'libcl'
             'opencl-headers>=2:1.2' 'mesa' 'pypy-mako'
             "pypy-${_pypyabi}-numpy" "pypy-pytools" 'git')
if ((BUILD_PYPY3)); then
    pkgname+=(${_pypy3name}-pyopencl-git)
    makedepends+=("pypy3-${_pypy3abi}" "pypy3-${_pypy3abi}-setuptools"
                  "pypy3-${_pypy3abi}-mako" "pypy3-${_pypy3abi}-numpy"
                  "pypy3-${_pypy3abi}-pytools")
fi
pkgver=2014.1.0.376.gc68089b
pkgrel=2
pkgdesc="A complete, object-oriented language binding of OpenCL to Python. PyPy version on top of cffi"
arch=('i686' 'x86_64')
url="http://mathema.tician.de/software/pyopencl"
license=('custom')
epoch=1
source=('git://github.com/yuyichao/pyopencl#branch=cffi-test'
        'LICENSE.txt' 'cc')
sha1sums=('SKIP'
          '2e6966b3d9b15603ce2c3ff79eeadd63c5d066b7'
          '70e53f2b3b32efe852c9d2c266f49dab16df9b20')
options=('debug' 'strip')

prepare() {
  cd pyopencl

  git submodule init
  git submodule sync
  git submodule update
}

pkgver() {
  cd pyopencl

  git describe --tags | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

build() {
  rm -rf pyopencl-python2
  cp -a pyopencl{,-python2}

  # hack to use g++ since gcc doesn't link to the required c++ runtime libraries.
  export PATH="${srcdir}:${PATH}"

  if ((BUILD_PYPY3)); then
      (cd "${srcdir}/pyopencl"
       pypy3 ./configure.py --cl-enable-gl
       pypy3 setup.py build)
  fi

  (cd "$srcdir/pyopencl-python2"
   pypy ./configure.py --cl-enable-gl
   pypy setup.py build)
}

package_pypy3-pyopencl-git() {
  depends=('libcl' 'mesa' "pypy3-${_pypy3abi}" 'gcc-libs'
           "pypy3-${_pypy3abi}-setuptools" "pypy3-${_pypy3abi}-mako"
           "pypy3-${_pypy3abi}-numpy" "pypy3-${_pypy3abi}-pytools")
  provides=(pypy3-pyopencl pypy3-${_pypy3abi}-pyopencl)
  conflicts=(pypy3-pyopencl)

  cd "${srcdir}/pyopencl"
  pypy3 setup.py install --root="${pkgdir}" --optimize=1 --skip-build

  install -D -m644 "$srcdir"/LICENSE.txt \
          "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

package_pypy-pyopencl-git() {
  depends=('libcl' 'mesa' "pypy-${_pypyabi}" 'pypy-setuptools'
           'pypy-mako' "pypy-${_pypyabi}-numpy" "pypy-pytools" 'gcc-libs')
  provides=(pypy-pyopencl pypy-${_pypyabi}-pyopencl)
  conflicts=(pypy-pyopencl)

  cd "${srcdir}/pyopencl-python2"
  pypy setup.py install --root="${pkgdir}" --optimize=1 --skip-build

  install -D -m644 "$srcdir"/LICENSE.txt \
          "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
