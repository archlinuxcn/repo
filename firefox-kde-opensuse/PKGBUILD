# Contributor: Weng Xuetian <wengxt@gmail.com>
# Maintainer: Yegorius <yegorius@domic.us>

# enable this if you run out of memory during linking
#_lowmem=true

# try to build with PGO
#_pgo=true

pkgname=firefox-kde-opensuse
pkgver=27.0.1
pkgrel=1
pkgdesc="Standalone web browser from mozilla.org with OpenSUSE patch, integrate better with KDE"
arch=('i686' 'x86_64')
license=('MPL' 'GPL' 'LGPL')
url="https://build.opensuse.org/package/show/mozilla:Factory/MozillaFirefox"
depends=('gtk2' 'mozilla-common' 'libxt' 'startup-notification' 'mime-types'
         'dbus-glib' 'alsa-lib' 'desktop-file-utils' 'hicolor-icon-theme'
	 'libvpx' 'libevent' 'nss' 'hunspell' 'sqlite' 'libnotify' 'kmozillahelper')
makedepends=('unzip' 'zip' 'diffutils' 'python2' 'yasm' 'mesa' 'imake'
             'xorg-server-xvfb' 'libpulse' 'gstreamer0.10-base')
optdepends=('networkmanager: Location detection via available WiFi networks'
            'gstreamer0.10-base-plugins: vorbis decoding, ogg demuxing'
            'gstreamer0.10-good-plugins: webm and mp4 demuxing'
            'gstreamer0.10-bad-plugins: aac, vp8 and opus decoding'
            'gstreamer0.10-ugly-plugins: h.264 and mp3 decoding'
	    'gstreamer0.10-ffmpeg: more decoders'
            'libpulse: PulseAudio audio driver')
provides=("firefox=${pkgver}")
conflicts=('firefox')
install=firefox.install
options=(!emptydirs)
_patchrev=72225e57f863
_patchurl=http://www.rosenauer.org/hg/mozilla/raw-file/$_patchrev
source=(https://ftp.mozilla.org/pub/mozilla.org/firefox/releases/$pkgver/source/firefox-$pkgver.source.tar.bz2
        mozconfig firefox.desktop firefox-install-dir.patch vendor.js kde.js firefox-20.0.1-fixed-loading-icon.png
	# Firefox patchset
	$_patchurl/firefox-browser-css.patch
	$_patchurl/firefox-kde.patch
	$_patchurl/firefox-kde-114.patch
	$_patchurl/firefox-no-default-ualocale.patch
	$_patchurl/firefox-branded-icons.patch
	# Gecko/toolkit patchset
	$_patchurl/toolkit-download-folder.patch
	$_patchurl/mozilla-nongnome-proxies.patch
	$_patchurl/mozilla-prefer_plugin_pref.patch
	$_patchurl/mozilla-kde.patch
	$_patchurl/mozilla-preferences.patch
	$_patchurl/mozilla-language.patch
)

_google_api_key=AIzaSyDwr302FpOSkGRpLlUpPThNTDPbXcIn_FM

prepare() {
  cd mozilla-release

  cp "$srcdir/mozconfig" .mozconfig

  patch -Np1 -i "$srcdir/firefox-install-dir.patch"
  echo -n "$_google_api_key" > google-api-key

  msg "Patching for KDE"
  rm -f content/media/gstreamer/nsGStreamerFormatHelper.{h,cpp}
  rm -f browser/components/shell/src/{nsKDEShellService,nsUnixShellService}.{h,cpp}
  rm -f browser/base/content/browser-kde.xul
  rm -f toolkit/xre/nsKDEUtils.{h,cpp}
  rm -f uriloader/exthandler/unix/{nsCommonRegistry,nsKDERegistry}.{h,cpp}
  rm -f toolkit/content/widgets/{dialog,preferences}-kde.xml

  patch -Np1 -i "$srcdir/toolkit-download-folder.patch"
  patch -Np1 -i "$srcdir/mozilla-nongnome-proxies.patch"
  patch -Np1 -i "$srcdir/mozilla-prefer_plugin_pref.patch"
  patch -Np1 -i "$srcdir/mozilla-kde.patch"
  patch -Np1 -i "$srcdir/mozilla-preferences.patch"
  patch -Np1 -i "$srcdir/mozilla-language.patch"

  patch -Np1 -i "$srcdir/firefox-browser-css.patch"
  patch -Np1 -i "$srcdir/firefox-kde.patch"
  patch -Np1 -i "$srcdir/firefox-kde-114.patch"
  patch -Np1 -i "$srcdir/firefox-no-default-ualocale.patch"
  patch -Np1 -i "$srcdir/firefox-branded-icons.patch"

  # Fix PRE_RELEASE_SUFFIX
  sed -i '/^PRE_RELEASE_SUFFIX := ""/s/ ""//' browser/base/Makefile.in

  # configure script misdetects the preprocessor without an optimization level
  # https://bugs.archlinux.org/task/34644
  sed -i '/ac_cpp=/s/$CPPFLAGS/& -O2/' configure

  # WebRTC build tries to execute "python" and expects Python 2
  mkdir -p "$srcdir/path"
  ln -s /usr/bin/python2 "$srcdir/path/python"

  # Fix tab loading icon (flickers with libpng 1.6)
  # https://bugzilla.mozilla.org/show_bug.cgi?id=841734
  cp "$srcdir/firefox-20.0.1-fixed-loading-icon.png" \
    browser/themes/linux/tabbrowser/loading.png
}

build() {
  if pacman -T firefox && ! pacman -T "firefox=$pkgver"; then
    error "Please uninstall firefox temporarily before building it (pacman -Rdd ...)"
    exit 1
  fi

  cd mozilla-release

  export PATH="$srcdir/path:$PATH"
  export LDFLAGS="$LDFLAGS -Wl,-rpath,/usr/lib/firefox"
  export PYTHON="/usr/bin/python2"

  if [[ -n $_lowmem || $CARCH == i686 ]]; then
    LDFLAGS+=" -Wl,--no-keep-memory"
  fi

  export MOZ_MAKE_FLAGS="$MAKEFLAGS"
  unset MAKEFLAGS

  if [[ -n $_pgo ]]; then
    # Set up PGO
    export DISPLAY=:99
    Xvfb -nolisten tcp -extension GLX -screen 0 1280x1024x24 $DISPLAY &

    if ! make -f client.mk build MOZ_PGO=1; then
      kill $!
      return 1
    fi

    kill $! || true
  else
    make -f client.mk build
  fi
}

package() {
  cd mozilla-release

  [[ "$CARCH" == "i686" ]] && cp "$srcdir/kde.js" obj-i686-pc-linux-gnu/dist/bin/defaults/pref
  [[ "$CARCH" == "x86_64" ]] && cp "$srcdir/kde.js" obj-x86_64-unknown-linux-gnu/dist/bin/defaults/pref

  make -f client.mk DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir/vendor.js" "$pkgdir/usr/lib/firefox/browser/defaults/preferences/vendor.js"
  install -Dm644 "$srcdir/kde.js" "$pkgdir/usr/lib/firefox/browser/defaults/preferences/kde.js"

  for i in 16 22 24 32 48 256; do
      install -Dm644 browser/branding/official/default$i.png \
        "$pkgdir/usr/share/icons/hicolor/${i}x${i}/apps/firefox.png"
  done

  install -Dm644 browser/branding/official/content/icon64.png \
    "$pkgdir/usr/share/icons/hicolor/64x64/apps/firefox.png"
  install -Dm644 browser/branding/official/mozicon128.png \
    "$pkgdir/usr/share/icons/hicolor/128x128/apps/firefox.png"
  install -Dm644 browser/branding/official/content/about-logo.png \
    "$pkgdir/usr/share/icons/hicolor/192x192/apps/firefox.png"
  install -Dm644 browser/branding/official/content/about-logo@2x.png \
    "$pkgdir/usr/share/icons/hicolor/384x384/apps/firefox.png"

  install -Dm644 "$srcdir/firefox.desktop" "$pkgdir/usr/share/applications/firefox.desktop"

  # Use system-provided dictionaries
  rm -rf "$pkgdir"/usr/lib/firefox/{dictionaries,hyphenation}
  ln -s /usr/share/hunspell "$pkgdir/usr/lib/firefox/dictionaries"
  ln -s /usr/share/hyphen "$pkgdir/usr/lib/firefox/hyphenation"

  # We don't want the development stuff
  rm -r "$pkgdir"/usr/{include,lib/firefox-devel,share/idl}

  #workaround for now
  #https://bugzilla.mozilla.org/show_bug.cgi?id=658850
  ln -sf firefox "$pkgdir/usr/lib/firefox/firefox-bin"
}

sha256sums=('76057ac365fe60b51915e8dba2960546fb3a60a2b5747207a8c936e4fc280d8f'
            '644f40fc6b81ac213e863cacfb470d64b784033afab27ee87b24a47c0bbef823'
            '175d03cf3d8eb420d24a5a3072be69c9603e3ddacd3a83f68f49754eaecf6c5a'
            'bcf224d1f239b73e4ac6d268819c1e94a1606beee99c6d8f851f8192b6444de3'
            '7044b77fb5d4113359144d24beccaf5d1e7cf62c660e3f5c1374bee5b2bf9c00'
            'b8cc5f35ec35fc96ac5c5a2477b36722e373dbb57eba87eb5ad1276e4df7236d'
            '68e3a5b47c6d175cc95b98b069a15205f027cab83af9e075818d38610feb6213'
            'fb852af08b9c597cdd70ea2f143a286d79989e86f7cc706ad749b06cd27c3598'
            '973b19e2801817d3c40d3c64670c8f7a1265e1f7c6596c45b07b4a709c5fa017'
            '1fd54e93db3a4d454ece5a6dc788a4ae36805eb497482add82a11ac5cf990562'
            'd2baac322bc69000353f1825ba5cbcbd3a43af19f28a06fb67b37b116a44791d'
            '4785bdf086b7d537cb83369fea5c177eddb6fc3d8a69064599c64376bd985051'
            'e368d490192e8c83d2d209faa90a8bb0bff89e7d8d08d728001effe433f28e6f'
            'e8289ea4c1f8191e1e23661312ceee2128b8e790501b9a589d0d7bfc4384553f'
            '5f6b0970284d68d5ed18e6bb7ee1e9fc0025ab3c10aaa14c283adb21a4a20ee8'
            '403d03a8a2f413d0f3e07060b25b0b887cf61c5b5ffab0da9766af476b5a65bf'
            '75a0817c601bbe6b0a89fbc6ea327397ff296ad336d139b764421dab1dbf55cc'
            'aca072853b379201b0d8ac8128eb6e0e8255ab0c955d7bfd48f6273c8bf245d5')
