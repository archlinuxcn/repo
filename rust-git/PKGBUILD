# Maintainer: lilydjwg <lilydjwg@gmail.com>
# Maintainer: Daniel Micay <danielmicay@gmail.com>
pkgname=('rust-git' 'rust-doc-git')
pkgver=1.6.0.dev.20151123
pkgrel=1
epoch=2
pkgdesc="A safe, concurrent, practical language from Mozilla."
arch=('i686' 'x86_64')
url="http://www.rust-lang.org/"
license=('MIT' 'Apache')
makedepends=('git' 'gcc' 'curl'
             'libffi' 'python2')
# editor support has been removed, see
# 017b3a543182006fa64bdc8c721969ae38c125f9
source=('git://github.com/rust-lang/rust.git'
        "git://github.com/rust-lang/compiler-rt.git"
        "git://github.com/rust-lang/jemalloc.git"
        "git://github.com/rust-lang/llvm.git"
        "git://github.com/rust-lang/hoedown.git"
        "git://github.com/rust-lang/rust-installer.git"

        'git://github.com/rust-lang/zsh-config')
_submodules=(compiler-rt jemalloc llvm hoedown rust-installer)
sha512sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP')

pkgver() {
  cd rust
  ./configure --prefix=/usr 2>/dev/null >&2
  echo $(make --eval='version:; @echo $(firstword $(CFG_VERSION))' version | tail -n1 | tr '-' '.').$(git log -1 --pretty='%cd' --date=short | tr -d '-')
}

prepare() {
  cd rust
  git submodule init
  for submodule in "${_submodules[@]}"; do
    git config submodule.src/${submodule}.url "$srcdir/${submodule}"
  done
  git submodule update
}

build() {
  cd rust

  ./configure --prefix=/usr
  # avoid python makedepend (force fallback to python2)
  sed -i 's/^PYTHONVERSION.*/PYTHONVERSION := 3/' src/llvm/Makefile.rules

  make
}

package_rust-git() {
  optdepends=('rust-doc-git: language and API documentation'
              'vim-rust-git: support for Vim')
  provides=(rust)
  conflicts=(rust)
  options=(staticlibs)

  cd rust

  make DESTDIR="$pkgdir" install
  rm -fr "$pkgdir"/usr/share/doc/rust/html

  mkdir -p "$pkgdir/usr/share/licenses/rust-git"
  install -m644 COPYRIGHT LICENSE-* "$pkgdir/usr/share/licenses/rust-git"

  install -Dm644 "$srcdir"/zsh-config/_rust "$pkgdir/usr/share/zsh/site-functions/_rust"

  cd "$pkgdir/usr/lib"
  rm rustlib/{components,manifest-rustc,rust-installer-version,install.log,uninstall.sh}
  ln -sf rustlib/"$CARCH"-unknown-linux-gnu/lib/*.so .
}

package_rust-doc-git() {
  pkgdesc="A safe, concurrent, practical language from Mozilla. (Language and API documentation)"
  arch=('any')
  options+=('!strip' '!emptydirs')
  optdepends=('rust-git: to compile and run the programs you can write using this documentation')
  provides=('rust-doc')
  conflicts=('rust-doc')

  cd rust

  _docdir="$pkgdir/usr/share/doc/rust"
  mkdir -p "$_docdir"
  cp -r doc/* "$_docdir/" || true

  chmod -R u=rwX,go=rX "$_docdir"
  for ext in aux out log toc; do
    rm -f "$_docdir"/*."$ext"
  done

  mkdir -p "$pkgdir/usr/share/licenses/rust-doc-git"
  install -m644 COPYRIGHT LICENSE-* "$pkgdir/usr/share/licenses/rust-doc-git"
}
