# Maintainer: Jochen Schalanda <jochen+aur@schalanda.name>
_gemname=facter
pkgname=ruby-$_gemname
pkgver=2.0.1
pkgrel=1
pkgdesc="A library for collecting facts about your system."
arch=(any)
url='http://puppetlabs.com/puppet/related-projects/facter'
license=('MIT')
depends=('ruby' 'inetutils' 'net-tools')
provides=('facter')
conflicts=('ruby-facter1' 'facter')
noextract=($_gemname-$pkgver.gem)
source=(https://rubygems.org/downloads/$_gemname-$pkgver.gem)
sha256sums=('891804c0079a82f545c0ab5dbfb27d1ef65cff443708e9339b3deda7783dd7bc')

package() {
  cd "$srcdir"
  # _gemdir is defined inside package() because if ruby[gems] is not installed on
  # the system, makepkg will exit with an error when sourcing the PKGBUILD.
  local _gemdir="$(ruby -rubygems -e'puts Gem.default_dir')"

  gem install --no-user-install --ignore-dependencies -i "$pkgdir$_gemdir" \
    -n "$pkgdir/usr/bin" "$_gemname-$pkgver.gem"
}
