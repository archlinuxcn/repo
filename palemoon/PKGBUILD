# Maintainer:  WorMzy Tykashi <wormzy.tykashi@gmail.com>
# Contributor: artiom <a.mv at gmx dot fr>
# Contributor: ilikenwf
# Contributor: American_Jesus
pkgname=palemoon
pkgver=28.1.0
pkgrel=1
pkgdesc="Open source web browser based on Firefox focusing on efficiency."
arch=('i686' 'x86_64')
url="https://www.palemoon.org/"
license=('MPL' 'GPL' 'LGPL')
depends=('gtk2' 'dbus-glib' 'desktop-file-utils' 'libxt' 'mime-types' 'alsa-lib'
         'startup-notification')
makedepends=('git' 'python2' 'autoconf2.13' 'unzip' 'zip' 'yasm' 'gconf'
             'libpulse')
optdepends=('libpulse: PulseAudio audio driver'
            'ffmpeg: various video and audio support')
source=(git+"https://github.com/MoonchildProductions/UXP#branch=Pale_Moon-release"
        mozconfig.in)
md5sums=('SKIP'
         '17c1f14483018a6a258db20093cdceb8')

prepare() {
  cd UXP
  git fetch --tags
  latestTag=$(git describe --abbrev=0 --tags)
  git checkout $latestTag
  cd ..
  sed 's#%SRCDIR%#'"$srcdir"'#g' mozconfig.in > mozconfig
  sed -i 's#xlocale#locale#' UXP/intl/icu/source/i18n/digitlst.cpp
}

pkgver () {
  cd UXP
  git describe --abbrev=0 --tags | cut -c 1- | sed 's/_Release//g' | sed 's/PM//g'
}

build() {
  cd UXP

  export MOZBUILD_STATE_PATH="$srcdir/mozbuild"
  export MOZCONFIG="$srcdir/mozconfig"
  export CPPFLAGS="$CPPFLAGS -O2"
  python2 mach build
}

package() {
  cd pmbuild
  make package
  cd dist
  install -d "$pkgdir"/usr/{bin,lib}
  cp -r palemoon/ "$pkgdir/usr/lib/$pkgname"
  ln -s "../lib/$pkgname/palemoon" "$pkgdir/usr/bin/palemoon"

  # icons
  install -Dm644 palemoon/browser/chrome/icons/default/default16.png \
    "$pkgdir/usr/share/icons/hicolor/16x16/apps/$pkgname.png"
  install -Dm644 palemoon/browser/chrome/icons/default/default32.png \
    "$pkgdir/usr/share/icons/hicolor/32x32/apps/$pkgname.png"
  install -Dm644 palemoon/browser/chrome/icons/default/default48.png \
    "$pkgdir/usr/share/icons/hicolor/48x48/apps/$pkgname.png"
  install -Dm644 palemoon/browser/icons/mozicon128.png \
    "$pkgdir/usr/share/icons/hicolor/128x128/apps/$pkgname.png"

  # install desktop file
  install -Dm644 "$srcdir/UXP/application/palemoon/branding/official/palemoon.desktop" "$pkgdir/usr/share/applications/$pkgname.desktop"
}
