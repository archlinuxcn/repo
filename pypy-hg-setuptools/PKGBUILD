# Maintainer: Yichao Yu <yyc1992@gmail.com>

_pypyname=pypy-hg
_pypyabi=26
_pypy3name=pypy3-hg
_pypy3abi=25

pkgbase=${_pypyname}-setuptools
pkgname=(${_pypy3name}-setuptools ${_pypyname}-setuptools)
pkgver=7.0
pkgrel=4
pkgdesc="Easily download, build, install, upgrade, and uninstall Python packages"
arch=('any')
license=('PSF')
url="http://pypi.python.org/pypi/setuptools"
source=(http://pypi.python.org/packages/source/s/setuptools/setuptools-${pkgver}.tar.gz)
md5sums=('6245d6752e2ef803c365f560f7f2f940')
makedepends=(pypy pypy3-${_pypy3abi})

prepare() {
  cp -r setuptools-${pkgver} pypy3-setuptools-${pkgver}

  (cd setuptools-${pkgver}
   sed -i -e "s|^#\!.*/usr/bin/python|#!/usr/bin/pypy|" \
       setuptools/tests/test_resources.py
   sed -i -e "s|^#\!.*/usr/bin/env python|#!/usr/bin/env pypy|" \
       setuptools/command/easy_install.py)

  (cd pypy3-setuptools-${pkgver}
   sed -i -e "s|^#\!.*/usr/bin/python|#!/usr/bin/pypy3|" \
       setuptools/tests/test_resources.py
   sed -i -e "s|^#\!.*/usr/bin/env python|#!/usr/bin/env pypy3|" \
       setuptools/command/easy_install.py)
}

build() {
  (cd pypy3-setuptools-${pkgver}
   pypy3 setup.py build)

  (cd setuptools-${pkgver}
   pypy setup.py build)
}

package_pypy-hg-setuptools() {
  depends=(pypy)
  # provides=(pypy-setuptools=${pkgver}-${pkgrel}
  #           pypy-${_pypyabi}-setuptools=${pkgver}-${pkgrel})
  provides=(pypy-setuptools=${pkgver}-${pkgrel})
  conflicts=(pypy-setuptools)
  cd setuptools-${pkgver}
  pypy setup.py install \
       --root="${pkgdir}" --optimize=1 --skip-build

  cd "${pkgdir}"
  mkdir -p usr/bin
  ln -s ../../opt/pypy/bin/easy_install usr/bin/easy_install-pypy
}

package_pypy3-hg-setuptools() {
  depends=(pypy3-${_pypy3abi})
  provides=(pypy3-setuptools=${pkgver}-${pkgrel}
            pypy3-${_pypy3abi}-setuptools=${pkgver}-${pkgrel})
  conflicts=(pypy3-setuptools)
  cd pypy3-setuptools-${pkgver}
  pypy3 setup.py install \
        --root="${pkgdir}" --optimize=1 --skip-build

  cd "${pkgdir}"
  mkdir -p usr/bin
  ln -s ../../opt/pypy3/bin/easy_install usr/bin/easy_install-pypy3
}
