# Maintainer: Chih-Hsuan Yen <yan12125@gmail.com>
# Forked from community/ibus-chewing; original contributors:
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Rainy <rainylau(at)gmail(dot)com>
# Contributor: Lee.MaRS <leemars@gmail.com>
# Contributor: Hiroshi Yui <hiroshiyui@gmail.com>

_pkgname=ibus-chewing
pkgname=$_pkgname-git
pkgver=1.6.1.r9.gce28d40
pkgrel=1
pkgdesc='Chinese Chewing Engine for IBus Framework'
arch=('i686' 'x86_64')
license=('GPL')
url='https://github.com/definite/ibus-chewing'
depends=('ibus' 'libchewing-git')
makedepends=('gob2' 'cmake' 'git')
checkdepends=('xorg-server-xvfb')
source=("git+https://github.com/definite/ibus-chewing.git"
        "git+https://pagure.io/cmake-fedora.git")
sha512sums=('SKIP'
            'SKIP')
provides=("$_pkgname=$pkgver")
conflicts=("$_pkgname")

pkgver() {
  cd $_pkgname
  ( set -o pipefail
    git describe --long --tag 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/^v//'
  )
}

prepare() {
  cd $_pkgname

  git submodule init
  git config submodule.cmake-fedora.url "$srcdir/cmake-fedora"
  git submodule update

  mkdir -p build
}

build() {
  cd $_pkgname/build

  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DSYSCONF_INSTALL_DIR=/usr/share \
    -DLIBEXEC_DIR=/usr/lib/ibus \
    ..

  make
  make translations
}

check() {
  cd $_pkgname/build

  # How to run dbus-daemon in clean chroots?
  [[ -z "$DBUS_SESSION_BUS_ADDRESS" ]] && return

  GSETTINGS_SCHEMA_DIR="$srcdir/$_pkgname/build/bin/" xvfb-run make test
}

package() {
  cd $_pkgname/build

  make DESTDIR="$pkgdir" install
}
