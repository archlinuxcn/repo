#Maintainer: Xyne <ac xunilhcra enyx, backwards>
pkgname=powerpill
pkgver=2018.9.8
pkgrel=3
pkgdesc='Pacman wrapper for faster downloads.'
arch=(any)
license=(GPL)
url="https://xyne.archlinux.ca/projects/powerpill"
depends=(aria2 'pm2ml>2012.12.12' pyalpm python3 python3-xcgf python3-xcpf)
optdepends=('python3-threaded_servers: internal Pacserve support' 'reflector: Reflector and Rsync support' 'rsync: Rsync download support')
backup=(etc/powerpill/powerpill.json)
source=(
  https://xyne.archlinux.ca/projects/powerpill/src/powerpill-2018.9.8.tar.xz
#  https://xyne.archlinux.ca/projects/powerpill/src/powerpill-2018.9.8.tar.xz.sig
)
sha512sums=(
  12a464787be26c69b468de818da35d0c9f73396b705877923237792b0cbeb321f45097bcb9af4e74aababb2fe24df33b9f6ccc49c7ce185fa8fc1e3a13b99b4f
#  5cfe5bae1e0b05bd7affce46a5d36930b661b99d0313e148d4e95b79f4c1c106b2e85d5905bd6b70696bb87bff111ad46e4a718ca977af078f77827e530eeacd
)
md5sums=(
  b49a2cfbd1dd4ad8bd90c88ba17bbbd2
#  1619c95179a8e80e4f554dcb2ee5d725
)
#validpgpkeys=('EC3CBE7F607D11E663149E811D1F0DC78F173680')

package ()
{
  cd "$srcdir/$pkgname-$pkgver"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  install -Dm755 'powerpill' "$pkgdir/usr/bin/powerpill"
  install -Dm644 'powerpill.json' "$pkgdir/etc/powerpill/powerpill.json"
  install -Dm644 'man/powerpill.json.1.gz' "$pkgdir/usr/share/man/man1/powerpill.json.1.gz"
  install -Dm644 'powerpill-bash-completion.sh' "$pkgdir/usr/share/bash-completion/completions/powerpill"
  install -Dm644 '_powerpill.zsh' "$pkgdir/usr/share/zsh/site-functions/_powerpill"
}

# vim: set ts=2 sw=2 et:
