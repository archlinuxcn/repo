# Maintainer: Yichao Yu <yyc1992@gmail.com>
# Contributor: Florian Pritz <bluewind@xinu.at>
# Contributor: Jonas Heinrich <onny@project-insanity.org>
# Contributor: Jordi De Groof <jordi (dot) degroof (at) gmail (dot) com>
# Contributor: mickele
# Contributor: manwithgrenade
# Contributor: bricem13
# Contributor: gborzi

pkgname=freecad-oce
pkgver=0.15.4671
pkgrel=2
pkgdesc='A general purpose 3D CAD modeler'
arch=('i686' 'x86_64')
url='http://www.freecadweb.org/'
license=('LGPL')
depends=(curl oce python2-pivy xerces-c libspnav shared-mime-info
         hicolor-icon-theme python2-matplotlib python2-shiboken
         python2-pyside-tools pyside-tools-common qtwebkit)
makedepends=(boost eigen gcc-fortran swig xerces-c desktop-file-utils
             cmake 'coin>=3.1.3-9')
# TODO add reasons
optdepends=('pycollada-git' 'python2-pyqt4')
options=(!libtool !staticlibs debug strip)
install=freecad.install
source=("http://downloads.sourceforge.net/sourceforge/free-cad/freecad_${pkgver}.tar.gz"
        "freecad.desktop"
        "freecad.xml")
md5sums=('7afa95d3e8cd845bef83202e76db7f24'
         '4b9656e677b277a3c7babf70b21670e8'
         'c2f4154c8e4678825411de8e7fa54c6b')

prepare() {
  sed -i \
      -e 's|\bpyside-uic\b|python2-pyside-uic|' \
      $srcdir/freecad-$pkgver/cMake/FindPySideTools.cmake
}

build() {
  cd freecad-${pkgver}
  mkdir -p build
  cd build
  export CXXFLAGS+=" -DBOOST_SIGNALS_NO_DEPRECATION_WARNING"

  cmake .. \
        -Wno-dev -DCMAKE_INSTALL_PREFIX=/usr/lib/freecad \
        -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_DATADIR=/usr/share/freecad \
        -DCMAKE_INSTALL_DOCDIR=/usr/share/freecad/doc \
        -DCMAKE_INSTALL_INCLUDEDIR=/usr/include \
        -DOCC_INCLUDE_DIR=/usr/include/oce/ \
        -DPYTHON_EXECUTABLE=/usr/bin/python2 \
        -DFREECAD_USE_EXTERNAL_PIVY=ON \
        -DOCE_DIR=/usr/lib/oce-0.17-dev/
  make
}

package_freecad-oce() {
  cd freecad-${pkgver}/build
  provides=('freecad')
  conflicts=('freecad')

  # Install main program
  make DESTDIR="${pkgdir}" install

  # Symlink to /usr/bin
  mkdir -p "${pkgdir}/usr/bin/"
  ln -sf "../lib/freecad/bin/FreeCAD" "${pkgdir}/usr/bin/freecad"
  ln -sf "../lib/freecad/bin/FreeCADCmd" "${pkgdir}/usr/bin/freecadcmd"

  # Install pixmaps and desktop shortcut
  desktop-file-install \
      --dir="${pkgdir}/usr/share/applications" \
      "${srcdir}/freecad.desktop"

  # Mime info
  install -D -m644 "${srcdir}/freecad.xml" \
          "${pkgdir}/usr/share/mime/packages/freecad.xml"

  boostver=$(objdump -p /usr/lib/libboost_system.so | \
                  grep SONAME | sed -e 's/.*boost_system\.so\.//')
  [[ $boostver =~ ^([0-9]*)\.([0-9]*)\.([0-9]*) ]]
  maj_ver=${BASH_REMATCH[1]}
  min_ver=${BASH_REMATCH[2]}
  patch_ver=${BASH_REMATCH[3]}
  ((next_min=min_ver + 1))
  depends+=("boost-libs>=${maj_ver}.${min_ver}.${patch_ver}"
            "boost-libs<${maj_ver}.${next_min}.0")
}
