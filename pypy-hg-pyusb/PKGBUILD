# Maintainer: Yichao Yu <yyc1992@gmail.com>

_pypyname=pypy-hg
_pypyabi=26
_pypy3name=pypy3-hg
_pypy3abi=25

pkgbase=${_pypyname}-pyusb
pkgname=(${_pypy3name}-pyusb ${_pypyname}-pyusb)
pkgver=1.0.0b1
pkgrel=4
pkgdesc="A pure Python module which provides USB access."
arch=('any')
url="https://github.com/walac/pyusb"
license=('custom')
makedepends=(pypy3-${_pypy3abi} pypy3-${_pypy3abi}-setuptools pypy-setuptools)
optdepends=('libusb-compat: for libusb0.1 backend'
            'libusb: for libusb1.0 backend')
source=("https://github.com/walac/pyusb/archive/${pkgver}.tar.gz")
md5sums=('fe439e0a84749f06eddd65927b7496ad')

build() {
  cp -a pyusb-$pkgver py2usb-$pkgver

  (cd pyusb-$pkgver
   pypy3 setup.py build)

  (cd py2usb-$pkgver
   pypy setup.py build)
}

package_pypy3-hg-pyusb() {
  depends=(pypy3-${_pypy3abi})
  provides=(pypy3-${_pypy3abi}-pyusb=${pkgver}-${pkgrel}
            pypy3-pyusb=${pkgver}-${pkgrel})
  conflicts=(pypy3-pyusb)
  cd pyusb-$pkgver

  pypy3 setup.py install --root="$pkgdir"
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_pypy-hg-pyusb() {
  depends=(pypy)
  provides=(pypy-pyusb=${pkgver}-${pkgrel})
  conflicts=(pypy-pyusb)
  cd py2usb-$pkgver

  pypy setup.py install --root="$pkgdir"
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
