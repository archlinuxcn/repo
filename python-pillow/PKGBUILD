pkgname=python-pillow
_appname=Pillow
pkgver=2.0.0
pkgrel=1
pkgdesc="Python Imaging Library (PIL) fork. Python3 version"
arch=(i686 x86_64)
url="https://github.com/python-imaging/Pillow"
license=('BSD')
depends=('python')
provides=('python-imaging' 'python3-imaging')
conflicts=('python-imaging' 'python3-imaging' 'python-pillow-git')
makedepends=('python-distribute')
source=("http://pypi.python.org/packages/source/P/${_appname}/${_appname}-${pkgver}.zip")
md5sums=('a12b4506bb3838275fafaf7c569ff39f')

package() {
  cd "$srcdir/$_appname-$pkgver"
  python3 setup.py install --root="$pkgdir/" --optimize=1

  # do not have files ending in .py in /usr/bin
  for f in pildriver pilprint pilconvert pilfile pilfont; do
    mv -v "${pkgdir}"/usr/bin/${f}{.py,}
  done
}

