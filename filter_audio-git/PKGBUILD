# Maintainer: Ivan Shapovalov <intelfx100@gmail.com>

pkgname=filter_audio-git
pkgver=r51.2ebc753
pkgrel=1
pkgdesc="An easy to use audio filtering library made from webrtc code"
arch=('i686' 'x86_64')
url="https://github.com/irungentoo/filter_audio"
license=('custom:BSD')
makedepends=('git')
provides=("filter_audio")
conflicts=("filter_audio")
source=("git://github.com/irungentoo/filter_audio.git"
		'COPYING')
sha512sums=('SKIP'
            '2e1adab66b8b286d6ac5a373808e3f2e3a00c981bbede4bda16576d091e7df306b92aa24a8f759f53e0efcf12ca86d46e8d02845fd83d763c30f6153ee91ef23')

pkgver() {
    cd filter_audio
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
	cd filter_audio
	sed -e 's/lib64/lib/g' -i Makefile
}

build() {
	cd filter_audio
	make
}

package() {
    cd filter_audio
    make DESTDIR="$pkgdir" PREFIX="/usr" install
    install -Dm644 "$srcdir/COPYING" "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
