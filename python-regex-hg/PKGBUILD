# Contributor: Spyros Stathopoulos <foucault.online@gmail.com>
pkgname=python-regex-hg
pkgver=275.cda06eaca779
pkgrel=1
pkgdesc="Alternative regular expression module, to replace re."
arch=('i686' 'x86_64')
url="https://bitbucket.org/mrabarnett/mrab-regex"
license=('custom')
depends=('python')
makedepends=('mercurial')
_hgname='mrab-regex'
source=("hg+https://bitbucket.org/mrabarnett/$_hgname")
md5sums=(SKIP)
provides=(python-regex)
conflicts=(python-regex)

pkgver() {
  cd "$srcdir/${_hgname}"
  echo $(hg identify -n).$(hg identify -i)
}

build() {
  cd "$srcdir/${_hgname}"
  python setup.py build
}

package() {
  cd "$srcdir/${_hgname}"
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
}

