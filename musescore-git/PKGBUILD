# Contributor: Dr.Egg <hondoheal@gmail.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Maintainer: Yichao Yu <yyc1992@gmail.com>

pkgname=musescore-git
_realver=2.0.0
pkgver=7063.12de34c
pkgrel=1
epoch=1
pkgdesc="A music score editor - git-version"
arch=('i686' 'x86_64')
url="http://www.musescore.org/en/"
license=('GPL')
depends=('shared-mime-info' 'alsa-lib' 'zlib' 'libvorbis'
  'qt5-base' 'qt5-webkit' 'qt5-tools' 'qt5-quick1' 'qt5-script'
  'qt5-xmlpatterns' 'qt5-svg' 'qt5-declarative' 'qt5-sensors'
  'portaudio' 'libpulse' 'libsndfile' 'libogg' 'gcc-libs')
makedepends=('cmake' 'doxygen' 'git' 'lame')
optdepends=('jack')
conflicts=("musescore" 'mscore' 'musescore-svn')
provides=("musescore=${_realver}" 'musescore-svn')
replaces=('musescore-svn')
source=('git://github.com/musescore/MuseScore.git'
  'mime.xml'
  '0001-fix-for-pch.patch'
  '0002-add-back-native-style.patch')
md5sums=('SKIP'
         '969696178e56de36f9af37d7da61baaa'
         'ce2b9884d275a9c805384dd12bc8f660'
         'e04a2f1a22da61ebdf20ef1bec082482')
install=musescore.install
options=(strip debug)

pkgver() {
  cd MuseScore

  echo "$(git rev-list --count master).$(git rev-parse --short master)"
}

build() {
  cd MuseScore

  # FIXME: Remove hack for gcc 4.8 when the gcc bug is fixed. gcc 4.8 seems to
  # crash when using pre-compiled headers, remove the following patch to
  # (probably) gain some compiling speed if you are not affected by the issue.
  # Adding back pch support also requires hacking with musescore's borken build
  # system.
  patch -Np1 < ../0001-fix-for-pch.patch

  # Upstream add hard coded builtin oxygen based theme (which has X11 support
  # remove, therefore no menu shadow under KDE) and they refuse to add it back.
  # This patch add (back) an option to use system theme.
  # More patch may be coming adding icon theme support later if necessary.
  patch -Np1 < ../0002-add-back-native-style.patch
  export CXXFLAGS+=" -std=gnu++0x -Wno-unused-local-typedefs -Wno-unused-parameter -Wno-maybe-uninitialized -Wno-unused-function"
  mkdir -p build.release
  cd build.release
  cmake .. -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=On
  make lrelease -j1
  make
}

package() {
  cd MuseScore/build.release

  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir/mime.xml" \
    "$pkgdir/usr/share/mime/packages/x-musescore.xml"
  ln -s mscore "$pkgdir/usr/bin/musescore"
  install -dm755 "$pkgdir/usr/share/mscore-2.0/manual"
  mv share/manual/plugins/* \
    "$pkgdir/usr/share/mscore-2.0/manual/plugins"
}
