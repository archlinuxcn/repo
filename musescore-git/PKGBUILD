# Contributor: Dr.Egg <hondoheal@gmail.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Maintainer: Yichao Yu <yyc1992@gmail.com>

pkgname=musescore-git
_realver=2.0.4
pkgver=14099.f24e334c0
pkgrel=1
epoch=1
pkgdesc="A music score editor - git-version"
arch=('i686' 'x86_64')
url="http://www.musescore.org/en/"
license=('GPL')
depends=('shared-mime-info' 'alsa-lib' 'zlib' 'libvorbis'
         'qt5-base' 'qt5-webkit' 'qt5-tools' 'qt5-script'
         'qt5-xmlpatterns' 'qt5-svg' 'qt5-declarative' 'qt5-sensors'
         'qt5-webengine'
         'portaudio' 'libpulse' 'libsndfile' 'libogg' 'gcc-libs'
         'portmidi')
makedepends=('cmake' 'doxygen' 'git' 'lame')
optdepends=('jack')
conflicts=("musescore" 'mscore' 'musescore-svn')
provides=("musescore=${_realver}" 'musescore-svn')
replaces=('musescore-svn')
source=('git://github.com/musescore/MuseScore.git'
  'mime.xml'
  '0001-fix-for-pch.patch'
  '0002-add-back-native-style.patch'
  '0001-Fix-webengine-installation.patch')
md5sums=('SKIP'
         '969696178e56de36f9af37d7da61baaa'
         '23beb91bfc1e17368a50b12afe8ddbaf'
         '44848366f6cccd8727c178ad3c74de05'
         '7d59b37ae10834acef679683a8137695')
options=(strip debug)

pkgver() {
  cd MuseScore

  echo "$(git rev-list --count master).$(git rev-parse --short master)"
}

build() {
  cd MuseScore

  # Upstream add hard coded builtin oxygen based theme (which has X11 support
  # remove, therefore no menu shadow under KDE) and they refuse to add it back.
  # This patch add (back) an option to use system theme.
  # More patch may be coming adding icon theme support later if necessary.
  patch -Np1 < ../0002-add-back-native-style.patch

  # Upstream install script is broken
  patch -Np1 < ../0001-Fix-webengine-installation.patch

  # Fix Musescore + gcc's broken pch.
  # `-DCMAKE_CXX_FLAGS_RELWITHDEBINFO="-fPIC"`:
  #     Make sure the pch compilation doesn't error, since musescore passes the
  #     wrong flags to GCC.
  # Not defining `__cpp_aggregate_nsdmi`:
  #     Seems to cause GCC to ignore the PCH file.
  # `-Wno-invalid-pch`:
  #     Suppresses invalid PCH warning (doesn't seem to work....).
  export CXXFLAGS+=" -fPIC -std=gnu++11 -Wno-unused-local-typedefs -Wno-unused-parameter -Wno-maybe-uninitialized -Wno-unused-function -Wno-invalid-pch"
  mkdir -p build.release
  cd build.release
  cmake .. -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=On \
    -DCMAKE_CXX_FLAGS_RELWITHDEBINFO="-fPIC"
  make lrelease -j1
  make manpages -j1
  make mscore_alias -j1
  make
}

package() {
  cd MuseScore/build.release

  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir/mime.xml" \
    "$pkgdir/usr/share/mime/packages/x-musescore.xml"
  # ln -s mscore "$pkgdir/usr/bin/musescore"
  # install -dm755 "$pkgdir/usr/share/mscore-2.0/manual"
  # mv share/manual/plugins/* \
  #   "$pkgdir/usr/share/mscore-2.0/manual/plugins"
}
